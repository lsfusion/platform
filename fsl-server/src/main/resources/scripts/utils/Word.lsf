MODULE Word;

REQUIRE System, MasterData;

CLASS Template 'Шаблон';
TABLE template(Template);

name 'Имя' = DATA VARSTRING[100] (Template);
file 'Файл шаблона' (Template) = DATA WORDFILE (Template);
loadFile 'Загрузить шаблон'(Template t) = { INPUT =file(t) CHANGE; }
openFile 'Открыть шаблон'(Template t) = { OPEN file(t); }
id 'Идентификатор' = DATA VARISTRING[100] (Template) MINCHARWIDTH 5 MAXCHARWIDTH 10 PREFCHARWIDTH 10;

process = CUSTOM 'lsfusion.erp.utils.word.ProcessTemplateActionProperty' (Template);

resultTemplate = DATA LOCAL WORDFILE ();
openResultTemplate () = { OPEN resultTemplate(); }

openProcess 'Сгенерировать документ'(Template template) = {
    process(template);
    openResultTemplate();
}

templateId (string1) = GROUP AGGR Template template BY id(template) WHERE template IS Template;

CLASS TemplateEntry 'Строка шаблона';
TABLE templateEntry(TemplateEntry);

template 'Шаблон' = DATA Template (TemplateEntry);
isTable 'Таблица' = DATA BOOLEAN(TemplateEntry);
key 'Параметр' = DATA VARSTRING[100](TemplateEntry);
description 'Описание' = DATA VARSTRING[100](TemplateEntry);
value 'Значение' = DATA LOCAL TEXT (TemplateEntry);

templateEntryKeyEntryIdTemplate (string1, string2) = GROUP AGGR TemplateEntry templateEntry BY  key(templateEntry), id(template(templateEntry)) WHERE templateEntry IS TemplateEntry;

FORM template 'Шаблон Word'

    OBJECTS t = Template PANEL
    PROPERTIES (t) name, id
    PROPERTIES (t) loadFile, openFile

    OBJECTS te = TemplateEntry
    PROPERTIES (te) isTable, key, description, value, NEW, DELETE GRID
    PROPERTIES (t) TODRAW te TOOLBAR openProcess

    FILTERS template (te) == t

    EDIT Template OBJECT t
;

DESIGN template {
    BOX {
        preferredSize = (1024, 768);
        type = CONTAINERV;
        MOVE BOX(t) {
            type = CONTAINERH;
            NEW row1 {
                MOVE PROPERTY(name(t));
                MOVE PROPERTY(id(t));
            };
            NEW row2 {
                type = CONTAINERH;
                MOVE PROPERTY(loadFile(t));
                MOVE PROPERTY(openFile(t));
            };
        }
        MOVE BOX(te);
    }
    MOVE TOOLBARBOX;
}


FORM templates 'Шаблоны Word'

    OBJECTS t = Template
    PROPERTIES (t) READONLY name, id
    PROPERTIES (t) NEWSESSION NEW, EDIT, DELETE GRID

    OBJECTS te = TemplateEntry
    PROPERTIES (te) READONLY isTable, key, description, value
    FILTERS template (te) == t
;

FORM dialogTemplates 'Шаблоны Word'

    OBJECTS t = Template
    PROPERTIES (t) READONLY name, id
    PROPERTIES (t) NEWSESSION NEW, EDIT, DELETE GRID

    LIST Template OBJECT t
;
DESIGN dialogTemplates {
    BOX {
        preferredSize = (1024, 768);
    }
}


NAVIGATOR {
    masterData {
        templatesNavigator  {
            ADD templates;
        }
    }
}