MODULE Sample;

REQUIRE System;

GROUP base : base;

CLASS article 'Товар' : Named;
CLASS store 'Склад' : Named;
CLASS document 'Документ' : transaction;
CLASS incomeDocument 'Приход' : document;
CLASS outcomeDocument 'Расход' : document;

CLASS storeSize 'Размер склада'
{
    huge 'огромный',
    big 'большой',
    medium 'средний',
    small 'маленький'
} : Named;

store = DATA Store (Document) IN base;
quantity = DATA DOUBLE (Document, Article) IN base;
storeSize = DATA StoreSize (Store) IN base;

storeName(document) = name(store(document)) IN base;
storeSizeName(store) = name(storeSize(store)) IN base;

inQuantity(document, article) = quantity(document, article) IF document IS IncomeDocument;
outQuantity(document, article) = quantity(document, article) IF document IS OutcomeDocument;

incomeQuantity(store, article) = GROUP SUM inQuantity(document, article) BY store(document), article;
outcomeQuantity(store, article) = GROUP SUM outQuantity(document, article) BY store(document), article;

balanceQuantity(store, article) = incomeQuantity(store, article) - outcomeQuantity(store, article);

documentBalance(document, article) = balanceQuantity(store(document), article);

// PRESENTATION LOGIC

FORM test_document 'По документам'
OBJECTS document, article
PROPERTIES name(document), store(document), name(article), storeName(document)
PROPERTIES quantity(document, article)
PROPERTIES inQuantity(document, article), outQuantity(document, article), documentBalance(document, article);

FORM test_storeArticle 'По складам'
OBJECTS store, article
PROPERTIES SELECTION(store), name(store), OBJVALUE(article), name(article)
PROPERTIES incomeQuantity(store, article), outcomeQuantity(store, article), balanceQuantity(store, article), storeSizeName(store);

