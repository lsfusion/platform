MODULE ConsignmentBy;

REQUIRE System,
        LegalEntityBy,
        Stock,
        Employee,
        Utils,
        Ware,
        Transport,
        Numerator; // вот эту зависимость нужно будет убрать

NAMESPACE Consignment;

CLASS wayOfLoading 'Способ ПРР' : named;
TABLE wayOfLoading (wayOfLoading);


GROUP sumConsignmentGroup 'Суммы' : base;

CLASS ABSTRACT consignment 'Накладная';
CLASS ABSTRACT consignmentDetail 'Строка накладной';

dateConsignment 'Дата' (consignment) = ABSTRACT DATE (consignment);

// ---------------------------------- Юридические лица ---------------------- //
supplierConsignment = ABSTRACT legalEntity (consignment);
UNPSupplierConsignment 'УНП отправителя' (consignment) = UNPLegalEntity(supplierConsignment(consignment));
addressSupplierConsignment 'Юридический адрес отправителя' (consignment) =
    addressLegalEntityDate(supplierConsignment(consignment), dateConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 50;
fullNameSupplierConsignment 'Наименование для накладных отправителя' (consignment) = fullNameLegalEntity(supplierConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 50;

customerConsignment = ABSTRACT legalEntity (consignment);
UNPCustomerConsignment 'УНП получателя' (consignment) = UNPLegalEntity(customerConsignment(consignment));
addressCustomerConsignment 'Юридический адрес получателя' (consignment) =
    addressLegalEntityDate(customerConsignment(consignment), dateConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 50;
fullNameCustomerConsignment 'Наименование для накладных получателя' (consignment) = fullNameLegalEntity(customerConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 50;

payerConsignment = ABSTRACT legalEntity (consignment);
UNPPayerConsignment 'УНП заказчика перевозки' (consignment) = UNPLegalEntity(payerConsignment(consignment));
addressPayerConsignment 'Юридический адрес заказчика перевозки' (consignment) =
    addressLegalEntityDate(payerConsignment(consignment), dateConsignment(consignment));
fullNamePayerConsignment 'Наименование для накладных заказчика перевозки' (consignment) =
    fullNameLegalEntity(payerConsignment(consignment)) ;

// ------------------------------------- Атрибуты --------------------------------- //

GROUP carConsignmentGroup 'Автомобиль' : base;

truckConsignment = ABSTRACT truck(consignment);
dataNameTruckConsignment = ABSTRACT STRING[70] (consignment);
nameTruckConsignment 'Автомобиль' (consignment) = UNION OVERRIDE nameTruck(truckConsignment(consignment)),
                                                                 dataNameTruckConsignment(consignment) IN carConsignmentGroup;

dataOwnerConsignment = ABSTRACT STRING[100] (consignment);
ownerTruckConsignment 'Владелец автомобиля' (consignment) = UNION OVERRIDE ownerTruck(truckConsignment(consignment)),
                                                                           dataOwnerConsignment(consignment) IN carConsignmentGroup;

dataTrailerConsignment = ABSTRACT STRING[100] (consignment);
trailerConsignment 'Прицеп' (consignment) = UNION OVERRIDE trailerTruck(truckConsignment(consignment)),
                                                           dataTrailerConsignment(consignment) IN carConsignmentGroup;

driverConsignment = ABSTRACT driver(consignment);
dataNameDriverConsignment 'Водитель' = ABSTRACT STRING[40] (consignment);
nameDriverConsignment 'Водитель' (consignment) = UNION OVERRIDE commonName(driverConsignment(consignment)),
                                                                dataNameDriverConsignment(consignment) IN carConsignmentGroup;

dataWaybillConsignment 'Путевой лист' (consignment) = ABSTRACT STRING[20] (consignment) IN carConsignmentGroup;

waybillConsignment 'Путевой лист' (consignment) = ABSTRACT STRING[200] (consignment) IN carConsignmentGroup;

overWaybillConsignment 'Путевой лист' (consignment) = UNION OVERRIDE waybillConsignment(consignment), dataWaybillConsignment(consignment);


// Склад погрузки
supplierStockConsignment (consignment) = ABSTRACT stock (consignment);
nameSupplierStockConsignment 'Склад погрузки' (consignment) = name(supplierStockConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 30;

dataAddressSupplierStockConsignment 'Пункт погрузки' (consignment) = ABSTRACT STRING[50] (consignment);
addressSupplierStockConsignment 'Пункт погрузки' (consignment) = UNION OVERRIDE addressStock(supplierStockConsignment(consignment)),
                                                                                dataAddressSupplierStockConsignment (consignment) IN carConsignmentGroup;

// Склад разгрузки
customerStockConsignment (consignment) = ABSTRACT stock (consignment);
nameCustomerStockConsignment 'Склад разгрузки' (consignment) = name(customerStockConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 30;

dataAddressCustomerStockConsignment 'Пункт разгрузки' (consignment) = ABSTRACT STRING[50] (consignment) IN carConsignmentGroup;
addressCustomerStockConsignment 'Пункт разгрузки' (consignment) = UNION OVERRIDE addressStock(customerStockConsignment(consignment)),
                                                                                 dataAddressCustomerStockConsignment (consignment) IN carConsignmentGroup;

readdressingConsignment 'Переадресовка' (consignment) = ABSTRACT STRING[50] (consignment) IN carConsignmentGroup;

// ------------------------------------- Отпуск товара --------------------------------- //

GROUP issuanceConsignmentGroup 'Отпуск' : base;

shipmentBaseConsignment 'Основание отпуска' (consignment) = ABSTRACT STRING[30] (consignment) IN issuanceConsignmentGroup;

issuanceAllowedConsignment (consignment) = ABSTRACT employee (consignment);
commonNameIssuanceAllowedConsignment 'Отпуск разрешил' (consignment) = commonName(issuanceAllowedConsignment(consignment)) IN issuanceConsignmentGroup;

issuanceExecutedConsignment (consignment) = ABSTRACT employee(consignment);
commonNameIssuanceExecutedConsignment 'Отпуск произвел' (consignment) = commonName(issuanceExecutedConsignment(consignment)) IN issuanceConsignmentGroup;

forwarderConsignment 'Товар к перевозке принял (экспедитор), должность, фамилия, инициалы' (consignment) = ABSTRACT STRING[40] (consignment) IN issuanceConsignmentGroup;

warrantConsignment 'По доверенности (номер, дата)' (consignment) = ABSTRACT STRING[30] (consignment) IN issuanceConsignmentGroup;
warrantHolderConsignment 'По доверенности выданной (наименование орг-ии)' (consignment) = ABSTRACT STRING[100] (consignment) IN issuanceConsignmentGroup;

goodsAcceptedConsignment 'Принял грузополучатель' (consignment) = ABSTRACT STRING[40] (consignment) IN issuanceConsignmentGroup;

// ------------------------------------- Погрузочно-разгрузочные работы --------------------------------- //

GROUP loadingConsignmentGroup 'ПРР' : base;

loadingExecuterConsignment (consignment) = ABSTRACT employee(consignment);
commonNameLoadingExecuterConsignment 'Исполнитель ПРР' (consignment) = commonName(loadingExecuterConsignment(consignment)) IN loadingConsignmentGroup;

wayOfLoadingConsignment (consignment) = ABSTRACT wayOfLoading(consignment);
nameWayOfLoadingConsignment 'Способ ПРР' (consignment) = name(wayOfLoadingConsignment(consignment)) IN loadingConsignmentGroup;

codeLoadingConsignment 'Код ПРР' (consignment) = ABSTRACT STRING[3] (consignment)IN loadingConsignmentGroup;

arrivalTimeConsignment 'Время прибытия' (consignment) = ABSTRACT DATETIME(consignment) IN carConsignmentGroup;
departureTimeConsignment 'Время убытия' (consignment) = ABSTRACT DATETIME(consignment) IN carConsignmentGroup;
downtimeConsignment 'Время простоя' (consignment) = ABSTRACT STRING[10] (consignment) IN carConsignmentGroup;

raceQuantityConsignment 'Количество ездок' (consignment) = ABSTRACT INTEGER (consignment) IN carConsignmentGroup;

noteConsignment 'Примечание' (consignment) = ABSTRACT STRING[100] (consignment) MINCHARWIDTH 30 PREFCHARWIDTH 80;

namePayerConsignment 'Заказчик перевозки ' (consignment) =
    [FORMULA STRING[100] 'CAST($1 AS TEXT) ||  \' , \' || CAST($2 AS TEXT)']
    (name(payerConsignment(consignment)), UNPLegalEntity(payerConsignment(consignment))) IN carConsignmentGroup;

skuConsignmentDetail (consignmentDetail) = ABSTRACT sku (consignmentDetail);
nameSkuConsignmentDetail 'Наименование товара' (consignmentDetail) = nameSku(skuConsignmentDetail(consignmentDetail));

shortNameUOMConsignmentDetail 'Единица измерения' (consignmentDetail) = shortNameUOMSku(skuConsignmentDetail(consignmentDetail));
//shortNameUOMConsignmentDetail 'Единица измерения' (consignmentDetail) = ABSTRACT STRING[15] (consignmentDetail);

consignmentConsignmentDetail (consignmentDetail) = ABSTRACT consignment (consignmentDetail);

quantityConsignmentDetail 'Количество' (consignmentDetail) = ABSTRACT NUMERIC[14,3] (consignmentDetail);

priceConsignmentDetail 'Цена' (consignmentDetail) = ABSTRACT NUMERIC[14,2] (consignmentDetail);

sumConsignmentDetail 'Стоимость' (consignmentDetail) = ABSTRACT NUMERIC[16,2] (consignmentDetail);

vatConsignmentDetail 'НДС, %' (consignmentDetail) = ABSTRACT NUMERIC[10,5] (consignmentDetail);

sumVATConsignmentDetail 'Сумма НДС' (consignmentDetail) = ABSTRACT NUMERIC[16,2] (consignmentDetail);

sumInvoiceConsignmentDetail 'Сумма с НДС' (consignmentDetail) = ABSTRACT NUMERIC[16,2] (consignmentDetail);

quantityPackConsignmentDetail 'Количество грузовых мест' (consignmentDetail) = ABSTRACT NUMERIC[14,3] (consignmentDetail);

grossWeightConsignmentDetail 'Масса груза, кг.' (consignmentDetail) = ABSTRACT NUMERIC[14,3] (consignmentDetail);
grossWeightTonConsignmentDetail 'Масса груза, т.' (consignmentDetail) = round6(grossWeightConsignmentDetail(consignmentDetail) / 1000);

noteConsignmentDetail 'Примечание' (consignmentDetail) = ABSTRACT STRING[100] (consignmentDetail) MINCHARWIDTH 30 PREFCHARWIDTH 80;

countConsignmentDetailConsignment 'Количество строк' (consignment) = GROUP SUM 1 IF consignmentDetail IS consignmentDetail
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

quantityConsignmentDetailConsignment 'Количество (всего)' (consignment) = GROUP SUM quantityConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

quantityPackConsignmentDetailConsignment 'Общее количество грузовых мест' (consignment) = GROUP SUM quantityPackConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

grossWeightTonConsignmentDetailConsignment 'Общая масса груза, т.' (consignment) = GROUP SUM grossWeightTonConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

grossWeightConsignmentDetailConsignment 'Общая масса груза, кг.'(consignment) = GROUP SUM grossWeightConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;


isCustomsFlowConsignment 'Расход с СВХ' (consignment) = ABSTRACT BOOLEAN (consignment);

currencyConsignment 'Валюта (ИД)' (consignment) = ABSTRACT currency (consignment);
shortNameCurrencyConsignment 'Валюта' (consignment) = shortNameCurrency(currencyConsignment(consignment));
documentNameCurrencyConsignment 'Валюта в накладной сокр.' (consignment) = documentNameCurrency(currencyConsignment(consignment));
isCurrencyConsignment 'Выбрана валюта' (consignment) = TRUE IF currencyConsignment(consignment);

// Посуда на возврате
isWareConsignment 'Возврат поставщику' (consignment) = ABSTRACT BOOLEAN (consignment);
wareSumConsignmentDetailConsignment 'Сумма посуды с НДС' (consignment) = ABSTRACT NUMERIC[14,2] (consignment);
supplierSumConsignmentDetailConsignment 'Сумма поставщика без НДС' (consignment) = ABSTRACT NUMERIC[14,2] (consignment);

wareConsignmentDetail (consignmentDetail) = ABSTRACT ware (consignmentDetail);
nameWareConsignmentDetail 'Посуда' (consignmentDetail) = name(wareConsignmentDetail(consignmentDetail));
warePriceConsignmentDetail 'Цена посуды с НДС' (consignmentDetail) = ABSTRACT NUMERIC[14,2] (consignmentDetail);
wareSumConsignmentDetail 'Сумма посуды с НДС' (consignmentDetail) = ABSTRACT NUMERIC[14,2] (consignmentDetail);
valueWareRangeConsignmentDetail 'НДС посуды, %' (consignmentDetail) = ABSTRACT NUMERIC[6,2] (consignmentDetail);
wareVATSumConsignmentDetail 'Сумма НДС по посуде' (consignmentDetail) = ABSTRACT NUMERIC[14,2] (consignmentDetail);
wareSupplierPriceConsignmentDetail 'Цена посуды без НДС' (consignmentDetail) =  ABSTRACT NUMERIC[14,2] (consignmentDetail);
wareSupplierSumConsignmentDetail 'Сумма посуды без НДС' (consignmentDetail) =  ABSTRACT NUMERIC[14,2] (consignmentDetail);

sumIaWConsignmentDetail 'Сумма с посудой без НДС' (consignmentDetail) = sumConsignmentDetail(consignmentDetail) (+) wareSupplierSumConsignmentDetail(consignmentDetail);
sumConsignmentDetailConsignment 'Сумма с посудой без НДС (всего)' (consignment) = GROUP SUM sumIaWConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

sumVATIaWConsignmentDetail 'Сумма НДС' (consignmentDetail) = sumVATConsignmentDetail(consignmentDetail) (+) wareVATSumConsignmentDetail(consignmentDetail);
sumVATConsignmentDetailConsignment 'Сумма НДС (всего)' (consignment) = GROUP SUM sumVATIaWConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

sumInvoiceIaWConsignmentDetail 'Сумма с НДС' (consignmentDetail) = sumInvoiceConsignmentDetail(consignmentDetail) (+) wareSumConsignmentDetail(consignmentDetail);
sumInvoiceConsignmentDetailConsignment 'Сумма c НДС (всего)' (consignment) = GROUP SUM sumInvoiceIaWConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

// ---------------------------- Формы для накладных --------------------------------- //

META defineConsignmentFormSimple(form, caption)

    FORM form caption PRINT
        OBJECTS c=consignment FIXED PANEL

        PROPERTIES (c) dateConsignment, shipmentBaseConsignment, commonNameIssuanceAllowedConsignment, commonNameIssuanceExecutedConsignment,
                       forwarderConsignment, warrantConsignment, warrantHolderConsignment, goodsAcceptedConsignment,
                       quantityConsignmentDetailConsignment, sumConsignmentDetailConsignment, sumVATConsignmentDetailConsignment, sumInvoiceConsignmentDetailConsignment, UNPCustomerConsignment,
                       UNPSupplierConsignment, addressSupplierConsignment, addressCustomerConsignment, fullNameSupplierConsignment,
                       fullNameCustomerConsignment, numberObject, seriesObject, countConsignmentDetailConsignment,
                       isWareConsignment, isCustomsFlowConsignment, isCurrencyConsignment, noteConsignment, documentNameCurrencyConsignment

        OBJECTS d=consignmentDetail

        PROPERTIES(d) nameSkuConsignmentDetail, shortNameUOMConsignmentDetail, quantityConsignmentDetail, priceConsignmentDetail,
                      sumConsignmentDetail, vatConsignmentDetail, sumVATConsignmentDetail, sumInvoiceConsignmentDetail,
                      noteConsignmentDetail, nameWareConsignmentDetail, warePriceConsignmentDetail, wareSumConsignmentDetail,
                      valueWareRangeConsignmentDetail, wareVATSumConsignmentDetail, wareSupplierPriceConsignmentDetail,
                      wareSupplierSumConsignmentDetail

        FILTERS consignmentConsignmentDetail(d) == c
    ;
    print###form caption (consignment) = ACTION FORM form OBJECTS c IMAGE 'print.png' IN printGroup;
END

@defineConsignmentFormSimple(consignmentSimpleVertical, 'ТН-2, вертикальная');
@defineConsignmentFormSimple(consignmentSimpleHorizontal, 'ТН-2, горизонтальная');
@defineConsignmentFormSimple(consignmentSimpleAttach, 'Приложение к ТН-2');

META defineConsignmentForm(form, caption)
    FORM form caption PRINT
        OBJECTS c=consignment FIXED PANEL

        PROPERTIES (c) dateConsignment, UNPCustomerConsignment, UNPSupplierConsignment, addressSupplierConsignment, addressCustomerConsignment, fullNameSupplierConsignment,
                       fullNameCustomerConsignment, nameTruckConsignment, ownerTruckConsignment, trailerConsignment, nameDriverConsignment, overWaybillConsignment,
                       namePayerConsignment, shipmentBaseConsignment, addressSupplierStockConsignment, addressCustomerStockConsignment,
                       readdressingConsignment, commonNameIssuanceAllowedConsignment, commonNameIssuanceExecutedConsignment,
                       forwarderConsignment, warrantConsignment, warrantHolderConsignment, goodsAcceptedConsignment,
                       commonNameLoadingExecuterConsignment, nameWayOfLoadingConsignment, codeLoadingConsignment,
                       arrivalTimeConsignment, departureTimeConsignment, downtimeConsignment, raceQuantityConsignment,
                       quantityConsignmentDetailConsignment, quantityPackConsignmentDetailConsignment, grossWeightTonConsignmentDetailConsignment, grossWeightConsignmentDetailConsignment,
                       sumConsignmentDetailConsignment, sumVATConsignmentDetailConsignment, sumInvoiceConsignmentDetailConsignment,
                       countConsignmentDetailConsignment, wareSumConsignmentDetailConsignment, supplierSumConsignmentDetailConsignment,
                       seriesObject, numberObject, isWareConsignment, isCustomsFlowConsignment, isCurrencyConsignment, noteConsignment, documentNameCurrencyConsignment

        OBJECTS d=consignmentDetail

        PROPERTIES(d) nameSkuConsignmentDetail, shortNameUOMConsignmentDetail, quantityConsignmentDetail, priceConsignmentDetail,
                      sumConsignmentDetail, vatConsignmentDetail, sumVATConsignmentDetail, sumInvoiceConsignmentDetail,
                      quantityPackConsignmentDetail, grossWeightTonConsignmentDetail, noteConsignmentDetail,
                      nameWareConsignmentDetail, warePriceConsignmentDetail, valueWareRangeConsignmentDetail,
                      wareSupplierPriceConsignmentDetail, wareSumConsignmentDetail, wareSupplierSumConsignmentDetail,
                      wareVATSumConsignmentDetail, wareSumConsignmentDetail

        FILTERS consignmentConsignmentDetail(d) == c
    ;
    print###form caption (consignment) = ACTION FORM form OBJECTS c IMAGE 'print.png' IN printGroup;
END

@defineConsignmentForm(consignmentVerticalA, 'ТТН-1, вертикальная (сторона А)');
@defineConsignmentForm(consignmentHorizontalA, 'ТТН-1, горизонтальная (сторона А)');
@defineConsignmentForm(consignmentVerticalB, 'ТТН-1, вертикальная (сторона Б)');
@defineConsignmentForm(consignmentHorizontalB, 'ТТН-1, горизонтальная (сторона Б)');
@defineConsignmentForm(consignmentAttach, 'Приложение к ТТН-1');

FORM consignment 'Атрибуты накладной'
    OBJECTS c=consignment FIXED PANEL

    PROPERTIES (c) READONLY objectClassName, nameSupplierStockConsignment, seriesObject, numberObject
    PROPERTIES (c) dateConsignment, UNPCustomerConsignment, UNPSupplierConsignment, addressSupplierConsignment, addressCustomerConsignment, fullNameSupplierConsignment,
                   fullNameCustomerConsignment, nameTruckConsignment, ownerTruckConsignment, trailerConsignment, nameDriverConsignment, overWaybillConsignment,
                   namePayerConsignment, shipmentBaseConsignment, addressSupplierStockConsignment, addressCustomerStockConsignment,
                   readdressingConsignment, commonNameIssuanceAllowedConsignment, commonNameIssuanceExecutedConsignment,
                   forwarderConsignment, warrantConsignment, warrantHolderConsignment, goodsAcceptedConsignment,
                   commonNameLoadingExecuterConsignment, nameWayOfLoadingConsignment, codeLoadingConsignment,
                   arrivalTimeConsignment, departureTimeConsignment, downtimeConsignment, raceQuantityConsignment,
                   quantityConsignmentDetailConsignment, quantityPackConsignmentDetailConsignment, grossWeightTonConsignmentDetailConsignment,
                   sumConsignmentDetailConsignment, sumVATConsignmentDetailConsignment, sumInvoiceConsignmentDetailConsignment,
                   countConsignmentDetailConsignment, noteConsignment, shortNameCurrencyConsignment//, SHOWIF isCustomsFlowConsignment(c)


    OBJECTS d=consignmentDetail

    PROPERTIES(d) nameSkuConsignmentDetail READONLY, shortNameUOMConsignmentDetail READONLY, quantityConsignmentDetail, priceConsignmentDetail,
                  sumConsignmentDetail, vatConsignmentDetail, sumVATConsignmentDetail, sumInvoiceConsignmentDetail,
                  quantityPackConsignmentDetail, grossWeightTonConsignmentDetail, noteConsignmentDetail,
                  nameWareConsignmentDetail, warePriceConsignmentDetail, valueWareRangeConsignmentDetail,
                  wareSupplierPriceConsignmentDetail, wareSumConsignmentDetail, wareSupplierSumConsignmentDetail,
                  wareVATSumConsignmentDetail, wareSumConsignmentDetail

    FILTERS consignmentConsignmentDetail(d) == c
;

DESIGN consignment FROM DEFAULT {
    main {
        preferredSize = (1024, 768);

        NEW specification.box BEFORE functions.box{

            NEW consignment{
                title = 'Атрибуты накладной';
                childConstraints = TO THE RIGHT;

                NEW wor1 {
                    childConstraints = TO THE BOTTOM;
                    ADD c.carConsignmentGroup;
                    NEW wor11 {
                        title = 'Дополнительно';
                        childConstraints = TO THE RIGHT;
                        ADD PROPERTY (noteConsignment(c));
                        ADD PROPERTY (shortNameCurrencyConsignment(c));
                    }
                }
                NEW wor2 {
                    childConstraints = TO THE BOTTOM;
                    ADD c.issuanceConsignmentGroup;
                    ADD c.loadingConsignmentGroup;
                    ADD c.sumConsignmentGroup {
                        childConstraints = TO THE BOTTOM;
                        NEW wor21 {
                            childConstraints = TO THE RIGHT;
                            ADD PROPERTY(countConsignmentDetailConsignment);
                            ADD PROPERTY(quantityConsignmentDetailConsignment);
                            ADD PROPERTY(quantityPackConsignmentDetailConsignment);
                            ADD PROPERTY(grossWeightTonConsignmentDetailConsignment);
                        }
                        NEW wor22 {
                            childConstraints = TO THE RIGHT;
                            ADD PROPERTY(sumConsignmentDetailConsignment);
                            ADD PROPERTY(sumVATConsignmentDetailConsignment);
                            ADD PROPERTY(sumInvoiceConsignmentDetailConsignment);
                        }
                    }
                }

            }
            ADD d.box {
                title = 'Спецификация';
            }

        }

        NEW header.box BEFORE specification.box {
            title = 'Накладная';
            childConstraints = TO THE RIGHT;

            NEW headerCol1 {
                childConstraints = TO THE BOTTOM;
                NEW container5 {
                    childConstraints = TO THE RIGHT;
                    NEW container55 {
                        title = 'Объект';
                        ADD PROPERTY (objectClassName) {
                            preferredCharWidth = 15;
                        }
                    }
                    NEW headerCol11 {
                        title = 'Шапка документа';
                        childConstraints = TO THE RIGHT;
                        ADD PROPERTY(nameSupplierStockConsignment);
                        ADD PROPERTY(numberObject);
                        ADD PROPERTY(seriesObject);
                        ADD PROPERTY(dateConsignment);
                    }
                }

                NEW headerCol22 {
                    childConstraints = TO THE BOTTOM;
                    NEW row11 {
                        title = 'Грузоотправитель';
                        childConstraints = TO THE RIGHT;
                        ADD PROPERTY(fullNameSupplierConsignment);
                        ADD PROPERTY(UNPSupplierConsignment);
                        ADD PROPERTY(addressSupplierConsignment);
                    }
                    NEW row22 {
                        title = 'Грузополучатель';
                        childConstraints = TO THE RIGHT;
                        ADD PROPERTY(fullNameCustomerConsignment);
                        ADD PROPERTY(UNPCustomerConsignment);
                        ADD PROPERTY(addressCustomerConsignment);
                    }
                }
            }
        }
    }
}

editConsignment 'Заполнить атрибуты накладной' (consignment) = ACTION FORM consignment OBJECTS c DOCKED_MODAL IMAGE 'edit.png';


FORM printConsignment 'Печать накладных'
    OBJECTS c=consignment FIXED PANEL

    PROPERTIES (c)  printConsignmentSimpleVertical, printConsignmentSimpleHorizontal, printConsignmentSimpleAttach,
                    printConsignmentVerticalA, printConsignmentHorizontalA, printConsignmentVerticalB,
                    printConsignmentHorizontalB, printConsignmentAttach, editConsignment
;

DESIGN printConsignment FROM DEFAULT {

    c.box {

        childConstraints = TO THE BOTTOM;

        NEW case55{
            childConstraints = TO THE BOTTOM;

            NEW contOne {
                title = 'Накладная';
                ADD PROPERTY(editConsignment);
            }
            NEW tn{
                childConstraints = TO THE RIGHT;
                title = 'ТН-2';
                ADD PROPERTY(printConsignmentSimpleVertical);
                ADD PROPERTY(printConsignmentSimpleHorizontal);
                ADD PROPERTY(printConsignmentSimpleAttach);
            }
        }
        NEW ttn1{
            childConstraints = TO THE RIGHT;
            title = 'ТТН-1';
            NEW ttn1V {
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(printConsignmentVerticalA);
                ADD PROPERTY(printConsignmentVerticalB);
            }
            NEW ttn1H {
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(printConsignmentHorizontalA);
                ADD PROPERTY(printConsignmentHorizontalB);
            }
            NEW ttn1A {
                ADD PROPERTY(printConsignmentAttach);
            }
        }

    }
}

printConsignment 'Печать' (consignment) = ACTION FORM printConsignment OBJECTS c = consignment MODAL IMAGE 'print.png';


// ------------------------------------- Метакод по объявлению и имплементации накладных ------------------ //

// ----------------- Объявление заголовка накладной --------------- //

META defineConsignmentHeader (object)

    payer###object (object) = DATA legalEntity (object) IN carConsignmentGroup;

    dataNameTruck###object 'Автомобиль' (object) = DATA STRING[30] (object) IN carConsignmentGroup;
    dataOwner###object 'Владелец автомобиля' (object) = DATA STRING[100] (object) IN carConsignmentGroup;
    dataTrailer###object 'Прицеп' (object) = DATA STRING[10] (object) IN carConsignmentGroup;
    dataNameDriver###object 'Водитель' (object) = DATA STRING[40] (object) IN carConsignmentGroup;
    waybill###object 'Путевой лист' (object) = DATA STRING[20] (object) IN carConsignmentGroup;

    dataAddressSupplierStock###object 'Пункт погрузки' (object) = DATA STRING[50] (object);
    dataAddressCustomerStock###object 'Пункт разгрузки' (object) = DATA STRING[50] (object);

    readdressing###object 'Переадресовка' (object) = DATA STRING[50] (object) IN carConsignmentGroup;

    shipmentBase###object 'Основание отпуска' (object) = DATA STRING[30] (object) IN issuanceConsignmentGroup;

    issuanceAllowed###object (object) = DATA employee(object);

    issuanceExecuted###object (object) = DATA employee(object);

    forwarder###object 'Товар к перевозке принял (экспедитор), должность, фамилия, инициалы' (object) = DATA STRING[40] (object) IN issuanceConsignmentGroup;

    warrant###object 'По доверенности (номер, дата)' (object) = DATA STRING[30] (object) IN issuanceConsignmentGroup;
    warrantHolder###object 'По доверенности выданной (наименование орг-ии)' (object) = DATA STRING[100] (object) IN issuanceConsignmentGroup;

    goodsAccepted###object 'Принял грузополучатель' (object) = DATA STRING[40] (object) IN issuanceConsignmentGroup;

    loadingExecuter###object (object) = DATA employee(object);

    wayOfLoading###object (object) = DATA wayOfLoading(object);

    codeLoading###object 'Код ПРР' (object) = DATA STRING[3] (object) IN loadingConsignmentGroup;

    arrivalTime###object 'Время прибытия' (object) = DATA DATETIME(object) IN carConsignmentGroup;
    departureTime###object 'Время убытия' (object) = DATA DATETIME(object) IN carConsignmentGroup;
    downtime###object 'Время простоя' (object) = DATA STRING[10] (object) IN carConsignmentGroup;

    raceQuantity###object 'Количество ездок' (object) = DATA INTEGER (object) IN carConsignmentGroup;
END

META defineConsignmentAbstractHeader(object)

    payer###object 'Заказчик перевозки (ИД)' (object) = ABSTRACT legalEntity (object) IN carConsignmentGroup;

    dataNameTruck###object 'Автомобиль' (object) = ABSTRACT STRING[30] (object) IN carConsignmentGroup;
    dataOwner###object 'Владелец автомобиля' (object) = ABSTRACT STRING[100] (object) IN carConsignmentGroup;
    dataTrailer###object 'Прицеп' (object) = ABSTRACT STRING[10] (object) IN carConsignmentGroup;
    dataNameDriver###object 'Водитель' (object) = ABSTRACT STRING[40] (object) IN carConsignmentGroup;
    waybill###object 'Путевой лист' (object) = ABSTRACT STRING[20] (object) IN carConsignmentGroup;

    dataAddressSupplierStock###object 'Пункт погрузки' (object) = ABSTRACT STRING[50] (object);
    dataAddressCustomerStock###object 'Пункт разгрузки' (object) = ABSTRACT STRING[50] (object);

    readdressing###object 'Переадресовка' (object) = ABSTRACT STRING[50] (object) IN carConsignmentGroup;

    shipmentBase###object 'Основание отпуска' (object) = ABSTRACT STRING[30] (object) IN issuanceConsignmentGroup;

    issuanceAllowed###object (object) = ABSTRACT employee(object);

    issuanceExecuted###object (object) = ABSTRACT employee(object);

    forwarder###object 'Товар к перевозке принял (экспедитор), должность, фамилия, инициалы' (object) = ABSTRACT STRING[40] (object) IN issuanceConsignmentGroup;

    warrant###object 'По доверенности (номер, дата)' (object) = ABSTRACT STRING[30] (object) IN issuanceConsignmentGroup;
    warrantHolder###object 'По доверенности выданной (наименование орг-ии)' (object) = ABSTRACT STRING[100] (object) IN issuanceConsignmentGroup;

    goodsAccepted###object 'Принял грузополучатель' (object) = ABSTRACT STRING[40] (object) IN issuanceConsignmentGroup;

    loadingExecuter###object (object) = ABSTRACT employee(object);

    wayOfLoading###object (object) = ABSTRACT wayOfLoading(object);

    codeLoading###object 'Код ПРР' (object) = ABSTRACT STRING[3] (object) IN loadingConsignmentGroup;

    arrivalTime###object 'Время прибытия' (object) = ABSTRACT DATETIME(object) IN carConsignmentGroup;
    departureTime###object 'Время убытия' (object) = ABSTRACT DATETIME(object) IN carConsignmentGroup;
    downtime###object 'Время простоя' (object) = ABSTRACT STRING[10] (object) IN carConsignmentGroup;

    raceQuantity###object 'Количество ездок' (object) = ABSTRACT INTEGER (object) IN carConsignmentGroup;
END

META defineConsignmentInterfaceHeader(object, stockProp)
    @defineConsignmentAbstractHeader (object);
    @defineConsignmentHeader (user###object);
END

META defineConsignmentInterfaceHeader(object)
    @defineConsignmentInterfaceHeader(object, stock);
END

// ----------------- Implement заголовка накладной --------------- //

META implementConsignmentHeader (concrete)
    EXTEND CLASS concrete : consignment;

    dateConsignment (consignment) += date###concrete(consignment);

    payerConsignment (consignment) += payer###concrete (consignment);

    dataNameTruckConsignment (consignment) += dataNameTruck###concrete (consignment);
    dataOwnerConsignment (consignment) += dataOwner###concrete (consignment);
    dataTrailerConsignment (consignment) += dataTrailer###concrete (consignment);
    dataNameDriverConsignment (consignment) += dataNameDriver###concrete (consignment);
    dataWaybillConsignment (consignment) += waybill###concrete (consignment);

    dataAddressSupplierStockConsignment (consignment) += dataAddressSupplierStock###concrete (consignment);
    dataAddressCustomerStockConsignment (consignment) += dataAddressCustomerStock###concrete (consignment);

    readdressingConsignment (consignment) += readdressing###concrete (consignment);

    shipmentBaseConsignment (consignment) += shipmentBase###concrete (consignment);

    issuanceAllowedConsignment (consignment) += issuanceAllowed###concrete (consignment);

    issuanceExecutedConsignment (consignment) += issuanceExecuted###concrete (consignment);

    forwarderConsignment (consignment) += forwarder###concrete (consignment);

    warrantConsignment (consignment) += warrant###concrete (consignment);
    warrantHolderConsignment (consignment) += warrantHolder###concrete (consignment);

    goodsAcceptedConsignment (consignment) += goodsAccepted###concrete (consignment);

    loadingExecuterConsignment (consignment) += loadingExecuter###concrete (consignment);

    wayOfLoadingConsignment (consignment) += wayOfLoading###concrete (consignment);
    codeLoadingConsignment (consignment) += codeLoading###concrete (consignment);

    arrivalTimeConsignment (consignment) += arrivalTime###concrete (consignment);
    departureTimeConsignment (consignment) += departureTime###concrete (consignment);
    downtimeConsignment (consignment) += downtime###concrete (consignment);

    raceQuantityConsignment (consignment) += raceQuantity###concrete (consignment);
END

META implementConsignmentDocumentHeader (concrete, object, prefix)

    prefix###payer###concrete (concrete) += payer###object (concrete);

    prefix###dataNameTruck###concrete (concrete) += dataNameTruck###object (concrete);
    prefix###dataOwner###concrete (concrete) += dataOwner###object (concrete);
    prefix###dataTrailer###concrete (concrete) += dataTrailer###object (concrete);
    prefix###dataNameDriver###concrete (concrete) += dataNameDriver###object (concrete);
    prefix###waybill###concrete (concrete) += waybill###object (concrete);

    prefix###dataAddressSupplierStock###concrete (concrete) += dataAddressSupplierStock###object (concrete);
    prefix###dataAddressCustomerStock###concrete (concrete) += dataAddressCustomerStock###object (concrete);

    prefix###readdressing###concrete (concrete) += readdressing###object (concrete);

    prefix###shipmentBase###concrete (concrete) += shipmentBase###object (concrete);

    prefix###issuanceAllowed###concrete (concrete) += issuanceAllowed###object (concrete);

    prefix###issuanceExecuted###concrete (concrete) += issuanceExecuted###object (concrete);

    prefix###forwarder###concrete (concrete) += forwarder###object (concrete);

    prefix###warrant###concrete (concrete) += warrant###object (concrete);
    prefix###warrantHolder###concrete (concrete) += warrantHolder###object (concrete);

    prefix###goodsAccepted###concrete (concrete) += goodsAccepted###object (concrete);

    prefix###loadingExecuter###concrete (concrete) += loadingExecuter###object (concrete);

    prefix###wayOfLoading###concrete (concrete) += wayOfLoading###object (concrete);
    prefix###codeLoading###concrete (concrete) += codeLoading###object (concrete);

    prefix###arrivalTime###concrete (concrete) += arrivalTime###object (concrete);
    prefix###departureTime###concrete (concrete) += departureTime###object (concrete);
    prefix###downtime###concrete (concrete) += downtime###object (concrete);

    prefix###raceQuantity###concrete (concrete) += raceQuantity###object (concrete);

END

META implementConsignmentInterfaceHeader(object)
    @implementConsignmentDocumentHeader(object, user###object, );
    @implementConsignmentHeader (object);
END
//--

META implementConsignmentDocumentHeaderPrefix (concrete, object, NS)

    NS.payer###concrete (concrete) += payer###object (concrete);

    NS.dataNameTruck###concrete (concrete) += dataNameTruck###object (concrete);
    NS.dataOwner###concrete (concrete) += dataOwner###object (concrete);
    NS.dataTrailer###concrete (concrete) += dataTrailer###object (concrete);
    NS.dataNameDriver###concrete (concrete) += dataNameDriver###object (concrete);
    NS.waybill###concrete (concrete) += waybill###object (concrete);

    NS.dataAddressSupplierStock###concrete (concrete) += dataAddressSupplierStock###object (concrete);
    NS.dataAddressCustomerStock###concrete (concrete) += dataAddressCustomerStock###object (concrete);

    NS.readdressing###concrete (concrete) += readdressing###object (concrete);

    NS.shipmentBase###concrete (concrete) += shipmentBase###object (concrete);

    NS.issuanceAllowed###concrete (concrete) += issuanceAllowed###object (concrete);

    NS.issuanceExecuted###concrete (concrete) += issuanceExecuted###object (concrete);

    NS.forwarder###concrete (concrete) += forwarder###object (concrete);

    NS.warrant###concrete (concrete) += warrant###object (concrete);
    NS.warrantHolder###concrete (concrete) += warrantHolder###object (concrete);

    NS.goodsAccepted###concrete (concrete) += goodsAccepted###object (concrete);

    NS.loadingExecuter###concrete (concrete) += loadingExecuter###object (concrete);

    NS.wayOfLoading###concrete (concrete) += wayOfLoading###object (concrete);
    NS.codeLoading###concrete (concrete) += codeLoading###object (concrete);

    NS.arrivalTime###concrete (concrete) += arrivalTime###object (concrete);
    NS.departureTime###concrete (concrete) += departureTime###object (concrete);
    NS.downtime###concrete (concrete) += downtime###object (concrete);

    NS.raceQuantity###concrete (concrete) += raceQuantity###object (concrete);

END

// -------------------------- Implement строк накладной -------------------- //

META implementConsignmentDetail (concrete, skuProp)
    EXTEND CLASS concrete##Detail : consignmentDetail;

    consignmentConsignmentDetail (consignmentDetail) += concrete###concrete##Detail (consignmentDetail);
    skuConsignmentDetail (consignmentDetail) += skuProp###concrete##Detail (consignmentDetail);
    quantityConsignmentDetail (consignmentDetail) += quantity###concrete##Detail (consignmentDetail);

    quantityPackConsignmentDetail (consignmentDetail) += quantityPack###concrete##Detail (consignmentDetail);
    grossWeightConsignmentDetail (consignmentDetail) += grossWeight###concrete##Detail (consignmentDetail);
END
META implementConsignmentDetail (concrete)
    @implementConsignmentDetail (concrete, sku);
END

// -------------------------- Implement всей накладной -------------------- //

META implementConsignment(concrete, skuProp)
    @implementConsignmentHeader(concrete);
    @implementConsignmentDetail(concrete, skuProp);
END
