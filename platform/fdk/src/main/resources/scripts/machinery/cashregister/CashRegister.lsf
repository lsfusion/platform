MODULE CashRegister;

REQUIRE System, Machinery, Store, LegalEntity;

// Группы
CLASS GroupCashRegister 'Группы касс' : GroupMachinery;

departmentStoreGroupCashRegister = DATA DepartmentStore (GroupCashRegister);
stockGroupMachinery (groupMachinery) += departmentStoreGroupCashRegister(groupMachinery);

TABLE groupCashRegisterCustomUser(GroupCashRegister, CustomUser);
operatorNumberGroupCashRegisterCustomUser 'Номер в группе касс' = DATA INTEGER (GroupCashRegister, CustomUser);

// Модели
CLASS CashRegisterModel 'Модель касс' : Model;

dateToCashRegisterModel 'Дата, до которой модель внесена в реестр' = DATA DATE (CashRegisterModel) IN base;

FORM cashRegisterModel 'Модель касс'
    OBJECTS c=CashRegisterModel FIXED PANEL
    PROPERTIES(c) name, sidModel, noteModel, handlerModel, dateToCashRegisterModel
    EDIT CashRegisterModel OBJECT c
;

FORM cashRegistersModels 'Модели касс'
    OBJECTS c=CashRegisterModel
    PROPERTIES(c) READONLY name, sidModel, handlerModel, noteModel, objectClassName
    PROPERTIES(c) ADDFORM, EDITFORM, delete
    DIALOG CashRegisterModel OBJECT c
;

// Кассы
CLASS CashRegister 'Касса' : Machinery;
isGroupCashRegister (machinery) = machinery IS GroupCashRegister;

groupCashRegisterCashRegister = DATA GroupCashRegister (CashRegister);
groupMachineryMachinery(machinery) += groupCashRegisterCashRegister(machinery);

cashRegisterModelCashRegister = DATA CashRegisterModel (CashRegister);
modelMachinery(machinery) += cashRegisterModelCashRegister(machinery);

departmentStoreCashRegister (cashRegister) = departmentStoreGroupCashRegister(groupCashRegisterCashRegister(cashRegister));

numberCashRegister 'Регистрационный номер кассы' = DATA STRING[100] (CashRegister) IN base;
cashRegisterNumber (cashRegister) = GROUP UNIQUE cashRegister BY numberCashRegister (cashRegister) WHERE cashRegister IS CashRegister;

dateCashRegister 'Дата фискализации кассового аппарата' = DATA DATE (CashRegister) IN base;

directoryCashRegister 'Директория обмена с кассой' = DATA STRING[100] (CashRegister) IN base;

comPortCashRegister 'Порт кассы' = DATA INTEGER (CashRegister) IN base;
baudRateCashRegister 'Скорость кассы' = DATA INTEGER (CashRegister) IN base;
countryCashRegister 'Страна кассы' (cashRegister) = countryStock(stockGroupMachinery(groupCashRegisterCashRegister(cashRegister)));

userLegalEntityCashRegister (cashRegister) = DATA LegalEntity (CashRegister);
legalEntityCashRegister (cashRegister) = UNION OVERRIDE legalEntityDepartmentStore(departmentStoreCashRegister(cashRegister)),
                                                    userLegalEntityCashRegister(cashRegister);

nameLegalEntityCashRegister 'Владелец кассы' (cashRegister) = name(legalEntityCashRegister(cashRegister));

computerCashRegister = DATA Computer (CashRegister);
hostnameComputerCashRegister 'Компьютер' (cashRegister) = hostnameComputer(computerCashRegister(cashRegister)) IN base;

cashRegisterComputer (computer) = GROUP UNIQUE cashRegister BY computerCashRegister(cashRegister) WHERE cashRegister IS CashRegister PERSISTENT;

currentCashRegister 'Текущая касса' = cashRegisterComputer(currentComputer());

departmentStoreCurrentCashRegister 'Подразделение' = departmentStoreCashRegister(currentCashRegister());
numberCurrentCashRegister 'Номер кассы' = numberCashRegister(currentCashRegister());
sidModelCurrentCashRegister 'Код модели' = sidModelMachinery(currentCashRegister());
comPortCurrentCashRegister 'Порт' = comPortCashRegister(currentCashRegister());
baudRateCurrentCashRegister 'Скорость весов' = baudRateCashRegister(currentCashRegister());
nppMachineryCurrentCashRegister 'Номер кассового места' = nppMachinery(currentCashRegister());
operatorNumberCurrentCashRegister (user) = operatorNumberGroupCashRegisterCustomUser(groupCashRegisterCashRegister(currentCashRegister()), user);
countryCurrentCashRegister 'Страна кассы' = countryCashRegister(currentCashRegister());

FORM cashRegister 'Касса'
    OBJECTS c=CashRegister FIXED PANEL
    PROPERTIES(c) nameGroupMachineryMachinery, nppMachinery, descriptionMachinery, portMachinery, numberCashRegister, directoryCashRegister, comPortCashRegister, baudRateCashRegister, nameModelMachinery,
                  dateCashRegister, hostnameComputerCashRegister
    EDIT CashRegister OBJECT c
;

FORM groupCashRegister 'Группа касс'
    OBJECTS grc=GroupCashRegister FIXED PANEL
    PROPERTIES(grc) nameStockGroupMachinery, nameEquipmentServerGroupMachinery, nameGroupMachinery,
                    filterSkuGroupMachinery, showFilterSkuGroupMachinery SHOWIF filterSkuGroupMachinery(grc),
                    namePriceListTypeGroupMachinery

    OBJECTS c=CashRegister
    PROPERTIES(c)   nameGroupMachineryMachinery, nppMachinery, descriptionMachinery, portMachinery, numberCashRegister,
                    directoryCashRegister, comPortCashRegister, baudRateCashRegister, nameModelMachinery,
                    dateCashRegister, hostnameComputerCashRegister, nameLegalEntityCashRegister, ADDOBJ, delete

    OBJECTS cu=CustomUser
    PROPERTIES(cu) loginCustomUser, firstNameContact, lastNameContact, sidMainRoleCustomUser
    PROPERTIES(grc, cu) operatorNumberGroupCashRegisterCustomUser

    FILTERGROUP filters2
        FILTER 'Показывать только для данной группы' 'F10' groupCashRegisterCashRegister(c) == grc DEFAULT

    EDIT GroupCashRegister OBJECT grc
;

FORM groupsCashRegister 'Группы касс'
    OBJECTS grc=GroupCashRegister, c=CashRegister
    PROPERTIES(grc)  READONLY nameStockGroupMachinery, nameLegalEntityCashRegister, nameEquipmentServerGroupMachinery, nameGroupMachinery,
                     filterSkuGroupMachinery
    PROPERTIES(grc)  ADDFORM, EDITFORM, delete

    PROPERTIES(c) READONLY nppMachinery, descriptionMachinery, portMachinery,
                           numberCashRegister, directoryCashRegister, comPortCashRegister, baudRateCashRegister, nameModelMachinery,
                           dateCashRegister, nameLegalEntityCashRegister
    FILTERS groupCashRegisterCashRegister(c) == grc
;
@extendFormFilterAccess(GroupMachinery, grc, groupsCashRegister, stock);

DESIGN groupsCashRegister FROM DEFAULT {
    NEW topContainer {
        type = SPLITV;
        childConstraints = TO THE BOTTOM;

        ADD grc.box;
        ADD c.box;
    }
    ADD functions.box;
}

// ----------------------------------------- Загрузка в ВУ -------------------------------------- //

CLASS CashRegisterPriceTransaction 'Загрузка прайса в кассы' : MachineryPriceTransaction;
groupCashRegisterCashRegisterPriceTransaction 'Группа касс' = DATA GroupCashRegister (CashRegisterPriceTransaction);
groupMachineryMachineryPriceTransaction (transaction) += groupCashRegisterCashRegisterPriceTransaction(transaction);

EXTEND FORM groupMachineryInput
    PROPERTIES(m) READONLY FORCE GRID SHOWIF isGroupCashRegister(g) numberCashRegister, directoryCashRegister, comPortCashRegister, baudRateCashRegister,
                                                                    nameModelMachinery, dateCashRegister
;

createMachineryPriceTransactionGroupMachinery (groupMachinery) += ACTION (groupMachinery) {
    IF groupMachinery IS GroupCashRegister THEN
        ADDOBJ CashRegisterPriceTransaction;
}

// ------------------------------------------------ Стандартные значения ------------------------------------ //

loadDefaultCashRegisterModel 'Добавить модель касс' = ACTION (iname, isid, ihandler)  {
    ADDOBJ CashRegisterModel;
    FOR crm == addedObject() DO {
         SET name(crm) <- iname;
         SET sidModel(crm) <- isid;
         SET handlerModel(crm) <- ihandler;
    }
}

loadDefaultCashRegisterModels 'Загрузить стандартные модели касс' = ACTION () {
    EXEC loadDefaultCashRegisterModel('Кассы EasyCSV', 'EasyCSV', 'equ.clt.handler.easy.EasyCSVHandler');
    EXEC loadDefaultCashRegisterModel('Кассы Kristal', 'Kristal', 'equ.clt.handler.kristal.KristalHandler');
    EXEC loadDefaultCashRegisterModel('Кассы Maxishop', 'Maxishop', 'equ.clt.handler.maxishop.MaxishopHandler');
    EXEC loadDefaultCashRegisterModel('Кассы UKM4', 'Ukm4', 'equ.clt.handler.ukm4.UKM4Handler');
    EXEC loadDefaultCashRegisterModel('Фискальный регистратор DateCS', 'DateCS', '');
} IN loadDefaultGroup;

@implementLoadDefaultData(loadDefaultCashRegisterModels);

// --------------------------------------- Генерация групп касс -------------------------------- //
loadDefaultGroupCashRegister 'Сгенерировать группу касс' = ACTION(equipmentServer, priceListType, stock, model, num) {
    ADDOBJ GroupCashRegister;
    FOR g == addedObject() DO {
        SET departmentStoreGroupCashRegister(g) <- stock AS Stock;
        SET equipmentServerGroupMachinery(g) <- equipmentServer AS EquipmentServer;
        SET priceListTypeGroupMachinery(g) <- priceListType AS PriceListType;
        SET nameGroupMachinery(g) <- 'Группа касс по умолчанию' AS STRING[110];

        LOCAL numCashRegister = INTEGER();
        SET numCashRegister() <- 0;
        WHILE numCashRegister() < (num AS INTEGER) DO {
            ADDOBJ CashRegister;
            FOR c == addedObject() DO {
                SET groupCashRegisterCashRegister(c) <- g AS GroupCashRegister;
                SET numCashRegister() <- numCashRegister() + 1;
                SET nppMachinery(c) <- numCashRegister();
                SET cashRegisterModelCashRegister(c) <- model AS CashRegisterModel;
                SET descriptionMachinery(c) <- string5CM('Касса', toString255(numCashRegister()), '(', name(stock), ')');
            }
        }
    }
}

EXTEND FORM defaultData
    OBJECTS         crm=CashRegisterModel FIXED PANEL
    PROPERTIES(crm) SELECTOR name

    OBJECTS         cCashReg=INTEGER FIXED PANEL
    PROPERTIES(cCashReg)   intValueCashRegister = OBJVALUE
    PROPERTIES (es, pt, s, crm, cCashReg) loadDefaultGroupCashRegister
;

EXTEND DESIGN defaultData {
    PROPERTY(name(crm)) {
        caption = 'Модель касс';
    }
    PROPERTY(intValueCashRegister) {
        caption = 'Количество касс';
    }
    machinery {
        NEW cashRegister {
            title = 'Кассы';
            ADD PROPERTY(name(crm));
            ADD PROPERTY(intValueCashRegister);
            ADD PROPERTY(loadDefaultGroupCashRegister);
        }
    }
}