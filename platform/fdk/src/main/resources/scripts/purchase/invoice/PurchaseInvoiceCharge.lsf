MODULE PurchaseInvoiceCharge;

REQUIRE PurchaseInvoice;

NAMESPACE Purchase;

@defineDocumentInterfaceCreate(invoice, showChargePrice, 'Услуги');

// -- Операция
@defineOperationProperty(showChargePrice, 'Услуги', showContainer);
@deriveDocumentOperationProperty(UserInvoice, showChargePrice);

chargeSumUserInvoice 'Сумма услуг в документе' (userInvoice) = GROUP SUM sumUserInvoiceDetail(idetail) IF isChargeSkuUserInvoiceDetail(idetail)
    BY userInvoiceUserInvoiceDetail(idetail) IN documentSumGroup;
chargeSumInvoice 'Сумма услуг в документе' (invoice) = GROUP SUM sumInvoiceDetail(idetail) IF isChargeSkuInvoiceDetail(idetail)
    BY invoiceInvoiceDetail(idetail) IN documentSumGroup;

itemSumUserInvoice 'Сумма товара в документе' (userInvoice) = GROUP SUM sumUserInvoiceDetail(idetail) IF isStockSkuUserInvoiceDetail(idetail)
    BY userInvoiceUserInvoiceDetail(idetail) IN documentSumGroup;
itemSumInvoice 'Сумма товара в документе' (invoice) = GROUP SUM sumInvoiceDetail(idetail) IF isStockSkuInvoiceDetail(idetail)
    BY invoiceInvoiceDetail(idetail) IN documentSumGroup;

calcChargePriceUserInvoiceDetail  (detail) = [(X*Y)/(Z*W)](
    sumUserInvoiceDetail(detail),
    chargeSumUserInvoice(userInvoiceUserInvoiceDetail(detail)),
    itemSumUserInvoice(userInvoiceUserInvoiceDetail(detail)),
    quantityUserInvoiceDetail(detail)
) IF isStockSkuInvoiceDetail(detail);

@defineDocumentInterfaceDetailPriceCustomPrefix (invoiceDetail, charge, ' услуг за ед.');

deriveChargePriceUserInvoice 'Расписать сумму услуг по товарам' = ACTION (userInvoice) {
    SET chargePriceUserInvoiceDetail(detail) <- calcChargePriceUserInvoiceDetail(detail) WHERE userInvoiceUserInvoiceDetail(detail) == userInvoice;
}

EXTEND CLASS SystemLedgerPriceListType {chargePriceStockPriceListType 'Услуги (последняя по складу)' }
batchLedgerPriceListType(type) += type == SystemLedgerPriceListType. chargePriceStockPriceListType;
inPriceListLedgerSystemLedgerPriceListType (ledger, type) += TRUE WHEN CLASS(chargePriceInvoiceDetail(ledger)) AND type == SystemLedgerPriceListType.chargePriceStockPriceListType;
pricePriceListLedgerSystemLedgerPriceListType (ledger, type) += chargePriceInvoiceDetail(ledger) WHEN CLASS(chargePriceInvoiceDetail(ledger)) AND type == SystemLedgerPriceListType.chargePriceStockPriceListType;

EXTEND CLASS SystemLedgerPriceListType { chargePricePriceListType 'Услуги (последняя по ценовой группе)' }
batchLedgerPriceListType(type) += type == SystemLedgerPriceListType.chargePricePriceListType;
inPriceListLedgerSystemLedgerPriceListType (ledger, type) += TRUE WHEN CLASS(chargePriceInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger))) AND type == SystemLedgerPriceListType.chargePricePriceListType;
pricePriceListLedgerSystemLedgerPriceListType (ledger, type) += chargePriceInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger)) WHEN CLASS(chargePriceInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger))) AND type == SystemLedgerPriceListType.chargePricePriceListType;

extraCostPriceUserInvoiceDetail(detail) += chargePriceUserInvoiceDetail(detail);

EXTEND FORM userInvoice
    PROPERTIES(i)                                        showChargePriceUserInvoice
    PROPERTIES(i) SHOWIF showChargePriceUserInvoice(i) chargeSumUserInvoice, deriveChargePriceUserInvoice TODRAW d FORCE PANEL TOOLBAR
    PROPERTIES(d) SHOWIF showChargePriceUserInvoice(i) chargePriceUserInvoiceDetail BEFORE numberVATUserInvoiceDetail(d)
;

EXTEND DESIGN userInvoice {
    headerExtraParams {
        NEW headerCharge {
            title = 'Услуги';
            ADD PROPERTY(showChargePriceUserInvoice);
        }
    }
}

EXTEND FORM invoices
    PROPERTIES(i) READONLY showChargePriceInvoice, chargeSumInvoice
    PROPERTIES(d) SHOWIF showChargePriceInvoice(i) READONLY chargePriceInvoiceDetail BEFORE numberVATInvoiceDetail(d)
;

//-------------Расчет транспорта по коэффициенту в шапке накладной-------------//

chargePercentInvoice '% трансп. расходов' = ABSTRACT NUMERIC[6,3] (Invoice) IN documentSumGroup;

chargePercentUserInvoice '% трансп. расходов' (userInvoice) = DATA NUMERIC[6,3] (UserInvoice) IN documentSumGroup FIXEDCHARWIDTH 6;
chargePercentUserInvoiceDetail '% трансп. расходов' (userInvoiceDetail) = DATA NUMERIC[6,3] (UserInvoiceDetail) MINCHARWIDTH 6;

calcChargePercentUserInvoiceDetail (userInvoiceDetail) =
    chargePercentUserInvoiceDetail(userInvoiceDetail) * priceUserInvoiceDetail(userInvoiceDetail) / 100;

deriveCalcChargePriceUserInvoice 'Рассчитать сумму услуг по товарам' = ACTION (userInvoice) {
    SET chargePercentUserInvoiceDetail(detail) <- chargePercentUserInvoice(userInvoiceUserInvoiceDetail(detail)) WHERE userInvoiceUserInvoiceDetail(detail) == userInvoice;
    SET chargePriceUserInvoiceDetail(detail) <- calcChargePercentUserInvoiceDetail (detail) WHERE userInvoiceUserInvoiceDetail(detail) == userInvoice;
}

WHEN SESSION FORMS userInvoice CHANGED(chargePercentUserInvoice(userInvoice)) DO EXEC deriveCalcChargePriceUserInvoice(userInvoice);

chargePercentInvoice(invoice) += chargePercentUserInvoice(invoice);

EXTEND FORM userInvoice
    PROPERTIES(i) SHOWIF showChargePriceUserInvoice(i) chargePercentUserInvoice
    PROPERTIES(d) SHOWIF showChargePriceUserInvoice(i) chargePercentUserInvoiceDetail BEFORE chargePriceUserInvoiceDetail(d)
;
EXTEND DESIGN userInvoice {
    headerCharge {
        ADD PROPERTY(chargePercentUserInvoice);
    }
}

EXTEND FORM invoices
    PROPERTIES(i) READONLY chargePercentInvoice
;

