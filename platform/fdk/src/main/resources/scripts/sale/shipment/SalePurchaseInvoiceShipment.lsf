MODULE SalePurchaseInvoiceShipment;

REQUIRE PurchaseShipment, SalePurchaseInvoice;

PRIORITY Sale;

//----------------------------------------------------------------------------//

GROUP shipmentGroup 'Информация о поставке' : base;

// Галочка по созданию акта расценки на закупке
@defineDocumentInterfaceCreate (invoice, createPurchaseShipment, 'Создать поставку (закупка)');

// -- Операция
@defineOperationProperty(createPurchaseShipment, 'Поставка (закупка)', createContainer);
@deriveDocumentOperationProperty(UserInvoice, createPurchaseShipment);

backgroundPurchaseShipmentInvoice 'Цвет' (invoice) = RGB(238, 250, 200) IF invoice IS Invoice;
//--
Purchase.createShipmentInvoice(invoice) += createPurchaseShipmentInvoice(invoicePurchaseInvoice(invoice));

@defineDocumentInterfaceDetailPricePrefix(invoice, purchaseShipment, ' учетная (закупка)');
@defineDocumentInterfaceDetailQuantityPrefix (invoice, purchaseShipment, ' поставлено (закупка)');
purchaseShipmentQuantityUserInvoiceDetail(detail) <- quantityUserInvoiceDetail(detail) WHEN CHANGED(quantityUserInvoiceDetail(detail));

@defineDocumentInterfaceDetailExpiryDatePrefix(invoice, purchase);
purchaseExpiryDateUserInvoiceDetail(detail) <- prevExpiryDateBatch(batchUserInvoiceDetail(detail)) WHEN CHANGED (batchUserInvoiceDetail(detail));
Purchase.expiryDateInvoiceDetail(detail) += purchaseExpiryDateUserInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(detail));

@defineDocumentInterfaceHeaderTimePrefix(Invoice, purchaseShipment, ' поставки (закупка)');

@defineDocumentInterfaceDetailDataSumPrefix (invoice, purchaseShipment, ' учетная (закупка)');
@deriveDocumentDetailSumPrefix(userInvoice, purchaseShipment, currency, purchaseShipmentQuantity);

@defineDocumentInterfaceHeaderCreateShipment (invoice);
@defineDocumentInterfaceHeaderSumPrefix (invoice, purchaseShipment, ' учетная (закупка)');

Purchase.shipmentDateInvoice(invoice) += purchaseShipmentDateInvoice(invoicePurchaseInvoice(invoice));
Purchase.shipmentTimeInvoice(invoice) += purchaseShipmentTimeInvoice(invoicePurchaseInvoice(invoice));

Purchase.shipmentQuantityInvoiceDetail(detail) += purchaseShipmentQuantityInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(detail));
Purchase.shipmentPriceInvoiceDetail(detail) += purchaseShipmentPriceInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(detail));
Purchase.shipmentSumInvoiceDetail (detail) += purchaseShipmentSumInvoiceDetail(invoiceDetailPurchaseInvoiceDetail(detail));

EXTEND FORM userInvoice
    PROPERTIES(i) BACKGROUND backgroundPurchaseShipmentInvoice(i) SHOWIF createPurchaseInvoiceUserInvoice(i) createPurchaseShipmentUserInvoice
    PROPERTIES(i) BACKGROUND backgroundPurchaseShipmentInvoice(i) SHOWIF createPurchaseShipmentUserInvoice(i) purchaseShipmentSumUserInvoiceDetailUserInvoice,
                  purchaseShipmentDateUserInvoice, purchaseShipmentTimeUserInvoice
    PROPERTIES(d) BEFORE delete(d) SHOWIF createPurchaseShipmentInvoice(i) BACKGROUND backgroundPurchaseShipmentInvoice(i)
                  purchaseExpiryDateUserInvoiceDetail, nameCustomerStockUserInvoiceDetail, purchaseShipmentPriceUserInvoiceDetail, purchaseShipmentSumUserInvoiceDetail
    PROPERTIES(d) purchaseShipmentQuantityUserInvoiceDetail AFTER quantityUserInvoiceDetail SHOWIF createPurchaseShipmentInvoice(i) BACKGROUND backgroundPurchaseShipmentInvoice(i)
;
EXTEND DESIGN userInvoice {
    headerCreateDocuments{
        NEW headerCreatePurchaseShipment {
            title = 'Поставка (закупка)';
            childConstraints = TO THE RIGHTBOTTOM;
            ADD PROPERTY(createPurchaseShipmentUserInvoice);
            ADD PROPERTY(purchaseShipmentDateUserInvoice);
            ADD PROPERTY(purchaseShipmentTimeUserInvoice);
        }
    }
}

EXTEND FORM invoices
    PROPERTIES(i) READONLY BACKGROUND backgroundPurchaseShipmentInvoice(i) createPurchaseShipmentInvoice
    PROPERTIES(i) READONLY BACKGROUND backgroundPurchaseShipmentInvoice(i) purchaseShipmentSumInvoiceDetailInvoice BEFORE ordersInvoice
    PROPERTIES(d) READONLY SHOWIF createPurchaseShipmentInvoice(i) BACKGROUND backgroundPurchaseShipmentInvoice(i)
                  purchaseExpiryDateInvoiceDetail, nameCustomerStockInvoiceDetail, purchaseShipmentPriceInvoiceDetail, purchaseShipmentSumInvoiceDetail
    PROPERTIES(d) READONLY purchaseShipmentQuantityInvoiceDetail AFTER quantityInvoiceDetail SHOWIF createPurchaseShipmentInvoice(i) BACKGROUND backgroundPurchaseShipmentInvoice(i)
;

// ------------------------------- Расчет учетной цены для поставки ------------------------ //
overPurchaseShipmentPriceUserInvoiceDetail = ABSTRACT NUMERIC[14,2] (UserInvoiceDetail) EXCLUSIVE PERSISTENT;
calcPurchaseShipmentPriceUserInvoiceDetail (detail) = priceUserInvoiceDetail(detail) OVERRIDE overPurchaseShipmentPriceUserInvoiceDetail(detail) PERSISTENT;

extraPurchaseShipmentPriceUserInvoiceDetail = ABSTRACT NUMERIC[14,2] (UserInvoiceDetail) PERSISTENT;    // на всякий случай

// Цены учетные

purchaseShipmentPriceUserInvoiceDetail(detail) <- calcPurchaseShipmentPriceUserInvoiceDetail(detail) (+) extraPurchaseShipmentPriceUserInvoiceDetail(detail) WHEN
    (CHANGED(calcPurchaseShipmentPriceUserInvoiceDetail(detail)) OR
     CHANGED(extraPurchaseShipmentPriceUserInvoiceDetail(detail)) OR
     CHANGED(createPurchaseShipmentUserInvoiceDetail(detail)))
        AND createPurchaseShipmentUserInvoiceDetail(detail);











