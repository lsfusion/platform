MODULE SalePurchaseShipment;

REQUIRE SaleShipment, PurchaseShipment;

PRIORITY Sale;

//----------------------------------------------------------------------------//

GROUP shipmentGroup 'Информация о поставке' : base;

createPurchaseShipment 'Создать поставку (закупка)' = ABSTRACT BOOLEAN (Shipment) PERSISTENT;
createPurchaseUserShipment 'Создать поставку (закупка)' = DATA BOOLEAN (UserShipment) PERSISTENT;
createPurchaseShipment(shipment) += createPurchaseUserShipment(shipment);

createPurchaseShipmentDetail 'Создать поставку (закупка)' (shipmentDetail) = createPurchaseShipment(shipmentShipmentDetail(shipmentDetail))PERSISTENT;
createPurchaseUserShipmentDetail 'Создать поставку (закупка)' (userShipmentDetail) = createPurchaseUserShipment(userShipmentUserShipmentDetail(userShipmentDetail));
backgroundPurchaseShipment 'Цвет' (shipment) = RGB(255, 224, 255) IF shipment IS Shipment;

@defineDocumentInterfaceHeaderTimePrefix(Shipment, purchaseShipment, ' поставки (закупка)');

EXTEND FORM userShipment
    PROPERTIES(s) BACKGROUND backgroundPurchaseShipment(s) createPurchaseUserShipment
    PROPERTIES(s) BACKGROUND backgroundPurchaseShipment(s) SHOWIF createPurchaseUserShipment(s) purchaseShipmentDateUserShipment,
                  purchaseShipmentTimeUserShipment
;
EXTEND DESIGN userShipment {
    headerCreateDocuments{
        NEW headerShipment{
            title = 'Поставка';
            childConstraints = TO THE RIGHTBOTTOM;
            ADD PROPERTY(createPurchaseUserShipment);
            ADD PROPERTY(purchaseShipmentDateUserShipment);
            ADD PROPERTY(purchaseShipmentTimeUserShipment);
        }
    }
}

EXTEND FORM shipments
    PROPERTIES(s) READONLY BACKGROUND backgroundPurchaseShipment(s) createPurchaseShipment
    PROPERTIES(s) BACKGROUND backgroundPurchaseShipment(s) SHOWIF createPurchaseShipment(s) purchaseShipmentDateShipment,
                  purchaseShipmentTimeShipment
;
//--  Связь поставки с поставкой

shipmentDetailShipmentDetail = ABSTRACT ShipmentDetail (Purchase.ShipmentDetail) PERSISTENT;
shipmentDetailUserShipmentDetail = DATA ShipmentDetail (Purchase.UserShipmentDetail);
shipmentDetailShipmentDetail(shipmentDetail) += shipmentDetailUserShipmentDetail(shipmentDetail);

CONSTRAINT Purchase.supplierShipmentDetail(detail) != supplierShipmentDetail(shipmentDetailUserShipmentDetail(detail)) OR
           Purchase.supplierStockShipmentDetail(detail) != supplierStockShipmentDetail(shipmentDetailUserShipmentDetail(detail)) OR
           Purchase.customerShipmentDetail(detail) != customerShipmentDetail(shipmentDetailUserShipmentDetail(detail)) OR
           Purchase.customerStockShipmentDetail(detail) != customerStockShipmentDetail(shipmentDetailUserShipmentDetail(detail)) OR
           Purchase.skuShipmentDetail(detail) != skuShipmentDetail(shipmentDetailUserShipmentDetail(detail))
    CHECKED BY shipmentDetailUserShipmentDetail
        MESSAGE 'Поставщик, покупатель, склад поставщика и склад покупателя в поставке и поставке на осове поставке должны соответствовать друг другу';


descriptionIndexShipmentDetailShipmentDetail 'Строка поставки (продажа)' (detail) = descriptionIndexShipmentDetail(shipmentDetailShipmentDetail(detail));
descriptionIndexShipmentDetailUserShipmentDetail 'Строка поставки (продажа)' (detail) = descriptionIndexShipmentDetail(shipmentDetailUserShipmentDetail(detail));

quantityShipmentDetailShipmentShipment (sale, purchase) = GROUP SUM Purchase.quantityShipmentDetail(detail) BY shipmentShipmentDetail(shipmentDetailShipmentDetail(detail)), Purchase.shipmentShipmentDetail(detail);

saleShipmentsShipment 'Поставки (продажа)' (purchase) = GROUP CONCAT toString255(descriptionShipment(sale)) IF quantityShipmentDetailShipmentShipment(sale, purchase) , ', '
                                                        BY purchase
                                                        ORDER sale IN shipmentGroup MINCHARWIDTH 30 PREFCHARWIDTH 50 PERSISTENT;

relationPurchaseShipment 'Связь' (purchase) = GROUP SUM quantityShipmentDetailShipmentShipment (sale, purchase) BY purchase PERSISTENT;

shippedShipmentDetail 'Кол-во (поставлено)' (shipmentDetail) = GROUP SUM Purchase.quantityShipmentDetail(detail) IF Purchase.isPostedShipmentDetail(detail)
                                                                   BY shipmentDetailShipmentDetail(detail) PERSISTENT;

toShipShipmentDetail 'Не поставлено' (shipmentDetail) = quantityShipmentDetail (shipmentDetail) (-) shippedShipmentDetail(shipmentDetail);

CLASS PurchaseShipment 'Поставка на основе поставки': Purchase.Shipment;
CLASS PurchaseShipmentPosted 'Проведенная поставка на основе поставки' : PurchaseShipment, PostedObject;
CLASS PurchaseShipmentDetail 'Строка поставки на основе поставки' : Purchase.ShipmentDetail;

@defineDocumentTables(purchaseShipment);

@defineDocumentAggregation(shipment, purchaseShipment, createPurchaseShipment);
Purchase.shipmentShipmentDetail(detail) += purchaseShipmentPurchaseShipmentDetail(detail);

@defineDocumentDetailIndex(purchaseShipment);

Purchase.dateShipment(shipment) += purchaseShipmentDateShipment(shipmentPurchaseShipment(shipment));
Purchase.timeShipment(shipment) += purchaseShipmentTimeShipment(shipmentPurchaseShipment(shipment));

@defineDocumentAggregationStockPrefix(shipment, purchaseShipment, supplierStock, 'Склад поставщика', , );
Purchase.supplierStockShipment(shipment) += supplierStockPurchaseShipment(shipment);
Purchase.dataSupplierStockShipmentDetail(shipmentDetail) += dataSupplierStockShipmentDetail(shipmentDetailPurchaseShipmentDetail(shipmentDetail));
@defineDocumentAggregationStockPrefix(shipment, purchaseShipment, customerStock, 'Склад покупателя', , );
Purchase.customerStockShipment(shipment) += customerStockPurchaseShipment(shipment);
Purchase.dataCustomerStockShipmentDetail(shipmentDetail) += dataCustomerStockShipmentDetail(shipmentDetailPurchaseShipmentDetail(shipmentDetail));

@defineDocumentAggregationStockPrefix(shipment, purchaseShipment, supplier, 'Поставщик', , );
Purchase.supplierShipment(shipment) += supplierPurchaseShipment(shipment);
@defineDocumentAggregationStockPrefix(shipment, purchaseShipment, customer, 'Покупатель', , );
Purchase.customerShipment(shipment) += customerPurchaseShipment(shipment);

@defineDocumentAggregationPosted(shipment, purchaseShipment);
Purchase.isPostedShipment(shipment) += isPostedPurchaseShipment(shipment);

Purchase.numberShipment(shipment) += numberShipment(shipmentPurchaseShipment(shipment));
Purchase.seriesShipment(shipment) += seriesShipment(shipmentPurchaseShipment(shipment));
seriesNumberPurchaseShipment 'Серия/номер документа' (purchaseShipment) = seriesNumberShipment(shipmentPurchaseShipment(purchaseShipment));

noteShipmentPurchaseShipment 'Примечание' (purchaseShipment) = noteShipment(shipmentPurchaseShipment(purchaseShipment));
Purchase.noteShipment(shipment) += noteShipmentPurchaseShipment(shipment);

currencyPurchaseShipment  (purchaseShipment) = currencyShipment(shipmentPurchaseShipment(purchaseShipment));
Purchase.currencyShipment (shipment) += currencyPurchaseShipment(shipment);

@defineDocumentDescription(purchaseShipment, PurchaseShipmentDetail, seriesNumberPurchaseShipment, 'Поставка на основе поставки ');
Purchase.descriptionShipment (shipment) += descriptionPurchaseShipment(shipment);

@defineDocumentAggregationDetailSku(shipment, purchaseShipment, sku);
Purchase.skuShipmentDetail(shipmentDetail) +=  skuPurchaseShipmentDetail(shipmentDetail);

@defineDocumentAggregationDetailBatch(shipment, purchaseShipment);
Purchase.batchShipmentDetail(shipmentDetail) += batchPurchaseShipmentDetail(shipmentDetail);

Purchase.quantityShipmentDetail(shipmentDetail) += quantityShipmentDetail(shipmentDetailPurchaseShipmentDetail(shipmentDetail));

Purchase.priceShipmentDetail(shipmentDetail) += priceShipmentDetail(shipmentDetailPurchaseShipmentDetail(shipmentDetail));

Purchase.sumShipmentDetail(shipmentDetail) += sumShipmentDetail(shipmentDetailPurchaseShipmentDetail(shipmentDetail));

Purchase.editShipment(shipment) += ACTION EXEC editShipment(shipmentPurchaseShipment(shipment));

Purchase.showPackShipment(shipment) +=  showPackShipment(shipmentPurchaseShipment(shipment));
Purchase.barcodePackShipmentDetail(shipmentDetail) += barcodePackShipmentDetail(shipmentDetailPurchaseShipmentDetail(shipmentDetail));
Purchase.amountPackShipmentDetail(shipmentDetail) +=  amountPackShipmentDetail(shipmentDetailPurchaseShipmentDetail(shipmentDetail));
Purchase.packQuantityShipmentDetail(shipmentDetail) +=  packQuantityShipmentDetail(shipmentDetailPurchaseShipmentDetail(shipmentDetail));

shipmentDetailShipmentDetail(shipmentDetail) += shipmentDetailPurchaseShipmentDetail(shipmentDetail);

// Создание поставки на основе поставки //

FORM shipmentShipments 'Поставки'
    OBJECTS s = LegalEntity FIXED PANEL
    PROPERTIES (s) READONLY nameLegalEntity
    OBJECTS c = LegalEntity FIXED PANEL
    PROPERTIES (c) READONLY nameLegalEntity

    OBJECTS ss = Stock FIXED PANEL
    PROPERTIES (ss) READONLY nameStock
    OBJECTS cs = Stock FIXED PANEL
    PROPERTIES (cs) READONLY nameStock

    OBJECTS o = Shipment
    PROPERTIES (o) READONLY isPostedShipment FORCE GRID, numberShipment, seriesShipment, dateShipment, timeShipment, nameCurrencyShipment,
                            countShipmentDetailShipment, quantityShipmentDetailShipment, sumShipmentDetailShipment,
                            noteShipment, objectClassName
    FILTERS supplierShipment(o) == s,
            customerShipment(o) == c,
            supplierStockShipment(o) == ss,
            customerStockShipment(o) == cs,
            isPostedShipment(o) AND NOT createPurchaseShipment(o)

    OBJECTS d = ShipmentDetail

    PROPERTIES (d) READONLY indexShipmentDetail, idBarcodeSkuShipmentDetail, nameSkuShipmentDetail, shortNameUOMSkuShipmentDetail,
                            quantityShipmentDetail
    PROPERTIES (d) READONLY SHOWIF showPackShipment(o) idBarcodePackShipmentDetail, shortNameUOMPackShipmentDetail, amountPackShipmentDetail, packQuantityShipmentDetail
    PROPERTIES (d) READONLY priceShipmentDetail, sumShipmentDetail,
                            nameSupplierStockShipmentDetail, toShipShipmentDetail BACKGROUND backgroundPurchaseShipment(o)
    FILTERS shipmentShipmentDetail(d) == o
;

DESIGN shipmentShipments FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        NEW documentContainer BEFORE functions.box {

            childConstraints = TO THE BOTTOM;
            NEW headerBox {
                childConstraints = TO THE RIGHTBOTTOM;
                NEW headerBox1 {
                    childConstraints = TO THE RIGHTBOTTOM;
                    title = 'Поставщик';
                    ADD s.box;
                    ADD ss.box;
                }
                NEW headerBox2 {
                    childConstraints = TO THE RIGHTBOTTOM;
                    title = 'Покупатель';
                    ADD c.box;
                    ADD cs.box;
                }
            }
            ADD o.box;
            NEW documentDetail {
                type = TABBED;

                ADD d.box {
                    title = 'Спецификация';
                }
                NEW printTab {
                    title = 'Печатные формы';
                    NEW printContainer {
                        title = 'Печать';
                        childConstraints = TO THE BOTTOM;
                        fillVertical = 1.0;
                    }
                }
            }
        }
    }
}


fillShipmentUserShipment 'Заполнить на основе поставки' =  ACTION (userShipment) {       // подставляем все количество ???
    FORM shipmentShipments OBJECTS s = Purchase.supplierUserShipment(userShipment), c = Purchase.customerUserShipment(userShipment),
                                   ss = Purchase.supplierStockUserShipment(userShipment), cs = Purchase.customerStockUserShipment(userShipment) MODAL;
    IF formResult() == FormResult.ok THEN {
        LOCAL saleShipment = Shipment();
        SET saleShipment() <- chosenObject('o');

        FOR shipmentShipmentDetail(shipmentDetail) == saleShipment() ADDOBJ d = Purchase.UserShipmentDetail DO {
            SET Purchase.userShipmentUserShipmentDetail(d) <- userShipment;
            SET shipmentDetailUserShipmentDetail(d) <- shipmentDetail;

            SET Purchase.priceUserShipmentDetail(d) <- priceShipmentDetail(shipmentDetail);

            SET Purchase.skuUserShipmentDetail(d) <- skuShipmentDetail(shipmentDetail);
            SET Purchase.batchUserShipmentDetail(d) <- batchShipmentDetail(shipmentDetail);
            SET Purchase.quantityUserShipmentDetail (d) <- quantityShipmentDetail(shipmentDetail);

            SET Purchase.barcodePackUserShipmentDetail(d) <- barcodePackShipmentDetail(shipmentDetail);
            SET Purchase.amountPackUserShipmentDetail(d) <- amountPackShipmentDetail(shipmentDetail);
            SET Purchase.packQuantityUserShipmentDetail(d) <- packQuantityShipmentDetail(shipmentDetail);
        }
    }
} IN shipmentGroup;

EXTEND FORM Purchase.userShipment
    PROPERTIES(s) fillShipmentUserShipment, saleShipmentsShipment READONLY
    PROPERTIES(d) descriptionIndexShipmentDetailUserShipmentDetail BEFORE delete(d)
;
EXTEND DESIGN Purchase.userShipment { headerCreateDetail{ ADD s.shipmentGroup {childConstraints = TO THE RIGHTBOTTOM;}}}

EXTEND FORM Purchase.shipments
//    PROPERTIES(s) READONLY saleShipmentsShipment
    PROPERTIES(d) READONLY descriptionIndexShipmentDetailShipmentDetail SHOWIF relationPurchaseShipment(s)
;

//-- Действие

//overFillShipmentUserShipmentShipment = ABSTRACT ACTION (userShipment, shipment);
//overFillShipmentUserShipmentDetailShipmentDetail = ABSTRACT ACTION (userDetail, detail);

moveUserShipmentShipment 'Поставка (закупка)' =  ACTION (shipment) NEWSESSION{

    FOR ADDOBJ s = Purchase.UserShipment DO {



        SET Purchase.supplierUserShipment(s) <- supplierShipment(shipment);
        SET Purchase.customerUserShipment(s) <- customerShipment(shipment);
        SET Purchase.supplierStockUserShipment(s) <- supplierStockShipment(shipment);
        SET Purchase.customerStockUserShipment(s) <- customerStockShipment(shipment);
        SET numberObject(s) <- numberShipment(shipment);
        SET seriesObject(s) <- seriesShipment(shipment);
        SET Purchase.noteUserShipment(s) <- noteShipment(shipment);
        SET Purchase.currencyUserShipment(s) <- currencyShipment(shipment);

        SET Purchase.showPackUserShipment(s) <- showPackShipment(shipment);
        SET Purchase.operationUserShipment(s) <- operationShipment(shipment);
//        EXEC overFillShipmentUserShipmentShipment(s, shipment);

        FOR shipmentShipmentDetail(detail)==shipment ADDOBJ d = Purchase.UserShipmentDetail DO {

            SET Purchase.userShipmentUserShipmentDetail(d) <- s;

            SET Purchase.dataSupplierStockUserShipmentDetail(d) <- dataSupplierStockShipmentDetail(detail);
            SET Purchase.dataCustomerStockUserShipmentDetail(d) <- dataCustomerStockShipmentDetail(detail);

            SET Purchase.skuUserShipmentDetail(d) <- skuShipmentDetail(detail);
            SET Purchase.quantityUserShipmentDetail (d) <- quantityShipmentDetail(detail);
            SET Purchase.priceUserShipmentDetail (d) <- priceShipmentDetail(detail);

            SET Purchase.barcodePackUserShipmentDetail (d) <- barcodePackShipmentDetail(detail);
            SET Purchase.amountPackUserShipmentDetail (d) <- amountPackShipmentDetail(detail);
            SET Purchase.packQuantityUserShipmentDetail (d) <- packQuantityShipmentDetail(detail);

            SET Purchase.batchUserShipmentDetail(d) <- batchShipmentDetail(detail);
//            EXEC overFillShipmentUserShipmentDetailShipmentDetail(d,detail);
        }

    FORM Purchase.userShipment OBJECTS s = s MANAGESESSION DOCKEDMODAL;
    }
} TOOLBAR;

EXTEND FORM shipments
    PROPERTIES(s) moveUserShipmentShipment
;
EXTEND DESIGN shipments {
    createdContainer{
        ADD PROPERTY(moveUserShipmentShipment);
    }
}

