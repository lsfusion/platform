MODULE Consignment;

REQUIRE System,
        ByLegalEntity,
        Stock,
        Employee,
        Utils,
        Ware,
        Numerator; // вот эту зависимость нужно будет убрать

CLASS wayOfLoading 'Способ ПРР' : named;
TABLE wayOfLoading (wayOfLoading);


GROUP sumConsignmentGroup 'Суммы' : baseGroup;

CLASS ABSTRACT consignment 'Накладная';
CLASS ABSTRACT consignmentDetail 'Строка накладной';

dateConsignment 'Дата' (consignment) = ABSTRACT DATE (consignment);
stockConsignment (consignment) = ABSTRACT stock (consignment);
nameStockConsignment 'Отдел документа' (consignment) = name(stockConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 30;

// ------------------------------------- Атрибуты --------------------------------- //

GROUP carConsignmentGroup 'Автомобиль' : baseGroup;

carConsignment 'Автомобиль' (consignment) = ABSTRACT STRING[30] (consignment) IN carConsignmentGroup;
ownerCarConsignment 'Владелец автомобиля' (consignment) = ABSTRACT STRING[100] (consignment) IN carConsignmentGroup;
trailerConsignment 'Прицеп' (consignment) = ABSTRACT STRING[10] (consignment) IN carConsignmentGroup;
driverCarConsignment 'Водитель' (consignment) = ABSTRACT STRING[40] (consignment) IN carConsignmentGroup;
waybillConsignment 'Путевой лист' (consignment) = ABSTRACT STRING[20] (consignment) IN carConsignmentGroup;

transportPayerConsignment 'Заказчик перевозки (ИД)' (consignment) = ABSTRACT legalEntity (consignment);

dataLoadingPlaceConsignment 'Пункт погрузки' (consignment) = ABSTRACT STRING[50] (consignment);
loadingPlaceConsignment 'Пункт погрузки' (consignment) = UNION OVERRIDE addressStock(stockConsignment(consignment)),
                                                                 dataLoadingPlaceConsignment (consignment) IN carConsignmentGroup;

dataUnloadingPlaceConsignment 'Пункт разгрузки' (consignment) = ABSTRACT STRING[50] (consignment) IN carConsignmentGroup;

readdressingConsignment 'Переадресовка' (consignment) = ABSTRACT STRING[50] (consignment) IN carConsignmentGroup;

// ------------------------------------- Отпуск товара --------------------------------- //

GROUP issuanceConsignmentGroup 'Отпуск' : baseGroup;

shipmentBaseConsignment 'Основание отпуска' (consignment) = ABSTRACT STRING[30] (consignment) IN issuanceConsignmentGroup;

issuanceAllowedConsignment (consignment) = ABSTRACT employee (consignment);
commonNameIssuanceAllowedConsignment 'Отпуск разрешил' (consignment) = commonName(issuanceAllowedConsignment(consignment)) IN issuanceConsignmentGroup;

issuanceExecutedConsignment (consignment) = ABSTRACT employee(consignment);
commonNameIssuanceExecutedConsignment 'Отпуск произвел' (consignment) = commonName(issuanceExecutedConsignment(consignment)) IN issuanceConsignmentGroup;

forwarderConsignment 'Товар к перевозке принял (экспедитор), должность, фамилия, инициалы' (consignment) = ABSTRACT STRING[40] (consignment) IN issuanceConsignmentGroup;

warrantConsignment 'По доверенности (номер, дата)' (consignment) = ABSTRACT STRING[30] (consignment) IN issuanceConsignmentGroup;
warrantHolderConsignment 'По доверенности выданной (наименование орг-ии)' (consignment) = ABSTRACT STRING[100] (consignment) IN issuanceConsignmentGroup;

goodsAcceptedConsignment 'Принял грузополучатель' (consignment) = ABSTRACT STRING[40] (consignment) IN issuanceConsignmentGroup;

// ------------------------------------- Погрузочно-разгрузочные работы --------------------------------- //

GROUP loadingConsignmentGroup 'ПРР' : baseGroup;

loadingExecuterConsignment (consignment) = ABSTRACT employee(consignment);
commonNameLoadingExecuterConsignment 'Исполнитель ПРР' (consignment) = commonName(loadingExecuterConsignment(consignment)) IN loadingConsignmentGroup;

wayOfLoadingConsignment (consignment) = ABSTRACT wayOfLoading(consignment);
nameWayOfLoadingConsignment 'Способ ПРР' (consignment) = name(wayOfLoadingConsignment(consignment)) IN loadingConsignmentGroup;

codeLoadingConsignment 'Код ПРР' (consignment) = ABSTRACT STRING[3] (consignment)IN loadingConsignmentGroup;

arrivalTimeConsignment 'Время прибытия' (consignment) = ABSTRACT DATETIME(consignment) IN carConsignmentGroup;
departureTimeConsignment 'Время убытия' (consignment) = ABSTRACT DATETIME(consignment) IN carConsignmentGroup;
downtimeConsignment 'Время простоя' (consignment) = ABSTRACT STRING[10] (consignment) IN carConsignmentGroup;

raceQuantityConsignment 'Количество ездок' (consignment) = ABSTRACT INTEGER (consignment) IN carConsignmentGroup;

recipientConsignment 'Получатель (ИД)' = ABSTRACT legalEntity (consignment);
UNPRecipientConsignment 'УНП получателя' (consignment) = UNPLegalEntity(recipientConsignment(consignment));
addressRecipientConsignment 'Юридический адрес получателя' (consignment) = addressLegalEntityDate(recipientConsignment(consignment), dateConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 50;
fullNameRecipientConsignment 'Наименование для накладных получателя' (consignment) = fullNameLegalEntity(recipientConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 50;

unloadingPlaceConsignment 'Пункт разгрузки' (consignment) = UNION OVERRIDE addressRecipientConsignment(consignment),
                                                                           dataUnloadingPlaceConsignment (consignment) IN carConsignmentGroup;

senderConsignment (consignment) = ABSTRACT legalEntity (consignment);

UNPSenderConsignment 'УНП отправителя' (consignment) = UNPLegalEntity(senderConsignment(consignment));
addressSenderConsignment 'Юридический адрес отправителя' (consignment) = addressLegalEntityDate(senderConsignment(consignment), dateConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 50;
fullNameSenderConsignment 'Наименование для накладных отправителя' (consignment) = fullNameLegalEntity(senderConsignment(consignment)) MINCHARWIDTH 30 PREFCHARWIDTH 50;

UNPTransportPayerConsignment 'УНП заказчика перевозки' (consignment) = UNPLegalEntity(transportPayerConsignment(consignment));
addressTransportPayerConsignment 'Юридический адрес заказчика перевозки' (consignment) =
    addressLegalEntityDate(transportPayerConsignment(consignment), dateConsignment(consignment));
fullNameTransportPayerConsignment 'Наименование для накладных заказчика перевозки' (consignment) =
    fullNameLegalEntity(transportPayerConsignment(consignment)) ;

noteConsignment 'Примечание' (consignment) = ABSTRACT STRING[100] (consignment) MINCHARWIDTH 30 PREFCHARWIDTH 80;

nameTransportPayerConsignment 'Заказчик перевозки ' (consignment) =
    [FORMULA STRING[100] 'CAST($1 AS TEXT) ||  \' , \' || CAST($2 AS TEXT)']
    (name(transportPayerConsignment(consignment)), UNPLegalEntity(transportPayerConsignment(consignment))) IN carConsignmentGroup;

skuConsignmentDetail (consignmentDetail) = ABSTRACT sku (consignmentDetail);
nameSkuConsignmentDetail 'Наименование товара' (consignmentDetail) = nameSku(skuConsignmentDetail(consignmentDetail));

//UOMConsignmentDetail (consignmentDetail) = ABSTRACT UOM (consignmentDetail);
//shortNameUOMConsignmentDetail 'Единица измерения' (consignmentDetail) = shortName(UOMConsignmentDetail(consignmentDetail));
shortNameUOMConsignmentDetail 'Единица измерения' (consignmentDetail) = ABSTRACT STRING[15] (consignmentDetail);


consignmentConsignmentDetail (consignmentDetail) = ABSTRACT consignment (consignmentDetail);

quantityConsignmentDetail 'Количество' (consignmentDetail) = ABSTRACT NUMERIC[14,3] (consignmentDetail);

priceConsignmentDetail 'Цена' (consignmentDetail) = ABSTRACT NUMERIC[14,2] (consignmentDetail);

sumConsignmentDetail 'Стоимость' (consignmentDetail) = ABSTRACT NUMERIC[16,2] (consignmentDetail);

vatConsignmentDetail 'НДС, %' (consignmentDetail) = ABSTRACT NUMERIC[10,5] (consignmentDetail);

sumVATConsignmentDetail 'Сумма НДС' (consignmentDetail) = ABSTRACT NUMERIC[16,2] (consignmentDetail);

sumInvoiceConsignmentDetail 'Сумма с НДС' (consignmentDetail) = ABSTRACT NUMERIC[16,2] (consignmentDetail);

packQuantityConsignmentDetail 'Количество грузовых мест' (consignmentDetail) = ABSTRACT NUMERIC[14,3] (consignmentDetail);
grossWeightConsignmentDetail 'Масса груза, т.' (consignmentDetail) = ABSTRACT NUMERIC[14,6] (consignmentDetail);

noteConsignmentDetail 'Примечание' (consignmentDetail) = ABSTRACT STRING[100] (consignmentDetail) MINCHARWIDTH 30 PREFCHARWIDTH 80;

countConsignmentDetailConsignment 'Количество строк' (consignment) = GROUP SUM 1 IF consignmentDetail IS consignmentDetail
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

quantityConsignmentDetailConsignment 'Количество (всего)' (consignment) = GROUP SUM quantityConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

packQuantityConsignmentDetailConsignment 'Общее количество грузовых мест' (consignment) = GROUP SUM packQuantityConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

grossWeightConsignmentDetailConsignment 'Общая масса груза, т.' (consignment) = GROUP SUM grossWeightConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;;

sumKgGrossWeightConsignment 'Общая масса груза, кг.'(consignment) = grossWeightConsignmentDetailConsignment(consignment)*1000;


isCustomsFlowConsignment 'Расход с СВХ' (consignment) = ABSTRACT BOOLEAN (consignment);

currencyConsignment 'Валюта (ИД)' (consignment) = ABSTRACT currency (consignment);
shortNameCurrencyConsignment 'Валюта' (consignment) = shortNameCurrency(currencyConsignment(consignment));
documentNameCurrencyConsignment 'Валюта в накладной сокр.' (consignment) = documentNameCurrency(currencyConsignment(consignment));
isCurrencyConsignment 'Выбрана валюта' (consignment) = TRUE IF currencyConsignment(consignment);

// Посуда на возврате
isWareConsignment 'Возврат поставщику' (consignment) = ABSTRACT BOOLEAN (consignment);
wareSumConsignmentDetailConsignment 'Сумма посуды с НДС' (consignment) = ABSTRACT NUMERIC[14,2] (consignment);
supplierSumConsignmentDetailConsignment 'Сумма поставщика без НДС' (consignment) = ABSTRACT NUMERIC[14,2] (consignment);

wareConsignmentDetail (consignmentDetail) = ABSTRACT ware (consignmentDetail);
nameWareConsignmentDetail 'Посуда' (consignmentDetail) = name(wareConsignmentDetail(consignmentDetail));
warePriceConsignmentDetail 'Цена посуды с НДС' (consignmentDetail) = ABSTRACT NUMERIC[14,2] (consignmentDetail);
wareSumConsignmentDetail 'Сумма посуды с НДС' (consignmentDetail) = ABSTRACT NUMERIC[14,2] (consignmentDetail);
valueWareRangeConsignmentDetail 'НДС посуды, %' (consignmentDetail) = ABSTRACT NUMERIC[6,2] (consignmentDetail);
wareVATSumConsignmentDetail 'Сумма НДС по посуде' (consignmentDetail) = ABSTRACT NUMERIC[14,2] (consignmentDetail);
wareSupplierPriceConsignmentDetail 'Цена посуды без НДС' (consignmentDetail) =  ABSTRACT NUMERIC[14,2] (consignmentDetail);
wareSupplierSumConsignmentDetail 'Сумма посуды без НДС' (consignmentDetail) =  ABSTRACT NUMERIC[14,2] (consignmentDetail);

sumIaWConsignmentDetail 'Сумма с посудой без НДС' (consignmentDetail) = sumConsignmentDetail(consignmentDetail) (+) wareSupplierSumConsignmentDetail(consignmentDetail);
sumConsignmentDetailConsignment 'Сумма с посудой без НДС (всего)' (consignment) = GROUP SUM sumIaWConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

sumVATIaWConsignmentDetail 'Сумма НДС' (consignmentDetail) = sumVATConsignmentDetail(consignmentDetail) (+) wareVATSumConsignmentDetail(consignmentDetail);
sumVATConsignmentDetailConsignment 'Сумма НДС (всего)' (consignment) = GROUP SUM sumVATIaWConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

sumInvoiceIaWConsignmentDetail 'Сумма с НДС' (consignmentDetail) = sumInvoiceConsignmentDetail(consignmentDetail) (+) wareSumConsignmentDetail(consignmentDetail);
sumInvoiceConsignmentDetailConsignment 'Сумма c НДС (всего)' (consignment) = GROUP SUM sumInvoiceIaWConsignmentDetail(consignmentDetail)
                                                            BY consignmentConsignmentDetail(consignmentDetail) IN sumConsignmentGroup;

// ---------------------------- Формы для накладных --------------------------------- //

META defineConsignmentFormSimple(form, caption)

    FORM form caption PRINT
        OBJECTS c=consignment FIXED PANEL

        PROPERTIES (c) dateConsignment, shipmentBaseConsignment, commonNameIssuanceAllowedConsignment, commonNameIssuanceExecutedConsignment,
                       forwarderConsignment, warrantConsignment, warrantHolderConsignment, goodsAcceptedConsignment,
                       quantityConsignmentDetailConsignment, sumConsignmentDetailConsignment, sumVATConsignmentDetailConsignment, sumInvoiceConsignmentDetailConsignment, UNPRecipientConsignment,
                       UNPSenderConsignment, addressSenderConsignment, addressRecipientConsignment, fullNameSenderConsignment,
                       fullNameRecipientConsignment, numberObject, seriesObject, countConsignmentDetailConsignment,
                       isWareConsignment, isCustomsFlowConsignment, isCurrencyConsignment, noteConsignment, documentNameCurrencyConsignment

        OBJECTS d=consignmentDetail

        PROPERTIES(d) nameSkuConsignmentDetail, shortNameUOMConsignmentDetail, quantityConsignmentDetail, priceConsignmentDetail,
                      sumConsignmentDetail, vatConsignmentDetail, sumVATConsignmentDetail, sumInvoiceConsignmentDetail,
                      noteConsignmentDetail, nameWareConsignmentDetail, warePriceConsignmentDetail, wareSumConsignmentDetail,
                      valueWareRangeConsignmentDetail, wareVATSumConsignmentDetail, wareSupplierPriceConsignmentDetail,
                      wareSupplierSumConsignmentDetail

        FILTERS consignmentConsignmentDetail(d) == c
    ;
    print###form caption (consignment) = ACTION FORM form OBJECTS c IMAGE 'print.png' IN printGroup;
END

@defineConsignmentFormSimple(consignmentSimpleVertical, 'ТН-2, вертикальная');
@defineConsignmentFormSimple(consignmentSimpleHorizontal, 'ТН-2, горизонтальная');
@defineConsignmentFormSimple(consignmentSimpleAttach, 'Приложение к ТН-2');

META defineConsignmentForm(form, caption)
    FORM form caption PRINT
        OBJECTS c=consignment FIXED PANEL

        PROPERTIES (c) dateConsignment, UNPRecipientConsignment, UNPSenderConsignment, addressSenderConsignment, addressRecipientConsignment, fullNameSenderConsignment,
                       fullNameRecipientConsignment, carConsignment, ownerCarConsignment, trailerConsignment, driverCarConsignment, waybillConsignment,
                       nameTransportPayerConsignment, shipmentBaseConsignment, loadingPlaceConsignment, unloadingPlaceConsignment,
                       readdressingConsignment, commonNameIssuanceAllowedConsignment, commonNameIssuanceExecutedConsignment,
                       forwarderConsignment, warrantConsignment, warrantHolderConsignment, goodsAcceptedConsignment,
                       commonNameLoadingExecuterConsignment, nameWayOfLoadingConsignment, codeLoadingConsignment,
                       arrivalTimeConsignment, departureTimeConsignment, downtimeConsignment, raceQuantityConsignment,
                       quantityConsignmentDetailConsignment, packQuantityConsignmentDetailConsignment, grossWeightConsignmentDetailConsignment, sumKgGrossWeightConsignment,
                       sumConsignmentDetailConsignment, sumVATConsignmentDetailConsignment, sumInvoiceConsignmentDetailConsignment,
                       countConsignmentDetailConsignment, wareSumConsignmentDetailConsignment, supplierSumConsignmentDetailConsignment,
                       seriesObject, numberObject, isWareConsignment, isCustomsFlowConsignment, isCurrencyConsignment, noteConsignment, documentNameCurrencyConsignment

        OBJECTS d=consignmentDetail

        PROPERTIES(d) nameSkuConsignmentDetail, shortNameUOMConsignmentDetail, quantityConsignmentDetail, priceConsignmentDetail,
                      sumConsignmentDetail, vatConsignmentDetail, sumVATConsignmentDetail, sumInvoiceConsignmentDetail,
                      packQuantityConsignmentDetail, grossWeightConsignmentDetail, noteConsignmentDetail,
                      nameWareConsignmentDetail, warePriceConsignmentDetail, valueWareRangeConsignmentDetail,
                      wareSupplierPriceConsignmentDetail, wareSumConsignmentDetail, wareSupplierSumConsignmentDetail,
                      wareVATSumConsignmentDetail, wareSumConsignmentDetail

        FILTERS consignmentConsignmentDetail(d) == c
    ;
    print###form caption (consignment) = ACTION FORM form OBJECTS c IMAGE 'print.png' IN printGroup;
END

@defineConsignmentForm(consignmentVerticalA, 'ТТН-1, вертикальная (сторона А)');
@defineConsignmentForm(consignmentHorizontalA, 'ТТН-1, горизонтальная (сторона А)');
@defineConsignmentForm(consignmentVerticalB, 'ТТН-1, вертикальная (сторона Б)');
@defineConsignmentForm(consignmentHorizontalB, 'ТТН-1, горизонтальная (сторона Б)');
@defineConsignmentForm(consignmentAttach, 'Приложение к ТТН-1');

FORM consignment 'Атрибуты накладной'
    OBJECTS c=consignment FIXED PANEL

    PROPERTIES (c) READONLY objectClassName, nameStockConsignment, seriesObject, numberObject
    PROPERTIES (c) dateConsignment, UNPRecipientConsignment, UNPSenderConsignment, addressSenderConsignment, addressRecipientConsignment, fullNameSenderConsignment,
                   fullNameRecipientConsignment, carConsignment, ownerCarConsignment, trailerConsignment, driverCarConsignment, waybillConsignment,
                   nameTransportPayerConsignment, shipmentBaseConsignment, loadingPlaceConsignment, unloadingPlaceConsignment,
                   readdressingConsignment, commonNameIssuanceAllowedConsignment, commonNameIssuanceExecutedConsignment,
                   forwarderConsignment, warrantConsignment, warrantHolderConsignment, goodsAcceptedConsignment,
                   commonNameLoadingExecuterConsignment, nameWayOfLoadingConsignment, codeLoadingConsignment,
                   arrivalTimeConsignment, departureTimeConsignment, downtimeConsignment, raceQuantityConsignment,
                   quantityConsignmentDetailConsignment, packQuantityConsignmentDetailConsignment, grossWeightConsignmentDetailConsignment,
                   sumConsignmentDetailConsignment, sumVATConsignmentDetailConsignment, sumInvoiceConsignmentDetailConsignment,
                   countConsignmentDetailConsignment, noteConsignment, shortNameCurrencyConsignment//, SHOWIF isCustomsFlowConsignment(c)


    OBJECTS d=consignmentDetail

    PROPERTIES(d) nameSkuConsignmentDetail READONLY, shortNameUOMConsignmentDetail READONLY, quantityConsignmentDetail, priceConsignmentDetail,
                  sumConsignmentDetail, vatConsignmentDetail, sumVATConsignmentDetail, sumInvoiceConsignmentDetail,
                  packQuantityConsignmentDetail, grossWeightConsignmentDetail, noteConsignmentDetail,
                  nameWareConsignmentDetail, warePriceConsignmentDetail, valueWareRangeConsignmentDetail,
                  wareSupplierPriceConsignmentDetail, wareSumConsignmentDetail, wareSupplierSumConsignmentDetail,
                  wareVATSumConsignmentDetail, wareSumConsignmentDetail

    FILTERS consignmentConsignmentDetail(d) == c
;

DESIGN consignment FROM DEFAULT {
    main {
        preferredSize = (1024, 768);

        NEW specification.box BEFORE functions.box{

            NEW consignment{
                title = 'Атрибуты накладной';
                childConstraints = TO THE RIGHT;

                NEW wor1 {
                    childConstraints = TO THE BOTTOM;
                    ADD c.carConsignmentGroup;
                    NEW wor11 {
                        title = 'Дополнительно';
                        childConstraints = TO THE RIGHT;
                        ADD PROPERTY (noteConsignment(c));
                        ADD PROPERTY (shortNameCurrencyConsignment(c));
                    }
                }
                NEW wor2 {
                    childConstraints = TO THE BOTTOM;
                    ADD c.issuanceConsignmentGroup;
                    ADD c.loadingConsignmentGroup;
                    ADD c.sumConsignmentGroup {
                        childConstraints = TO THE BOTTOM;
                        NEW wor21 {
                            childConstraints = TO THE RIGHT;
                            ADD PROPERTY(countConsignmentDetailConsignment);
                            ADD PROPERTY(quantityConsignmentDetailConsignment);
                            ADD PROPERTY(packQuantityConsignmentDetailConsignment);
                            ADD PROPERTY(grossWeightConsignmentDetailConsignment);
                        }
                        NEW wor22 {
                            childConstraints = TO THE RIGHT;
                            ADD PROPERTY(sumConsignmentDetailConsignment);
                            ADD PROPERTY(sumVATConsignmentDetailConsignment);
                            ADD PROPERTY(sumInvoiceConsignmentDetailConsignment);
                        }
                    }
                }

            }
            ADD d.box {
                title = 'Спецификация';
            }

        }

        NEW header.box BEFORE specification.box {
            title = 'Накладная';
            childConstraints = TO THE RIGHT;

            NEW headerCol1 {
                childConstraints = TO THE BOTTOM;
                NEW container5 {
                    childConstraints = TO THE RIGHT;
                    NEW container55 {
                        title = 'Объект';
                        ADD PROPERTY (objectClassName) {
                            preferredCharWidth = 15;
                        }
                    }
                    NEW headerCol11 {
                        title = 'Шапка документа';
                        childConstraints = TO THE RIGHT;
                        ADD PROPERTY(nameStockConsignment);
                        ADD PROPERTY(numberObject);
                        ADD PROPERTY(seriesObject);
                        ADD PROPERTY(dateConsignment);
                    }
                }

                NEW headerCol22 {
                    childConstraints = TO THE BOTTOM;
                    NEW row11 {
                        title = 'Грузоотправитель';
                        childConstraints = TO THE RIGHT;
                        ADD PROPERTY(fullNameSenderConsignment);
                        ADD PROPERTY(UNPSenderConsignment);
                        ADD PROPERTY(addressSenderConsignment);
                    }
                    NEW row22 {
                        title = 'Грузополучатель';
                        childConstraints = TO THE RIGHT;
                        ADD PROPERTY(fullNameRecipientConsignment);
                        ADD PROPERTY(UNPRecipientConsignment);
                        ADD PROPERTY(addressRecipientConsignment);
                    }
                }
            }
        }
    }
}

editConsignment 'Заполнить атрибуты накладной' (consignment) = ACTION FORM consignment OBJECTS c MODAL IMAGE 'edit.png';


FORM printConsignment 'Печать накладных'
    OBJECTS c=consignment FIXED PANEL

    PROPERTIES (c)  printConsignmentSimpleVertical, printConsignmentSimpleHorizontal, printConsignmentSimpleAttach,
                    printConsignmentVerticalA, printConsignmentHorizontalA, printConsignmentVerticalB,
                    printConsignmentHorizontalB, printConsignmentAttach, editConsignment
;

DESIGN printConsignment FROM DEFAULT {

    c.box {

        childConstraints = TO THE BOTTOM;

        NEW case55{
            childConstraints = TO THE BOTTOM;

            NEW contOne {
                title = 'Накладная';
                ADD PROPERTY(editConsignment);
            }
            NEW tn{
                childConstraints = TO THE RIGHT;
                title = 'ТН-2';
                ADD PROPERTY(printConsignmentSimpleVertical);
                ADD PROPERTY(printConsignmentSimpleHorizontal);
                ADD PROPERTY(printConsignmentSimpleAttach);
            }
        }
        NEW ttn1{
            childConstraints = TO THE RIGHT;
            title = 'ТТН-1';
            NEW ttn1V {
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(printConsignmentVerticalA);
                ADD PROPERTY(printConsignmentVerticalB);
            }
            NEW ttn1H {
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(printConsignmentHorizontalA);
                ADD PROPERTY(printConsignmentHorizontalB);
            }
            NEW ttn1A {
                ADD PROPERTY(printConsignmentAttach);
            }
        }

    }
}

printConsignment 'Печать' (consignment) = ACTION FORM printConsignment OBJECTS c = consignment MODAL IMAGE 'print.png';


// ------------------------------------- Метакод по объявлению и имплементации накладных ------------------ //

META defineConsignment (object)

    car###object 'Автомобиль' (object) = DATA STRING[30] (object) IN carConsignmentGroup;
    ownerCar###object 'Владелец автомобиля' (object) = DATA STRING[100] (object) IN carConsignmentGroup;
    trailer###object 'Прицеп' (object) = DATA STRING[10] (object) IN carConsignmentGroup;
    driverCar###object 'Водитель' (object) = DATA STRING[40] (object) IN carConsignmentGroup;
    waybill###object 'Путевой лист' (object) = DATA STRING[20] (object) IN carConsignmentGroup;

    transportPayer###object 'Заказчик перевозки (ИД)' (object) = DATA legalEntity (object) IN carConsignmentGroup;

    dataLoadingPlace###object 'Пункт погрузки' (object) = DATA STRING[50] (object);

    dataUnloadingPlace###object 'Пункт разгрузки' (object) = DATA STRING[50] (object);

    readdressing###object 'Переадресовка' (object) = DATA STRING[50] (object) IN carConsignmentGroup;

    shipmentBase###object 'Основание отпуска' (object) = DATA STRING[30] (object) IN issuanceConsignmentGroup;

    issuanceAllowed###object (object) = DATA employee(object);

    issuanceExecuted###object (object) = DATA employee(object);

    forwarder###object 'Товар к перевозке принял (экспедитор), должность, фамилия, инициалы' (object) = DATA STRING[40] (object) IN issuanceConsignmentGroup;

    warrant###object 'По доверенности (номер, дата)' (object) = DATA STRING[30] (object) IN issuanceConsignmentGroup;
    warrantHolder###object 'По доверенности выданной (наименование орг-ии)' (object) = DATA STRING[100] (object) IN issuanceConsignmentGroup;

    goodsAccepted###object 'Принял грузополучатель' (object) = DATA STRING[40] (object) IN issuanceConsignmentGroup;

    loadingExecuter###object (object) = DATA employee(object);

    wayOfLoading###object (object) = DATA wayOfLoading(object);

    codeLoading###object 'Код ПРР' (object) = DATA STRING[3] (object) IN loadingConsignmentGroup;

    arrivalTime###object 'Время прибытия' (object) = DATA DATETIME(object) IN carConsignmentGroup;
    departureTime###object 'Время убытия' (object) = DATA DATETIME(object) IN carConsignmentGroup;
    downtime###object 'Время простоя' (object) = DATA STRING[10] (object) IN carConsignmentGroup;

    raceQuantity###object 'Количество ездок' (object) = DATA INTEGER (object) IN carConsignmentGroup;
END

META implementConsignmentHeader (concrete, stockProp)
    dateConsignment (consignment) += date###concrete(consignment);
    stockConsignment (consignment) += stockProp###concrete(consignment);

    carConsignment (consignment) += car###concrete (consignment);
    ownerCarConsignment (consignment) += ownerCar###concrete (consignment);
    trailerConsignment (consignment) += trailer###concrete (consignment);
    driverCarConsignment (consignment) += driverCar###concrete (consignment);
    waybillConsignment (consignment) += waybill###concrete (consignment);

    transportPayerConsignment (consignment) += transportPayer###concrete (consignment);

    dataLoadingPlaceConsignment (consignment) += dataLoadingPlace###concrete (consignment);

    dataUnloadingPlaceConsignment (consignment) += dataUnloadingPlace###concrete (consignment);

    readdressingConsignment (consignment) += readdressing###concrete (consignment);

    shipmentBaseConsignment (consignment) += shipmentBase###concrete (consignment);

    issuanceAllowedConsignment (consignment) += issuanceAllowed###concrete (consignment);

    issuanceExecutedConsignment (consignment) += issuanceExecuted###concrete (consignment);

    forwarderConsignment (consignment) += forwarder###concrete (consignment);

    warrantConsignment (consignment) += warrant###concrete (consignment);
    warrantHolderConsignment (consignment) += warrantHolder###concrete (consignment);

    goodsAcceptedConsignment (consignment) += goodsAccepted###concrete (consignment);

    loadingExecuterConsignment (consignment) += loadingExecuter###concrete (consignment);

    wayOfLoadingConsignment (consignment) += wayOfLoading###concrete (consignment);
    codeLoadingConsignment (consignment) += codeLoading###concrete (consignment);

    arrivalTimeConsignment (consignment) += arrivalTime###concrete (consignment);
    departureTimeConsignment (consignment) += departureTime###concrete (consignment);
    downtimeConsignment (consignment) += downtime###concrete (consignment);

    raceQuantityConsignment (consignment) += raceQuantity###concrete (consignment);
END

META implementConsignmentDetail (concrete)
    consignmentConsignmentDetail (consignmentDetail) += concrete###concrete##Detail (consignmentDetail);
    skuConsignmentDetail (consignmentDetail) += item###concrete##Detail (consignmentDetail);
    quantityConsignmentDetail (consignmentDetail) += quantity###concrete##Detail (consignmentDetail);

    packQuantityConsignmentDetail (consignmentDetail) += packQuantity###concrete##Detail (consignmentDetail);
    grossWeightConsignmentDetail (consignmentDetail) += grossWeight###concrete##Detail (consignmentDetail);
END

META implementConsignment (concrete, stockProp)
    @implementConsignmentHeader(concrete, stockProp);
    @implementConsignmentDetail(concrete);
END

// ------------------------------------- Метакод по объявлению и имплементации накладных (ABSTRACT)------------------ //
META defineConsignmentAbstract(object)

    car###object 'Автомобиль' (object) = ABSTRACT STRING[30] (object) IN carConsignmentGroup;
    ownerCar###object 'Владелец автомобиля' (object) = ABSTRACT STRING[100] (object) IN carConsignmentGroup;
    trailer###object 'Прицеп' (object) = ABSTRACT STRING[10] (object) IN carConsignmentGroup;
    driverCar###object 'Водитель' (object) = ABSTRACT STRING[40] (object) IN carConsignmentGroup;
    waybill###object 'Путевой лист' (object) = ABSTRACT STRING[20] (object) IN carConsignmentGroup;

    transportPayer###object 'Заказчик перевозки (ИД)' (object) = ABSTRACT legalEntity (object) IN carConsignmentGroup;

    dataLoadingPlace###object 'Пункт погрузки' (object) = ABSTRACT STRING[50] (object);

    dataUnloadingPlace###object 'Пункт разгрузки' (object) = ABSTRACT STRING[50] (object);

    readdressing###object 'Переадресовка' (object) = ABSTRACT STRING[50] (object) IN carConsignmentGroup;

    shipmentBase###object 'Основание отпуска' (object) = ABSTRACT STRING[30] (object) IN issuanceConsignmentGroup;

    issuanceAllowed###object (object) = ABSTRACT employee(object);

    issuanceExecuted###object (object) = ABSTRACT employee(object);

    forwarder###object 'Товар к перевозке принял (экспедитор), должность, фамилия, инициалы' (object) = ABSTRACT STRING[40] (object) IN issuanceConsignmentGroup;

    warrant###object 'По доверенности (номер, дата)' (object) = ABSTRACT STRING[30] (object) IN issuanceConsignmentGroup;
    warrantHolder###object 'По доверенности выданной (наименование орг-ии)' (object) = ABSTRACT STRING[100] (object) IN issuanceConsignmentGroup;

    goodsAccepted###object 'Принял грузополучатель' (object) = ABSTRACT STRING[40] (object) IN issuanceConsignmentGroup;

    loadingExecuter###object (object) = ABSTRACT employee(object);

    wayOfLoading###object (object) = ABSTRACT wayOfLoading(object);

    codeLoading###object 'Код ПРР' (object) = ABSTRACT STRING[3] (object) IN loadingConsignmentGroup;

    arrivalTime###object 'Время прибытия' (object) = ABSTRACT DATETIME(object) IN carConsignmentGroup;
    departureTime###object 'Время убытия' (object) = ABSTRACT DATETIME(object) IN carConsignmentGroup;
    downtime###object 'Время простоя' (object) = ABSTRACT STRING[10] (object) IN carConsignmentGroup;

    raceQuantity###object 'Количество ездок' (object) = ABSTRACT INTEGER (object) IN carConsignmentGroup;
END

META implementConsignmentDocumentHeaderCustom (concrete, object, stockProp)
//    date###concrete (concrete) += date###object(concrete);
//    stock###concrete (concrete) += stockProp###object(concrete);

    car###concrete (concrete) += car###object (concrete);
    ownerCar###concrete (concrete) += ownerCar###object (concrete);
    trailer###concrete (concrete) += trailer###object (concrete);
    driverCar###concrete (concrete) += driverCar###object (concrete);
    waybill###concrete (concrete) += waybill###object (concrete);

    transportPayer###concrete (concrete) += transportPayer###object (concrete);

    dataLoadingPlace###concrete (concrete) += dataLoadingPlace###object (concrete);

    dataUnloadingPlace###concrete (concrete) += dataUnloadingPlace###object (concrete);

    readdressing###concrete (concrete) += readdressing###object (concrete);

    shipmentBase###concrete (concrete) += shipmentBase###object (concrete);

    issuanceAllowed###concrete (concrete) += issuanceAllowed###object (concrete);

    issuanceExecuted###concrete (concrete) += issuanceExecuted###object (concrete);

    forwarder###concrete (concrete) += forwarder###object (concrete);

    warrant###concrete (concrete) += warrant###object (concrete);
    warrantHolder###concrete (concrete) += warrantHolder###object (concrete);

    goodsAccepted###concrete (concrete) += goodsAccepted###object (concrete);

    loadingExecuter###concrete (concrete) += loadingExecuter###object (concrete);

    wayOfLoading###concrete (concrete) += wayOfLoading###object (concrete);
    codeLoading###concrete (concrete) += codeLoading###object (concrete);

    arrivalTime###concrete (concrete) += arrivalTime###object (concrete);
    departureTime###concrete (concrete) += departureTime###object (concrete);
    downtime###concrete (concrete) += downtime###object (concrete);

    raceQuantity###concrete (concrete) += raceQuantity###object (concrete);


END
META implementConsignmentDocumentHeader (concrete, stockProp)
    @implementConsignmentDocumentHeaderCustom (concrete, user###concrete, stockProp)
END


META defineConsignmentInterface(object, stockProp)
    @defineConsignmentAbstract (object);
    @defineConsignment (user###object);
    @implementConsignmentDocumentHeaderCustom(object, user###object, stockProp);
    @implementConsignmentHeader (object, stockProp);
END

META defineConsignmentInterface(object)
    @defineConsignmentInterface(object, stock);
END

META implementConsignmentDetailSku (concrete, skuProp)
    consignmentConsignmentDetail (consignmentDetail) += concrete###concrete##Detail (consignmentDetail);
    skuConsignmentDetail (consignmentDetail) += skuProp###concrete##Detail (consignmentDetail);
    quantityConsignmentDetail (consignmentDetail) += quantity###concrete##Detail (consignmentDetail);

    packQuantityConsignmentDetail (consignmentDetail) += packQuantity###concrete##Detail (consignmentDetail);
    grossWeightConsignmentDetail (consignmentDetail) += grossWeight###concrete##Detail (consignmentDetail);
END
META implementConsignmentDetailSku (concrete)
    @implementConsignmentDetailSku (concrete, sku);
END

