MODULE  BOM;

REQUIRE Stock,
        Numerator,
        Barcode,
        Document,
        Utils,
        Manufacturing,
        Integration;


// ---------------- Материалы ------------------- //
CLASS ABSTRACT Material 'Материал';
TABLE material(Material);

nameMaterial 'Наименование' (material) = ABSTRACT STRING[255] (Material) PERSISTENT MINCHARWIDTH 40 PREFCHARWIDTH 80;

UOMMaterial (material) = ABSTRACT UOM (Material) PERSISTENT;
shortNameUOMMaterial 'Ед. изм.' (material) = shortName(UOMMaterial(material)) IN recognize;

editMaterial 'Редактировать' = ABSTRACT ACTION (material) IMAGE 'edit.png' EDITKEY 'BACK_SPACE' HIDE TOOLBAR;
addMaterial 'Добавить' = ABSTRACT ACTION () IMAGE 'add.png' EDITKEY 'INSERT' HIDE TOOLBAR;

skuMaterial (material) = ABSTRACT Sku (Material) PERSISTENT;

skuGroupMaterial (material) = skuGroupSku(skuMaterial(material)) PERSISTENT;
isParentSkuGroupMaterial (skuGroup, material) = isParentSkuGroupSkuGroup(skuGroupMaterial(material), skuGroup);

FORM materials 'Материалы'

    TREE skuTree sg = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuGroupName = name(sg)

    OBJECTS m = Material
    PROPERTIES(m) READONLY nameMaterial, shortNameUOMMaterial, objectClassName
    PROPERTIES() addMaterial TODRAW m
    PROPERTIES(m) editMaterial
    FILTERS isParentSkuGroupMaterial(sg, m)
    ORDER BY nameMaterial

    DIALOG Material OBJECT m
;

DESIGN materials FROM DEFAULT {
    main{
        preferredSize = (1024, 768);

        NEW topContainer {
            type = SPLITH;
            childConstraints = TO THE RIGHT;

            ADD skuTree.tree.box {
                title = 'Группы Материалов';
            }

            ADD m.box {
                m.grid {
                    defaultComponent = TRUE;
                    fillHorizontal = 4;
                }
            }
        }

        ADD functions.box;
    }
}


// --------------- Товары ----------------- //
EXTEND CLASS Sku : Material;

nameMaterial(material) += nameSku(material);
UOMMaterial(material) += UOMSku(material);

editMaterial(sku) += editSku(sku);
addMaterial () += addSku();

skuMaterial(material) += material AS Sku;

// -----------  Технологии -------------- //
CLASS Technology 'Технология' : Named;
TABLE technology (Technology);

@defineHistorizable(wastageTechnologyMaterial, '% потерь', NUMERIC[8,3], technology, name, material, nameMaterial, base);


FORM technology 'Технология'

    OBJECTS dt = DATE FIXED PANEL
    PROPERTIES valDt = OBJVALUE(dt) BACKGROUND dateDiffersCurrent(dt)

    OBJECTS t=Technology FIXED PANEL
    PROPERTIES(t) name

    TREE skuTree sg = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuGroupName = name(sg)

    OBJECTS m = Material
    PROPERTIES(m) nameMaterial, shortNameUOMMaterial
    PROPERTIES overWastageTechnologyMaterialDate(t,m,dt)

    FILTERS isParentSkuGroupMaterial(sg, m)
    ORDER BY nameMaterial

    FILTERGROUP filters
        FILTER 'С % потерь' 'F9' wastageTechnologyMaterialDate(t,m,dt)

    OBJECTS d=DATE
    PROPERTIES  READONLY OBJVALUE(d), dataWastageTechnologyMaterialDate(t,m,d)
    FILTERS dataWastageTechnologyMaterialDate(t,m,d)

    EDIT Technology OBJECT t
;

DESIGN technology FROM DEFAULT{
    main {
        preferredSize = (1024, 768);
        NEW specification.box BEFORE functions.box {
            childConstraints = TO THE RIGHT;
            ADD skuTree.tree.box {title = 'Группа материалов'; fillHorizontal=1; }
            NEW row {
                childConstraints = TO THE BOTTOM;
                ADD m.box {
                    title = 'Материал';
                    fillHorizontal=2;
                    fillVertical=3;
                }
                ADD d.box {
                    title = 'История изменений';
                    fillVertical=1;
                }
            }
        }
        NEW headerBox BEFORE specification.box {
            childConstraints = TO THE RIGHT;
            ADD t.box { childConstraints = TO THE RIGHT;}
            ADD dt.box {childConstraints = TO THE RIGHT;}
        }
    }
}

FORM yearFromTo 'Год'

    OBJECTS y = INTEGER FIXED PANEL
    PROPERTIES valY = OBJVALUE(y)
;

DESIGN yearFromTo FROM DEFAULT{
    main {
        NEW year{
            ADD PROPERTY (valY) { caption = 'Укажите на какой год скопировать значения (4 цифры)'; panelLabelAbove = TRUE; font = 'Tahoma bold 72'; minimumCharWidth = 4; preferredCharWidth = 4; maximumCharWidth = 4; }
        }
        ADD functions.box;
    }
}


fillWastageTechnology 'Заполнить технологии на другой год' = ACTION (technology) {

    FORM yearFromTo MODAL;
    IF formResult() == FormResult.ok THEN {

        LOCAL newYear = INTEGER();
        SET newYear() <- chosenInteger('y');

        FOR dataWastageTechnologyMaterialDate(technology, material, dateFrom) DO {

            IF 1 == 2 THEN
                BREAK;

            IF yearInDate(dateFrom) == (newYear() - 1) THEN {
                SET dataWastageTechnologyMaterialDate(technology, material, dateTo) <- dataWastageTechnologyMaterialDate(technology, material, dateFrom) WHERE dateTo == incrementYearDate(dateFrom);
            }

        }
        EXEC apply();
    }
} TOOLBAR;

FORM technologies 'Технологии'

    OBJECTS t=Technology
    PROPERTIES(t)  READONLY name

    PROPERTIES (t) ADDFORM, EDITFORM, delete FORCE PANEL TOOLBAR
    PROPERTIES (t) fillWastageTechnology

    OBJECTS m=Material
    PROPERTIES     READONLY nameMaterial(m), shortNameUOMMaterial(m), wastageTechnologyMaterial(t,m)
    ORDER BY nameMaterial

    FILTERS        wastageTechnologyMaterial(t,m)
;
DESIGN technologies FROM DEFAULT{
    main {
        ADD t.box;
        ADD m.box { title = 'Материалы с % потерь на текущую дату';}
        ADD functions.box;
    }
}

// ---------------------- BOM'ы ----------------------

CLASS BOM 'Спецификация' : Named, NumeratedObject, Historizable;
TABLE BOM (BOM);

@defineDocumentHeaderNote(BOM);
@defineNumeratedObjectDefault(BOM, 'Нумератор для спецификаций', 'СП');

descriptionBOM 'Название спецификации' (BOM) =
    [FORMULA STRING[200]  ' CAST($1 AS TEXT) || \' / \' || CAST($2 AS TEXT) || \' № \' || CAST($3 AS TEXT)'](
    'Спецификация', name(BOM), seriesNumberObject(BOM)) IF BOM IS BOM MINCHARWIDTH 30 PREFCHARWIDTH 50;

// ------------- Изделия ----------- //
CLASS Product 'Изделие' : Externalizable, Material;
TABLE product (Product);

EXTEND FORM materials

    FILTERGROUP filters
        FILTER 'Изделие' 'F11' m AS Product
        FILTER 'Sku' 'F10' m AS Sku
;

EXTEND FORM technology

    FILTERGROUP filters
        FILTER 'Изделие' 'F11' m AS Product
        FILTER 'Sku' 'F10' m AS Sku
;

skuProduct = DATA Sku (Product);
nameSkuProduct 'SKU' (product) = nameSku(skuProduct(product)) IN recognize;
idBarcodeSkuProduct 'Штрих-код' (product)= idBarcodeSku(skuProduct(product));

UOMProduct = DATA UOM (Product);
shortNameUOMProduct 'Ед.изм.' (product) = shortName(UOMProduct(product)) IN recognize;
UOMProduct(product) <- UOMSku(skuProduct(product)) WHEN CHANGED (skuProduct(product));


@defineDocumentRelation (BOM, Product, ' (изделие)');
descriptionProduct 'Название документа' (product) = descriptionBOM(BOMProduct(product));

@defineDocumentDetailIndex (BOM, Product);
@defineDocumentDetailActionsCustom (BOM, Product);

@defineDocumentDetailQuantityCustomPrefix (product, , );

@defineDocumentHeaderQuantityCustomPrefix (BOM, product, , ' (изделие)');

pricePercentProduct 'Удельный вес цены,%' = DATA NUMERIC[8,3] (Product);
pricePercentProduct(product) <- 100.000 WHEN ASSIGNED (product IS Product);
priceCoeffProduct (product) = pricePercentProduct(product) / 100 PERSISTENT;

pricePercentBOM 'Удельный вес цен, %' = GROUP SUM pricePercentProduct(product) BY BOMProduct(product) PERSISTENT IN documentSumGroup;

nameMaterial(material) += [FORMULA STRING[255]  ' CAST($1 AS TEXT) || \' / \' ||  CAST($2 AS TEXT)'](
                                      name(BOMProduct(material)),
                                      nameSkuProduct(material));

UOMMaterial(material) += UOMSku(skuProduct(material));
skuMaterial (material) += skuProduct(material);

FORM products 'Изделия'
    OBJECTS p=Product
    PROPERTIES(p) READONLY descriptionProduct, indexProduct, nameSkuProduct, shortNameUOMProduct, quantityProduct
    PROPERTIES (p) ADDSESSIONFORM, EDITSESSIONFORM, delete

    DIALOG Product OBJECT p
;


// ------------- Компоненты ----------- //
CLASS Component 'Компонент' : Named;
TABLE component (Component);

@defineDocumentRelation (BOM, Component, ' (компонент)');
descriptionComponent 'Название документа' (component)= descriptionBOM(BOMComponent(component));

@defineDocumentDetailIndex (BOM, Component);
@defineDocumentDetailActionsCustom (BOM, Component);

materialComponent = DATA Material (Component);
nameMaterialComponent 'Материал' (component) = nameMaterial(materialComponent(component));
idBarcodeSkuComponent 'Штрих-код' (component)= idBarcodeSku(materialComponent(component));

componentsBOM 'Компоненты' (BOM) = GROUP CONCAT toString255(nameMaterialComponent(component)), ', '
                                       BY BOMComponent(component)
                                       ORDER indexComponent(component) MINCHARWIDTH 100 PERSISTENT;

nameComponent 'Компонент' (component) = [FORMULA STRING[255]  ' CAST($1 AS TEXT) || \' / \' ||  CAST($2 AS TEXT)'](
                                      name(BOMComponent(component)),
                                      nameMaterialComponent(component)) PERSISTENT;

UOMComponent = DATA UOM (Component);
shortNameUOMComponent 'Ед. изм.' (component) = shortName(UOMComponent(component));
UOMComponent(component) <- UOMMaterial(materialComponent(component)) WHEN CHANGED (materialComponent(component));

// Кол-во нетто
@defineDocumentDetailQuantityCustomPrefix (component, netto, ' (нетто)');
@defineDocumentHeaderQuantityCustomPrefix (BOM, component, netto, ' (нетто компонент)');

// Технология
technologyComponent = DATA Technology (Component);
nameTechnologyComponent 'Технология' (component) = name(technologyComponent(component));

inTechnologyMaterial 'Связь' (technology, material) = GROUP SUM 1 IF dataWastageTechnologyMaterialDate(technology, material, date) BY technology, material;

CONSTRAINT technologyComponent(component) AND NOT inTechnologyMaterial(technologyComponent(component), materialComponent(component))
    CHECKED BY technologyComponent MESSAGE 'Для компонента выбрана технология, у которой нет значения % потерь для SKU';

// % Потерь
wastageComponent '% потерь'= DATA NUMERIC[8,3] (Component);
changeWastageComponent = ACTION (component) {
    REQUEST NUMERIC[8,3] INPUT;

    IF requestedNumeric() AND NOT technologyComponent(component) THEN {
        SET wastageComponent(component) <- requestedNumeric();
    } ELSE {
        SET wastageComponent(component) <- NULL;
    }
}
backgroundWastageComponent 'Цвет' (component) = RGB(229,229,229) IF technologyComponent(component);

wastageComponentDate '% потерь' (component, date) = IF technologyComponent(component) THEN wastageTechnologyMaterialDate(technologyComponent(component),materialComponent(component), date)
                                                    ELSE wastageComponent(component) AND date IS DATE;

curWastageComponent '% потерь' (component) =  wastageComponentDate(component, currentDate());

overCurWastageComponent '% потерь' (component) =  wastageTechnologyMaterialDate(technologyComponent(component),materialComponent(component), currentDate()) OR
                                                  wastageComponent(component);

// Кол-во брутто

@defineDocumentDetailQuantityCustomPrefix (component, brutto, ' (брутто)');
@defineDocumentHeaderQuantityCustomPrefix (BOM, component, brutto, ' (брутто компонент)');

bruttoQuantityComponent(component) <- nettoQuantityComponent(component)/(1 - (wastageComponent(component)/100)) WHEN CHANGED (wastageComponent(component)) OR
                                                                                                                     CHANGED (nettoQuantityComponent(component));
changeBruttoQuantityComponent = ACTION (component) {
    REQUEST NUMERIC[14,3] INPUT;

    IF requestedNumeric() AND NOT technologyComponent(component) THEN {
        SET bruttoQuantityComponent(component) <- requestedNumeric();
    } ELSE {
        SET bruttoQuantityComponent(component) <- NULL;
    }
}
backgroundBruttoQuantityComponent 'Цвет' (component) = RGB(229,229,229) IF technologyComponent(component);

bruttoQuantityComponentDate(component, date) = IF technologyComponent(component) THEN (nettoQuantityComponent(component)/(1 - (wastageTechnologyMaterialDate(technologyComponent(component), materialComponent(component), date)/100)))
                                                                                 ELSE bruttoQuantityComponent(component) AND date IS DATE;

curBruttoQuantityComponent 'Кол-во (брутто)' (component) =  bruttoQuantityComponentDate(component, currentDate());

overCurBruttoQuantityComponent 'Кол-во (брутто)' (component) = (nettoQuantityComponent(component)/(1 - (wastageTechnologyMaterialDate(technologyComponent(component),materialComponent(component), currentDate())/100))) OR
                                                            bruttoQuantityComponent(component);

FORM components 'Компоненты'
    OBJECTS c=Component
    PROPERTIES(c) READONLY descriptionComponent, indexComponent, nameMaterialComponent, shortNameUOMComponent, nettoQuantityComponent
    PROPERTIES (c) ADDSESSIONFORM, EDITSESSIONFORM, delete

    DIALOG Component OBJECT c
;

//------------------------------- Формы -------------------------------------//
FORM BOM 'Спецификация'

    OBJECTS b=BOM FIXED PANEL
//    OBJECTS dt = DATE FIXED PANEL

    PROPERTIES(b) name, nameNumeratorObject, numberObject, seriesObject, noteBOM, objectClassName
    PROPERTIES(b) READONLY quantityProductBOM, nettoQuantityComponentBOM, pricePercentBOM//, bruttoQuantityComponentBOM
    OBJECTS p = Product
    PROPERTIES(p) indexProduct, idBarcodeSkuProduct,nameSkuProduct, shortNameUOMProduct, quantityProduct, pricePercentProduct

    PROPERTIES(p) ADDOBJ, delete
    PROPERTIES(b) TODRAW p deleteProductBOM
    FILTERS       BOMProduct(p) == b

    OBJECTS c = Component
    PROPERTIES(c) indexComponent, idBarcodeSkuComponent, nameMaterialComponent, shortNameUOMComponent, nameTechnologyComponent, nettoQuantityComponent,
                  overCurWastageComponent ON CHANGE changeWastageComponent(c) BACKGROUND backgroundWastageComponent(c) READONLYIF technologyComponent(c),
                  overCurBruttoQuantityComponent ON CHANGE changeBruttoQuantityComponent(c) BACKGROUND backgroundBruttoQuantityComponent(c) READONLYIF technologyComponent(c)

    PROPERTIES(c) ADDOBJ, delete
    PROPERTIES(b) TODRAW c deleteComponentBOM
    FILTERS       BOMComponent(c) == BOMProduct(p)//b

    EDIT BOM OBJECT b
;

DESIGN BOM FROM DEFAULT{

    main {
        preferredSize = (1024, 768);
        NEW specification.box BEFORE functions.box{
            type = TABBED;
            ADD p.box {title = 'Изделие';}
            ADD c.box {title = 'Компонент';}
        }

        ADD b.box BEFORE specification.box {
            childConstraints = TO THE RIGHT;
            NEW headerRow1 {
                childConstraints = TO THE BOTTOM;

                NEW headerRow11 {
                    title = 'Шапка';
                    childConstraints = TO THE RIGHTBOTTOM;
                    ADD PROPERTY(objectClassName) { preferredCharWidth = 10; }
                    ADD PROPERTY(name) { preferredCharWidth = 20; }
                    ADD PROPERTY(nameNumeratorObject);
                    ADD PROPERTY(numberObject);
                    ADD PROPERTY(seriesObject);
                }
                ADD b.documentPrmGroup {
                    childConstraints = TO THE RIGHTBOTTOM;
                }
                NEW kitchen {
                    title = 'Кухня';
                    childConstraints = TO THE RIGHTBOTTOM;
                }

            }
            ADD b.documentSumGroup {
                childConstraints = TO THE BOTTOM;
            }
        }
    }
}
copyBOM 'Копировать' = ACTION (BOM) NEWSESSION {
    FOR ADDOBJ b = BOM DO {

        SET noteBOM(b) <- noteBOM(BOM);
        FOR BOMProduct(product) == BOM ADDOBJ p=Product DO {
            SET BOMProduct(p) <- b;
            SET skuProduct(p) <- skuProduct(product);
            SET quantityProduct(p) <- quantityProduct(product);
            SET pricePercentProduct(p) <- pricePercentProduct(product);
        }

        FOR BOMComponent(component) == BOM ADDOBJ c=Component DO {
            SET BOMComponent(c) <- b;
            SET materialComponent(c) <- materialComponent(component);
            SET nettoQuantityComponent(c) <- nettoQuantityComponent(component);
            SET technologyComponent(c) <- technologyComponent(component);
            SET bruttoQuantityComponent(c) <- bruttoQuantityComponent(component);
            SET wastageComponent(c) <- wastageComponent(component);
        }

        FORM BOM OBJECTS b = b MANAGESESSION DOCKEDMODAL;
    }
} TOOLBAR;

FORM BOMs 'Спецификации'

    OBJECTS b=BOM

    PROPERTIES(b) READONLY name, numberObject, seriesObject, quantityProductBOM, nettoQuantityComponentBOM, pricePercentBOM, noteBOM, objectClassName
    PROPERTIES (b) ADDFORM, EDITFORM, copyBOM, delete FORCE PANEL TOOLBAR
    PROPERTIES (b) READONLY FORCE PANEL nameUserCreatedHistorizable, timeCreatedHistorizable, hostnameComputerCreatedHistorizable, nameUserClosed, timeClosed, hostnameComputerClosed

    OBJECTS p = Product
    PROPERTIES(p) READONLY indexProduct, idBarcodeSkuProduct, nameSkuProduct, shortNameUOMProduct, quantityProduct, pricePercentProduct
    FILTERS       BOMProduct(p) == b

    OBJECTS c = Component
    PROPERTIES(c) READONLY indexComponent, idBarcodeSkuComponent, nameMaterialComponent, shortNameUOMComponent, nameTechnologyComponent, nettoQuantityComponent,
                  overCurWastageComponent, overCurBruttoQuantityComponent
    FILTERS       BOMComponent(c) == b
;

DESIGN BOMs FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        PROPERTY (delete(b)) {
            askConfirm = TRUE;
        }

        NEW documentContainer BEFORE functions.box {
            type = SPLITV;
            childConstraints = TO THE BOTTOM;

            ADD b.box;

            NEW documentDetail {
                type = TABBED;

                ADD p.box {
                    title = 'Изделие';
                }
                ADD c.box {
                    title = 'Компонент';
                }

                NEW documentHistory {
                    title = 'История';

                    ADD b.historyGroup;
                }
                NEW printTab {
                    title = 'Печатные формы';
                    NEW printContainer {
                        title = 'Печать';
                        childConstraints = TO THE BOTTOM;
                        fillVertical = 2.0; // todo : иначе кнопка не всегда показывается, нужно будет пофиксить как-нибудь
                    }
                }
            }
        }
    }
}

//-------------------------------- Комиссия для акта контрольной проработки ----------------------------------//
CLASS ActStudyOfCulinaryCommittee 'Комиссия для акта контрольной проработки продукции' : Committee;

FORM actStudyOfCulinaryCommittee 'Комиссия для акта контрольной проработки продукции'
    OBJECTS c=ActStudyOfCulinaryCommittee FIXED PANEL
    PROPERTIES(c)      name

    TREE stockTree sg = StockGroup PARENT parentStockGroup
    PROPERTIES READONLY sgTreeName = name(sg)

    OBJECTS ts=Stock
    PROPERTIES    READONLY tsTreeName = name(ts)
    PROPERTIES(c, ts) inCommitteeEmployeeDivision FORCE GRID

    FILTERS isParentStockGroupStock(sg, ts)
    ORDER BY tsTreeName


    OBJECTS e=Employee
    PROPERTIES(e)      READONLY name, firstNameContact, lastNameContact, namePositionEmployee
    PROPERTIES(e)      ADDSESSIONFORM, EDITSESSIONFORM, delete

    PROPERTIES(c, e)   inCommitteeEmployee
    FILTERS            countDivisionEmployeeCommittee (e, c)
    FILTERGROUP filters6
        FILTER 'Показывать только членов комиссии' 'F10' inCommitteeEmployee(c, e)
    FILTERGROUP filters5
        FILTER 'Показывать отделы только для данной комиссии' 'F9' inCommitteeEmployeeDivision(c, ts)

    EDIT ActStudyOfCulinaryCommittee OBJECT c
;

DESIGN actStudyOfCulinaryCommittee FROM DEFAULT {
    main {
        preferredSize = (1024, 768);

        NEW caseOne BEFORE e.box {
            childConstraints = TO THE RIGHT;
            title = 'Отделы, для которых действуют комиссии';

            ADD stockTree.tree {
                fillHorizontal = 1;
            }

            ADD ts.box {
                fillHorizontal = 2;
            }
        }
    }
}

FORM actStudyOfCulinaryCommittees 'Комиссии для акта контрольной проработки продукции'
    OBJECTS a=ActStudyOfCulinaryCommittee
    PROPERTIES(a)      READONLY name, nameEmployeeCommittee
    PROPERTIES(a)      ADDFORM, EDITFORM

    DIALOG ActStudyOfCulinaryCommittee OBJECT a
;

// ----------------- Акт контрольной проработки-------------------- //

actStudyOfCulinaryCommitteeBOM (BOM) = DATA ActStudyOfCulinaryCommittee(BOM);
nameActStudyOfCulinaryCommitteeBOM 'Комиссия для акта контрольной проработки продукции' (BOM) = name(actStudyOfCulinaryCommitteeBOM (BOM)) IN documentPrmGroup MINCHARWIDTH 20 PREFCHARWIDTH 40;
nameEmployeeCommitteeBOM 'Члены комиссии' (BOM)= nameEmployeeCommittee(actStudyOfCulinaryCommitteeBOM(BOM));

inCommitteeBOMEmployee (BOM, employee) = inCommitteeEmployee(actStudyOfCulinaryCommitteeBOM (BOM), employee);
commonNameEmployeeCommitteeBOM 'Члены комиссии' (BOM) = namePositionEmployeeCommittee(actStudyOfCulinaryCommitteeBOM (BOM));

companyBOM (BOM) = DATA LegalEntity(BOM);
nameCompanyBOM 'Компания' (BOM) = name(companyBOM (BOM));

CONSTRAINT  companyBOM(BOM) AND NOT isCompanyLegalEntity(companyBOM (BOM)) CHECKED BY companyBOM
    MESSAGE 'Для спецификации выбрано организация, которая не является компанией';

EXTEND FORM BOM
    PROPERTIES(b) nameActStudyOfCulinaryCommitteeBOM, nameCompanyBOM

    OBJECTS e=Employee
    PROPERTIES(e) READONLY nameContact, namePositionEmployee
    FILTERS       inCommitteeBOMEmployee(b,e)
;
EXTEND DESIGN BOM {

    specification.box {
        NEW row {
            childConstraints= TO THE BOTTOM;
            title = 'Акт контрольной проработки';
            NEW row1{
                title = 'Акт';
                childConstraints= TO THE RIGHTBOTTOM;
                ADD PROPERTY(nameCompanyBOM);
                ADD PROPERTY(nameActStudyOfCulinaryCommitteeBOM);
            }
            ADD e.box {title = 'Сотрудники комиссии';}
        }
    }
}

// ----- Рекурсивный расчет потребностей брутто ---------- //

// кол-во требуемых материалов материалов
bruttoQuantityBOMMaterialDate (BOM, material, date) = GROUP SUM bruttoQuantityComponentDate(component, date) BY BOMComponent(component), materialComponent(component), date;
bruttoQuantityBOMSkuDate(BOM, sku, date) = bruttoQuantityBOMMaterialDate(BOM, sku, date) AND sku IS Sku;

bruttoQuantityBOMBOMDate (parent, child, date) = GROUP MAX bruttoQuantityBOMMaterialDate(BOM, material, date) / quantityProduct(material) BY BOM, BOMProduct(material), date;

recBruttoQuantityBOMBOMDateResult (parent, child, date, q) = RECURSION 1.00000000000 AND q == 1.00000000000 AND child == (parent AS BOM) AND date IS DATE STEP 1.00000000000 AND q == $q * bruttoQuantityBOMBOMDate($child, child, date) CYCLES YES;
recBruttoQuantityBOMBOMDate (parent, child, date) = GROUP SUM recBruttoQuantityBOMBOMDateResult(parent, child, date, q) * q BY parent, child, date;
//recBruttoQuantityBOMBOMDate (parent, child, date) = 1.0 IF (parent AS BOM) == (child AS BOM) AND date IS DATE;

recBruttoQuantityBOMSkuDate (BOM, sku, date) = GROUP SUM recBruttoQuantityBOMBOMDate(BOM, childBOM, date) * bruttoQuantityBOMSkuDate(childBOM, sku, date) BY BOM, sku, date; //](BOM, sku);

recBruttoNodeQuantityBOMSkuDate (BOM, sku, date) = GROUP SUM recBruttoQuantityBOMBOMDate(BOM, childBOM, date) * bruttoQuantityBOMMaterialDate(childBOM, material, date) BY BOM, skuMaterial(material), date; //](BOM, sku);

// Расчет цены
priceCoeffBOMBOMDate(parent, child, date) = GROUP SUM bruttoQuantityBOMMaterialDate(BOM, material, date) * priceCoeffProduct(material) / quantityProduct(material) BY BOM, BOMProduct(material), date;

recPriceCoeffBOMBOMDateResult (parent, child, date, q) = RECURSION 1.00000000000 AND q == 1.00000000000 AND child == (parent AS BOM) AND date IS DATE STEP 1.00000000000 AND q == $q * priceCoeffBOMBOMDate($child, child, date) CYCLES YES;
recPriceCoeffBOMBOMDate (parent, child, date) = GROUP SUM recPriceCoeffBOMBOMDateResult(parent, child, date, q) * q BY parent, child, date;
//recPriceCoeffBOMBOMDate (parent, child, date) = 1.0 IF (parent AS BOM) == (child AS BOM) AND date IS DATE;

//
recPriceCoeffBOMSkuDate (BOM, sku, date) = GROUP SUM recPriceCoeffBOMBOMDate(BOM, childBOM, date) * bruttoQuantityBOMMaterialDate(childBOM, material, date) BY BOM, skuMaterial(material), date;
recPricePercentProductSkuDate (product, sku, date) = recPriceCoeffBOMSkuDate (BOMProduct(product), sku, date) * pricePercentProduct(product);

// ----- Рекурсивный расчет потребностей нетто ---------- //

nettoQuantityBOMMaterial (BOM, material) = GROUP SUM nettoQuantityComponent(component) BY BOMComponent(component), materialComponent(component);
nettoQuantityBOMSku(BOM, sku) = nettoQuantityBOMMaterial(BOM, sku) AND sku IS Sku;

nettoQuantityBOMBOM (parent, child) = GROUP MAX nettoQuantityBOMMaterial(BOM, material) / quantityProduct(material) BY BOM, BOMProduct(material);

recNettoQuantityBOMBOMResult (parent, child, q) = RECURSION 1.00000000000 AND q == 1.00000000000 AND child == (parent AS BOM)  STEP 1.00000000000 AND q == $q * nettoQuantityBOMBOM($child, child) CYCLES YES;
recNettoQuantityBOMBOM (parent, child) = GROUP SUM recNettoQuantityBOMBOMResult(parent, child, q) * q BY parent, child;

recNettoQuantityBOMSku (BOM, sku) = GROUP SUM recNettoQuantityBOMBOM(BOM, childBOM) * nettoQuantityBOMSku(childBOM, sku) BY BOM, sku; //](BOM, sku);

recNettoNodeQuantityBOMSku (BOM, sku) = GROUP SUM recNettoQuantityBOMBOM(BOM, childBOM) * nettoQuantityBOMMaterial(childBOM, material) BY BOM, skuMaterial(material); //](BOM, sku);

//recNettoNodeQuantityBOMSku (BOM, sku) =  DATA NUMERIC[14,3] (BOM, Sku);   // пока сделал так, потому что очень тормозит.

NAVIGATOR {
    manufacturingMasterData {
        ADD BOMs;
        ADD technologies;
        ADD actStudyOfCulinaryCommittees;
    }
}