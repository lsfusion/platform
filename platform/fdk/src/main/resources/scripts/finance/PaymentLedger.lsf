MODULE PaymentLedger;

REQUIRE LegalEntity;

// ---------------- Счет ------------- //

CLASS ABSTRACT Account 'Счет';
TABLE account (Account);
nameAccount 'Имя' = ABSTRACT STRING[30] (Account) PERSISTENT;

currencyAccount = ABSTRACT Currency (Account);
nameCurrencyAccount 'Валюта счета' = nameCurrency(currencyAccount(account));

legalEntityAccount = ABSTRACT LegalEntity (Account);
nameLegalEntityAccount 'Организация' (account) = nameLegalEntity(legalEntityAccount(account));

editAccount 'Редактировать' (account) = ABSTRACT ACTION (Account) IMAGE 'edit.png' EDITKEY 'BACK_SPACE' HIDE TOOLBAR;

FORM accounts 'Счета'
    OBJECTS a = Account
    PROPERTIES(a) READONLY nameAccount, nameCurrencyAccount, nameLegalEntityAccount
    PROPERTIES(a) editAccount FORCE PANEL TOOLBAR, DELETE

    DIALOG Account OBJECT a
;

// ---------------- Регистр денег ------------- //

CLASS ABSTRACT PaymentLedger 'Движение денег';
TABLE paymentLedger(PaymentLedger);

dateTimePaymentLedger 'Дата/время' = ABSTRACT DATETIME (PaymentLedger) PERSISTENT;

legalEntityFromPaymentLedger = ABSTRACT LegalEntity (PaymentLedger) PERSISTENT;
nameLegalEntityFromPaymentLedger 'Организация-плательщик' = nameLegalEntity(legalEntityFromPaymentLedger(ledger));

accountFromPaymentLedger = ABSTRACT Account (PaymentLedger) PERSISTENT;
nameAccountFromPaymentLedger 'Счет (с)' (ledger) = nameAccount(accountFromPaymentLedger(ledger));

legalEntityToPaymentLedger = ABSTRACT LegalEntity (PaymentLedger) PERSISTENT;
nameLegalEntityToPaymentLedger 'Организация-получатель' = nameLegalEntity(legalEntityToPaymentLedger(ledger));

accountToPaymentLedger = ABSTRACT Account (PaymentLedger) PERSISTENT;
nameAccountToPaymentLedger 'Счет (на)' (ledger) = nameAccount(accountToPaymentLedger(ledger));

sumFromPaymentLedger 'Сумма (с)' = ABSTRACT NUMERIC[16,2] (PaymentLedger) PERSISTENT;
sumToPaymentLedger 'Сумма (на)' = ABSTRACT NUMERIC[16,2] (PaymentLedger) PERSISTENT;

descriptionPaymentLedger 'Описание' = ABSTRACT STRING[200] (PaymentLedger) PERSISTENT;

isPostedPaymentLedger 'Проведен' = ABSTRACT BOOLEAN(PaymentLedger);

FORM paymentLedger 'Движение денег'
    OBJECTS pl = PaymentLedger
    PROPERTIES(pl) READONLY isPostedPaymentLedger, descriptionPaymentLedger, dateTimePaymentLedger, nameLegalEntityFromPaymentLedger,
                            nameAccountFromPaymentLedger, sumFromPaymentLedger, nameLegalEntityToPaymentLedger, nameAccountToPaymentLedger,
                            sumToPaymentLedger
;

EXTEND DESIGN legalEntity {
    extendContainer {
        NEW accountContainer{
            caption = 'Счета';
            childConstraints = TO THE RIGHT;
            type = SPLITH;
        }
    }
}

NAVIGATOR {
    financeNavigator {
        NEW paymentNavigator 'Движение денежных среств' {
            ADD paymentLedger;
        }
    }
}
