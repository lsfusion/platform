MODULE Cash;

REQUIRE PaymentLedger;

// ----------------------------------- Кассовый счет ------------------------------------------ //

CLASS Account 'Касса';
TABLE account (Account);

numberAccount 'Номер кассового счета'  = DATA STRING[13] (Account) FIXEDCHARWIDTH 13;

legalEntityAccount = DATA LegalEntity (Account);
nameLegalEntityAccount 'Организация' = name(legalEntityAccount(account));
defaultAccountLegalEntity(legalEntity) = GROUP MAX account BY legalEntityAccount(account);

currencyAccount = DATA Currency (Account) NOT NULL;
currencyAccount(account) <- currencyLegalEntity(legalEntityAccount(account))
    WHEN ASSIGNED(legalEntityAccount(account));
nameCurrencyAccount 'Валюта счета' = name(currencyAccount(account));

EXTEND CLASS Account : PaymentLedger.Account;
PaymentLedger.nameAccount(account) += numberAccount(account);
PaymentLedger.currencyAccount(account) += currencyAccount(account);
PaymentLedger.legalEntityAccount(account) += legalEntityAccount(account);

FORM account 'Касса'
    OBJECTS a = Account FIXED PANEL
    PROPERTIES(a) numberAccount, nameCurrencyAccount, nameLegalEntityAccount

    EDIT Account OBJECT a
;

addAccount 'Добавить кассу' = ACTION ADDFORM Cash.Account;
editAccount 'Редактировать' (account) = ACTION EDITFORM Cash.Account;
PaymentLedger.editAccount(account) += editAccount(account);

EXTEND FORM accounts
    PROPERTIES() addAccount TODRAW a FORCE PANEL
;

FORM accounts 'Счета'
    OBJECTS a = Account
    PROPERTIES(a) READONLY numberAccount, nameCurrencyAccount, nameLegalEntityAccount
    PROPERTIES(a) editAccount FORCE PANEL TOOLBAR, delete
    PROPERTIES() addAccount TODRAW a FORCE PANEL

    DIALOG Account OBJECT a
;

EXTEND FORM legalEntity
    OBJECTS ca=Account
    PROPERTIES(ca)  numberAccount, nameCurrencyAccount, ADDOBJ, delete
    FILTERS legalEntityAccount(ca) == l
;

EXTEND DESIGN legalEntity {
    accountContainer {
        ADD ca.box{
            caption = 'Кассы';
        }
    }
}