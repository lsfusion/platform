MODULE Store;

REQUIRE System, Utils, Historizable, Stock, Employee, Tax, WriteOff, LegalEntity, Retail, Bank, Cash;

// -------------------------------------- Торговая сеть ------------------------------- //

CLASS ChainStores 'Торговая сеть' : StockGroup;
TABLE chainStores (ChainStores);

nameChainStores 'Наименование' = DATA ISTRING[100](ChainStores);

nameStockGroup(group) += nameChainStores(group) IF group IS ChainStores;

// -------------------------------------- Формат магазина ----------------------------- //

CLASS StoreType 'Формат магазина' : StockGroup;
TABLE storeType (StoreType);

nameStoreType 'Наименование' = DATA ISTRING[100](StoreType);

nameStockGroup(group) += nameStoreType(group) IF group IS StoreType;



chainStoresStoreType = DATA ChainStores (StoreType) NOT NULL DELETE;
nameChainStoresStoreType 'Торговая сеть' (storeType) = nameChainStores(chainStoresStoreType(storeType)) IN base;

inChainStoresStoreType(chainStores, storeType) = chainStoresStoreType(storeType) == chainStores;

parentStockGroup (storeType) += chainStoresStoreType(storeType);

FORM storeType 'Формат магазина'
    OBJECTS t=StoreType FIXED PANEL
    PROPERTIES(t) nameStoreType
    EDIT StoreType OBJECT t
;

FORM storeTypes 'Форматы магазинов'
    OBJECTS t=StoreType
    PROPERTIES(t) nameStoreType READONLY, DELETE
    PROPERTIES(t) ADDFORM, EDITFORM
    ORDER BY nameStoreType
    DIALOG StoreType OBJECT t
;
DESIGN storeTypes FROM DEFAULT { main{ preferredSize = (600, 400); } }

FORM chainStores 'Торговая сеть'
    OBJECTS n=ChainStores FIXED PANEL
    PROPERTIES(n) nameChainStores

    OBJECTS s=StoreType
    PROPERTIES(s) nameStoreType
    PROPERTIES(s) ADDOBJ, DELETESESSION
    FILTERS inChainStoresStoreType(n, s)

    EDIT ChainStores OBJECT n
;

FORM dialogStoreTypes 'Форматы магазинов'
    OBJECTS n=ChainStores FIXED PANEL
    PROPERTIES(n) nameChainStores SELECTOR

    OBJECTS s=StoreType
    PROPERTIES(s) nameStoreType, DELETESESSION
    PROPERTIES(s) ADDOBJ, EDITFORM
    FILTERS inChainStoresStoreType(n, s)

    DIALOG StoreType OBJECT s
;

FORM dialogChainStores 'Торговая сеть'
    OBJECTS n=ChainStores
    PROPERTIES(n) nameChainStores READONLY, DELETESESSION
    PROPERTIES(n) ADDFORM, EDITFORM

    OBJECTS s=StoreType
    PROPERTIES(s) READONLY nameStoreType
    FILTERS inChainStoresStoreType(n, s)

    DIALOG ChainStores OBJECT n
;
// -------------------------------------- Магазин ------------------------------- //

CLASS Store 'Магазин' : StockGroup, TaxUnit, POI;
TABLE store (Store);

nameStore 'Наименование' = DATA ISTRING[100](Store) INDEXED;

nameStockGroup(group) += nameStore(group) IF group IS Store;

legalEntityStore = DATA LegalEntity (Store) INDEXED NOT NULL;
nameLegalEntityStore 'Компания' (store) = nameLegalEntity(legalEntityStore(store)) IN base;

bankAccountStore = DATA Bank.Account (Store);
bankAccountStore(store) <- accountLegalEntity(legalEntityStore(store))
    WHEN CHANGED(legalEntityStore(store));
numberBankAccountStore 'Номер счета для инкасаций' = Bank.numberAccount(bankAccountStore(store));

CONSTRAINT Bank.legalEntityAccount(bankAccountStore(store)) != legalEntityStore(store)
    CHECKED BY bankAccountStore MESSAGE 'Организация магазина должна совпадать с организацией расчетного счета';

isCompanyStore(store) = isCompanyLegalEntity(legalEntityStore(store));

languageStore (store) = languageLegalEntity(legalEntityStore(store));

regionStore = DATA Region (Store);
nameRegionStore 'Регион' (store) = nameRegion(regionStore(store));

addressStore 'Адрес' = DATA STRING[100] (Store);
addressPOI (store) += addressStore(store);

latitudeStore 'Координата X' = DATA NUMERIC[10,5](Store);
longitudeStore 'Координата Y' = DATA NUMERIC[10,5](Store);

latitudePOI (store) += latitudeStore(store);
longitudePOI (store) += longitudeStore(store);

storeTypeStore = DATA StoreType (Store) INDEXED AUTOSET;
nameStoreTypeStore 'Формат' (store) = nameStoreType(storeTypeStore(store)) IN base;

inStoreTypeStore (storeType, store) = storeTypeStore (store) == storeType;

chainStoresStore (store) = chainStoresStoreType(storeTypeStore(store));
nameChainStoresStore 'Торговая сеть' (store) = nameChainStores(chainStoresStore(store)) IN base;

inChainStoresStore (chainStores, store) = chainStoresStore(store) == chainStores;

parentStockGroup (store) += storeTypeStore(store);

taxUnitGroupTaxUnit(store) += legalEntityStore(store);
descriptionTaxUnit(store) += string2SP(nameStore(store), addressStore(store));

inChainStoresStoreTypeStore (chainStores, storeType, store) =
    (storeTypeStore(store) == storeType AND chainStores) OR
    (chainStoresStore(store) == chainStores AND NOT storeType) OR
    (store IS Store AND NOT storeType AND NOT chainStores);

// -------------------------------------- Отдел магазина ------------------------------- //

CLASS DepartmentStore 'Отдел магазина' : Stock;
TABLE departmentStore (DepartmentStore);
TABLE departmentStoreDate (DepartmentStore, DATE);

nameDepartmentStore 'Наименование' = DATA ISTRING[150](DepartmentStore) INDEXED;

nameStock(stock) += nameDepartmentStore(stock) IF stock IS DepartmentStore;

storeDepartmentStore = DATA Store (DepartmentStore) INDEXED AUTOSET;
nameStoreDepartmentStore 'Магазин' (departmentStore) = nameStore(storeDepartmentStore(departmentStore)) IN base;
stockGroupStock (departmentStore) += storeDepartmentStore(departmentStore);

inStoreDepartment(store, departmentStore) = storeDepartmentStore(departmentStore) == store;

storeTypeDepartmentStore(departmentStore) = storeTypeStore(storeDepartmentStore(departmentStore)) PERSISTENT INDEXED;
chainStoresDepartmentStore(departmentStore) = chainStoresStoreType(storeTypeDepartmentStore(departmentStore)) PERSISTENT INDEXED;

legalEntityDepartmentStore (departmentStore) = legalEntityStore(storeDepartmentStore(departmentStore)) PERSISTENT INDEXED;
legalEntityStock (stock) += legalEntityDepartmentStore(stock);

quantityDaysCloseDepartmentStore 'Срок автоматического закрытия заказов' = DATA INTEGER(DepartmentStore);
quantityDaysCloseOrdersStock(stock) += quantityDaysCloseDepartmentStore(stock);

userLegalEntityDepartmentStore 'Отм.' = DATA BOOLEAN (LegalEntity, DepartmentStore);
userLegalEntityStock(legalEntity, stock) += userLegalEntityDepartmentStore(legalEntity, stock);

nameLegalEntityDepartmentStore 'Компания' (departmentStore) = nameLegalEntity(legalEntityDepartmentStore(departmentStore));

primaryDepartmentStoreStore 'Основной отдел' (store) =
    GROUP MIN departmentStore BY storeDepartmentStore(departmentStore);

addressDepartmentStore 'Адрес' (departmentStore) = addressStore(storeDepartmentStore(departmentStore));
addressStock (departmentStore) += addressDepartmentStore(departmentStore);

latitudeDepartmentStore 'Координата X' (departmentStore) = latitudeStore(storeDepartmentStore(departmentStore));
longitudeDepartmentStore 'Координата Y' (departmentStore) = longitudeStore(storeDepartmentStore(departmentStore));

latitudePOI (departmentStore) += latitudeDepartmentStore(departmentStore);
longitudePOI (departmentStore) += longitudeDepartmentStore(departmentStore);

explicitBatchLedgerDepartmentStore 'Партионный учет' = DATA BOOLEAN (DepartmentStore)IN  bookkeepingGroup;
explicitBatchLedgerStock(stock) += explicitBatchLedgerDepartmentStore(stock);

regionStock(stock) += regionStore(storeDepartmentStore(stock));

@defineHistorizable(tradingSquareDepartmentStore, 'Торговая площадь', NUMERIC[10,2], departmentStore, nameDepartmentStore, public);

tradingSquareStoreDate (store, date) = GROUP SUM tradingSquareDepartmentStoreDate(departmentStore, date) BY storeDepartmentStore(departmentStore), date;
tradingSquareStore 'Торговая площадь' (store) = tradingSquareStoreDate(store, currentDate());

cashAccountDepartmentStore 'Счет' = DATA Cash.Account (DepartmentStore);
numberCashAccountDepartmentStore 'Номер счета' = Cash.numberAccount(cashAccountDepartmentStore(departmentStore));

CONSTRAINT legalEntityDepartmentStore(departmentStore) != Cash.legalEntityAccount(cashAccountDepartmentStore(departmentStore))
    CHECKED BY cashAccountDepartmentStore MESSAGE 'Организация отдела магазина должна совпадать с организацией счета';

inChainStoresStoreTypeStoreDepartmentStore (chainStores, storeType, store, department) =
    (storeDepartmentStore(department) == store AND storeType AND chainStores) OR
    (storeTypeDepartmentStore(department) == storeType AND chainStores AND NOT store) OR
    (chainStoresDepartmentStore(department) == chainStores AND NOT store AND NOT storeType) OR
    (department IS DepartmentStore AND NOT store AND NOT storeType AND NOT chainStores);

balanceASkuStoreDateTime(sku, stock, dateTime) =
    GROUP SUM balanceASkuStockDateTime(sku, stock, dateTime)
    BY sku, storeDepartmentStore(stock), dateTime;

// -------------------------------------- Формы отдела магазинов ------------------------------------------ //

FORM departmentStore 'Отдел магазина'
    OBJECTS d = DepartmentStore FIXED PANEL
    PROPERTIES(d) nameDep=nameDepartmentStore, nameStoreDepartmentStore, tradingSquareDepartmentStore, nameWriteOffCommitteeStock,
                  explicitBatchLedgerDepartmentStore, numberCashAccountDepartmentStore, quantityDaysCloseDepartmentStore

    OBJECTS e = Employee
    PROPERTIES(e) READONLY firstNameContact, lastNameContact
    PROPERTIES(e) ADDSESSIONFORM, EDITSESSIONFORM, DELETESESSION
    FILTERS inEmployeeDivisionEmployee(d, e)

    EDIT DepartmentStore OBJECT d
;

DESIGN departmentStore FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        d.panel {
            childConstraints = TO THE BOTTOM;
            NEW topContainer {
                childConstraints = TO THE RIGHTBOTTOM;
                ADD PROPERTY(nameDep);
                ADD PROPERTY(nameStoreDepartmentStore);
                ADD PROPERTY(tradingSquareDepartmentStore);
                ADD PROPERTY(numberCashAccountDepartmentStore);
                ADD PROPERTY(quantityDaysCloseDepartmentStore);
            }
            NEW row {
                childConstraints = TO THE BOTTOM;
                NEW row1 {
                    childConstraints = TO THE RIGHT;
                    ADD d.committeeGroup {childConstraints = TO THE BOTTOM;}
                }
                NEW row2 {
                    childConstraints = TO THE RIGHT;
                    ADD d.bookkeepingGroup {childConstraints = TO THE BOTTOM;}
                    NEW row21{
                        title = 'Создавать по умолчанию';
                        childConstraints = TO THE BOTTOM;
                    }

                }
            }

        }
    }
}

editDepartmentStore 'Редактировать' = ACTION EDITFORM DepartmentStore;
editStock(stock) += editDepartmentStore(stock);

editSessionDepartmentStore 'Редактировать' = ACTION EDITFORM SESSION DepartmentStore;
editSessionStock(stock) += editSessionDepartmentStore(stock);

FORM departmentStores 'Отделы магазинов'
    TREE treeStore a=STRING[3], t=ChainStores, st=StoreType, s=Store
    PROPERTIES READONLY OBJVALUE(a), nameChainStores(t), nameStoreType(st), nameStore(s)
    FILTERS stringEqualsAll(a), inChainStoresStoreType (t, st), inStoreTypeStore(st, s)

    OBJECTS d=DepartmentStore
    PROPERTIES(d) READONLY depName = nameDepartmentStore
    FILTERS inChainStoresStoreTypeStoreDepartmentStore(t, st, s, d)
    ORDER BY depName

    DIALOG DepartmentStore OBJECT d
;

DESIGN departmentStores FROM DEFAULT {
    POSITION treeStore.tree.box TO THE LEFT d.box;
    treeStore.tree {
        fillHorizontal = 0.3;
    }
    d.grid {
        defaultComponent = TRUE;
    }
}

changeStore = ACTION (store) {
    REQUEST STRING[110] INPUT;
    IF requestedString() THEN {
        FOR storeDepartmentStore(dep) == store DO {
            SET nameDepartmentStore(dep) <- replace(nameDepartmentStore(dep), nameStore(store), requestedString());
        }
    }
    SET nameStore(store) <- requestedString();
}


FORM store 'Магазин'
    OBJECTS s=Store FIXED PANEL
    PROPERTIES(s) nameStore ON CHANGE changeStore(s), addressStore, nameStoreTypeStore, tradingSquareStore, nameLegalEntityStore, numberBankAccountStore,
                  nameRegionStore, latitudeStore, longitudeStore, showOnMapPOI

    OBJECTS d=DepartmentStore
    PROPERTIES(d) READONLY nameDepartmentStore, tradingSquareDepartmentStore, objectClassName
    PROPERTIES(d)          ADDSESSIONFORM, EDITSESSIONFORM, DELETESESSION
    FILTERS inStoreDepartment(s, d)

    EDIT Store OBJECT s
;

// -------------------------------------- Формы магазинов ------------------------------------------ //

FORM stores 'Магазины'
    TREE treeStore a=STRING[3], t=ChainStores, st=StoreType
    PROPERTIES READONLY OBJVALUE(a), nameChainStores(t), nameStoreType(st)

    FILTERS stringEqualsAll(a)
    FILTERS inChainStoresStoreType (t, st)

    PROPERTIES(t)          addT=ADDFORM FORCE PANEL, editT=EDITFORM FORCE PANEL, deletet=DELETE FORCE PANEL TOOLBAR

    OBJECTS s=Store
    PROPERTIES(s) READONLYIF isReadonly() nameStore, addressStore, nameStoreTypeStore, nameLegalEntityStore, nameRegionStore
    PROPERTIES(s)          ADDFORM, EDITFORM, DELETE TOOLBAR
    FILTERS inChainStoresStoreTypeStore(t, st, s)

    OBJECTS d=DepartmentStore
    PROPERTIES(d) READONLY nameDepartmentStore, objectClassName
    FILTERS inStoreDepartment(s, d)
;

DESIGN stores FROM DEFAULT {

    NEW topContainer {

        type = SPLITH;
        childConstraints = TO THE RIGHT;

        ADD treeStore.tree.box;

        NEW firstCase {

            type = SPLITV;
            childConstraints = TO THE BOTTOM;
            fillHorizontal = 3;

            ADD s.box;
            ADD d.box;
        }
    }

    PROPERTY (deletet) {
        caption = 'Удалить';
        askConfirm = TRUE;
    }

    PROPERTY (addT) {
        caption = 'Добавить';
    }

    PROPERTY (editT) {
        caption = 'Ред-ть';
        maximumCharWidth = 5;
    }

    ADD functions.box;
}
@extendFormEditable(stores);

NAVIGATOR {
    retailMasterData {
        ADD stores;
    }
}

// -------------------------------------- Макросы ----------------------------------------------- //

META defineDocumentHeaderDepartmentStore (object)
    @defineDocumentHeaderStock(object, departmentStore, 'Отдел магазина');
END

META defineDocumentDetailDepartmentStoreCustom (object, detail)
   @defineDocumentDetailStock (object, detail, departmentStore, 'Отдел магазина');
   store###detail (detail) = storeDepartmentStore(departmentStore###detail(detail));
   nameStore###detail 'Магазин' (detail) = nameStore(store###detail(detail));
END
META defineDocumentDetailDepartmentStore (object)
    @defineDocumentDetailDepartmentStoreCustom (object, object##Detail);
END

META defineDocumentDepartmentStore (object)
    @defineDocumentHeaderDepartmentStore(object);
    @defineDocumentDetailDepartmentStore(object);
END

addStoreLegalEntity 'Добавить магазин' = ACTION (legalEntity) {

    FOR ADDOBJ st = Store DO {
        SET legalEntityStore(st) <- legalEntity;
        FORM store OBJECTS s = st DOCKEDMODAL;
    }
}

EXTEND FORM legalEntity

    PROPERTIES addStoreLegalEntity(l) TODRAW st FORCE PANEL TOOLBAR
;

