MODULE Operation;

REQUIRE Utils, Historizable;

CLASS ABSTRACT Operation 'Операция' : Historizable;
TABLE operation(Operation);

nameOperation 'Наименование' = ABSTRACT ISTRING[100](Operation);

idOperation 'ID' = ABSTRACT ISTRING[100](Operation);

// -------------------------------------- Макросы по созданию операций ---------------------------------- //

META defineOperation(postfix)

    CLASS Operation 'Операция' : Operation.Operation;
    TABLE operation(Operation);

    nameOperation 'Наименование' = DATA ISTRING[100](Operation);
    Operation.nameOperation(operation) += nameOperation(operation);

    idOperation 'ID' = DATA ISTRING[100](Operation);
    Operation.idOperation(operation) += idOperation(operation);

    TABLE legalEntity###operation(LegalEntity, Operation);
    TABLE legalEntityGroup###operation(LegalEntityGroup, Operation);

    FORM operation 'Операция'##postfix
        OBJECTS o = Operation FIXED PANEL
        PROPERTIES(o) nameOperation, idOperation, timeCreatedHistorizable, nameUserCreatedHistorizable, hostnameComputerCreatedHistorizable

        EDIT Operation OBJECT o
    ;

    DESIGN operation FROM DEFAULT {
        main {
            preferredSize = (1024, 768);
            NEW topContainer{
                type = SPLITV;
                NEW headContainer{
                    childConstraints = TO THE RIGHTBOTTOM;
                    NEW propertyContainer{
                        childConstraints = TO THE RIGHTBOTTOM;
                        caption = 'Параметры';
                        ADD PROPERTY(nameOperation);
                        ADD PROPERTY(idOperation);
                    }
                    ADD o.historyGroup;
                }
                NEW tabContainer{
                    type = TABBED;
                }
            }
            ADD functions.box;
        }
    }

    FORM operations 'Операции' TITLE 'Операции'##postfix
        OBJECTS o = Operation
        PROPERTIES(o) READONLY nameOperation, idOperation, timeCreatedHistorizable, nameUserCreatedHistorizable, hostnameComputerCreatedHistorizable
        PROPERTIES(o) ADDFORM, EDITFORM, DELETE
    ;

    FORM dialogOperations 'Операции' TITLE 'Операции'##postfix
        OBJECTS o = Operation
        PROPERTIES(o) READONLY nameOperation, idOperation, timeCreatedHistorizable, nameUserCreatedHistorizable, hostnameComputerCreatedHistorizable
        PROPERTIES(o) ADDFORM, EDITFORM, DELETE

        DIALOG Operation OBJECT o
    ;

END

// -------------------------------------- Макросы по объявлению операций в документах ---------------------------------- //
META defineDocumentOperationAbstractHeaderPrefix(object, prefix, NSR, caption)
    prefix###operation###object = ABSTRACT NSR.Operation (###object) PERSISTENT;
    name###prefix###operation###object 'Операция'###caption = NSR.nameOperation(prefix###operation###object(object));
    id###prefix###operation###object 'ID операции'###caption = NSR.idOperation(prefix###operation###object(object));
END
META defineDocumentOperationHeaderPrefix(object, prefix, NSR, caption)
    prefix###operation###object = DATA NSR.Operation (###object);
    name###prefix###operation###object 'Операция'###caption = NSR.nameOperation(prefix###operation###object(object));
    id###prefix###operation###object 'ID операции'###caption = NSR.idOperation(prefix###operation###object(object));
END
META defineDocumentOperationInterfaceHeaderPrefix(object, prefix, NSR, caption)
    @defineDocumentOperationAbstractHeaderPrefix(object, prefix, NSR, caption);
    @defineDocumentOperationHeaderPrefix(user###object, prefix, NSR, caption);
    prefix###operation###object(object) += prefix###operation###user###object(object);
END
META defineDocumentOperationDetailPrefix(object, detail, prefix, NSR, caption)
    prefix###operation###detail (idetail) = prefix###operation###object(object###detail(idetail));
    name###prefix###operation###detail 'Операция'###caption (idetail) = NSR.nameOperation(prefix###operation###detail(idetail));
    id###prefix###operation###detail 'ID операции'###caption (idetail) = NSR.idOperation(prefix###operation###detail(idetail));
END
META defineDocumentOperationDetailPrefix(object, prefix, NSR, caption)
    @defineDocumentOperationDetailPrefix(object, object##Detail, prefix, NSR, caption);
END
META defineDocumentInterfaceOperationPrefix (object, prefix, NSR, caption)
    @defineDocumentOperationInterfaceHeaderPrefix(object, prefix, NSR, caption);
    @defineDocumentOperationDetailPrefix(object, prefix, NSR, caption);
    @defineDocumentOperationDetailPrefix(user###object, prefix, NSR, caption);
END
META defineDocumentInterfaceOperation (object, NSR, caption)
    @defineDocumentInterfaceOperationPrefix(object, , NSR, caption);
END

META extendFormOperationPrefix(object, char, afterProp, prefix, containerProp)

    EXTEND FORM user###object
        PROPERTIES(char) name###prefix###operationUser###object
    ;

    EXTEND DESIGN user###object{
        containerProp{
            ADD PROPERTY (name###prefix###operationUser###object) AFTER PROPERTY(afterProp###object);
        }
    }
END


META defineDocumentOperation(object, char, befProp)

    operation###object = ABSTRACT Operation (###object) PERSISTENT;
    name###operation###object 'Операция' = nameOperation(operation###object(object));
    id###operation###object 'ID операции' = idOperation(operation###object(object));
    operationUser###object = DATA Operation (User###object);
    name###operationUser###object 'Операция' = nameOperation(operationUser###object(user###object));
    id###operationUser###object 'ID операции' = idOperation(operationUser###object(user###object));
    operation###object(object) += operationUser###object(object);

    operation###object##Detail 'Операция' (object##Detail) = operation###object(object###object##Detail(object##Detail));
    operationUser###object##Detail 'Операция' (user###object##Detail) = operationUser###object(user###object##User###object##Detail(user###object##Detail));

    EXTEND FORM user###object
        PROPERTIES(char) name###operationUser###object
    ;

    EXTEND DESIGN user###object{
        char.documentPrmGroup{
            ADD PROPERTY(name###operationUser###object) BEFORE PROPERTY(befProp###object);
        }
    }

END

META defineDocumentOperation(object, char)
    @defineDocumentOperation(object, char, nameCurrencyUser);
END
// -------------------------------------- Организации ---------------------------------- //

META defineOperationLegalEntity(prop, object, tabcaption)

    // Отметка для групп
    dataIn###prop##GroupOperation 'Отм.' = DATA BOOLEAN (LegalEntityGroup, Operation);

    levelParent###prop##OperationLegalEntityGroup (operation, legalEntityGroup) =
        GROUP MIN levelLegalEntityGroupLegalEntityGroup(legalEntityGroup, parent) IF dataIn###prop##GroupOperation(parent, operation)
              BY operation, legalEntityGroup PERSISTENT;
    nearestParent###prop##GroupLegalEntityGroup (operation, legalEntityGroup) = legalEntityGroupLegalEntityGroupLevel(legalEntityGroup, levelParent###prop##OperationLegalEntityGroup(operation, legalEntityGroup));
    nearestIn###prop##GroupOperation (legalEntityGroup, operation) =
        dataIn###prop##GroupOperation(nearestParent###prop##GroupLegalEntityGroup(operation, legalEntityGroup), operation) PERSISTENT;

    in###prop##GroupOperation 'Отм' (legalEntityGroup, operation) =
        nearestIn###prop##GroupOperation(legalEntityGroup, operation) OVERRIDE dataIn###prop##GroupOperation(legalEntityGroup, operation);

    // Отметка для организаций
    dataIn###prop##Operation 'Отм.' = DATA BOOLEAN (LegalEntity, Operation);
    in###prop##Operation 'Отм' (legalEntity, operation) =
        nearestIn###prop##GroupOperation(legalEntityGroupLegalEntity(legalEntity), operation) OVERRIDE dataIn###prop##Operation(legalEntity, operation);

    EXTEND FORM operation
        TREE prop###groupTree object##g = LegalEntityGroup PARENT parentLegalEntityGroup
        PROPERTIES READONLY sgTreeName = nameLegalEntityGroup(object##g)
        PROPERTIES in###prop###GroupOperation(object##g, o)

        OBJECTS object = LegalEntity
        PROPERTIES(object, o) in###prop##Operation
        PROPERTIES(object) READONLY nameLegalEntity, fullNameLegalEntity, shortNameOwnershipLegalEntity, nameLegalEntityGroupLegalEntity,
                               addressLegalEntity, phoneLegalEntity,
                               isSupplierLegalEntity, isCompanyLegalEntity, isCustomerLegalEntity
        FILTERS isParentLegalEntityGroupLegalEntityGroup(legalEntityGroupLegalEntity(object), object##g) OR
                object IS LegalEntity AND NOT object##g IS LegalEntityGroup OR
                (object IS LegalEntity AND object##g IS LegalEntityGroup AND NOT legalEntityGroupLegalEntity(object))
        FILTERGROUP filters2
                FILTER 'Показывать отмеченные' 'F10' in###prop##Operation(object, o)
    ;

    EXTEND DESIGN operation {
        tabContainer {
            NEW prop###Container{
                childConstraints = TO THE RIGHT;
                type = SPLITH;
                caption = tabcaption;
                ADD prop###groupTree.tree.box;
                ADD object.box { fillHorizontal = 2.0; };
            }
        }
    }

END

META defineDocumentOperationLegalEntity(object, prop, caption)
    CONSTRAINT operation###object(object)
               AND NOT in###prop##Operation(prop###object(object), operation###object(object))
        CHECKED BY prop###object
        MESSAGE caption##' должен совпадать с отмеченными в операции';
END

//Валюта

META defineOperationCurrency(dumb)
    TABLE currencyOperation(Currency, Operation);
    inCurrencyOperation 'Отм.' = DATA BOOLEAN (Currency, Operation);

    EXTEND FORM operation
        OBJECTS c = Currency
        PROPERTIES(c, o) inCurrencyOperation
        PROPERTIES(c) READONLY nameCurrency
        FILTERGROUP filters3
            FILTER 'Показывать отмеченные' 'F11' inCurrencyOperation(c, o)
    ;

    EXTEND DESIGN operation{
        tabContainer {
            NEW currencyContainer{
                caption = 'Валюта';
                ADD c.box;
            }
        }
    }
END

META defineOperationRole(dumb)

    allRolesOperation 'Для всех ролей' = DATA BOOLEAN (Operation);

    TABLE userRoleOperation (UserRole, Operation);
    dataInUserRoleOperation 'Отм.' = DATA BOOLEAN (UserRole, Operation);

    inUserRoleOperation 'Отм' (role, operation) =
        allRolesOperation(operation) AND role IS UserRole OVERRIDE dataInUserRoleOperation(role, operation);

    inUserOperation (user, operation) = allRolesOperation(operation) AND user IS User OR inUserRoleOperation(mainRoleUser(user), operation);

    countOperationsUser (user) = GROUP SUM 1 IF inUserOperation(user, operation) BY user;
    defaultOperationUser 'Операция по умолчанию' (user) = GROUP MIN operation IF inUserOperation(user, operation) BY user;


    EXTEND FORM operation
        PROPERTIES(o) allRolesOperation

        OBJECTS r = UserRole
        PROPERTIES(r) READONLY nameUserRole
        PROPERTIES(r, o) inUserRoleOperation

        FILTERGROUP filtersRole
                FILTER 'Показывать отмеченные' 'F8' inUserRoleOperation(r, o)
    ;

    EXTEND DESIGN operation {
        tabContainer {
            NEW roleContainer{
                childConstraints = TO THE BOTTOM;
                caption = 'Роли';
                NEW roleContainer1 {
                    ADD PROPERTY(allRolesOperation);
                    caption = 'Для всех';
                }
                ADD r.box;
            }
        }
    }

END

META extendFormFilterRole(concrete, form)
    EXTEND FORM form FILTERS inUserOperation(currentUser(), concrete);
END

META defineDocumentOperationRole(object, objectClass)
    operation###object(o) <- defaultOperationUser(currentUser()) IF countOperationsUser(currentUser()) == 1
        WHEN ASSIGNED(o IS objectClass);
END
META defineDocumentOperationRole(object)
    @defineDocumentOperationRole(object, ###object);
END
META defineDocumentOperationRolePrefix(object, objectClass, prefix)
    prefix###operation###object(o) <- defaultOperationUser(currentUser()) IF countOperationsUser(currentUser()) == 1
        WHEN ASSIGNED(o IS objectClass);
END
META defineDocumentOperationRolePrefix(object, prefix)
    @defineDocumentOperationRolePrefix(object, ###object, prefix);
END


META defineOperationProperty(property, caption, container)
    property###operation caption = DATA BOOLEAN (Operation);

    EXTEND FORM operation
        PROPERTIES(o) property###operation
    ;
    EXTEND DESIGN operation {
        container {
            ADD PROPERTY(property###operation);
        }
    }
END

META deriveDocumentOperationProperty(objectClass, property)
    property###objectClass (o) <- property###operation(operation###objectClass(o))
        WHEN CHANGED(operation###objectClass(o));
END