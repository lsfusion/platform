MODULE Order;

REQUIRE System,
        Utils,
        Historizable,
        Stock,
        Numerator,
        Document,
        Currency,
        Barcode,
        PriceList,
        Agreement,
        PurchaseLedger,
        SaleLedger,
        StockReserve,
        OrderLedger,
        Employee,
        Operation,
        StockTax;


PRIORITY Utils, Stock;

CLASS ABSTRACT Order 'Заказ' : Document;
CLASS ABSTRACT OrderDetail 'Строка заказа' : DocumentDetail;

@defineDocumentAbstract(order);
@defineDocumentAbstractNumber(order);
//@defineDocumentDetailIndex(order);

@defineDocumentAbstractLegalEntity(order, supplier, 'Поставщик');
addressSupplierOrder 'Адрес поставщика' (order) = addressLegalEntity(supplierOrder(order));

@defineDocumentAbstractLegalEntity(order, customer, 'Покупатель');
addressCustomerOrder 'Адрес покупателя' (order) = addressLegalEntity(customerOrder(order));

closeDateOrder 'Дата автоматического закрытия' = ABSTRACT DATE (Order) IN documentPrmGroup PERSISTENT;

@defineDocumentAbstractDataStock(order, stock, 'Склад поставщика', supplier);
@defineDocumentAbstractDataStock(order, stock, 'Склад покупателя', customer);

@defineDocumentAbstractDataStock(order, stock, 'Склад (отправитель)', from);
@defineDocumentAbstractDataStock(order, stock, 'Склад (получатель)', to);

@defineDocumentAbstractPosted(order);

@defineDocumentAbstractDetailSku(order, sku);

@defineDocumentAbstractDetailPrice(order);
@defineDocumentAbstractDetailDataSumCustomPrefix (orderDetail, , );
@defineDocumentAbstractDetailVAT(order, , );
@defineDocumentAbstractDetailVATDataSumCustom (orderDetail, invoice);

@defineDocumentAbstractDetailQuantity(order);
@defineDocumentDetailGrossWeight(order, sku);
@defineDocumentAbstractDetailQuantityPrefix(order, pack, ' (упаковок)');
@defineDocumentHeaderSkuQuantity(order, sku);

@defineDocumentHeaderQuantity(order);
@defineDocumentHeaderVATSum(order, orderDetail, invoice);
@defineDocumentHeaderSum (order);

@defineDocumentHeaderGrossWeight(order);
@defineDocumentAbstractHeaderCurrency (order);

operationOrder = ABSTRACT Operation.Operation (Order) PERSISTENT;
nameOperationOrder 'Операция' (order) = name(operationOrder(order));

quantityFromOrderStock 'Кол-во со склада' (order, stock) = GROUP SUM quantityOrderDetail(orderDetail) BY orderOrderDetail(orderDetail),
                                                                                                         fromStockOrderDetail(orderDetail);

quantityToOrderStock 'Кол-во на склад' (order, stock) = GROUP SUM quantityOrderDetail(orderDetail) BY orderOrderDetail(orderDetail),
                                                                                                      toStockOrderDetail(orderDetail);

editOrder 'Редактировать' = ABSTRACT ACTION (order) IMAGE 'edit.png' EDITKEY 'BACK_SPACE' HIDE TOOLBAR;

//----------------------------------------------- Заказ ---------------------------------------------------//

META defineOrder(sign, legalEntityProp, stockProp)

    CLASS ABSTRACT Order 'Заказ'###sign : Order.Order;
    CLASS ABSTRACT OrderDetail 'Строка заказа'###sign : Order.OrderDetail, Named;

    CLASS UserOrder 'Заказ (польз.)'###sign : Order, Historizable, NumeratedObject;
    CLASS UserOrderDetail 'Строка заказа (польз.)'###sign : OrderDetail;
    CLASS UserOrderPosted 'Проведенный заказ (польз.)'###sign : UserOrder, PostedObject;

    @defineDocumentInterface(order);
    Order.orderOrderDetail (detail) += orderOrderDetail(detail);
    Order.dateOrder (order) += dateOrder(order);
    Order.timeOrder (order) += timeOrder(order);

    @defineDocumentInterfaceNumber(order);
    Order.numberOrder (order) += numberOrder(order);
    Order.seriesOrder (order) += seriesOrder(order);

    @defineDocumentInterfaceLegalEntity (order, supplier, 'Поставщик');
    @defineDocumentInterfaceLegalEntity (order, customer, 'Покупатель');
    Order.supplierOrder(order) += supplierOrder(order);
    Order.customerOrder(order) += customerOrder(order);

    @defineDocumentInterfaceDataStock(order, stock, 'Склад поставщика', supplier);
    @defineDocumentInterfaceDataStock(order, stock, 'Склад покупателя', customer);
    Order.supplierStockOrder(order) += supplierStockOrder(order);
    Order.customerStockOrder(order) += customerStockOrder(order);
    Order.dataSupplierStockOrderDetail(detail) += dataSupplierStockOrderDetail(detail);
    Order.dataCustomerStockOrderDetail(detail) += dataCustomerStockOrderDetail(detail);

    CONSTRAINT supplierUserOrder(userOrder) AND supplierStockUserOrder(userOrder) AND NOT
               inLegalEntityStock(supplierUserOrder(userOrder), supplierStockUserOrder(userOrder))
        CHECKED BY supplierStockUserOrder
            MESSAGE 'Поставщик и склад поставщика для заказа не имеют связи';
    CONSTRAINT customerUserOrder(userOrder) AND customerStockUserOrder(userOrder) AND NOT
               inLegalEntityStock(customerUserOrder(userOrder), customerStockUserOrder(userOrder))
        CHECKED BY customerStockUserOrder
            MESSAGE 'Покупатель и склад покупателя для заказа не имеют связи';

    @defineDocumentInterfacePosted(order);
    Order.isPostedOrder (order) += isPostedOrder(order);

    @defineDocumentInterfaceDataTimePrefix(order, shipment, ' поставки');

    closeDateOrder 'Дата автоматического закрытия' = ABSTRACT DATE (Order) IN documentPrmGroup;
    closeDateUserOrder 'Дата автоматического закрытия' = DATA DATE (UserOrder) IN documentPrmGroup;
    closeDateOrder(order) += closeDateUserOrder(order);
    Order.closeDateOrder (order) += closeDateOrder(order);
    closeDataDateOrderDetail 'Дата автоматического закрытия' = ABSTRACT DATE (OrderDetail) IN documentPrmGroup;
    closeDataDateUserOrderDetail 'Дата автоматического закрытия' = DATA DATE (UserOrderDetail) IN documentPrmGroup;
    closeDataDateOrderDetail(detail) += closeDataDateUserOrderDetail(detail);

    closeDateOrderDetail 'Дата автоматического закрытия' (detail) = UNION OVERRIDE closeDateOrder(orderOrderDetail(detail)), closeDataDateOrderDetail(detail) PERSISTENT;
    closeDateUserOrderDetail 'Дата автоматического закрытия' (detail) = UNION OVERRIDE closeDateUserOrder(userOrderUserOrderDetail(detail)), closeDataDateUserOrderDetail(detail) PERSISTENT;

    closeDataDateUserOrderDetail(detail) <- sumDate(shipmentDateUserOrderDetail(detail), quantityDaysCloseOrdersStock(stockProp###userOrderDetail(detail)))
        WHEN CHANGED(shipmentDateUserOrderDetail(detail)) OR CHANGED(stockProp###userOrderDetail(detail));

    isDataClosedOrder 'Закрыт' = ABSTRACT BOOLEAN(Order);
    isDataClosedUserOrder 'Закрыт' = DATA BOOLEAN(UserOrder);
    isDataClosedOrder(order) += isDataClosedUserOrder(order);

    isDataClosedOrderDetail 'Закрыт' = ABSTRACT BOOLEAN(OrderDetail);
    isDataClosedUserOrderDetail 'Закрыт' = DATA BOOLEAN(UserOrderDetail);
    isDataClosedOrderDetail(order) += isDataClosedUserOrderDetail(order);

    defaultClosedOrderDetail(detail) = closeDateOrderDetail(detail) < currentDate() ;
    isClosedOrderDetail 'Закрыт' (detail) = UNION OVERRIDE defaultClosedOrderDetail(detail),
                                                           isDataClosedOrder(orderOrderDetail(detail)),
                                                           isDataClosedOrderDetail(detail);
    countClosedOrderDetailOrder (order) = GROUP SUM 1 IF isClosedOrderDetail(detail) AND isPostedOrderDetail(detail) BY orderOrderDetail(detail);
    isClosedOrder 'Закрыт' (order) =  countClosedOrderDetailOrder(order) == countOrderDetailOrder(order);

    defaultClosedUserOrderDetail(detail) = closeDateUserOrderDetail(detail) < currentDate();
    isClosedUserOrderDetail 'Закрыт' (detail) = UNION OVERRIDE defaultClosedUserOrderDetail(detail),
                                                               isDataClosedUserOrder(userOrderUserOrderDetail(detail)),
                                                               isDataClosedUserOrderDetail(detail);
    countClosedUserOrderDetailUserOrder (userOrder) = GROUP SUM 1 IF isClosedUserOrderDetail(detail) AND isPostedUserOrderDetail(detail) BY userOrderUserOrderDetail(detail);
    isClosedUserOrder 'Закрыт' (userOrder) =  countClosedUserOrderDetailUserOrder(userOrder) == countUserOrderDetailUserOrder(userOrder);

    isOpenedUserOrder 'Не закрыт' (o) = isPostedUserOrder(o) AND NOT isClosedUserOrder(o) PERSISTENT;

    closeUserOrder 'Закрыть' (o) = [ACTION (o) NEWSESSION AUTOAPPLY { SET isDataClosedUserOrder(o) <- TRUE; } ] (o)
                                                IF isPostedUserOrder(o) AND NOT isClosedUserOrder(o) TOOLBAR CONFIRM;

    openUserOrder 'Открыть' (o) = [ACTION (o) NEWSESSION AUTOAPPLY { SET isDataClosedUserOrder(o) <- NULL; } ] (o)
                                      IF isClosedUserOrder(o) TOOLBAR CONFIRM;

    @defineDocumentInterfaceDescription(order, 'Заказ'###sign);

    @defineDocumentInterfaceCurrency(order);
    @deriveDocumentCurrency(userOrder, stockProp);
    Order.currencyOrder(order) += currencyOrder(order);

    @defineDocumentInterfaceHeaderAgreement(order);
    @deriveDocumentHeaderAgreement (userOrder, supplierUserOrder, customerUserOrder);

    @defineDocumentInterfaceDetailSku(order, sku);
    Order.skuOrderDetail(detail) += skuOrderDetail(detail);

    @defineDocumentInterfaceDetailQuantity(order);
    Order.quantityOrderDetail(detail) += quantityOrderDetail(detail);

    @defineDocumentInterfaceDetailPrice(order); // объявляем цену без НДС
    Order.priceOrderDetail(detail) += priceOrderDetail(detail);

    @defineDocumentInterfaceDetailVAT(order, country###stockProp, , ); // объявляем шкалу
    // саму шкалу не записываем, поскольку на продаже может быть расчетный НДС
    @deriveDocumentDetailValueVAT(userOrder); // записываем значение ставки

    Order.VATOrderDetail(detail) += VATOrderDetail(detail);
    Order.valueVATOrderDetail(detail) += valueVATOrderDetail(detail);

    @defineDocumentInterfaceDetailPricePrefix (order, invoice, ' с НДС'); // объявляем цену с НДС

    // объявляем Action'ы для ON CHANGE
    @defineDocumentDetailPriceVATOnChange(userOrder);
    @defineDocumentDetailNumberVATOnChange(userOrder, stockProp);
    @defineDocumentDetailValueVATOnChange(userOrder);
    @defineDocumentDetailInvoicePriceVATOnChange(userOrder);

    @defineDocumentInterfacePriceListType(order); // объявляем вид цены
    @deriveDocumentDetailPriceListTypeAgreement(userOrder); // записываем вид цены из соглашения
    @deriveDocumentDetailPricePriceListTypeVAT (userOrder, stockProp); // записываем цены с НДС и без НДС на основе priceListType

    // суммма без НДС
    @defineDocumentInterfaceDetailDataSum(order);
    @deriveDocumentDetailSum(userOrder, quantity);
    Order.sumOrderDetail(detail) += sumOrderDetail(detail);

    // суммы НДС и с НДС
    @defineDocumentInterfaceDetailVATDataSum (order, invoice); // объявляем свойства суммы НДС и суммы с НДС
    // суммы НДС и с НДС не записываем, поскольку для продажи нужен расчетный НДС

    Order.VATSumOrderDetail(detail) += VATSumOrderDetail(detail);
    Order.invoiceSumOrderDetail(detail) += invoiceSumOrderDetail(detail);

    @defineDocumentInterfaceHeaderVATSum (order, orderDetail, invoice);

    @defineDocumentInterfaceHeaderQuantity(order);
    @defineDocumentHeaderSkuQuantity(order, sku);
    //Order.quantityOrderSku (order, sku) += quantityOrderDetailSkuOrder(sku, order);

    @defineDocumentHeaderSkuQuantity(userOrder, sku);
    @defineDocumentInterfaceHeaderSum(order);

    @defineAddDetailDialogSkuStock(userOrder, sku, stockProp, dialogSku);
    @defineAddDetailDialogBarcode(userOrder, sku);

    noAgreementUserOrder (order) = order IS UserOrder AND NOT agreementUserOrder(order);

    countOrderDetailStockOrder 'Кол-во строк по складу '(stock, order) = GROUP SUM 1 BY stockProp###orderDetail(orderDetail), orderOrderDetail(orderDetail);

//-- Дата следующей поставки
    @defineDocumentInterfaceDatePrefix (order, nextShipment, ' следующей поставки');
    quantityDaysNextShipmentUserOrder 'Кол-во дней до след. поставки' (userOrder) =  subtractInteger(nextShipmentDateUserOrder(userOrder), dateUserOrder(userOrder)) + 1;
    fromShipmentDateUserOrder 'Начало периода' (userOrder) = subtractDate(dateUserOrder(userOrder), quantityDaysNextShipmentUserOrder(userOrder));

     @implementDocumentSupplierCustomer(order);

// --------------------------- Формы Заказа ---------------------------------

    editOrder 'Редактировать' = ABSTRACT ACTION (order) IMAGE 'edit.png' EDITKEY 'BACK_SPACE' HIDE TOOLBAR;
    Order.editOrder (order) += editOrder(order);

    FORM userOrder 'Заказ'###sign
        OBJECTS o = UserOrder FIXED PANEL
        PROPERTIES (o) objectClassName, nameSupplierUserOrder, nameCustomerUserOrder, nameSupplierStockUserOrder, nameCustomerStockUserOrder,
                       nameNumeratorObject, numberObject, seriesObject, dateUserOrder, timeUserOrder,
                       nameCurrencyUserOrder, nameAgreementUserOrder, namePriceListTypeUserOrder SHOWIF noAgreementUserOrder(o), noteUserOrder,
                       countUserOrderDetailUserOrder, quantityUserOrderDetailUserOrder, sumUserOrderDetailUserOrder,
                       VATSumUserOrderDetailUserOrder, invoiceSumUserOrderDetailUserOrder,
                       shipmentDateUserOrder, shipmentTimeUserOrder, closeDateUserOrder, nextShipmentDateUserOrder,
                       quantityDaysNextShipmentUserOrder READONLY

        OBJECTS d = UserOrderDetail
        PROPERTIES (d) indexUserOrderDetail, idBarcodeSkuUserOrderDetail, nameSkuUserOrderDetail, shortNameUOMSkuUserOrderDetail
        PROPERTIES (d) quantityUserOrderDetail, namePriceListTypeUserOrderDetail,
                       priceUserOrderDetail ON CHANGE changePriceUserOrderDetail(d), sumUserOrderDetail,
                       numberVATUserOrderDetail ON CHANGE changeNumberVATUserOrderDetail(d),
                       valueVATUserOrderDetail ON CHANGE changeValueVATUserOrderDetail(d),
                       invoicePriceUserOrderDetail ON CHANGE changeInvoicePriceUserOrderDetail(d),
                       VATSumUserOrderDetail, invoiceSumUserOrderDetail,
                       name###stockProp###userOrderDetail, shipmentDateUserOrderDetail,
                       shipmentTimeUserOrderDetail, closeDateUserOrderDetail, isDataClosedUserOrderDetail, ADDOBJ, delete

        PROPERTIES(o) TODRAW d fillDocumentOrder, addDetailDialogSkuStockUserOrderDetailUserOrder,
                               addDetailInputBarcodeUserOrderDetailUserOrder, deleteUserOrderDetailUserOrder
        FILTERS userOrderUserOrderDetail(d) == o

        EVENTS
            ON OK prePostUserOrder(o)

//        HINTTABLE LIST dateTimeUserOrderDetail, skuUserOrderDetail, supplierStockUserOrderDetail, customerStockUserOrderDetail,
//                       priceListTypeUserOrderDetail, currencyUserOrderDetail,
//                       priceUserOrderDetail, VATUserOrderDetail, valueVATUserOrderDetail, invoicePriceUserOrderDetail

        EDIT UserOrder OBJECT o
    ;

    DESIGN userOrder FROM DEFAULT{

        main {
            preferredSize = (1024, 768);
            NEW specification.box BEFORE functions.box{
                ADD d.box {
                    title = 'Спецификация';
                    d.panel {
                        childConstraints = TO THE BOTTOM;
                    }
                }
            }

            NEW header.box BEFORE specification.box {
                childConstraints = TO THE RIGHT;

                NEW headerRow1 {
                    childConstraints = TO THE BOTTOM;

                    ADD o.documentHeaderGroup {
                        childConstraints = TO THE RIGHTBOTTOM;
                        ADD PROPERTY(objectClassName) { preferredCharWidth = 10; }
                        ADD PROPERTY(nameNumeratorObject);
                        ADD PROPERTY(numberObject);
                        ADD PROPERTY(seriesObject);
                        ADD PROPERTY(dateUserOrder);
                        ADD PROPERTY(timeUserOrder);
                    }
                    NEW headerRow11 {

                        childConstraints = TO THE RIGHTBOTTOM;
                        NEW headerRow111 {
                            title = 'Поставщик';
                            childConstraints = TO THE RIGHTBOTTOM;
                            ADD PROPERTY(nameSupplierUserOrder);
                            ADD PROPERTY(nameSupplierStockUserOrder);

                        }
                        NEW headerRow112 {
                            title = 'Покупатель';
                            childConstraints = TO THE RIGHTBOTTOM;
                            ADD PROPERTY(nameCustomerUserOrder);
                            ADD PROPERTY(nameCustomerStockUserOrder);
                        }

                    }
                    NEW headerTabbedPane {
                        type = TABBED;
                        NEW headerMainParams {
                            title = 'Основные параметры';
                            childConstraints = TO THE BOTTOM;
                            ADD o.documentPrmGroup {
                                childConstraints = TO THE RIGHTBOTTOM;
                            }
                        }
                        NEW headerExtraParams {
                            title = 'Дополнительные параметры';
                            minimumSize = (500, -1);
                            childConstraints = TO THE RIGHTBOTTOM;
                            NEW documentShipmentGroup {
                                title = 'Исполнение';
                                childConstraints = TO THE RIGHTBOTTOM;
                                ADD PROPERTY(shipmentDateUserOrder);
                                ADD PROPERTY(shipmentTimeUserOrder);
                            }
                            NEW headerRow121 {
                                title = 'Поставка';
                                childConstraints = TO THE RIGHTBOTTOM;
                                ADD PROPERTY(nextShipmentDateUserOrder);
                                ADD PROPERTY(quantityDaysNextShipmentUserOrder);
                            }
                            NEW headerDate {
                                title = 'Закрытие';
                                ADD PROPERTY(closeDateUserOrder);
                            }
                        }
                        NEW headerCreateDetail {
                            title = 'Основание';
                            childConstraints = TO THE RIGHTBOTTOM;
                        }
                        NEW headerCreateDocuments {
                            title = 'Производные документы';
                            minimumSize = (500, -1);
                            childConstraints = TO THE RIGHTBOTTOM;
                        }
                    }
                }

                ADD o.documentSumGroup {
                    childConstraints = TO THE BOTTOM;
                }
            }

            PROPERTY(formOk) {
                caption = 'Провести';
            }
        }
    }

//-- SKU
    @defineDocumentSkuStock(userOrder, sku, stockProp);
    @extendFormDocumentSkuStockCustom(userOrder, userOrder, o, legalEntityProp, stockProp);
    @extendFormDocumentSkuStockCustomAll(userOrder, userOrder, o);

    addUserOrder 'Добавить' = ACTION ADDFORM UserOrder;
    editUserOrder 'Редактировать' (userOrder) = ACTION EDITFORM UserOrder;
    editOrder(order) += editUserOrder(order);

    overCopyOrder = ABSTRACT ACTION (s, d);
    copyOrder 'Копировать' = ACTION (order) NEWSESSION {
        FOR ADDOBJ o = UserOrder DO {
            SET supplierUserOrder(o) <- supplierOrder(order);
            SET supplierStockUserOrder(o) <- supplierStockOrder(order);
            SET customerUserOrder(o) <- customerOrder(order);
            SET customerStockUserOrder(o) <- customerStockOrder(order);
            SET currencyUserOrder(o) <- currencyOrder(order);
            SET agreementUserOrder(o) <- agreementOrder(order);
            SET priceListTypeUserOrder(o) <- priceListTypeOrder(order);
            SET noteUserOrder(o) <- noteOrder(order);

            EXEC overCopyOrder(order, o);

            FOR orderOrderDetail(orderDetail) == order DO {
                FOR ADDOBJ d=UserOrderDetail DO {
                    SET userOrderUserOrderDetail(d) <- o;
                    SET skuUserOrderDetail(d) <- skuOrderDetail(orderDetail);
                    SET quantityOrderDetail(d) <- quantityOrderDetail(orderDetail);
                    SET data###stockProp###userOrderDetail(d) <- data###stockProp###orderDetail(orderDetail);
                }
            }

            FORM userOrder OBJECTS o = o MANAGESESSION DOCKEDMODAL;
        }
    } TOOLBAR;


    FORM orders 'Заказы' TITLE 'Заказы'###sign
        OBJECTS o = Order
        PROPERTIES (o) READONLY isPostedOrder FORCE GRID, isClosedOrder, numberOrder, seriesOrder, dateOrder, timeOrder,
                                nameSupplierOrder, nameSupplierStockOrder, nameCustomerOrder, nameCustomerStockOrder, nameCurrencyOrder,
                                nameAgreementOrder, countOrderDetailOrder, quantityOrderDetailOrder, sumOrderDetailOrder,
                                VATSumOrderDetailOrder, invoiceSumOrderDetailOrder, shipmentDateOrder, shipmentTimeOrder, closeDateOrder,
                                noteOrder, objectClassName

        PROPERTIES (o) READONLY FORCE PANEL nameUserCreatedHistorizable, timeCreatedHistorizable, hostnameComputerCreatedHistorizable, nameUserClosed, timeClosed, hostnameComputerClosed

        PROPERTIES ()  addUserOrder TODRAW o
        PROPERTIES (o) editOrder, copyOrder
        PROPERTIES (o) closeUserOrder SHOWIF isOpenedUserOrder(o), openUserOrder SHOWIF isClosedUserOrder(o)
        PROPERTIES     delete(o) FORCE PANEL TOOLBAR  SHOWIF isUserOrder(o)

        OBJECTS d=OrderDetail
        PROPERTIES (d) READONLY indexOrderDetail, idBarcodeSkuOrderDetail, nameSkuOrderDetail, shortNameUOMSkuOrderDetail
        PROPERTIES (d) READONLY quantityOrderDetail, namePriceListTypeOrderDetail, priceOrderDetail, sumOrderDetail,
                       numberVATOrderDetail, valueVATOrderDetail, VATSumOrderDetail, invoiceSumOrderDetail,
                       name###stockProp###orderDetail, shipmentDateOrderDetail, shipmentTimeOrderDetail

        FILTERS orderOrderDetail(d) == o
        DIALOG Order OBJECT o
    ;
    @extendFormFilterAccess(Order, o, orders, stockProp);

    DESIGN orders FROM DEFAULT {
        PROPERTY (delete(o)) {
            askConfirm = TRUE;
        }

        NEW documentContainer BEFORE functions.box {
            type = SPLITV;
            childConstraints = TO THE BOTTOM;

            ADD o.box { fillVertical = 2.0; }

            NEW documentDetail {
                type = TABBED;

                ADD d.box {
                    title = 'Спецификация';
                }
                NEW documentHistory {
                    title = 'История';

                    ADD o.historyGroup;
                    ADD o.postedGroup;
                }
                NEW printTab {
                    title = 'Печатные формы';
                    NEW printContainer {
                        title = 'Печать';
                        childConstraints = TO THE BOTTOM;
                    }
                }
                NEW actionContainer {
                    title = 'Действия';
                    childConstraints = TO THE RIGHT;
                    NEW createdContainer {
                        title = 'Создание на основе';
                        childConstraints = TO THE BOTTOM;
                        NEW copyContainer {
                            title = 'Копирование';
                            ADD PROPERTY(copyOrder);
                        }
                    }
                }
            }
        }
    }
END

META defineOrderStockDestination(fromStock, toStock)
    Order.fromStockOrder(order) += fromStock##Order(order);
    Order.toStockOrder(order) += toStock##Order(order);
    Order.dataFromStockOrderDetail(detail) += data###fromStock##OrderDetail(detail);
    Order.dataToStockOrderDetail(detail) += data###toStock##OrderDetail(detail);
END
