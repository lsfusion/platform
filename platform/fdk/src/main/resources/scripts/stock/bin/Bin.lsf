MODULE Bin;

REQUIRE Stock, Barcode;

//------------------------- Ячейка -----------------------------//


CLASS  BinGroup 'Группа ячеек';
TABLE binGroup (BinGroup);

nameBinGroup 'Наименование' = DATA ISTRING[100](BinGroup);

TABLE binGroupBinGroup(BinGroup, BinGroup);
@defineHierarchy(binGroup, BinGroup);

FORM binGroup 'Группа ячеек'
    OBJECTS g=BinGroup FIXED PANEL
    PROPERTIES(g) nameBinGroup, nameParentBinGroup
    EDIT BinGroup OBJECT g
;

FORM binGroups 'Группы ячеек'
    TREE treeGroups g=BinGroup PARENT parentBinGroup
    PROPERTIES READONLY nameBinGroup(g), canonicalNameBinGroup (g)
    PROPERTIES(g) ADDFORM, EDITFORM, deleteg=DELETE
    ORDER BY canonicalNameBinGroup
    DIALOG BinGroup OBJECT g
;

CLASS Bin 'Ячейка' ;
TABLE bin (Bin);

nameBin 'Наименование' = DATA ISTRING[100](Bin);
binGroupBin = DATA BinGroup (Bin) AUTOSET;
nameBinGroupBin 'Группа' (bin) = nameBinGroup(binGroupBin(bin));

TABLE binGroupBin(BinGroup, Bin);
isParentBinGroupBin (binGroup, bin) = isParentBinGroupBinGroup(binGroupBin(bin), binGroup);
canonicalNameBin 'Каноническое имя' (bin) = canonicalNameBinGroup(binGroupBin(bin));

FORM bin 'Ячейка'
    OBJECTS s = Bin FIXED PANEL
    PROPERTIES(s) nameBin, nameBinGroupBin
    EDIT Bin OBJECT s
;

FORM bins 'Ячейки'
    TREE binTree sg = BinGroup PARENT parentBinGroup
    PROPERTIES READONLY nameBinGroup(sg)

    OBJECTS s = Bin
    PROPERTIES(s) READONLY nameBin, canonicalNameBin
    PROPERTIES(s) ADDFORM, EDITFORM, DELETE TOOLBAR

    FILTERS isParentBinGroupBin(sg, s)
    ORDER BY nameBin

    DIALOG Bin OBJECT s
;

DESIGN bins FROM DEFAULT {
    main{
        preferredSize = (1024, 768);

        NEW topContainer {
            type = SPLITH;
            childConstraints = TO THE RIGHT;

            ADD binTree.tree.box {
                title = 'Группы ячеек';
            }

            ADD s.box {
                s.grid {
                    defaultComponent = TRUE;
                    fillHorizontal = 4;
                }
            }
        }

        ADD functions.box;
    }
}

// ----------------------------------- Ledger ------------------------------------------ //

binDataSkuLedger = ABSTRACT Bin(DataSkuLedger) PERSISTENT INDEXED;
nameBinDataSkuLedger 'Ячейка' (ledger) = nameBin(binDataSkuLedger(ledger)) MINCHARWIDTH 10 PREFCHARWIDTH 10;

binBatch (batch) = ABSTRACT Bin (Batch) PERSISTENT INDEXED ;
nameBinBatch 'Ячейка' (batch) = nameBin(binBatch(batch)) MINCHARWIDTH 10 PREFCHARWIDTH 10;

binSkuLedger (ledger) = UNION CLASS binDataSkuLedger (ledger), binBatch (ledger) PERSISTENT INDEXED;
nameBinSkuLedger 'Ячейка' (ledger) = nameBin(binSkuLedger(ledger)) MINCHARWIDTH 10 PREFCHARWIDTH 10;

TABLE skuBin(Sku, Bin);
TABLE skuStockBin(Sku, Stock, Bin);
TABLE skuStockBinDate(Sku, Stock, Bin, DATE);

META defineBinSkuLedgerBalanceProperties(postfix, caption)

    currentBalance##postfix##SkuStockBin 'Текущий остаток'###caption (sku, stock, bin) =
        GROUP SUM signedQuantityActive##postfix##SkuLedger(ledger) BY skuSkuLedger(ledger), stockSkuLedger(ledger), binSkuLedger(ledger) PERSISTENT;
    prevCurrentBalance##postfix##SkuStockBin 'Текущий остаток'###caption (sku, stock, bin) = PREV(currentBalance##postfix##SkuStockBin(sku, stock, bin));
    countLedger##postfix##SkuStockBin 'Кол-во движений'###caption (sku, stock, bin) =
        GROUP SUM 1 IF signedQuantityActive##postfix##SkuLedger(ledger) BY skuSkuLedger(ledger), stockSkuLedger(ledger), binSkuLedger(ledger) PERSISTENT;

    quantityIn##postfix##SkuStockBinDate (sku, stock, bin, date) =
        GROUP SUM quantityInActiveSkuLedger(ledger) BY skuSkuLedger(ledger), stockSkuLedger(ledger), binSkuLedger(ledger), dateSkuLedger(ledger) PERSISTENT;

    quantityOut##postfix##SkuStockBinDate (sku, stock, bin, date) =
        GROUP SUM quantityOutActiveSkuLedger(ledger) BY skuSkuLedger(ledger), stockSkuLedger(ledger), binSkuLedger(ledger), dateSkuLedger(ledger) PERSISTENT;

    quantity##postfix##SkuStockBinDate (sku, stock, bin, date) = quantityIn##postfix##SkuStockBinDate(sku, stock, bin, date)
                                                         (+)
                                                         quantityOut##postfix##SkuStockBinDate(sku, stock, bin, date) PERSISTENT;

    signedQuantity##postfix##SkuStockBinDate (sku, stock, bin, date) = quantityIn##postfix##SkuStockBinDate(sku, stock, bin, date)
                                                               (-)
                                                               quantityOut##postfix##SkuStockBinDate(sku, stock, bin, date) PERSISTENT;

    // без учета текущей даты/времени
    balanceB##postfix##SkuStockBinDate 'Остаток на начало дня'###caption (sku, stock, bin, date) = (currentBalance##postfix##SkuStockBin(sku, stock, bin) IF date AS DATE)
                                                                      (-) [GROUP SUM signedQuantity##postfix##SkuStockBinDate (sku, stock, bin, dateIn) IF dateIn >= (date AS DATE)
                                                                                 BY sku, stock, bin, date](sku, stock, bin, date);

    // с учетом текущей даты/времени
    balanceA##postfix##SkuStockBinDate 'Остаток на конец дня'###caption (sku, stock, bin, date) = (currentBalance##postfix##SkuStockBin(sku, stock, bin) IF date AS DATE)
                                                                     (-) [GROUP SUM signedQuantity##postfix##SkuStockBinDate (sku, stock, bin, dateIn) IF dateIn > (date AS DATE)
                                                                                BY sku, stock, bin, date](sku, stock, bin, date);

    // без учета текущей даты/времени
    balanceB##postfix##SkuStockBinDateTime 'Остаток (до)'###caption (sku, stock, bin, dateTime) = (currentBalance##postfix##SkuStockBin(sku, stock, bin) IF dateTime AS DATETIME)
                                                                              (-) [GROUP SUM signedQuantityActive##postfix##SkuLedger(ledger) IF dateTimeSkuLedger(ledger) >= (dateTime AS DATETIME)
                                                                                         BY skuSkuLedger(ledger), stockSkuLedger(ledger), binSkuLedger(ledger), dateTime](sku, stock, bin, dateTime);

    // с учетом текущей даты/времени
    balanceA##postfix##SkuStockBinDateTime 'Остаток (после)'###caption (sku, stock, bin, dateTime) = (currentBalance##postfix##SkuStockBin(sku, stock, bin) IF dateTime AS DATETIME)
                                                                             (-) [GROUP SUM signedQuantityActive##postfix##SkuLedger(ledger) IF dateTimeSkuLedger(ledger) > (dateTime AS DATETIME)
                                                                                        BY skuSkuLedger(ledger), stockSkuLedger(ledger), binSkuLedger(ledger), dateTime](sku, stock, bin, dateTime);

    currentBalance##postfix##SkuBin 'Остаток (всего)'###caption (sku, bin) = GROUP SUM currentBalance##postfix##SkuStockBin(sku, stock, bin) BY sku, bin PERSISTENT;

    // ---------------------------- Суммы по товару -------------------------------- //

    currentSum##postfix##SkuStockBin 'Сумма остатка'###caption (sku, stock, bin) =
        GROUP SUM signedSumActiveSkuLedger(ledger) BY skuSkuLedger(ledger), stockSkuLedger(ledger), binSkuLedger(ledger) PERSISTENT;

    sumIn##postfix##SkuStockBinDate (sku, stock, bin, date) =
        GROUP SUM sumInActiveSkuLedger(ledger) BY skuSkuLedger(ledger), stockSkuLedger(ledger), binSkuLedger(ledger), dateSkuLedger(ledger) PERSISTENT;

    sumOut##postfix##SkuStockBinDate (sku, stock, bin, date) =
        GROUP SUM sumOutActiveSkuLedger(ledger) BY skuSkuLedger(ledger), stockSkuLedger(ledger), binSkuLedger(ledger), dateSkuLedger(ledger) PERSISTENT;

    sum##postfix##SkuStockBinDate (sku, stock, bin, date) = sumIn##postfix##SkuStockBinDate(sku, stock, bin, date)
                                                         (+)
                                                         sumOut##postfix##SkuStockBinDate(sku, stock, bin, date) PERSISTENT;

    signedSum##postfix##SkuStockBinDate (sku, stock, bin, date) = sumIn##postfix##SkuStockBinDate(sku, stock, bin, date)
                                                               (-)
                                                               sumOut##postfix##SkuStockBinDate(sku, stock, bin, date) PERSISTENT;

    // без учета текущей даты/времени
    sumB##postfix##SkuStockBinDate 'Сумма на начало дня'###caption (sku, stock, bin, date) = (currentSum##postfix##SkuStockBin(sku, stock, bin) IF date AS DATE)
                                                                      (-) [GROUP SUM signedSum##postfix##SkuStockBinDate (sku, stock, bin, dateIn) IF dateIn >= (date AS DATE)
                                                                                 BY sku, stock, bin, date](sku, stock, bin, date);

    // с учетом текущей даты/времени
    sumA##postfix##SkuStockBinDate 'Сумма на конец дня'###caption (sku, stock, bin, date) = (currentSum##postfix##SkuStockBin(sku, stock, bin) IF date AS DATE)
                                                                     (-) [GROUP SUM signedSum##postfix##SkuStockBinDate (sku, stock, bin, dateIn) IF dateIn > (date AS DATE)
                                                                                BY sku, stock, bin, date](sku, stock, bin, date);

    averagePrice##postfix##SkuStockBin 'Цена (средневзв.)'###caption (sku, stock, bin) = currentSum##postfix##SkuStockBin(sku, stock, bin) / currentBalance##postfix##SkuStockBin(sku, stock, bin);

END

@defineBinSkuLedgerBalanceProperties(,);

EXTEND FORM currentBalanceSkuStock

    PROPERTIES(bil)   READONLY nameBinSkuLedger  BEFORE signedQuantitySkuLedger
    PROPERTIES(bt)    READONLY nameBinBatch  BEFORE quantityBatch

    OBJECTS           bin=Bin
    PROPERTIES        READONLY nameBin(bin), currentBalanceSkuStockBin(s,st,bin)
    FILTERS           currentBalanceSkuStockBin(s,st,bin)
;
EXTEND DESIGN currentBalanceSkuStock {
    ledger.box {
        ADD bin.box;
    }
}

EXTEND FORM balanceSkuStock

    PROPERTIES(bil)   READONLY nameBinSkuLedger  BEFORE signedQuantitySkuLedger
    PROPERTIES(bt)    READONLY nameBinBatch  BEFORE quantityBatch

    OBJECTS           bin=Bin
    PROPERTIES        READONLY nameBin(bin), balanceASkuStockBinDateTime(s,st,bin,t)
    FILTERS           balanceASkuStockBinDateTime(s,st,bin,t)
;
EXTEND DESIGN balanceSkuStock {
    ledger.box {
        ADD bin.box;
    }
}

// ---------------------  Склады с указанием ячеек ----------------------- //

TABLE stockBinGroup(Stock, BinGroup);
dataInStockBinGroup 'Отм' = DATA BOOLEAN (Stock, BinGroup);

TABLE stockBin(Stock, Bin);
dataInStockBin 'Отм' = DATA BOOLEAN (Stock, Bin);


levelParentStockBinGroup (stock, binGroup) = GROUP MIN levelBinGroupBinGroup(binGroup, parent) IF dataInStockBinGroup(stock, parent)
                                                               BY stock, binGroup PERSISTENT;
nearestParentStockBinGroup (stock, binGroup) =
    binGroupBinGroupLevel(binGroup, levelParentStockBinGroup(stock, binGroup));
nearestInStockBinGroup (stock, binGroup) =
    dataInStockBinGroup(stock, nearestParentStockBinGroup(stock, binGroup));

inStockBinGroup 'Отм' (stock, binGroup) =
    nearestInStockBinGroup (stock, binGroup) OVERRIDE
    dataInStockBinGroup(stock, binGroup) PERSISTENT;

inStockBin 'Отм' (stock, bin) =
     inStockBinGroup(stock, binGroupBin(bin)) OVERRIDE
     dataInStockBin(stock, bin) PERSISTENT;

binsStock 'Ячейки' (stock) = GROUP CONCAT nameBin(bin) IF inStockBin(stock, bin) , ', '
                                       BY stock
                                       ORDER bin MINCHARWIDTH 20 PREFCHARWIDTH 40 PERSISTENT;
stocksBin 'Склады' (bin) = GROUP CONCAT nameStock(stock) IF inStockBin(stock, bin) , ', '
                                       BY bin
                                       ORDER stock MINCHARWIDTH 20 PREFCHARWIDTH 40 PERSISTENT;


FORM binStocks 'Ячейки/склады'

    TREE stockTree a=STRING[3], sg = StockGroup PARENT parentStockGroup
    PROPERTIES READONLY OBJVALUE(a), sgTreeName = nameStockGroup(sg)

    ORDER BY sgTreeName
    FILTERS stringEqualsAll(a)
    FILTERS quantityStocksStockGroup(sg)

    OBJECTS ts = Stock
    PROPERTIES READONLY  nameStock(ts)
    FILTERS ts IS Stock AND NOT sg IS StockGroup OR isParentStockGroupStock(sg, ts),
            isCompanyStock(ts)

    TREE binTree bg = BinGroup PARENT parentBinGroup
    PROPERTIES READONLY nameBinGroup(bg)
    PROPERTIES(ts, bg) inStockBinGroup

    OBJECTS b = Bin
    PROPERTIES(b) READONLY nameBin, canonicalNameBin
    PROPERTIES(ts, b) inStockBin
    FILTERS b IS Bin AND NOT bg IS BinGroup OR isParentBinGroupBin(bg, b)
    ORDER BY nameBin
    FILTERGROUP filters1
            FILTER 'Выбранные ячейки' 'F10' inStockBin(ts, b) DEFAULT

;

DESIGN binStocks FROM DEFAULT {
    main{
        preferredSize = (1024, 768);
        childConstraints = TO THE BOTTOM;

        NEW topContainer1 {
            type = SPLITH;
            childConstraints = TO THE RIGHT;

            ADD stockTree.tree.box {
                title = 'Группы складов';
            }

            ADD ts.box {
                ts.grid {
                    defaultComponent = TRUE;
                    fillHorizontal = 4;
                }
            }
        }
        NEW topContainer2 {
            type = SPLITH;
            childConstraints = TO THE RIGHT;

            ADD binTree.tree.box {
                title = 'Группы ячеек';
            }

            ADD b.box {
                b.grid {
                    defaultComponent = TRUE;
                    fillHorizontal = 4;
                }
            }
        }

        ADD functions.box;
    }
}

// ---------------- Ячейка по классификатору ------------------- //

groupTypeStock = DATA GroupType (Stock);
nameGroupTypeStock 'Тип классификатора' (stock) = nameGroupType(groupTypeStock(stock));

dataBinStockGroup = DATA Bin (Stock, Group);
nameDataBinStockGroup 'Ячейка' (stock,group) = nameBin(dataBinStockGroup(stock,group)) MINCHARWIDTH 10 PREFCHARWIDTH 10;

dataBinStockSku = DATA Bin (Stock, Sku);
nameDataBinStockSku 'Ячейка' (stock,sku) = nameBin(dataBinStockSku(stock,sku)) MINCHARWIDTH 10 PREFCHARWIDTH 10;

levelParentBinStockGroup (stock, group) = GROUP MIN levelGroupGroup(group, parent) IF dataBinStockGroup(stock, parent)
                                                              BY stock, group PERSISTENT;

nearestParentBinStockGroup (stock, group) = groupGroupLevel(group, levelParentBinStockGroup (stock, group));
nearestInBinStockGroup  (stock, group) =
    dataBinStockGroup(stock, nearestParentBinStockGroup (stock, group)) PERSISTENT;

binStockGroup (stock, group) =
    nearestInBinStockGroup(stock, group) OVERRIDE dataBinStockGroup(stock, group) PERSISTENT;
nameBinStockGroup 'Ячейка'= nameBin(binStockGroup(stock, group)) MINCHARWIDTH 10 PREFCHARWIDTH 10;

binStockSku (stock, sku) =
    binStockGroup(stock, groupGroupTypeSku(groupTypeStock(stock), sku)) OVERRIDE
    dataBinStockSku(stock, sku) PERSISTENT;
nameBinStockSku 'Ячейка'= nameBin(binStockSku(stock, sku)) MINCHARWIDTH 10 PREFCHARWIDTH 10;

CONSTRAINT dataBinStockGroup(stock,group) AND NOT inStockBin(stock,dataBinStockGroup(stock,group))
           CHECKED BY dataBinStockGroup MESSAGE 'Ячейка (для группы sku) не включена для склада';

CONSTRAINT dataBinStockSku(stock,sku) AND NOT inStockBin(stock,dataBinStockSku(stock,sku))
           CHECKED BY dataBinStockSku MESSAGE 'Ячейка (для sku) не включена для склада';
// ------------------------------------- Формы ---------------------------------- //

FORM binStockSkus 'Ячейки/склады/sku'

    OBJECTS ts = Stock FIXED PANEL
    PROPERTIES(ts) SELECTOR nameStock
    PROPERTIES(ts) nameGroupTypeStock
    FILTERS isCompanyStock(ts)

    TREE treeGroup g=Group PARENT parentGroup
    PROPERTIES READONLY nameGroup(g)
    FILTERS groupTypeGroup(g) == groupTypeStock(ts)
    ORDER BY nameGroup(g)
    PROPERTIES(ts, g) nameBinStockGroup

    OBJECTS s = Sku
    PROPERTIES(s) READONLY nameSku, idBarcodeSku
    FILTERS isParentGroupSku(g, s) OR s IS Sku AND NOT g
    PROPERTIES(ts, s) nameBinStockSku
;

DESIGN binStockSkus FROM DEFAULT{
    NEW mainContainer{
        childConstraints = TO THE BOTTOM;
        ADD ts.box{
            fillHorizontal = 3.0;
            childConstraints = TO THE RIGHTBOTTOM;
        }
        NEW groupContainer {
            childConstraints = TO THE BOTTOM;
            NEW treeContainer{
                type = SPLITH;
                childConstraints = TO THE RIGHT;
                ADD treeGroup.tree.box { fillHorizontal = 2.0; }
                ADD s.box { fillHorizontal = 2.0; }
            }
        }
    }
    ADD functions.box;
}

//--------------------------------------------------Ячейка для Detail--------------------------------------//

META defineDocumentDetailBinCustomPrefix(detail, prefix, caption)
    prefix###bin###detail = DATA Bin (###detail);
    prefix###nameBin###detail 'Ячейка'###caption (detail) = nameBin(prefix###bin###detail(detail)) MINCHARWIDTH 10 PREFCHARWIDTH 10;
END
META defineDocumentAbstractDetailBinCustomPrefix(detail, prefix, caption)
    prefix###bin###detail = ABSTRACT Bin (###detail) PERSISTENT;
    prefix###nameBin###detail 'Ячейка'###caption (detail) = nameBin(prefix###bin###detail(detail)) MINCHARWIDTH 10 PREFCHARWIDTH 10;
END
META defineDocumentInterfaceDetailBinCustomPrefix(detail, prefix, caption)
    @defineDocumentAbstractDetailBinCustomPrefix(detail, prefix, caption);
    @defineDocumentDetailBinCustomPrefix(user###detail, prefix, caption);
    prefix###bin###detail(detail) += prefix###bin###user###detail(detail);
END
META defineDocumentInterfaceDetailBinCustom(detail)
    @defineDocumentInterfaceDetailBinCustomPrefix(detail, , );
END
META defineDocumentInterfaceDetailBin(object, caption)
    @defineDocumentInterfaceDetailBinCustomPrefix(object###detail, , caption);
END
META defineDocumentInterfaceDetailBin(object)
    @defineDocumentInterfaceDetailBinCustomPrefix(object###detail, , );
END

META extendFormInterfaceDetailBin(object, prefix, befProp)
    @defineDocumentInterfaceHeaderCreate (object, showBin, 'Ячейка');

    EXTEND FORM user###object
        PROPERTIES(s) showBin###user###object
        PROPERTIES (d) SHOWIF showBin###user###object(s) prefix###nameBin###user###object###detail BEFORE befProp###user###object###detail
    ;
    EXTEND DESIGN user###object {
        headerExtraParams {
            NEW headerBin {
                title = 'Ячейка';
                ADD PROPERTY(showBin###user###object);
            }
        }
    }
    EXTEND FORM object##s
    PROPERTIES(s) READONLY showBin###object
    PROPERTIES (d) READONLY SHOWIF showBin###object(s) prefix###nameBin###object###detail BEFORE befProp###object###detail
    ;
END

//---------------- invoiceShipment --------------------//

META defineInvoiceShipmentBin(dumb)

    @defineDocumentInterfaceDetailBin(invoice);
    binShipmentDetail (detail) += binInvoiceDetail(invoiceDetailInvoiceShipmentDetail(detail));

    @defineDocumentInterfaceHeaderCreate (invoice, showBin, 'Ячейка');

    EXTEND FORM userInvoice
        PROPERTIES(i) showBinUserInvoice
        PROPERTIES(d) BEFORE expiryDateUserInvoiceDetail SHOWIF showBinUserInvoice(i) BACKGROUND backgroundShippedInvoice(i)
                      nameBinUserInvoiceDetail
    ;
    EXTEND DESIGN userInvoice {
        headerExtraParams {
            NEW headerBin {
                title = 'Ячейка';
                ADD PROPERTY(showBinUserInvoice);
            }
        }
    }

    EXTEND FORM invoices
        PROPERTIES(i) READONLY showBinInvoice
        PROPERTIES(d) READONLY BEFORE expiryDateInvoiceDetail SHOWIF showBinInvoice(i) BACKGROUND backgroundShippedInvoice(i)
                      nameBinInvoiceDetail
    ;

END

NAVIGATOR {
    stockNavigator {
        NEW binNavigator 'Ячейки' {
            ADD binGroups;
            ADD bins;
            ADD binStocks;
            ADD binStockSkus;
        }
    }
}



