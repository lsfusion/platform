MODULE DocumentTransfer;

REQUIRE System, Stock, Barcode;

META defineDocumentTransferOut(object, caption, skuProp, stockProp)

    @defineDocument(object##Out);

    @defineDocumentStock(object##Out, stockProp, 'Склад-отправитель');
    @defineDocumentStock(object##Out, stockProp, 'Склад-получатель', destination);

    @defineDocumentPosted(object##Out);

    @defineDocumentDescription (object##Out, caption);

    @defineDocumentDetailNumber(object##Out);

    @defineDocumentDetailSku(object##Out, skuProp);

    @defineDocumentDetailQuantity(object##Out);

    @defineDocumentDetailGrossWeight(object##Out, skuProp);
    @defineDocumentDetailQuantityPack(object##Out, skuProp);

    @defineDocumentDetailSkuBalance(object##Out, skuProp, stockProp);

    @defineDocumentHeaderQuantity(object##Out);
    @defineDocumentHeaderSkuQuantity(object##Out, skuProp);

    // Кнопки подбора
    @defineAddDetailDialogSkuStock(object##Out, skuProp, stockProp, dialogSku);
    @defineAddDetailDialogBarcode(object##Out, skuProp);

END

META defineDocumentFormTransferOut(object, formCaption)

    FORM object##Out formCaption
        OBJECTS t = object##Out FIXED PANEL

        PROPERTIES (t) objectClassName, numberObject, seriesObject, date###object##Out, time###object##Out,
                       nameStock###object##Out, nameDestinationStock###object##Out, note###object##Out,
                       count###object##OutDetail###object##Out, quantity###object##OutDetail###object##Out

        OBJECTS d = object##OutDetail

        PROPERTIES(t) TODRAW d addDetailDialogSkuStock###object##OutDetail###object##Out, addDetailInputBarcode###object##OutDetail###object##Out,
                               delete###object##OutDetail###object##Out

        FILTERS object##Out###object##OutDetail(d) == t

        EVENTS
            ON OK EXEC prePost###object##Out(t)

        EDIT object##Out OBJECT t
    ;

    DESIGN object##Out FROM DEFAULT {
        main {
            preferredSize = (1024, 768);
            t.box {
                childConstraints = TO THE RIGHT;
                NEW columnHeaderPrm {
                    childConstraints = TO THE BOTTOM;
                    ADD t.documentHeaderGroup {
                        childConstraints = TO THE RIGHTBOTTOM;
                        ADD PROPERTY (objectClassName){preferredCharWidth = 15;}
                        ADD PROPERTY (nameStock###object##Out);
                        ADD PROPERTY (nameDestinationStock###object##Out);
                        ADD PROPERTY (numberObject);
                        ADD PROPERTY (seriesObject);
                        ADD PROPERTY (date###object##Out);
                        ADD PROPERTY (time###object##Out);
                    }
                    ADD t.documentPrmGroup;
                }
                NEW columnHeaderSum {
                    ADD t.documentSumGroup {
                        childConstraints = TO THE BOTTOM;
                    }
                }
            }

            PROPERTY(formOkAction) {
                caption = 'Провести';
            }
        }
    }

    FORM object##Out##s formCaption
        OBJECTS t = object##Out
        PROPERTIES (t) READONLY isPosted###object##Out FORCE GRID, numberObject, seriesObject, date###object##Out, time###object##Out,
                                nameStock###object##Out, nameDestinationStock###object##Out, note###object##Out,
                                count###object##OutDetail###object##Out, quantity###object##OutDetail###object##Out

        PROPERTIES (t) READONLY FORCE PANEL nameUserCreated, timeCreated, hostnameComputerCreated, nameUserClosed, timeClosed, hostnameComputerClosed

        PROPERTIES (t) ADDFORM, EDITFORM, delete FORCE PANEL DRAWTOTOOLBAR

        OBJECTS d = object##OutDetail

        FILTERS object##Out###object##OutDetail(d) == t
    ;

    DESIGN object##Outs FROM DEFAULT {
        PROPERTY (delete(t)) {
            askConfirm = TRUE;
        }

        NEW documentContainer BEFORE functions.box {
            type = SPLITV;
            childConstraints = TO THE BOTTOM;

            ADD t.box;

            NEW documentDetail {
                type = TABBED;

                ADD d.box {
                    title = 'Спецификация';
                }
                NEW documentHistory {
                    childConstraints = TO THE RIGHT;
                    title = 'История';

                    ADD t.historyGroup {
                        childConstraints = TO THE BOTTOM;
                    }
                    ADD t.postedGroup {
                        childConstraints = TO THE BOTTOM;
                    }
                }
            }
        }
    }
END

META defineDocumentTransferOutBatch(object, skuProp)

    TABLE object##OutBatchDetail(object##OutBatchDetail);

    cost###object##OutDetailBatch (ledger, batch) = costDataSkuLedgerBatch(ledger, batch) AND ledger IS object##OutDetail;
    @defineAggregation(object##OutDetail, batch, object##OutBatchDetail, cost###object##OutDetailBatch);

    object##Out###object##OutBatchDetail (detail) = object##Out###object##OutDetail(object##OutDetail###object##OutBatchDetail(detail));
    @defineDocumentDetailIndex(object##Out, object##OutBatchDetail);

    @defineDocumentAggregationDetailSku(object##Out, object##OutBatch, sku);
    @defineDocumentDetailSkuArticle(object##OutBatch);

    descriptionBatch###object##OutBatchDetail 'Партия' (detail) = descriptionBatch(batch###object##OutBatchDetail(detail));
    quantity###object##OutBatchDetail 'Кол-во' (detail) = costDataSkuLedgerBatch(object##OutDetail###object##OutBatchDetail(detail), batch###object##OutBatchDetail(detail));

END

META defineDocumentFormTransferOutBatch(object)

    EXTEND FORM object##Outs
        OBJECTS o = object##OutBatchDetail
        PROPERTIES(o) READONLY descriptionBatch###object##OutBatchDetail
        FILTERS object##Out###object##OutBatchDetail(o) == t
    ;

    EXTEND DESIGN object##Outs {
        documentDetail {
            ADD o.box {
                title = 'Партии';
            }
        }
    }

END


META defineDocumentFormTransferOutConsignment(object, prefix)

    EXTEND FORM object##Outs
        PROPERTIES (prefix) FORCE PANEL printConsignmentVerticalA, printConsignmentHorizontalA,
                            printConsignmentVerticalB, printConsignmentHorizontalB,
                            printConsignmentSimpleHorizontal, editConsignment,
                            printConsignmentSimpleVertical, printConsignmentSimpleAttach,
                            printConsignmentAttach
    ;

    EXTEND DESIGN object##Outs {
        documentDetail {
            ADD prefix.printGroup {
                childConstraints = TO THE BOTTOM;
                NEW case55{
                    childConstraints = TO THE BOTTOM;

                    NEW contOne {
                        title = 'Накладная';
                        ADD PROPERTY(editConsignment);
                    }
                    NEW tn{
                        childConstraints = TO THE RIGHT;
                        title = 'ТН-2';
                        ADD PROPERTY(printConsignmentSimpleVertical);
                        ADD PROPERTY(printConsignmentSimpleHorizontal);
                        ADD PROPERTY(printConsignmentSimpleAttach);
                    }
                }
                NEW ttn1{
                    childConstraints = TO THE RIGHT;
                    title = 'ТТН-1';
                    NEW ttn1V {
                        childConstraints = TO THE BOTTOM;
                        ADD PROPERTY(printConsignmentVerticalA);
                        ADD PROPERTY(printConsignmentVerticalB);
                    }
                    NEW ttn1H {
                        childConstraints = TO THE BOTTOM;
                        ADD PROPERTY(printConsignmentHorizontalA);
                        ADD PROPERTY(printConsignmentHorizontalB);
                    }
                    NEW ttn1A {
                        ADD PROPERTY(printConsignmentAttach);
                    }
                }
            }
        }
    }

END

META defineDocumentTransferIn(object, caption, skuProp, stockProp)

    @defineDocumentTables(object##In);
    TABLE object##InBatch (object##In, batch);

    @defineDocumentAggregation(object##Out, object##In, is###object##Out);
    @defineDocumentDetailIndex(object##In);

    @defineDocumentAggregationStockPrefix(object##Out, object##In, stockProp, 'Склад-получатель', destination, );
    @defineDocumentAggregationStockPrefix(object##Out, object##In, stockProp, 'Склад-отправитель', , source);

    @defineDocumentAggregationPosted(object##Out, object##In);

    @defineDocumentAggregationHeaderNumber(object##Out, object##In);

    @defineDocumentDescription(object##In, object##InDetail, seriesNumber###object##In, caption);

    @defineDocumentAggregationDetailSku(object##Out, object##In, skuProp);

    @defineDocumentAggregationDetailQuantity(object##Out, object##In);

    @defineDocumentHeaderQuantity(object##In);
    @defineDocumentHeaderSkuQuantity(object##In, skuProp);

END

META defineDocumentFormTransferIn(object, caption)

    FORM object##Ins caption
        OBJECTS t = object##In
        PROPERTIES (t) READONLY isPosted###object##In FORCE GRID, number###object##In, series###object##In, date###object##In, time###object##In,
                                nameStock###object##In, nameSourceStock###object##In, note###object##In,
                                count###object##InDetail###object##In, quantity###object##InDetail###object##In

        PROPERTIES (t) READONLY FORCE PANEL nameUserCreated, timeCreated, hostnameComputerCreated, nameUserClosed, timeClosed, hostnameComputerClosed

        OBJECTS d = object##InDetail

        FILTERS object##In###object##InDetail(d) == t
    ;

    DESIGN object##Ins FROM DEFAULT {
        NEW documentContainer BEFORE functions.box {
            type = SPLITV;
            childConstraints = TO THE BOTTOM;

            ADD t.box;

            NEW documentDetail {
                type = TABBED;

                ADD d.box {
                    title = 'Спецификация';
                }
                NEW documentHistory {
                    title = 'История';

                    ADD t.historyGroup;
                    ADD t.postedGroup;
                }
            }
        }
    }

END

META defineDocumentTransferRec(object, caption, skuProp, stockProp)

    @defineDocument(object##Rec);

    object##In###object##Rec = DATA object##In (object##Rec) NOT NULL DELETE;
    object##Rec###object##In = GROUP UNIQUE object##Rec BY object##In###object##Rec(object##Rec) WHERE object##Rec IS object##Rec;

    object##In###object##RecDetail (detail) = object##In###object##Rec(object##Rec###object##RecDetail(detail));
    object##Rec###object##InDetail (detail) = object##Rec###object##In(object##In###object##InDetail(detail));

    @defineDocumentAggregationHeaderStock(object##In, object##Rec, stockProp, 'Склад-получатель');

    @defineDocumentPosted(object##Rec);

    @defineDocumentAggregationHeaderNumberCustom(object##In, object##Rec, object##In);

    @defineDocumentDetailSku(object##Rec, skuProp);

    @defineDocumentDetailQuantity(object##Rec);

    @defineAddDetailDialogSkuStock(object##Rec, skuProp, stockProp, dialogSku);
    @defineAddDetailDialogBarcode(object##Rec, skuProp);

    @defineDocumentHeaderQuantity(object##Rec);
    @defineDocumentHeaderSkuQuantity(object##Rec, skuProp);

END

META defineDocumentFormTransferRec(object, caption)

    FORM object##Rec caption
        OBJECTS t = object##Rec FIXED PANEL

        PROPERTIES (t) number###object##Rec, series###object##Rec, date###object##Rec, time###object##Rec,
                       nameStock###object##Rec, note###object##Rec,
                       count###object##RecDetail###object##Rec, quantity###object##RecDetail###object##Rec

        OBJECTS d = object##RecDetail

        PROPERTIES(t) TODRAW d addDetailDialogSkuStock###object##RecDetail###object##Rec, addDetailInputBarcode###object##RecDetail###object##Rec,
                               delete###object##RecDetail###object##Rec

        FILTERS object##Rec###object##RecDetail(d) == t

        EDIT object##Rec OBJECT t
    ;

    DESIGN object##Rec FROM DEFAULT {
        main {
            preferredSize = (1024, 768);
            t.box {
                childConstraints = TO THE RIGHT;
                NEW column1 {
                    childConstraints = TO THE BOTTOM;
                    ADD t.documentHeaderGroup {
                        childConstraints = TO THE RIGHT;
                        ADD PROPERTY (nameStock###object##Rec);
                        ADD PROPERTY (number###object##Rec);
                        ADD PROPERTY (series###object##Rec);
                        ADD PROPERTY (date###object##Rec);
                        ADD PROPERTY (time###object##Rec);
                    }
                    ADD t.documentPrmGroup;
                }
                ADD t.documentSumGroup {
                    childConstraints = TO THE BOTTOM;
                }
            }
        }
    }

    // Действия
    receive###object##In 'Принять' (object##In) = ACTION (object##In) NEWSESSION {
        IF object##Rec###object##In(object##In) IS object##Rec THEN {
            FOR r == object##Rec###object##In(object##In) DO {
                IF r IS object##RecPosted THEN {
                    CHANGECLASS r TO object##Rec;
                }
                FORM object##Rec OBJECTS t = r MODAL;
            }
        } ELSE {
            ADDOBJ object##Rec;
            FOR t == addedObject() DO {
                SET object##In###object##Rec (t) <- object##In AS object##In;
                FORM object##Rec OBJECTS t = t MODAL;
            }
        }
        IF formResult() == formResult.ok THEN EXEC apply();
    } TOOLBAR;

    postRec###object##In 'Пометить как принятое' (object##In) = post###object##Rec(object##Rec###object##In(object##In)) TOOLBAR;
    isDraftRec###object##In (object##In) = isDraft###object##Rec(object##Rec###object##In(object##In));

    statusRec###object##In 'Статус приемки' (object##In) =
        IF object##Rec###object##In (object##In) IS object##RecPosted THEN 'Принят' IF object##In IS object##In ELSE
        IF object##Rec###object##In (object##In) IS object##Rec THEN 'Принимается' IF object##In IS object##In ELSE
        'Не принят' IF object##In IS object##In PREFCHARWIDTH 15 MINCHARWIDTH 15;

    quantity###object##RecDetail###object##In 'Кол-во принятое' (object##In) = quantity###object##RecDetail###object##Rec(object##Rec###object##In(object##In));

    EXTEND FORM object##Ins
        PROPERTIES(t) quantity###object##RecDetail###object##In, statusRec###object##In,
                      receive###object##In, postRec###object##In SHOWIF isDraftRec###object##In(t)

        OBJECTS r = object##RecDetail
        FILTERS object##In###object##RecDetail(r) == t
    ;

    EXTEND DESIGN object##Ins {
        documentDetail {
            ADD r.box {
                title = 'Приемка';
            }
        }
    }

END

META defineDocumentTransferDiff(object, caption, skuProp, stockProp)

    // Излишки / Недостачи
    diffSku###object##Rec(sku, object##Rec) = (quantity###object##RecDetailSku###object##Rec(sku, object##Rec) (-)
                                            quantity###object##InDetailSku###object##In(sku, object##In###object##Rec(object##Rec)))
                                            AND isPosted###object##Rec(object##Rec);

    hasDiffSku###object##Rec(sku, object##Rec) = diffSku###object##Rec(sku, object##Rec) != 0;

    countDiff###object##Rec 'Расхождение (позиции)' (object##Rec) = GROUP SUM 1 IF hasDiffSku###object##Rec(sku, object##Rec) BY object##Rec;
    quantityDiff###object##Rec 'Расхождение (кол-во)' (object##Rec) = GROUP SUM diffSku###object##Rec(sku, object##Rec) BY object##Rec;

    countDiff###object##In 'Расхождение (позиции)' (object##In) = countDiff###object##Rec(object##Rec###object##In(object##In));
    quantityDiff###object##In 'Расхождение (кол-во)' (object##In) = quantityDiff###object##Rec(object##Rec###object##In(object##In));

    hasShortageSku###object##Rec(sku, object##Rec) = diffSku###object##Rec(sku, object##Rec) < 0;
    hasExcessSku###object##Rec(sku, object##Rec) = diffSku###object##Rec(sku, object##Rec) > 0;

    // Создание документов

    @defineDocumentTables(object##Diff);
    TABLE object##DiffSDetail (object##DiffSDetail);
    TABLE object##DiffEDetail (object##DiffEDetail);

    @defineAggregation(object##Rec, object##Diff, countDiff###object##Rec);

    @defineAggregation(skuProp, object##Rec, object##DiffEDetail, hasExcessSku###object##Rec);
    quantity###object##DiffEDetail 'Кол-во' (detail) = diffSku###object##Rec(sku###object##DiffEDetail(detail),
                                                                       object##Rec###object##DiffEDetail(detail)) PERSISTENT;

    @defineAggregation(skuProp, object##Rec, object##DiffSDetail, hasShortageSku###object##Rec);
    quantity###object##DiffSDetail 'Кол-во' (detail) =-diffSku###object##Rec(sku###object##DiffSDetail(detail),
                                                                       object##Rec###object##DiffSDetail(detail)) PERSISTENT;

    object##Rec###object##DiffDetail (detail) = UNION EXCLUSIVE object##Rec###object##DiffEDetail(detail), object##Rec###object##DiffSDetail(detail) PERSISTENT;
    object##In###object##DiffDetail (detail) = object##In###object##Rec(object##Rec###object##DiffDetail(detail));
    object##Diff###object##DiffDetail (detail) = object##Diff###object##Rec(object##Rec###object##DiffDetail(detail)) PERSISTENT;

    object##In###object##DiffSDetail (detail) = object##In###object##Rec(object##Rec###object##DiffSDetail(detail)) PERSISTENT;

    object##Diff###object##DiffEDetail (detail) = object##Diff###object##Rec(object##Rec###object##DiffEDetail(detail));
    object##Diff###object##DiffSDetail (detail) = object##Diff###object##Rec(object##Rec###object##DiffSDetail(detail));

    @defineDocumentDetailIndex(object##Diff);
    @defineDocumentDetailIndex(object##Diff, object##DiffEDetail);
    @defineDocumentDetailIndex(object##Diff, object##DiffSDetail);

    @defineDocumentAggregationHeaderTime(object##Rec, object##Diff);
    @defineDocumentDetailTime(object##Diff, object##DiffEDetail);
    @defineDocumentDetailTime(object##Diff, object##DiffSDetail);

    @defineDocumentAggregationHeaderNumberCustom(object##Rec, object##Diff, object##Rec);

    @defineDocumentHeaderDescription(object##Diff, seriesNumber###object##Diff, caption);
    @defineDocumentDetailDescription(object##Diff, object##DiffEDetail);
    @defineDocumentDetailDescription(object##Diff, object##DiffSDetail);

    @defineDocumentAggregationHeaderStock(object##Rec, object##Diff, stockProp, 'Склад-получатель');
    @defineDocumentDetailStock(object##Diff, object##DiffEDetail, stockProp, 'Склад-получатель');
    @defineDocumentDetailStock(object##Diff, object##DiffSDetail, stockProp, 'Склад-получатель');

    @defineDocumentAggregationHeaderPosted(object##Rec, object##Diff);
    @defineDocumentDetailPosted(object##Diff, object##DiffEDetail);
    @defineDocumentDetailPosted(object##Diff, object##DiffSDetail);

    skuProp###object##DiffDetail (detail) = UNION EXCLUSIVE skuProp###object##DiffEDetail(detail), skuProp###object##DiffSDetail(detail) PERSISTENT;
    quantity###object##DiffDetail 'Кол-во' (detail) = UNION EXCLUSIVE quantity###object##DiffEDetail(detail), quantity###object##DiffSDetail(detail) PERSISTENT;
    signedQuantity###object##DiffDetail 'Кол-во' (detail) = UNION EXCLUSIVE quantity###object##DiffEDetail(detail), -quantity###object##DiffSDetail(detail) PERSISTENT;

END

META defineDocumentFormTransferDiff(object)

    EXTEND FORM object##Ins
        PROPERTIES(t) countDiff###object##In, quantityDiff###object##In

        OBJECTS f = object##DiffDetail
        FILTERS object##In###object##DiffDetail(f) == t
    ;

    EXTEND DESIGN object##Ins {
        documentDetail {
            ADD f.box {
                title = 'Акт расхождений';
            }
        }
    }

END

// ---------------------------------------------- Не проводить по учету------------------------------- //

META defineDocumentHeaderSkipSkuLedgerCustom(object)

    skipSkuLedger###object 'Не проводить по учету' (object) = DATA BOOLEAN (object) IN documentPrmGroup;
END

META defineDocumentDetailSkipSkuLedgerCustom(object, detail)

    skipSkuLedger###detail 'Не проводить по учету' (idetail) = skipSkuLedger###object(object###detail(idetail));
END

META defineDocumentSkipSkuLedgerCustom(object, detail)
    @defineDocumentHeaderSkipSkuLedgerCustom(object);
    @defineDocumentDetailSkipSkuLedgerCustom(object, detail);
END


META extendFormDocumentSkipSkuLedgerCustom(form, object, concrete)

    EXTEND FORM form
        PROPERTIES(object) skipSkuLedger###concrete

    ;
END
META extendFormDocumentSkipSkuLedgerCustomReadonly(form, object, concrete)

    EXTEND FORM form
        PROPERTIES(object) READONLY skipSkuLedger###concrete

    ;
END
