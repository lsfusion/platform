MODULE Trip;

REQUIRE Order, Shipment, Route, ContractLedger;

NAMESPACE Trip;

CLASS Trip 'Рейс' : NumeratedObject;
TABLE trip(Trip);

@defineDocumentHeaderTime(Trip);
@defineNumeratedObjectDefault(Trip, 'Нумератор для рейсов', 'РЕ');

truckTrip = DATA Truck(Trip);
sidTruckTrip 'Машина (номер)'(trip) = sidTruck(truckTrip(trip));
weightTruckTrip 'Грузоподъёмность (кг)'(trip) = weightTruck(truckTrip(trip));
ownerTruckTrip 'Владелец'(trip) = ownerTruck(truckTrip(trip));
truckModelTruckTrip (trip) = truckModelTruck(truckTrip(trip));
nameTruckModelTruckTrip 'Марка' (trip) = nameTruckModel(truckModelTruckTrip(trip));
trailerTruckTrip 'Прицеп' (trip) = trailerTruck(truckTrip(trip));

driverTrip = DATA Driver(Trip);
nameDriverTrip 'Водитель' (trip) = nameContact(driverTrip(trip));
sidDriverTrip 'Табельный номер' (trip) = sidDriver(driverTrip(trip));
typeDriverTrip 'Класс' (trip) = typeDriver(driverTrip(trip));

// заказы
tripOrder = DATA Trip(Order);
inTripOrder 'Включен' (trip, order) = tripOrder(order)==trip;

filterTripOrder(trip, order) = tripOrder(order)==trip OR (trip IS Trip AND order IS Order AND NOT tripOrder(order));

quantityFromTripStock (trip, stock) = GROUP SUM 1 IF quantityFromOrderStock(order, stock) BY tripOrder(order), stock;
quantityToTripStock (trip, stock) = GROUP SUM 1 IF quantityToOrderStock(order, stock) BY tripOrder(order), stock;
quantityTripStock(trip, stock) = UNION SUM quantityFromTripStock(trip, stock), quantityToTripStock(trip, stock);

TABLE tripStock(Trip, Stock);
numberStockTrip 'Порядковый номер' = DATA INTEGER(Stock, Trip);
numberStockTrip(stock, trip) => quantityTripStock(trip, stock)>0 RESOLVE FALSE;

firstStockTrip = DATA Stock(Trip);
isFirstStockTrip 'Начальный пункт' (stock, trip) = firstStockTrip(trip)==stock;
addressFirstStockTrip 'Начальный пункт' (trip) = addressStock(firstStockTrip(trip));

stockPrevTripStock (trip, stock) = PARTITION PREV stock IF quantityTripStock(trip, stock) BY trip ORDER numberStockTrip(stock, trip);
addressStockPrevTripStock 'Адрес' (trip, stock) = addressStock(stockPrevTripStock (trip, stock));

maxNumberTrip(trip) = GROUP MAX numberStockTrip(stock, trip) BY trip;
indexTripOrder 'Номер' (trip, order) = maxNumberTrip(trip) - numberStockTrip(toStockOrder(order), trip) + 1 IF inTripOrder(trip, order);

CONSTRAINT numberStockTrip(stock1, tripOrder(order)) >= numberStockTrip(stock2, tripOrder(order)) IF quantityFromOrderStock(order, stock1)>0 AND quantityToOrderStock(order, stock2)>0
    MESSAGE 'Номер не соответствует направлению заказа';

grossWeightOrderedTrip 'Суммарный вес заказов' (trip) = GROUP SUM grossWeightOrderDetailOrder(order) BY tripOrder(order);

grossWeightTripToStock 'Вес' = GROUP SUM round3(grossWeightOrderDetailOrder(order)/1000) BY tripOrder(order), toStockOrder(order);

quantityOrderedTripSku 'Кол-во в заказах' (trip, sku) = GROUP SUM quantityOrderDetail(orderDetail) BY tripOrder(orderOrderDetail(orderDetail)), skuOrderDetail(orderDetail);

// поставки
tripShipment = DATA Trip(Shipment);
inTripShipment 'Включен' (trip, shipment) = tripShipment(shipment)==trip;

filterTripShipment(trip, shipment) = tripShipment(shipment)==trip OR (trip IS Trip AND shipment IS Shipment AND NOT tripShipment(shipment));

grossWeightShippedTrip 'Суммарный вес поставок' (trip) = GROUP SUM grossWeightShipmentDetailShipment(shipment) BY tripShipment(shipment);

quantityShipmentDetailTripShipment (trip, shipment) = GROUP SUM quantityShipmentDetailOrderShipment (order, shipment) BY tripOrder(order), shipment;

quantityShippedTripSku 'Кол-во в поставках' (trip, sku) = GROUP SUM quantityShipmentDetail(shipmentDetail) BY tripShipment(shipmentShipmentDetail(shipmentDetail)), skuShipmentDetail(shipmentDetail);

createShipmentsTrip 'Включить поставки' (trip) = ACTION (trip) {
    SET tripShipment(shipment) <- NULL WHERE tripShipment(shipment) == trip;
    SET tripShipment(shipment) <- trip WHERE quantityShipmentDetailTripShipment(trip, shipment);
} TOOLBAR CONFIRM;

// отображение на карте
showPointsTrip 'Показать путь на карте' (trip) = ACTION (trip) {
    SET numberPathPOI(stock) <- 1 IF quantityTripStock(trip, stock) > 0 OVERRIDE numberStockTrip(stock, trip);
    EXEC showOnMapPath();

} TOOLBAR;

selectTripOrder 'Отмечен' = DATA SESSION BOOLEAN(Trip, Order);

quantityAllFromTripStock (trip, stock) = GROUP SUM 1 IF selectTripOrder(trip, order) BY trip, fromStockOrder(order);
quantityAllToTripStock (trip, stock) = GROUP SUM 1 IF selectTripOrder(trip, order) BY trip, toStockOrder(order);
quantityAllTripStock(trip, stock) = UNION SUM quantityAllFromTripStock(trip, stock), quantityAllToTripStock(trip, stock);

showPointOrders 'Показать отмеченные заказы' (trip) = ACTION (trip) {
    SET numberPathPOI(stock) <- 1 IF quantityAllTripStock(trip, stock);
    EXEC showOnMapPath();

} TOOLBAR;

createPathTrip 'Рассчитать путь' (trip) = ACTION (trip) {
    SET startPathPOI() <- firstStockTrip(trip);
    SET inPathPOI(stock) <- TRUE IF quantityTripStock(trip, stock);
    EXEC calculatePath();
    SET numberStockTrip(stock, trip) <- numberPathPOI(stock);
} TOOLBAR;

FORM ordersTrip PRINT

    OBJECTS t=Trip FIXED PANEL
    PROPERTIES (t) nameNumeratorObject, numberObject, seriesObject, dateTrip, timeTrip, nameDriverTrip, sidTruckTrip, nameTruckModelTruckTrip, trailerTruckTrip

    OBJECTS o=Order FIXED GRID
    FILTERS isPostedOrder(o), inTripOrder(t, o)

    OBJECTS s=Sku
    PROPERTIES READONLY idBarcodeSku(s), nameSku(s), shortNameUOMSku(s)
    PROPERTIES READONLY quantityOrderedTripSku(t, s)
    PROPERTIES READONLY quantityOrderDetailSkuOrder(s, o) COLUMNS (o) HEADER seriesNumberOrder(o)
    FILTERS quantityOrderedTripSku(t, s) > 0
;

printOrdersTrip 'Печать заказов' (trip) = ACTION (trip) {
    FORM ordersTrip OBJECTS t = trip;
} TOOLBAR;

FORM trip 'Рейс'
    PROPERTIES() namePartyA FORCE PANEL, namePartyB FORCE PANEL

    OBJECTS t=Trip FIXED PANEL
    PROPERTIES (t) nameNumeratorObject, numberObject, seriesObject, dateTrip, timeTrip, nameDriverTrip, sidTruckTrip, nameTruckModelTruckTrip, trailerTruckTrip, weightTruckTrip
    PROPERTIES (t) READONLY grossWeightOrderedTrip, grossWeightShippedTrip

    OBJECTS o=Order
    PROPERTIES(t, o) selectTripOrder, inTripOrder
    PROPERTIES(o) READONLY numberOrder, seriesOrder, dateOrder, nameSupplierOrder, nameFromStockOrder, nameCustomerOrder, nameToStockOrder, grossWeightOrderDetailOrder
    PROPERTIES(o) editOrder
    PROPERTIES printOrdersTrip(t) TODRAW o FORCE PANEL
    PROPERTIES showPointOrders(t) TODRAW o

    FILTERS isPostedOrder(o)

    FILTERS customerOrder(o) == partyB() OR (o IS Order AND NOT partyB()),
            supplierOrder(o) == partyA() OR (o IS Order AND NOT partyA())

    FILTERGROUP filterDateOrder
        FILTER 'Текущие' 'F9' dateOrder(o) <= dateTrip(t) DEFAULT
    FILTERGROUP filterOrder
        FILTER 'Не расписанные или в текущем рейсе' 'F11' filterTripOrder(t, o) DEFAULT
        FILTER 'В текущем рейсе' 'F10' inTripOrder(t, o)

    OBJECTS od=OrderDetail
    PROPERTIES(od) READONLY idBarcodeSkuOrderDetail, nameSkuOrderDetail, shortNameUOMSkuOrderDetail, quantityOrderDetail, grossWeightOrderDetail, nameFromStockOrderDetail, nameToStockOrderDetail
    FILTERS orderOrderDetail(od)==o

    OBJECTS s=Shipment
    PROPERTIES (t, s) inTripShipment
    PROPERTIES(s) READONLY numberShipment, seriesShipment, dateShipment, nameSupplierShipment, nameFromStockShipment,
                  nameCustomerShipment, nameToStockShipment, grossWeightShipmentDetailShipment
    PROPERTIES (s) editShipment

    FILTERS isPostedShipment(s)

     FILTERS customerShipment(s) == partyB() OR (s IS Shipment AND NOT partyB()),
             supplierShipment(s) == partyA() OR (s IS Shipment AND NOT partyA())

    PROPERTIES createShipmentsTrip(t) TODRAW s FORCE PANEL

    FILTERGROUP filterDateShipment
        FILTER 'Текущие' 'F9' dateShipment(s) <= dateTrip(t) DEFAULT
    FILTERGROUP filterShipment
        FILTER 'Не расписанные или в текущем рейсе' 'F11' filterTripShipment(t, s)
        FILTER 'В текущем рейсе' 'F10' inTripShipment(t, s) DEFAULT

    OBJECTS sd=ShipmentDetail
    PROPERTIES(sd) READONLY idBarcodeSkuShipmentDetail, nameSkuShipmentDetail, shortNameUOMSkuShipmentDetail, quantityShipmentDetail, grossWeightShipmentDetail, nameFromStockShipmentDetail, nameToStockShipmentDetail
    FILTERS shipmentShipmentDetail(sd)==s

    OBJECTS sk=Sku
    PROPERTIES(sk) READONLY idBarcodeSku, nameSku, shortNameUOMSku
    PROPERTIES(t, sk) READONLY quantityOrderedTripSku, quantityShippedTripSku

    FILTERGROUP filterSku
        FILTER 'Товары в заказах или поставках' 'F11' quantityOrderedTripSku(t, sk) > 0 OR quantityShippedTripSku(t, sk) > 0 DEFAULT
        FILTER 'Товары в заказах' 'F9' quantityOrderedTripSku(t, sk) > 0
        FILTER 'Товары в поставках' 'F10' quantityShippedTripSku(t, sk) > 0
        FILTER 'Товары в заказах и поставках' 'F8' quantityOrderedTripSku(t, sk) > 0 AND quantityShippedTripSku(t, sk) > 0

    OBJECTS o2=Order
    PROPERTIES(o2) READONLY numberOrder, seriesOrder, dateOrder, nameFromStockOrder, nameToStockOrder
    PROPERTIES(o2) editOrder
    PROPERTIES(sk, o2) READONLY quantityOrderDetailSkuOrder
    FILTERS inTripOrder(t, o2), quantityOrderDetailSkuOrder(sk, o2)>0

    OBJECTS s2=Shipment
    PROPERTIES(s2) READONLY numberShipment, seriesShipment, dateShipment, nameFromStockShipment, nameToStockShipment
    PROPERTIES(s2) editShipment
    PROPERTIES(sk, s2) READONLY quantityShipmentDetailSkuShipment
    FILTERS inTripShipment(t, s2), quantityShipmentDetailSkuShipment(sk, s2)>0

    OBJECTS st=Stock
    PROPERTIES(st) READONLY nameStock, addressStock, nameLegalEntityStock
    PROPERTIES(t, st) grossWeightTripToStock
    PROPERTIES(st, t) numberStockTrip, isFirstStockTrip
    FILTERS quantityTripStock (t, st)>0
    PROPERTIES (t) showPointsTrip TODRAW st
    PROPERTIES (t) createPathTrip TODRAW st

    EDIT Trip OBJECT t
;


DESIGN trip FROM DEFAULT{
    main {
        NEW allContainer {
            type = SPLITV;
            NEW headContainer {
                childConstraints = TO THE BOTTOM;
                NEW topContainer {
                    caption = 'Шапка документа';
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY(nameNumeratorObject);
                    ADD PROPERTY(numberObject);
                    ADD PROPERTY(seriesObject);
                    ADD PROPERTY(dateTrip);
                    ADD PROPERTY(timeTrip);
                }

                NEW midContainer {
                    childConstraints = TO THE RIGHT;
                    NEW truckContainer {
                        caption = 'Параметры машины';
                        childConstraints = TO THE RIGHTBOTTOM;
                        ADD PROPERTY(nameDriverTrip);
                        ADD PROPERTY(sidTruckTrip);
                        ADD PROPERTY(nameTruckModelTruckTrip);
                        ADD PROPERTY(trailerTruckTrip);
                        ADD PROPERTY(weightTruckTrip);
                    }

                    NEW sumContainer {
                        caption = 'Итоги';
                        childConstraints = TO THE BOTTOM;
                        ADD PROPERTY(grossWeightOrderedTrip);
                        ADD PROPERTY(grossWeightShippedTrip);
                    }
                }

                NEW filterContainer {
                    caption = 'Фильтрация';
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY(namePartyA) {
                        caption = 'Поставщик';
                    };
                    ADD PROPERTY(namePartyB){
                        caption = 'Покупатель';
                    };
                }
            }

            NEW bottomContainer {
                type = TABBED;
                NEW orderContainer {
                    caption = 'Заказы';
                    childConstraints = TO THE BOTTOM;
                    ADD o.box;
                    ADD od.box;
                }

                NEW shipmentContainer {
                    caption = 'Поставки';
                    childConstraints = TO THE BOTTOM;
                    ADD s.box;
                    ADD sd.box;
                }

                NEW skuContainer {
                    caption = 'Итоги по товарам';
                    childConstraints = TO THE RIGHT;
                    ADD sk.box;

                    NEW documentContainer {
                        childConstraints = TO THE BOTTOM;
                        //type = SPLITV;
                        ADD o2.box;
                        ADD s2.box;
                    }
                }

                ADD st.box {
                    caption = 'Путевой лист';
                };
            }
        }
        ADD functions.box;
    }
}

FORM trips 'Рейсы'
    OBJECTS t=Trip
    PROPERTIES(t) READONLY numberObject, seriesObject, dateTrip, timeTrip, nameDriverTrip, sidTruckTrip, nameTruckModelTruckTrip, trailerTruckTrip, weightTruckTrip, grossWeightOrderedTrip, grossWeightShippedTrip
    PROPERTIES(t) ADDFORM, EDITFORM, delete FORCE PANEL TOOLBAR

    OBJECTS o=Order
    PROPERTIES(o) READONLY numberOrder, seriesOrder, dateOrder, nameSupplierOrder, nameFromStockOrder, nameCustomerOrder, nameToStockOrder, grossWeightOrderDetailOrder
    FILTERS inTripOrder(t, o)

    OBJECTS s=Shipment
    PROPERTIES(s) READONLY numberShipment, seriesShipment, dateShipment, nameSupplierShipment, nameFromStockShipment, nameCustomerShipment, nameToStockShipment, grossWeightShipmentDetailShipment
    FILTERS inTripShipment(t, s)

    OBJECTS sk=Sku
    PROPERTIES(sk) READONLY idBarcodeSku, nameSku, shortNameUOMSku
    PROPERTIES(t, sk) READONLY quantityOrderedTripSku, quantityShippedTripSku

    FILTERGROUP filterSku
        FILTER 'Товары в заказах или поставках' 'F11' quantityOrderedTripSku(t, sk)>0 OR quantityShippedTripSku(t, sk)>0 DEFAULT
        FILTER 'Товары в заказах' 'F9' quantityOrderedTripSku(t, sk)>0
        FILTER 'Товары в поставках' 'F10' quantityShippedTripSku(t, sk)>0
        FILTER 'Товары в заказах и поставках' 'F8' quantityOrderedTripSku(t, sk)>0 AND quantityShippedTripSku(t, sk)>0

    OBJECTS st=Stock
    PROPERTIES(st) READONLY nameStock, addressStock, nameLegalEntityStock
    PROPERTIES(st, t) READONLY numberStockTrip
    FILTERS quantityTripStock(t, st)>0
;

DESIGN trips FROM DEFAULT{
    main {
        ADD t.box;

        NEW bottomContainer {
            type = TABBED;
            ADD o.box {
                caption = 'Заказы';
            };
            ADD s.box{
                caption = 'Поставки';
            };
            ADD sk.box{
                caption = 'Итоги по товарам';
            };
            ADD st.box{
                caption = 'Путевой лист';
            };
        }

        ADD functions.box;
    }
}

NAVIGATOR {
    transportDocuments {
        ADD trips BEFORE routes;
    }
}