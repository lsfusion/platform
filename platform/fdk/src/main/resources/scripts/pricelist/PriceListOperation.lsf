MODULE PriceListOperation;

REQUIRE PriceList;

NAMESPACE PriceList;

@defineOperation('(прайс)');
@defineOperationRole();
@extendFormFilterRole(o, dialogOperations);
//@extendFormFilterRole(o, operations);     //-- пока не нужен

TABLE priceListTypeOperation(PriceListType, Operation);

changeDataPriceListTypeOperation 'Изменять цены' = DATA BOOLEAN (DataPriceListType, Operation);
showPriceListTypeOperation 'Показывать цены' = DATA BOOLEAN (PriceListType, Operation);

@defineOperationLegalEntity(legalEntity, l, 'Организации');

defaultLegalEntityOperation = DATA LegalEntity (Operation);

nameDefaultLegalEntityOperation 'Организация по умолчанию' (operation) = nameLegalEntity(defaultLegalEntityOperation(operation));
isDefaultLegalEntityOperation 'Организация по умолчанию' (legalEntity, operation) =
    defaultLegalEntityOperation(operation) == legalEntity;

CONSTRAINT defaultLegalEntityOperation(operation) AND NOT inLegalEntityOperation(defaultLegalEntityOperation(operation), operation)
    CHECKED BY defaultLegalEntityOperation MESSAGE 'Организация по умолчанию должна быть в списке отмечанных организаций';

@defineOperationCurrency();
userDefaultCurrencyOperation = DATA Currency (Operation);
//userDefaultCurrencyOperation(operation) => inCurrencyOperation(userDefaultCurrencyOperation(operation), operation) RESOLVE FALSE;
calcDefaultCurrencyOperation(operation) =
    GROUP MAX currency IF inCurrencyOperation(currency, operation)
    BY operation PERSISTENT;
defaultCurrencyOperation(operation) = calcDefaultCurrencyOperation(operation) OVERRIDE
                                      userDefaultCurrencyOperation(operation) PERSISTENT;
nameDefaultCurrencyOperation 'Валюта по умолчанию' (operation) = nameCurrency(defaultCurrencyOperation(operation));
isDefaultCurrencyOperation 'Валюта по умолчанию' (currency, operation) =
    defaultCurrencyOperation(operation) == currency;

CONSTRAINT defaultCurrencyOperation(operation) AND NOT inCurrencyOperation(defaultCurrencyOperation(operation), operation)
    CHECKED BY defaultCurrencyOperation MESSAGE 'Валюта по умолчанию должны быть в списке отмечанных валют';

//Определение операции для прайсов
@defineDocumentOperation(priceList, p);

inUserPriceListDataPriceListType(priceList, dataPriceListType) <- changeDataPriceListTypeOperation(dataPriceListType, operationUserPriceList(priceList))
    WHEN CHANGED(operationUserPriceList(priceList));

showUserPriceListPriceListType(priceList, priceListType) <- showPriceListTypeOperation(priceListType, operationUserPriceList(priceList))
    WHEN CHANGED(operationUserPriceList(priceList));

companyUserPriceList(priceList) <- defaultLegalEntityOperation(operationUserPriceList(priceList))
    WHEN CHANGED(operationUserPriceList(priceList));

currencyUserPriceList(priceList) <- defaultCurrencyOperation(operationUserPriceList(priceList))
    WHEN CHANGED(operationUserPriceList(priceList));

copyOperationPriceList = ACTION (priceList, userPriceList){
    SET operationUserPriceList(userPriceList) <- operationUserPriceList(priceList);
}
copyAbstractDataPriceList(priceList, userPriceList) +=copyOperationPriceList(priceList, userPriceList);

CONSTRAINT operationUserPriceList(priceList) AND NOT inLegalEntityOperation(companyUserPriceList(priceList), operationUserPriceList(priceList))
    CHECKED BY companyUserPriceList MESSAGE 'Компания прайса должна соответствовать ограничениям операции';

EXTEND FORM operation
    OBJECTS pt = PriceListType
    PROPERTIES(pt, o) changeDataPriceListTypeOperation TODRAW pt FORCE GRID, showPriceListTypeOperation
    PROPERTIES(pt) READONLY namePriceListType, nameCurrencyPriceListType, objectClassName
    FILTERGROUP filters
        FILTER 'Показывать отмеченные' 'F9' changeDataPriceListTypeOperation(pt, o)
    PROPERTIES(l, o) isDefaultLegalEntityOperation AFTER inLegalEntityOperation
    PROPERTIES(c, o) isDefaultCurrencyOperation AFTER inCurrencyOperation
    PROPERTIES(o) nameDefaultLegalEntityOperation, nameDefaultCurrencyOperation
;

EXTEND DESIGN operation {
    headContainer{
        NEW defaultContainer{
            childConstraints = TO THE RIGHT;
            caption = 'Параметры по умолчанию';
            ADD PROPERTY(nameDefaultLegalEntityOperation);
            ADD PROPERTY(nameDefaultCurrencyOperation);
        }
    }
    tabContainer {
        ADD pt.box BEFORE legalEntityContainer;
    }
}

NAVIGATOR {
    priceListMasterData {
        ADD operations;
    }
}