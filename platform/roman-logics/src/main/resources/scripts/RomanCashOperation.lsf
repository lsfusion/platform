MODULE RomanCashOperation;

REQUIRE System, CashOperation, RomanPOS;

NAMESPACE CashOperation;

CLASS AccountCollection 'Изъятие (упр.)' : OutcomeCashOrder, Historizable, NumeratedObject;
TABLE accountCollection (AccountCollection);

@defineNumeratedObjectDefault(AccountCollection, 'Нумератор для изъятий', 'ИЗ');

@defineDocumentHeaderTime(AccountCollection);

zReportAccountCollection 'Z-отчет (ИД)' (accountCollection) = DATA ZReport (AccountCollection);

cashRegisterAccountCollection (accountCollection) = cashRegisterZReport(zReportAccountCollection(accountCollection));
numberCashRegisterAccountCollection 'Касса инкассации' (accountCollection) =
    numberCashRegister(cashRegisterAccountCollection(accountCollection)) IN documentPrmGroup;

departmentStoreAccountCollection (accountCollection) = departmentStoreZReport(zReportAccountCollection(accountCollection));
nameDepartmentStoreAccountCollection 'Отдел' (accountCollection) = nameDepartmentStore(departmentStoreAccountCollection (accountCollection));

sumCashAccountCollection 'Сумма изъятия' = DATA NUMERIC[16,2] (AccountCollection) IN documentSumGroup;

castBasisAccountCollection =
    FORMULA STRING[100] '\'Изъятие №\' || CAST($1 AS TEXT) || \' с кассы \' || CAST($2 AS TEXT) || \' от \' || CAST($3 AS TEXT)';
basisAccountCollection 'Основание' (accountCollection) =
    castBasisCollection(seriesNumberObject(accountCollection), dateAccountCollection(accountCollection)) IN documentPrmGroup;

dateCashDocument(accountCollection) += dateAccountCollection(accountCollection);
timeCashDocument(accountCollection) += timeAccountCollection(accountCollection);
numberCashDocument(accountCollection) += seriesNumberObject(accountCollection) AND accountCollection IS AccountCollection;
departmentStoreCashDocument(accountCollection) += departmentStoreAccountCollection(accountCollection);
cashRegisterCashDocument(accountCollection) += cashRegisterAccountCollection(accountCollection);
sumCashOutcomeCashOrder(accountCollection) += sumCashAccountCollection(accountCollection);
basisCashDocument(accountCollection) += basisAccountCollection(accountCollection);
isPostedCashDocument(accountCollection) += TRUE AND accountCollection IS AccountCollection;

//sumCashIncomeCashOrder(zReport) += sumCashZReport(zReport) (-) sumSkipAReceiptSaleDetailZReport(zReport);

createAccountCollectionZReport 'Изъять сумму' (zReport) = ACTION (zReport) {
    EXEC selectDialogSkipAReceiptDetailZReport (zReport);
    ADDOBJ AccountCollection;
    FOR a == addedObject() DO {
        SET zReportAccountCollection (a) <- zReport;
        SET dateCashDocument (a) <- currentDate();
        SET timeCashDocument (a) <- currentTime();
        SET sumCashAccountCollection (a) <- selectSkipAReceiptDetailZReportSumSelected ();
    }
} TOOLBAR;

EXTEND FORM centralCashRegister PROPERTIES (i) createAccountCollectionZReport;



