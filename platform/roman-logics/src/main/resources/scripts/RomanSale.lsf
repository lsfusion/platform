MODULE RomanSale;

REQUIRE System,
        Document,
        RomanDocument,
        Stock,
        RomanStock,
        DocumentTransfer,
        Contract,
        ContractCompany,
        RetailPrice,
        StorePriceTransfer,
        WholesalePrice,
        ConsignmentBy,
        RomanLogicsModule;

PRIORITY Stock, RomanLogicsModule;

// ---------------------------------------- Перемещение ------------------------------------ //

CLASS SaleOut 'Продажа товаров по накладным' : Historizable, NumberedObject, OutStockDocumentLedger, InnerDocument;
CLASS SaleOutPosted 'Проведенная продажа товаров по накладным' : SaleOut, PostedObject;
CLASS SaleOutDetail 'Строка продажи товаров по накладным';
CLASS SaleOutBatchDetail 'Строка продажи товаров по накладным (по партиям)';

@defineDocumentTransferOut(sale, 'Продажа товаров по накладным', sku, stock);
@defineDocumentFormTransferOut(sale, 'Продажа товаров по накладным');

@defineDocumentTransferOutBatch(sale, sku);
@defineDocumentFormTransferOutBatch(sale);

@defineDocumentCurrency(saleOut);
@deriveDocumentCurrency(saleOut, stock);

@extendFormDocumentDetailSkuArticleReadonly(saleOuts, o, saleOutBatch);

importerPriceSaleOutBatchDetail 'Цена импортера' (detail) = importerPriceBatchA(batchSaleOutBatchDetail(detail));
supplierPriceSaleOutBatchDetail 'Цена поставщика' (detail) = supplierPriceBatchA(batchSaleOutBatchDetail(detail));
EXTEND FORM saleOuts
    PROPERTIES(o) READONLY importerPriceSaleOutBatchDetail, supplierPriceSaleOutBatchDetail
;

CONSTRAINT legalEntityStockSaleOut(saleOut) == destinationLegalEntityStockSaleOut(saleOut) CHECKED BY destinationStockSaleOut
    MESSAGE 'Продажа товаров должна идти между складами разных компаний';

@defineDocumentDetailSkuArticle(saleOut);
@extendFormDocumentDetailSkuArticle(saleOut, d, saleOut);
@extendFormDocumentDetailSkuArticleReadonly(saleOuts, d, saleOut);

@defineDocumentContract(saleOut, contractSku, legalEntityStock, destinationLegalEntityStock, singleContractSkuSupplierCustomer,
                        'Сторона A договора продажи товаров не соответствуют компании склада-отправителя',
                        'Сторона B договора продажи товаров не соответствуют компании склада-получателя');

supplierPriceSaleOutDetail 'Цена оптовая' = DATA NUMERIC[14,2] (SaleOutDetail);
//supplierPriceSaleOutDetail (saleOutDetail) <- priceWholesaleArticleDateTime(articleSku(skuSaleOutDetail(saleOutDetail)), dateTimeSaleOutDetail(saleOutDetail))
//    WHEN CHANGED(skuSaleOutDetail(saleOutDetail)) OR CHANGED(dateTimeSaleOutDetail(saleOutDetail));

supplierPriceSaleOutDetail (saleOutDetail) <- priceWholesaleArticleCurrencyDateTime(articleSku(skuSaleOutDetail(saleOutDetail)), currencySaleOutDetail(saleOutDetail), dateTimeSaleOutDetail(saleOutDetail))
    WHEN CHANGED(skuSaleOutDetail(saleOutDetail)) OR CHANGED(dateTimeSaleOutDetail(saleOutDetail)) OR CHANGED(currencySaleOutDetail(saleOutDetail));

EXTEND FORM saleOut
    PROPERTIES(t) numberContractSkuSaleOut, isCommissionSaleOut
    PROPERTIES(d) supplierPriceSaleOutDetail
;

EXTEND FORM saleOuts
    PROPERTIES(t) READONLY isCommissionSaleOut
    PROPERTIES(d) READONLY supplierPriceSaleOutDetail
;

@defineDocumentRetailPrice(saleOut, d);

isCommissionSaleOutDetail(detail) = isCommissionSaleOut(saleOutSaleOutDetail(detail));

useRetailPriceSaleOutDetail(detail) = [stockSaleOutDetail(detail) IS DepartmentStore
    AND NOT costLedgerDepartmentStore(stockSaleOutDetail(detail))](detail)
    OR isCommissionSaleOutDetail(detail);
@defineDocumentTransferAccount(saleOut, useRetailPriceSaleOutDetail);

@defineDocumentSkipSkuLedgerCustom(saleOut, saleOutDetail);
@extendFormDocumentSkipSkuLedgerCustom(saleOut, t, saleOut);
@extendFormDocumentSkipSkuLedgerCustomReadonly(saleOuts, t, saleOut);

// Проводим по регистру
@implementSkuLedgerOutFIFO(SaleOutDetail, sku, stock);
quantityOutFIFOSkuLedger (ledger) += quantitySaleOutDetail (ledger);
limitOutFIFOSkuLedgerBatch(ledger, batch) += currentBalanceABatchStock(batch, stockSaleOutDetail(ledger)) AND NOT commissionContractSkuBatchA(batch)
                                             AND batch IS BatchA;
sumOutSkuLedger(ledger) += accountSumSaleOutDetail(ledger);
skipSkuLedger (ledger) += skipSkuLedgerSaleOutDetail(ledger);

@defineConstraintSkuLedgerCosted(saleOut, 'Не хватает остатков по партиям для продажи товаров');

// Товарные накладные
@defineDocumentDetailGrossWeight(saleOutBatch, sku);
@defineDocumentDetailQuantityPack(saleOutBatch, sku);

@defineConsignmentHeader(saleOut);
isCommissionSaleOutBatchDetail 'Продажа на комиссию' (saleOutBatchDetail) = isCommissionSaleOut(saleOutSaleOutBatchDetail(saleOutBatchDetail));

@implementConsignmentHeader(SaleOut);

supplierConsignment (consignment) += legalEntityStock(stockSaleOut(consignment));
supplierStockConsignment (consignment) += stockSaleOut(consignment);
customerConsignment (consignment) += legalEntityStock(destinationStockSaleOut(consignment));
customerStockConsignment (consignment) += destinationStockSaleOut(consignment);

EXTEND CLASS SaleOutBatchDetail : ConsignmentDetail;
consignmentConsignmentDetail (consignmentDetail) += saleOutSaleOutBatchDetail (consignmentDetail);
skuConsignmentDetail (consignmentDetail) += skuSaleOutBatchDetail (consignmentDetail);
quantityConsignmentDetail (consignmentDetail) += quantitySaleOutBatchDetail (consignmentDetail);

priceSaleOutBatchDetailC (saleOutBatchDetail) = retailPriceSaleOutDetail(saleOutDetailSaleOutBatchDetail(saleOutBatchDetail));
priceSaleOutBatchDetailS (saleOutBatchDetail) = supplierPriceBatchA(batchSaleOutBatchDetail(saleOutBatchDetail));

supplierSumSaleOutBatchDetailC (saleOutBatchDetail) = priceSaleOutBatchDetailC(saleOutBatchDetail) *
    quantitySaleOutBatchDetail (saleOutBatchDetail);
supplierSumSaleOutBatchDetailS (saleOutBatchDetail) = priceSaleOutBatchDetailS(saleOutBatchDetail) *
    quantitySaleOutBatchDetail (saleOutBatchDetail);

sumVATSaleOutBatchDetailS (saleOutBatchDetail) =  [round0(X*Y/100)](supplierSumSaleOutBatchDetailS(saleOutBatchDetail),
    valueSupplierVATBatchA(batchSaleOutBatchDetail(saleOutBatchDetail)));


priceConsignmentDetail (consignmentDetail) += IF isCommissionSaleOutBatchDetail(consignmentDetail) THEN priceSaleOutBatchDetailC(consignmentDetail)
    ELSE priceSaleOutBatchDetailS(consignmentDetail);

sumConsignmentDetail (consignmentDetail) += IF isCommissionSaleOutBatchDetail(consignmentDetail) THEN supplierSumSaleOutBatchDetailC(consignmentDetail)
    ELSE supplierSumSaleOutBatchDetailS(consignmentDetail);

vatConsignmentDetail (consignmentDetail) += valueSupplierVATBatchA(batchSaleOutBatchDetail(consignmentDetail))
    AND NOT isCommissionSaleOutBatchDetail(consignmentDetail);

sumVATConsignmentDetail (consignmentDetail) +=  sumVATSaleOutBatchDetailS(consignmentDetail)
    AND NOT isCommissionSaleOutBatchDetail(consignmentDetail);

sumInvoiceConsignmentDetail (consignmentDetail) += IF isCommissionSaleOutBatchDetail(consignmentDetail) THEN supplierSumSaleOutBatchDetailC(consignmentDetail)
    ELSE supplierSumSaleOutBatchDetailS(consignmentDetail) (+) sumVATSaleOutBatchDetailS(consignmentDetail);

grossWeightConsignmentDetail (consignmentDetail) += grossWeightSaleOutBatchDetail (consignmentDetail);
quantityPackConsignmentDetail (consignmentDetail) += quantityPackSaleOutBatchDetail (consignmentDetail);
//shortNameUOMConsignmentDetail (consignmentDetail) += shortNameUOMSaleOutBatchDetail (consignmentDetail);


noteSaleOutBatchDetail (saleOutBatchDetail) = [FORMULA STRING[30] '\'Цена импортера:\' ||  \' \' || CAST($1 AS TEXT)'](
    importerPriceBatchA(batchSaleOutBatchDetail(saleOutBatchDetail)));

noteConsignmentDetail (consignmentDetail) += noteSaleOutBatchDetail(consignmentDetail) IF
    supplierPriceBatchA(batchSaleOutBatchDetail(consignmentDetail)) != importerPriceBatchA(batchSaleOutBatchDetail(consignmentDetail));

@defineDocumentFormTransferOutConsignment(sale, t);
// todo: заполнить необходимыми данными

EXTEND FORM saleOut
    PROPERTIES(t) nameCurrencySaleOut
;
EXTEND FORM saleOuts
    PROPERTIES(t) READONLY nameCurrencySaleOut
;
// ---------------------------------------- Поступление ------------------------------------ //

CLASS SaleIn 'Покупка товаров по накладным' : Historizable, IncStockDocumentLedger;
CLASS SaleInDetail 'Строка покупки товаров по накладным';
CLASS SaleInBatchDetail 'Строка покупки товаров по накладным (по партиям)';

@defineDocumentTransferIn(sale, 'Покупка товаров по накладным', sku, stock);
@defineDocumentFormTransferIn(sale, 'Покупка товаров по накладным');

@defineDocumentDetailSkuArticle(saleIn);
@extendFormDocumentDetailSkuArticleReadonly(saleIns, d, saleIn);

TABLE saleInBatchDetail(SaleInBatchDetail);

@defineAggregation(saleOutDetail, batch, saleInBatchDetail, costSaleOutDetailBatch);

saleOutSaleInBatchDetail (detail) = saleOutSaleOutDetail(saleOutDetailSaleInBatchDetail(detail));
saleInSaleInBatchDetail (detail) = saleInSaleOut(saleOutSaleInBatchDetail(detail));

skipSkuLedgerSaleIn 'Не проводить по учету' (saleIn) = skipSkuLedgerSaleOut(saleOutSaleIn(saleIn));
skipSkuLedgerSaleInBatchDetail (detail) = skipSkuLedgerSaleOutDetail(saleOutDetailSaleInBatchDetail(detail));

@defineDocumentDetailIndex(saleIn, saleInBatchDetail);

@defineDocumentAggregationDetailTime(saleOut, saleInBatch);
@defineDocumentAggregationDetailStockPrefix(saleOut, saleInBatch, stock, 'Склад-получатель', destination, );
@defineDocumentAggregationDetailStockPrefix(saleOut, saleInBatch, stock, 'Склад-отправитель', , destination);
@defineDocumentAggregationDetailPosted(saleOut, saleInBatch);
@defineDocumentAggregationDetailSku(saleOut, saleInBatch, sku);
@defineDocumentDetailDescription(saleIn, saleInBatchDetail);

quantitySaleInBatchDetail 'Кол-во' (detail) = costDataSkuLedgerBatch(saleOutDetailSaleInBatchDetail(detail), batchSaleInBatchDetail(detail));

contractSkuSaleInBatchDetail (detail) = contractSkuSaleOut(saleOutSaleInBatchDetail(detail));
isCommissionSaleInBatchDetail 'Продажа на комиссию' (detail) = isCommissionSaleOut(saleOutSaleInBatchDetail(detail));

isCommissionSaleIn 'Продажа на комиссию' (saleIn) = isCommissionSaleOut(saleOutSaleIn(saleIn));

costSaleInBatchDetail = DATA NUMERIC[14,2] (SaleInBatchDetail);
costSaleInBatchDetail (detail) <- [prevCostBatch(batch)](batchSaleInBatchDetail(detail)) WHEN ASSIGNED(isPostedSaleInBatchDetail(detail));

importerPriceSaleInBatchDetail = DATA NUMERIC[14,2] (SaleInBatchDetail);
importerPriceSaleInBatchDetail (detail) <- IF batchSaleInBatchDetail(detail) IS SaleInBatchDetail THEN
                                                [PREV(importerPriceBatchA(batch))](batchSaleInBatchDetail(detail)) ELSE
                                                supplierPriceSaleOutDetail(saleOutDetailSaleInBatchDetail(detail))
                                            WHEN ASSIGNED(isPostedSaleInBatchDetail(detail));

supplierPriceSaleInBatchDetail (detail) = supplierPriceSaleOutDetail(saleOutDetailSaleInBatchDetail(detail));

rangeVATSaleInBatchDetail (detail) = DATA Range (SaleInBatchDetail);
rangeVATSaleInBatchDetail (detail) <- [PREV(rangeVATBatchA(batch))](batchSaleInBatchDetail(detail)) WHEN ASSIGNED(isPostedSaleInBatchDetail(detail));

accountPriceSaleInBatchDetail (detail) = IF isCommissionSaleInBatchDetail(detail) THEN retailPriceSaleOutDetail(saleOutDetailSaleInBatchDetail(detail))
                                                                                  ELSE supplierPriceSaleOutDetail(saleOutDetailSaleInBatchDetail(detail));

costSaleInBatch (saleIn, batch) = quantitySaleInBatchDetail(batch) AND saleIn == saleInSaleInBatchDetail(batch) PERSISTENT;

EXTEND FORM saleIns
    PROPERTIES(t) READONLY isCommissionSaleIn BEFORE countSaleInDetailSaleIn, skipSkuLedgerSaleIn
    OBJECTS b = SaleInBatchDetail
    FILTERS saleInSaleInBatchDetail(b) == t
;

EXTEND DESIGN saleIns {
    documentDetail {
        ADD b.box {
            title = 'Партии';
        }
    }
}

@defineDocumentDetailSkuArticle(saleInBatch);
@extendFormDocumentDetailSkuArticleReadonly(saleIns, b, saleInBatch);

EXTEND FORM saleIns
    PROPERTIES(b) READONLY descriptionBatch, importerPriceBatchA, supplierPriceBatchA, valueSupplierVATBatchA
;

retailPriceSaleInBatchDetail 'Цена розничная' (detail) = retailPriceSaleOutDetail(saleOutDetailSaleInBatchDetail(detail));
retailSumSaleInBatchDetail 'Сумма розничная' (detail) = quantitySaleInBatchDetail(detail) * retailPriceSaleInBatchDetail(detail);

useRetailPriceSaleInBatchDetail(detail) = [stockSaleInBatchDetail(detail) IS DepartmentStore
    AND NOT costLedgerDepartmentStore(stockSaleInBatchDetail(detail))](detail)
    OR isCommissionSaleInBatchDetail(detail);
@defineDocumentTransferAccountCustom(saleIn, useRetailPriceSaleInBatchDetail, saleInBatchDetail, b);

EXTEND CLASS SaleInBatchDetail : BatchA;
@implementBatchCustom(saleInBatchDetail, sku, stock, cost);
quantityBatch(batch) += quantitySaleInBatchDetail(batch);
ownerBatchA (batch) += IF isCommissionSaleInBatchDetail(batch) THEN legalEntityStock(destinationStockSaleInBatchDetail(batch)) ELSE legalEntityStock(stockSaleInBatchDetail(batch));
commissionContractSkuBatchA (batch) += contractSkuSaleInBatchDetail(batch) AND isCommissionSaleInBatchDetail(batch);
importerPriceBatchA(batch) += importerPriceSaleInBatchDetail(batch);
supplierPriceBatchA(batch) += supplierPriceSaleInBatchDetail(batch);
rangeVATBatchA(batch) += rangeVATSaleInBatchDetail(batch);
sumInSkuLedger(batch) += accountSumSaleInBatchDetail(batch);
skipSkuLedger (ledger) += skipSkuLedgerSaleInBatchDetail(ledger);

nameCurrencySaleIn 'Валюта' (saleIn) = nameCurrencySaleOut(saleOutSaleIn(saleIn)) MINCHARWIDTH 10 PREFCHARWIDTH 10 MAXCHARWIDTH 15;

// ---------------------------------------- Приемка ------------------------------------ //

CLASS SaleRec 'Приемка товаров по накладной (продажа)' : Historizable;
CLASS SaleRecPosted 'Проведенная приемка товаров по накладной (продажа)' : SaleRec, PostedObject;

CLASS SaleRecDetail 'Строка приемки товаров по накладной (продажа)';

@defineDocumentTransferRec(sale, 'Приемка товаров по накладным (продажа)', sku, stock);
@defineDocumentFormTransferRec(sale, 'Приемка товаров по накладным (продажа)');

@defineDocumentDetailSkuArticle(saleRec);
@extendFormDocumentDetailSkuArticle(saleRec, d, saleRec);

@extendFormDocumentDetailSkuArticleReadonly(saleIns, r, saleRec);

saleRecSaleInBatchDetail (detail) = saleRecSaleIn(saleInSaleInBatchDetail(detail));

// ---------------------------------------- Акт расхождений ------------------------------------ //
CLASS SaleDiff 'Акт расхождений приемки товаров по накладной (продажа)' : Historizable;

CLASS ABSTRACT SaleDiffDetail 'Строка расхождений акта приемки товаров по накладной (продажа)';
CLASS SaleDiffEDetail 'Строка расхождений акта приемки товаров по накладной (продажа, излишек)' : SaleDiffDetail;
CLASS SaleDiffSDetail 'Строка расхождений акта приемки товаров по накладной (продажа, недостача)' : SaleDiffDetail;

@defineDocumentTransferDiff(sale, 'Акт расхождений приемки товаров по накладной (продажа)', sku, stock);
@defineDocumentFormTransferDiff(sale);

@defineDocumentDetailSkuArticle(saleDiff);
@extendFormDocumentDetailSkuArticleReadonlyQuantity(saleIns, f, saleDiff, signedQuantitySaleDiffDetail);

costSaleDiffEDetail(detail) = 0.0 IF detail IS SaleDiffEDetail;
EXTEND CLASS SaleDiffEDetail : BatchB;
@implementBatchCustom(saleDiffEDetail, sku, stock, cost);
quantityBatch (batch) += quantitySaleDiffEDetail(batch);
skipASkuLedger (ledger) += ledger IS SaleDiffEDetail;

@implementSkuLedgerOutFIFO(SaleDiffSDetail, sku, stock);
quantityOutFIFOSkuLedger (ledger) += quantitySaleDiffSDetail(ledger);
limitOutFIFOSkuLedgerBatch (ledger, batch) += costSaleInBatch(saleInSaleDiffSDetail(ledger), batch);
orderOutFIFOSkuLedgerBatch (ledger, batch) += orderBBatch(batch) IF ledger IS SaleDiffSDetail;

accountPriceSaleRecBatchA (saleRec, batchA) = GROUP MAX accountPriceSaleInBatchDetailBatchA(detail, batchA) BY saleRecSaleInBatchDetail(detail), batchA;
accountSumSaleDiffSDetail (detail) = GROUP SUM costDataSkuLedgerBatch(detail, batch) * accountPriceSaleRecBatchA(saleRecSaleDiffSDetail(detail), batch) BY detail PERSISTENT;
sumOutSkuLedger(ledger) += accountSumSaleDiffSDetail(ledger);

skipASkuLedger (ledger) += ledger IS SaleDiffSDetail;

// ---------------------------------------- Акт расценки ------------------------------------ //
CLASS TransferPriceActSale 'Акт расценки (поставка)' : TransferPriceAct;
CLASS TransferPriceActSaleDetail 'Строка акта расценки': TransferPriceActDetail;

prevImporterPriceBatch (batch) = PREV(importerPriceBatchA(batch)) AND batch IS Batch;
prevSupplierPriceBatch (batch) = PREV(supplierPriceBatchA(batch)) AND batch IS Batch;
prevRangeVATBatch (batch) = PREV(rangeVATBatchA(batch)) AND batch IS Batch;

@defineDocumentTransferPriceAct (sale, 'Акт расценки (продажа)', 'Строка акта расценки (продажа)', sku, stock, prevImporterPriceBatch, prevSupplierPriceBatch, prevRangeVATBatch);
@defineDocumentTransferPriceActExtra (sale, prevRangeVATBatch);
notPassToBookkeepingListRegister (inputListRegister) += isCommissionSaleOut(saleOutTransferPriceActSale(inputListRegister));

contractSkuTransferPriceActSale(transferPriceActSale) = contractSkuSaleOut(saleOutTransferPriceActSale(transferPriceActSale));

dataFromContractListRegister (listRegister) += dateFromContract(contractSkuTransferPriceActSale (listRegister));
numberContractListRegister (listRegister) += numberContract(contractSkuTransferPriceActSale (listRegister));
fullInputListRegister (inputListRegister) +=  inputListRegister IS TransferPriceActSale;

printInputListRegisterSaleIn 'Реестр цен' (saleIn) = printListRegister(transferPriceActSaleSaleOut(saleOutSaleIn(saleIn))) TOOLBAR;

EXTEND FORM saleIns
    PROPERTIES(t) READONLY nameCurrencySaleIn
    PROPERTIES (t) printInputListRegisterSaleIn

;
//---------------------------------------- Товарный отчет ------------------------- //

dateTimeStockDocumentLedger (ledger) += dateTimeSaleOut(ledger);
isPostedStockDocumentLedger (ledger) += isPostedSaleOut(ledger);
stockStockDocumentLedger (ledger) += stockSaleOut(ledger);
descriptionStockDocumentLedger (ledger) += descriptionSaleOut(ledger);

sumOutStockDocumentLedger (ledger) += accountSumSaleOut(ledger);
sumItemOutStockDocumentLedger (ledger) += accountSumSaleOut(ledger);
sumContainerOutStockDocumentLedger (ledger) += 0.0 IF ledger IS SaleOut;

// приход
dateTimeStockDocumentLedger (ledger) += dateTimeSaleIn(ledger);
isPostedStockDocumentLedger (ledger) += isPostedSaleIn(ledger);
stockStockDocumentLedger (ledger) += stockSaleIn(ledger);
descriptionStockDocumentLedger (ledger) += descriptionSaleIn(ledger);

sumIncStockDocumentLedger (ledger) += accountSumSaleIn(ledger);
sumItemIncStockDocumentLedger (ledger) += accountSumSaleIn(ledger);
sumContainerIncStockDocumentLedger (ledger) += 0.0 IF ledger IS SaleIn;
