MODULE RomanPOS;

REQUIRE POS, RomanStock;

NAMESPACE POS;

// ------------------------------------------------ Проведение по регистру ------------------------------------- //

userSkipAReceiptDetail (receiptDetail) = DATA BOOLEAN (ReceiptDetail);

diffCompanyReceipt (receipt) = legalEntityCashRegister (cashRegisterZReport(zReportReceipt(receipt))) != legalEntityDepartmentStore (departmentStoreReceipt (receipt));

skipAReceiptSaleDetail 'Не бухг.' (receiptDetail) = UNION OVERRIDE diffCompanyReceipt (receiptReceiptDetail (receiptDetail)),
                                                            userSkipAReceiptDetail (receiptDetail) PERSISTENT;

EXTEND FORM zReport PROPERTIES (d) skipAReceiptSaleDetail;
EXTEND FORM zReports PROPERTIES (d) READONLY FORCE GRID skipAReceiptSaleDetail;

skipASkuLedger (ledger) += skipAReceiptSaleDetail (ledger);

// ------------------------------------------------- Бухгалтерские суммы ---------------------------------------- //

sumSkipAReceiptSaleDetailZReport 'Сумма (не бухг.)' (zReport) = GROUP SUM sumReceiptSaleDetail (receiptSaleDetail)
                                                   IF skipAReceiptSaleDetail (receiptSaleDetail)
                                                   BY zReportReceiptDetail (receiptSaleDetail);

sumAccountReceiptDetailZReport 'Сумма учетная' (zReport) = sumReceiptDetailZReport (zReport) (-) sumSkipAReceiptSaleDetailZReport (zReport);

EXTEND FORM zReport PROPERTIES (z) sumSkipAReceiptSaleDetailZReport, sumAccountReceiptDetailZReport;
EXTEND FORM zReports PROPERTIES (z) READONLY sumSkipAReceiptSaleDetailZReport, sumAccountReceiptDetailZReport;

// --------------------------------------- Подбор строк для изъятия денег --------------------------- //

selectSkipAReceiptDetailZReportSumSelected = DATA SESSION NUMERIC[16,2] ();

selectSkipAReceiptDetailZReport 'Подобрать строки' = ACTION (zReport, sumSelect) {
    LOCAL sumLeft = NUMERIC[16,2] ();
    SET sumLeft() <- sumSelect;
    SET selectSkipAReceiptDetailZReportSumSelected() <- NULL;

    SET skipAReceiptSaleDetail(receiptDetail) <- NULL WHERE zReportReceiptDetail(receiptDetail) == zReport;
    FOR zReportReceiptDetail(receiptDetail) == zReport ORDER DESC sumReceiptSaleDetail(receiptDetail) DO {
        IF sumReceiptSaleDetail(receiptDetail) <= sumLeft() THEN {
            SET skipAReceiptSaleDetail(receiptDetail) <- TRUE;
            SET sumLeft() <- sumLeft() - sumReceiptSaleDetail(receiptDetail);
            SET selectSkipAReceiptDetailZReportSumSelected() <- selectSkipAReceiptDetailZReportSumSelected() (+) sumReceiptSaleDetail(receiptDetail);
        }
    }
}

selectDialogSkipAReceiptDetailZReport 'Подобрать строки' = ACTION (zReport) {
    FORM dialogNumeric MODAL;
    IF formResult() == FormResult.ok THEN {
        EXEC selectSkipAReceiptDetailZReport(zReport, chosenNumeric('n'));
    }
} TOOLBAR;

EXTEND FORM zReports PROPERTIES (z) selectDialogSkipAReceiptDetailZReport;