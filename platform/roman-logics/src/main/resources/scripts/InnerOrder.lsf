MODULE InnerOrder;

REQUIRE System,
        Document,
        RomanDocument,
        Stock,
        RomanStock,
        RetailPrice,
        DocumentTransfer,
        Move,
        Transfer,
        Sale,
        Return,
        WHtoLegalEntity,
        RomanLogicsModule;

PRIORITY Stock, RomanLogicsModule;

// ---------------------------------------- Внутренний заказ ------------------------------------ //
CLASS innerOrder 'Внутренний заказ': historyObject, numeratedObject;
CLASS innerOrderDetail 'Строка внутреннего заказа';

@defineNumeratedObjectDefault(innerOrder, 'Нумератор для внутренних заказов', 'ВЗ');

@defineDocument(innerOrder);

@defineDocumentStock(innerOrder, stock, 'Склад-отправитель');
@defineDocumentStock(innerOrder, stock, 'Склад-получатель', destination);

@defineDocumentDescription (innerOrder, 'Внутренний заказ');

@defineDocumentDetailNumber(innerOrder);

@defineDocumentDetailSku(innerOrder, sku);

@defineDocumentDetailQuantity(innerOrder);

@defineDocumentDetailPackWeightSku(innerOrder);

@defineDocumentDetailSkuBalance(innerOrder, sku, stock);

@defineDocumentHeaderQuantity(innerOrder);
@defineDocumentHeaderSkuQuantity(innerOrder, sku);

@defineDocumentDetailSkuArticle(innerOrder);

// Кнопки подбора
@defineAddDetailDialogSkuStock(innerOrder, sku, stock, dialogSku);
@defineAddDetailDialogBarcode(innerOrder, sku);

messageInnerOrder 'Сообщение' (innerOrder) = DATA TEXT (innerOrder) IN documentPrmGroup;

FORM innerOrder 'Внутренний заказ'
    OBJECTS o = innerOrder FIXED PANEL

    PROPERTIES (o) nameNumeratorObject, numberObject, seriesObject, dateInnerOrder, timeInnerOrder,
        nameStockInnerOrder, nameDestinationStockInnerOrder, countInnerOrderDetailInnerOrder, quantityInnerOrderDetailInnerOrder,
        messageInnerOrder

    OBJECTS d = innerOrderDetail

    PROPERTIES(o) TODRAW d addDetailDialogSkuStockInnerOrderDetailInnerOrder, addDetailInputBarcodeInnerOrderDetailInnerOrder,
                           deleteInnerOrderDetailInnerOrder

    FILTERS innerOrderInnerOrderDetail(d) == o

    EDIT innerOrder OBJECT o
;

DESIGN innerOrder FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        o.box {
            childConstraints = TO THE RIGHT;
            NEW columnHeaderPrm {
                childConstraints = TO THE BOTTOM;
                ADD o.documentHeaderGroup {
                    childConstraints = TO THE RIGHTBOTTOM;
                    ADD PROPERTY (nameStockInnerOrder);
                    ADD PROPERTY (nameDestinationStockInnerOrder);
                    ADD PROPERTY (nameNumeratorObject);
                    ADD PROPERTY (numberObject);
                    ADD PROPERTY (seriesObject);
                    ADD PROPERTY (dateInnerOrder);
                    ADD PROPERTY (timeInnerOrder);
                }
                ADD o.documentPrmGroup {
                    childConstraints = TO THE BOTTOM;
                };
            }
            NEW columnHeaderSum {
                ADD o.documentSumGroup {
                    childConstraints = TO THE BOTTOM;
                    PROPERTY (messageInnerOrder) {
                        preferredSize = (1024, 100);
                    }
                }
            }
        }
    }
}

FORM innerOrders 'Внутренние заказы'
    OBJECTS o = innerOrder
    PROPERTIES (o) READONLY numberObject, seriesObject, dateInnerOrder, timeInnerOrder,
                            nameStockInnerOrder, nameDestinationStockInnerOrder,
                            countInnerOrderDetailInnerOrder, quantityInnerOrderDetailInnerOrder

    PROPERTIES (o) READONLY FORCE PANEL nameUserCreated, timeCreated, hostnameComputerCreated, nameUserClosed, timeClosed, hostnameComputerClosed

    PROPERTIES (o) ADDFORM, EDITFORM, delete FORCE PANEL DRAWTOTOOLBAR

    OBJECTS d = innerOrderDetail

    FILTERS innerOrderInnerOrderDetail(d) == o
;

DESIGN innerOrders FROM DEFAULT {
    PROPERTY (delete(o)) {
        askConfirm = TRUE;
    }

    NEW documentContainer BEFORE functions.box {
        type = SPLITV;
        childConstraints = TO THE BOTTOM;

        ADD o.box {
            fillVertical = 1;
        };

        NEW documentDetail {
            type = TABBED;
            fillVertical = 2.5;
            ADD d.box {
                title = 'Спецификация';
            }
            NEW documentHistory {
                title = 'История';

                ADD o.historyGroup;
                ADD o.postedGroup;
            }
        }
    }
}

@extendFormDocumentDetailSkuArticle(innerOrder, d, innerOrder);
@extendFormDocumentDetailSkuArticleReadonly(innerOrders, d, innerOrder);

// ---------------------------------------- Подбор товара ------------------------------------ //
CLASS orderPicking 'Подбор товара по заказу': historyObject, numeratedObject;
CLASS orderPickingPosted 'Проведенный подбор товара по заказу': orderPicking, postedObject;

CLASS orderPickingDetail 'Строка подбора товара по заказу';

@defineNumeratedObjectDefault(orderPicking, 'Нумератор для подборок товара по заказу', 'ПЗ');

innerOrderOrderPicking 'Подбор товара по заказу (ИД)' (orderPicking) = DATA innerOrder (orderPicking) IN idGroup AUTOSET NOT NULL DELETE;
//orderPickingInnerOrder 'Подбор заказа (ИД)' (innerOrder) = GROUP UNIQUE  orderPicking BY innerOrderOrderPicking(orderPicking);

inInnerOrderOrderPicking (innerOrder, orderPicking) = innerOrderOrderPicking(orderPicking) == innerOrder;

//CONSTRAINT innerOrderOrderPicking(p1) == innerOrderOrderPicking(p2) AND p1 != p2 CHECKED BY innerOrderOrderPicking
//    MESSAGE 'Заказ задествован в другом подборе';

@defineDocument(orderPicking);
@defineDocumentPosted(orderPicking);

@defineDocumentAggregationHeaderStockPrefix(innerOrder, orderPicking, stock, 'Склад-получатель', destination, destination);
@defineDocumentAggregationHeaderStockPrefix(innerOrder, orderPicking, stock, 'Склад-отправитель', , );

@defineDocumentDetailStock(orderPicking, stock, 'Склад-отправитель');

companyOrderPicking(orderPicking) = legalEntityStock(stockOrderPicking(orderPicking));
destinationCompanyOrderPicking(orderPicking) = legalEntityStock(destinationStockOrderPicking(orderPicking));

@defineDocumentAggregationHeaderNumber(innerOrder, orderPicking);

@defineDocumentDescription(orderPicking, orderPickingDetail, seriesNumberObject, 'Подбор товара по заказу');

@defineDocumentDetailSku(orderPicking, sku);

@defineDocumentDetailQuantity(orderPicking);

@defineDocumentDetailSkuArticle(orderPicking);

@defineDocumentHeaderQuantity(orderPicking);
@defineDocumentHeaderSkuQuantity(orderPicking, sku);

@defineDocumentCurrency(orderPicking);
@deriveDocumentCurrency(orderPicking, stock);

// Кнопки подбора
@defineAddDetailDialogSkuStock(orderPicking, sku, stock, dialogSku);
@defineAddDetailDialogBarcode(orderPicking, sku);

messageOrderPicking 'Сообщение' (orderPicking) = messageInnerOrder(innerOrderOrderPicking(orderPicking)) IN documentPrmGroup;

quantitySkuInnerOrder 'Количество подобрано по всем подборкам' (sku, innerOrder) = GROUP SUM
    quantityOrderPickingDetailSkuOrderPicking (sku, orderPicking) BY sku, innerOrderOrderPicking(orderPicking);

quantityOrderPickingInnerOrderDetail 'Количество подобрано' (innerOrderDetail) =
    quantitySkuInnerOrder(skuInnerOrderDetail(innerOrderDetail), innerOrderInnerOrderDetail(innerOrderDetail));

FORM orderPicking 'Подбор товара'
    OBJECTS o = innerOrder FIXED PANEL
    OBJECTS p = orderPicking FIXED PANEL

    PROPERTIES (p) objectClassName, nameNumeratorObject, numberObject, seriesObject, dateOrderPicking, timeOrderPicking,
        nameStockOrderPicking, nameDestinationStockOrderPicking, nameCurrencyOrderPicking,
        countOrderPickingDetailOrderPicking, quantityOrderPickingDetailOrderPicking, messageOrderPicking READONLY


    OBJECTS s = sku
    PROPERTIES(s) READONLY FORCE GRID barcode, nameCategoryArticleSku, nameBrandSupplierArticleSku, sidArticleSku,
                           sidSizeSupplierItem, sidColorSupplierItem, nameColorSupplierItem, shortNameUOMSku
    PROPERTIES(s, o) quantityInnerOrderDetailSkuInnerOrder, quantitySkuInnerOrder
    PROPERTIES(s, p) quantityOrderPickingDetailSkuOrderPicking

    FILTERS quantityInnerOrderDetailSkuInnerOrder(s, o)

    FILTERGROUP filters1
        FILTER 'Неподобранные товары' 'F10' (quantityInnerOrderDetailSkuInnerOrder(s, o) (-) quantitySkuInnerOrder(s, o)) > 0 DEFAULT

    OBJECTS d = orderPickingDetail

    PROPERTIES(p) TODRAW d addDetailDialogSkuStockOrderPickingDetailOrderPicking, addDetailInputBarcodeOrderPickingDetailOrderPicking,
                           deleteOrderPickingDetailOrderPicking

    FILTERS orderPickingOrderPickingDetail(d) == p

    EVENTS
        ON OK EXEC prePostOrderPicking(p)

    EDIT orderPicking OBJECT p
;

@extendFormDocumentDetailSkuArticle(orderPicking, d, orderPicking);

DESIGN orderPicking FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        p.box {
            childConstraints = TO THE RIGHT;
            NEW columnHeaderPrm {
                childConstraints = TO THE BOTTOM;
                ADD p.documentHeaderGroup {
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY (objectClassName);
                    ADD PROPERTY (nameStockOrderPicking);
                    ADD PROPERTY (nameDestinationStockOrderPicking);
                    ADD PROPERTY (nameNumeratorObject);
                    ADD PROPERTY (numberObject);
                    ADD PROPERTY (seriesObject);
                    ADD PROPERTY (dateOrderPicking);
                    ADD PROPERTY (timeOrderPicking);
                }
                ADD p.documentPrmGroup {
                    childConstraints = TO THE BOTTOM;
                }
            }
            NEW columnHeaderSum {
                ADD p.documentSumGroup {
                    childConstraints = TO THE BOTTOM;
                    PROPERTY (messageOrderPicking) {
                        preferredSize = (1024, 100);
                    }
                }
            }
        }

        NEW details.box BEFORE functions.box {
            type = SPLITH;
            ADD s.box;
            ADD d.box;
            POSITION s.box TO THE LEFT d.box;
        }
        PROPERTY (quantityInnerOrderDetailSkuInnerOrder(s, o))  {caption = 'Количество товара в заказе'; }
        PROPERTY(formOkAction) {
            caption = 'Провести';
        }
    }
}

//Свойства генерируемых документов
orderPickingInnerDocument (innerDocument) = ABSTRACT orderPicking (innerDocument) PERSISTENT;

quantityInnerDocumentOrderPicking 'Количество документов' (orderPicking) =
    GROUP SUM 1 IF orderPickingInnerDocument (innerDocument) == orderPicking BY orderPicking;

needInnerDocumentOrderPicking 'Необходимо создать документы по заказу' (orderPicking) =
    orderPicking IS orderPickingPosted AND NOT quantityInnerDocumentOrderPicking(orderPicking);

quantityPostedInnerDocumentOrderPicking 'Количество проведенных документов' (orderPicking) =
    GROUP SUM 1 IF orderPickingInnerDocument (innerDocument) == orderPicking AND innerDocument IS postedObject BY orderPicking;;

quantityDraftInnerDocumentOrderPicking 'Количество непроведенных документов' (orderPicking) =
    quantityInnerDocumentOrderPicking(orderPicking) (-) quantityPostedInnerDocumentOrderPicking(orderPicking);


quantityInnerDocumentInnerOrder 'Количество документов' (innerOrder) = GROUP SUM quantityInnerDocumentOrderPicking(orderPicking)
    BY innerOrderOrderPicking(orderPicking);

quantityPostedInnerDocumentInnerOrder 'Количество проведенных документов' (innerOrder) = GROUP SUM quantityPostedInnerDocumentOrderPicking(orderPicking)
    BY innerOrderOrderPicking(orderPicking);

quantityDraftInnerDocumentInnerOrder 'Количество непроведенных документов' (innerOrder) = GROUP SUM quantityDraftInnerDocumentOrderPicking(orderPicking)
    BY innerOrderOrderPicking(orderPicking);

// Перемещение по накладным
orderPickingTransferOut (moveOut) = DATA orderPicking (transferOut);

orderPickingInnerDocument (innerDocument) += orderPickingTransferOut (innerDocument);
stockInnerDocument (innerDocument) += stockTransferOut (innerDocument);
nameDestinationStockInnerDocument (innerDocument) += nameDestinationStockTransferOut (innerDocument);

dateInnerDocument (innerDocument) += dateTransferOut (innerDocument);

isPostedInnerDocument (innerDocument) += isPostedTransferOut (innerDocument);
isDraftInnerDocument (innerDocument) += isDraftTransferOut (innerDocument);

countDetailInnerDocument (innerDocument) += countTransferOutDetailTransferOut (innerDocument);
quantityDetailInnerDocument (innerDocument) += quantityTransferOutDetailTransferOut (innerDocument);

skipSkuLedgerInnerDocument (innerDocument) += skipSkuLedgerTransferOut (innerDocument);

// Продажа товара
orderPickingSaleOut (saleOut) = DATA orderPicking (saleOut);

orderPickingInnerDocument (innerDocument) += orderPickingSaleOut (innerDocument);
stockInnerDocument (innerDocument) += stockSaleOut (innerDocument);
nameDestinationStockInnerDocument (innerDocument) += nameDestinationStockSaleOut (innerDocument);

dateInnerDocument (innerDocument) += dateSaleOut (innerDocument);

isPostedInnerDocument (innerDocument) += isPostedSaleOut (innerDocument);
isDraftInnerDocument (innerDocument) += isDraftSaleOut (innerDocument);

countDetailInnerDocument (innerDocument) += countSaleOutDetailSaleOut (innerDocument);
quantityDetailInnerDocument (innerDocument) += quantitySaleOutDetailSaleOut (innerDocument);

skipSkuLedgerInnerDocument (innerDocument) += skipSkuLedgerSaleOut (innerDocument);

// Возврат товара
orderPickingReturnOut (returnOut) = DATA orderPicking (returnOut);

orderPickingInnerDocument (innerDocument) += orderPickingReturnOut (innerDocument);
stockInnerDocument (innerDocument) += stockReturnOut (innerDocument);
nameDestinationStockInnerDocument (innerDocument) += nameDestinationStockReturnOut (innerDocument);

dateInnerDocument (innerDocument) += dateReturnOut (innerDocument);

isPostedInnerDocument (innerDocument) += isPostedReturnOut (innerDocument);
isDraftInnerDocument (innerDocument) += isDraftReturnOut (innerDocument);

countDetailInnerDocument (innerDocument) += countReturnOutDetailReturnOut (innerDocument);
quantityDetailInnerDocument (innerDocument) += quantityReturnOutDetailReturnOut (innerDocument);

skipSkuLedgerInnerDocument (innerDocument) += skipSkuLedgerReturnOut (innerDocument);

// Перемещение без накладных
orderPickingMoveOut (moveOut) = DATA orderPicking (moveOut);

orderPickingInnerDocument (innerDocument) += orderPickingMoveOut (innerDocument);
stockInnerDocument (innerDocument) += stockMoveOut (innerDocument);
nameDestinationStockInnerDocument (innerDocument) += nameDestinationStockMoveOut (innerDocument);

dateInnerDocument (innerDocument) += dateMoveOut (innerDocument);

isPostedInnerDocument (innerDocument) += isPostedMoveOut (innerDocument);
isDraftInnerDocument (innerDocument) += isDraftMoveOut (innerDocument);

countDetailInnerDocument (innerDocument) += countMoveOutDetailMoveOut (innerDocument);
quantityDetailInnerDocument (innerDocument) += quantityMoveOutDetailMoveOut (innerDocument);

// Расчет партий с которых будем создавать перемещения
quantityOrderPickingDetailBatchOrderPicking (batch, orderPicking) =
            PARTITION UNGROUP quantityOrderPickingDetailSkuOrderPicking
                  LIMIT STRICT currentBalanceBatchStock(batch, stockOrderPicking(orderPicking))
                  BY skuBatch(batch), orderPicking
                  ORDER orderBBatch(batch), orderBatch(batch);

quantityAOrderPickingDetailSkuOrderPicking (sku, orderPicking) = GROUP SUM
    quantityOrderPickingDetailBatchOrderPicking (batch, orderPicking) IF batch IS batchA AND NOT commissionContractSkuBatchA(batch)
    BY skuBatch(batch), orderPicking;

quantityOrderPickingDetailSkuContractSkuCompanyOrderPicking(sku, contractSkuCompany, orderPicking) = GROUP SUM
    quantityOrderPickingDetailBatchOrderPicking (batch, orderPicking) AND NOT commissionContractSkuBatchA(batch)
    BY skuBatch(batch), contractSkuCompanySaleInBatchDetail(batch), orderPicking;

quantityOrderPickingDetailSkuContractCompanyOrderPicking(contractSkuCompany, orderPicking) = GROUP SUM
    quantityOrderPickingDetailSkuContractSkuCompanyOrderPicking(sku, contractSkuCompany, orderPicking)
    BY contractSkuCompany, orderPicking;

quantityOrderPickingDetailSkuCompanyOrderPicking(sku, company, orderPicking) = GROUP SUM
    quantityOrderPickingDetailSkuContractSkuCompanyOrderPicking(sku, contractSkuCompany, orderPicking)
    BY sku, companyAContractSkuCompany(contractSkuCompany), orderPicking;

leftOrderPickingDetailSkuCompanyOrderPicking(sku, company, orderPicking) = quantityAOrderPickingDetailSkuOrderPicking (sku, orderPicking) (-)
                                                                           quantityOrderPickingDetailSkuCompanyOrderPicking(sku, company, orderPicking);

quantityOrderPickingDetailSkuCommissionContractOrderPicking (sku, commissionContract, orderPicking) = GROUP SUM
    quantityOrderPickingDetailBatchOrderPicking (batch, orderPicking)
    BY skuBatch(batch), commissionContractSkuBatchA(batch), orderPicking;

stockOrderPickingDetailCommissionContractOrderPicking (commissionContract, orderPicking) = GROUP MAX
    destinationStockSaleInBatchDetail(batch) IF quantityOrderPickingDetailBatchOrderPicking(batch, orderPicking)
    BY commissionContractSkuBatchA(batch), orderPicking;

quantityOrderPickingDetailCommissionContractOrderPicking (commissionContract, orderPicking) = GROUP SUM
    quantityOrderPickingDetailSkuCommissionContractOrderPicking(sku, commissionContract, orderPicking)
    BY commissionContract, orderPicking;


quantityBOrderPickingDetailSkuOrderPicking (sku, orderPicking) = GROUP SUM
    quantityOrderPickingDetailBatchOrderPicking (batch, orderPicking) IF batch IS batchB
    BY skuBatch(batch), orderPicking;

createDocumentsOrderPicking 'Создать документы' (orderPicking) = ACTION (orderPicking) {
    IF isPostedOrderPicking (orderPicking) THEN {
        IF companyOrderPicking(orderPicking) == destinationCompanyOrderPicking(orderPicking) THEN {
            IF [GROUP SUM quantityAOrderPickingDetailSkuOrderPicking(sku, orderPicking) BY orderPicking](orderPicking) THEN {
                ADDOBJ transferOutPosted;
                FOR t == addedObject() DO {
                    SET stockTransferOut(t) <- stockOrderPicking(orderPicking);
                    SET destinationStockTransferOut(t) <- destinationStockOrderPicking(orderPicking);
                    SET orderPickingTransferOut(t) <- orderPicking;

                    FOR quantityAOrderPickingDetailSkuOrderPicking(sku, orderPicking) DO {
                        ADDOBJ transferOutDetail;
                        FOR d == addedObject() DO {
                             SET transferOutTransferOutDetail(d) <- t;
                             SET skuTransferOutDetail(d) <- sku;
                             SET quantityTransferOutDetail(d) <- quantityAOrderPickingDetailSkuOrderPicking (sku, orderPicking);
                        }
                    }
                }
            }
            FOR quantityOrderPickingDetailCommissionContractOrderPicking(commissionContract, orderPicking) DO {
                ADDOBJ transferOutPosted;
                FOR t == addedObject() DO {
                    SET stockTransferOut(t) <- stockOrderPicking(orderPicking);
                    SET destinationStockTransferOut(t) <- destinationStockOrderPicking(orderPicking);
                    SET orderPickingTransferOut(t) <- orderPicking;
                    SET commissionContractSkuTransferOut(t) <- commissionContract;

                    FOR quantityOrderPickingDetailSkuCommissionContractOrderPicking(sku, commissionContract, orderPicking) DO {
                        ADDOBJ transferOutDetail;
                        FOR d == addedObject() DO {
                             SET transferOutTransferOutDetail(d) <- t;
                             SET skuTransferOutDetail(d) <- sku;
                             SET quantityTransferOutDetail(d) <- quantityOrderPickingDetailSkuCommissionContractOrderPicking(sku, commissionContract, orderPicking);
                        }
                    }
                }
            }
        } ELSE {
            // Делаем возвраты поставщику, если эти партии от него же и приходили
            FOR quantityOrderPickingDetailSkuContractCompanyOrderPicking(contractSkuCompany, orderPicking) AND
                companyAContractSkuCompany(contractSkuCompany) == destinationCompanyOrderPicking(orderPicking) DO {
                ADDOBJ returnOutPosted;
                FOR r == addedObject() DO {
                    SET stockReturnOut(r) <- stockOrderPicking(orderPicking);
                    SET destinationStockReturnOut(r) <- destinationStockOrderPicking(orderPicking);
                    SET orderPickingReturnOut(r) <- orderPicking;
                    SET contractSkuCompanyReturnOut(r) <- contractSkuCompany;

                    FOR quantityOrderPickingDetailBatchOrderPicking (batch, orderPicking) IF contractSkuCompanySaleInBatchDetail(batch) == contractSkuCompany DO {
                        ADDOBJ returnOutDetail;
                        FOR d == addedObject() DO {
                             SET returnOutReturnOutDetail(d) <- r;
                             SET skuReturnOutDetail(d) <- skuBatch(batch);
                             SET saleInBatchDetailReturnOutDetail(d) <- batch;
                             SET quantityReturnOutDetail(d) <- quantityOrderPickingDetailBatchOrderPicking (batch, orderPicking);
                        }
                    }
                }
            }
            // Все оставшееся кидаем договором продажи
            IF [GROUP SUM leftOrderPickingDetailSkuCompanyOrderPicking(sku, destinationCompanyOrderPicking(orderPicking), orderPicking) BY orderPicking](orderPicking) THEN {
                ADDOBJ saleOutPosted;
                FOR s == addedObject() DO {
                    SET stockSaleOut(s) <- stockOrderPicking(orderPicking);
                    SET destinationStockSaleOut(s) <- destinationStockOrderPicking(orderPicking);
                    SET orderPickingSaleOut(s) <- orderPicking;

                    FOR leftOrderPickingDetailSkuCompanyOrderPicking(sku, destinationCompanyOrderPicking(orderPicking), orderPicking) DO {
                        ADDOBJ saleOutDetail;
                        FOR d == addedObject() DO {
                             SET saleOutSaleOutDetail(d) <- s;
                             SET skuSaleOutDetail(d) <- sku;
                             SET quantitySaleOutDetail(d) <- leftOrderPickingDetailSkuCompanyOrderPicking(sku, destinationCompanyOrderPicking(orderPicking), orderPicking);
                        }
                    }
                }
            }
            FOR quantityOrderPickingDetailCommissionContractOrderPicking(commissionContract, orderPicking) DO {
                // значит просто возврат на указанный склад
                IF destinationCompanyOrderPicking(orderPicking) == companyAContractSkuCompany(commissionContract) THEN {
                    ADDOBJ returnOutPosted;
                    FOR r == addedObject() DO {
                        SET stockReturnOut(r) <- stockOrderPicking(orderPicking);
                        SET destinationStockReturnOut(r) <- destinationStockOrderPicking(orderPicking);
                        SET orderPickingReturnOut(r) <- orderPicking;
                        SET contractSkuCompanyReturnOut(r) <- commissionContract;

                        FOR quantityOrderPickingDetailBatchOrderPicking (batch, orderPicking) IF commissionContractSkuBatchA(batch) == commissionContract DO {
                            ADDOBJ returnOutDetail;
                            FOR d == addedObject() DO {
                                 SET returnOutReturnOutDetail(d) <- r;
                                 SET skuReturnOutDetail(d) <- skuBatch(batch);
                                 SET saleInBatchDetailReturnOutDetail(d) <- batch;
                                 SET quantityReturnOutDetail(d) <- quantityOrderPickingDetailBatchOrderPicking (batch, orderPicking);
                            }
                        }
                    }
                } ELSE {
                    // Возврат на один из складов
                    ADDOBJ returnOutPosted;
                    FOR r == addedObject() DO {
                        SET stockReturnOut(r) <- stockOrderPicking(orderPicking);
                        SET destinationStockReturnOut(r) <- stockOrderPickingDetailCommissionContractOrderPicking(commissionContract, orderPicking);
                        SET orderPickingReturnOut(r) <- orderPicking;
                        SET contractSkuCompanyReturnOut(r) <- commissionContract;

                        FOR quantityOrderPickingDetailBatchOrderPicking (batch, orderPicking) IF commissionContractSkuBatchA(batch) == commissionContract DO {
                            ADDOBJ returnOutDetail;
                            FOR d == addedObject() DO {
                                 SET returnOutReturnOutDetail(d) <- r;
                                 SET skuReturnOutDetail(d) <- skuBatch(batch);
                                 SET saleInBatchDetailReturnOutDetail(d) <- batch;
                                 SET quantityReturnOutDetail(d) <- quantityOrderPickingDetailBatchOrderPicking (batch, orderPicking);
                            }
                        }
                    }
                    // Продажа всей этой хрени
                    ADDOBJ saleOutPosted;
                    FOR s == addedObject() DO {
                        SET stockSaleOut(s) <- stockOrderPickingDetailCommissionContractOrderPicking(commissionContract, orderPicking);
                        SET destinationStockSaleOut(s) <- destinationStockOrderPicking(orderPicking);
                        SET orderPickingSaleOut(s) <- orderPicking;

                        FOR quantityOrderPickingDetailBatchOrderPicking (batch, orderPicking) IF commissionContractSkuBatchA(batch) == commissionContract DO {
                            ADDOBJ saleOutDetail;
                            FOR d == addedObject() DO {
                                 SET saleOutSaleOutDetail(d) <- s;
                                 SET skuSaleOutDetail(d) <- skuBatch(batch);
                                 SET quantitySaleOutDetail(d) <- quantityOrderPickingDetailBatchOrderPicking (batch, orderPicking);
                            }
                        }
                    }
                }
            }
        }
        // Все партии типа B перемещаем своим документом
        IF [GROUP SUM quantityBOrderPickingDetailSkuOrderPicking(sku, orderPicking) BY orderPicking](orderPicking) THEN {
            ADDOBJ moveOutPosted;
            FOR m == addedObject() DO {
                SET stockMoveOut(m) <- stockOrderPicking(orderPicking);
                SET destinationStockMoveOut(m) <- destinationStockOrderPicking(orderPicking);
                SET orderPickingMoveOut(m) <- orderPicking;

                FOR quantityBOrderPickingDetailSkuOrderPicking(sku, orderPicking) DO {
                    ADDOBJ moveOutDetail;
                    FOR d == addedObject() DO {
                         SET moveOutMoveOutDetail(d) <- m;
                         SET skuMoveOutDetail(d) <- sku;
                         SET quantityMoveOutDetail(d) <- quantityBOrderPickingDetailSkuOrderPicking (sku, orderPicking);
                    }
                }
            }
        }
        EXEC apply();
    }
} TOOLBAR CONFIRM;

editInnerDocument 'Редактировать' (innerDocument) = ACTION (innerDocument) NEWSESSION AUTOAPPLY {
    IF innerDocument IS moveOut THEN {
        FORM moveOut OBJECTS t = innerDocument MODAL;
    } ELSE {
        IF innerDocument IS transferOut THEN {
            FORM transferOut OBJECTS t = innerDocument MODAL;
        } ELSE
            IF innerDocument IS saleOut THEN {
                FORM saleOut OBJECTS t = innerDocument MODAL;
            } ELSE
                IF innerDocument IS returnOut THEN {
                    FORM returnOut OBJECTS t = innerDocument MODAL;
                } ELSE
                    IF innerDocument IS invoiceWHOut THEN {
                        FORM invoiceWHOut OBJECTS t = innerDocument MODAL;
                }
    }
} IMAGE 'edit.png' EDITKEY 'BACK_SPACE';

quantityOrderPickingPostedInnerOrder (innerOrder) = GROUP SUM 1 IF orderPicking IS orderPickingPosted BY innerOrderOrderPicking(orderPicking);
quantityOrderPickingInnerOrder (innerOrder) = GROUP SUM 1 IF orderPicking IS orderPicking BY innerOrderOrderPicking(orderPicking);

statusInnerOrder 'Статус' (innerOrder) =
    IF quantityInnerDocumentInnerOrder (innerOrder) AND NOT quantityDraftInnerDocumentInnerOrder (innerOrder) THEN 'Документы выверены' IF innerOrder IS innerOrder ELSE
    IF quantityPostedInnerDocumentInnerOrder (innerOrder) > 0 THEN 'Документы выверяются' IF innerOrder IS innerOrder ELSE
    IF quantityInnerDocumentInnerOrder (innerOrder)  THEN 'Документы сформированы' IF innerOrder IS innerOrder ELSE
    IF quantityOrderPickingPostedInnerOrder (innerOrder) == quantityOrderPickingInnerOrder(innerOrder)  THEN 'Товары подобраны частично' IF innerOrder IS innerOrder ELSE
    IF quantityOrderPickingPostedInnerOrder (innerOrder) != quantityOrderPickingInnerOrder(innerOrder) AND quantityOrderPickingInnerOrder (innerOrder) THEN 'Товары подобираются' IF innerOrder IS innerOrder ELSE
    'Требуется подборка' IF innerOrder IS innerOrder IN documentPrmGroup PREFCHARWIDTH 25 MINCHARWIDTH 25;


statusOrderPicking 'Статус' (orderPicking) =
    IF quantityInnerDocumentOrderPicking (orderPicking) AND NOT quantityDraftInnerDocumentOrderPicking (orderPicking) THEN 'Документы выверены' IF orderPicking IS orderPicking ELSE
    IF quantityPostedInnerDocumentOrderPicking (orderPicking) > 0 THEN 'Документы выверяются' IF orderPicking IS orderPicking ELSE
    IF quantityInnerDocumentOrderPicking (orderPicking)  THEN 'Документы сформированы' IF orderPicking IS orderPicking ELSE
    IF orderPicking IS orderPickingPosted THEN 'Товары подобраны' IF orderPicking IS orderPicking ELSE
    IF quantityOrderPickingDetailOrderPicking(orderPicking) AND orderPicking IS orderPicking THEN 'Товары подобираются' IF orderPicking IS orderPicking ELSE
    'Требуется подборка' IF orderPicking IS orderPicking PREFCHARWIDTH 25 MINCHARWIDTH 25;


EXTEND FORM innerOrders
    OBJECTS p = orderPicking

    PROPERTIES (p) READONLY isPostedOrderPicking FORCE GRID, numberObject, seriesObject, dateOrderPicking, timeOrderPicking,
        nameStockOrderPicking, nameDestinationStockOrderPicking,
        countOrderPickingDetailOrderPicking, quantityOrderPickingDetailOrderPicking, quantityInnerDocumentOrderPicking,
        quantityPostedInnerDocumentOrderPicking, quantityDraftInnerDocumentOrderPicking, statusOrderPicking

    PROPERTIES(p) ADDFORM, EDITFORM, delete FORCE PANEL DRAWTOTOOLBAR

    OBJECTS pd=orderPickingDetail

    PROPERTIES (o) READONLY  quantityInnerDocumentInnerOrder, quantityPostedInnerDocumentInnerOrder, quantityDraftInnerDocumentInnerOrder,
        statusInnerOrder

    FILTERS innerOrderOrderPicking(p)== o,
            orderPickingOrderPickingDetail (pd) == p


    OBJECTS id = innerDocument

    PROPERTIES (id) READONLY isPostedInnerDocument, objectClassName, seriesObject, numberObject, dateInnerDocument,
                    nameStockInnerDocument, nameDestinationStockInnerDocument, countDetailInnerDocument, quantityDetailInnerDocument

    PROPERTIES (id) editInnerDocument, delete
    PROPERTIES (p) TODRAW id FORCE PANEL  createDocumentsOrderPicking SHOWIF needInnerDocumentOrderPicking(p)

    FILTERS orderPickingInnerDocument(id)==p
;

@extendFormDocumentDetailSkuArticleReadonly(innerOrders, pd, orderPicking);

EXTEND DESIGN innerOrders {
    documentDetail {
        NEW picking  {
            title = 'Подборка';
            ADD p.box {
                PROPERTY (delete(p)) {
                    askConfirm = TRUE;
                }
            };
            NEW picking1 {
                type = TABBED;
                ADD pd.box;
                ADD id.box {
                    title = 'Документы';
                }
            }
        }

    }
}

// ---------------------------------------- Приемка ------------------------------------ //

CLASS innerOrderRec 'Приемка товаров по внутреннему заказу' : historyObject;
CLASS innerOrderRecPosted 'Проведенная приемка товаров по внутреннему заказу' : innerOrderRec, postedObject;

CLASS innerOrderRecDetail 'Строка приемки товаров по внутреннему заказу';

@defineDocument(innerOrderRec);

orderPickingInnerOrderRec (innerOrderRec) = DATA orderPicking (innerOrderRec);
innerOrderRecOrderPicking (orderPicking) = GROUP UNIQUE innerOrderRec BY orderPickingInnerOrderRec(innerOrderRec) WHERE innerOrderRec IS innerOrderRec;

orderPickingInnerOrderRecDetail (detail) = orderPickingInnerOrderRec(innerOrderRecInnerOrderRecDetail(detail));
innerOrderRecOrderPickingDetail (detail) = innerOrderRecOrderPicking(orderPickingOrderPickingDetail(detail));

@defineDocumentAggregationHeaderNumber(orderPicking, innerOrderRec);
@defineDocumentAggregationHeaderStockPrefix(orderPicking, innerOrderRec, stock, 'Склад-получатель', destination, );

@defineDocumentPosted(innerOrderRec);

@defineDocumentDetailSku(innerOrderRec, sku);

@defineDocumentDetailQuantity(innerOrderRec);

@defineAddDetailDialogSkuStock(innerOrderRec, sku, stock, dialogSku);
@defineAddDetailDialogBarcode(innerOrderRec, sku);

@defineDocumentHeaderQuantity(innerOrderRec);
@defineDocumentHeaderSkuQuantity(innerOrderRec, sku);

FORM innerOrderRec 'Приемка товаров по внутреннему заказу'
    OBJECTS t = innerOrderRec FIXED PANEL

    PROPERTIES (t) numberInnerOrderRec, seriesInnerOrderRec, dateInnerOrderRec, timeInnerOrderRec,
                   nameStockInnerOrderRec, noteInnerOrderRec,
                   countInnerOrderRecDetailInnerOrderRec, quantityInnerOrderRecDetailInnerOrderRec

    OBJECTS d = innerOrderRecDetail

    PROPERTIES(t) TODRAW d addDetailDialogSkuStockInnerOrderRecDetailInnerOrderRec, addDetailInputBarcodeInnerOrderRecDetailInnerOrderRec,
                           deleteInnerOrderRecDetailInnerOrderRec

    FILTERS innerOrderRecInnerOrderRecDetail(d) == t

    EDIT innerOrderRec OBJECT t
;

DESIGN innerOrderRec FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        t.box {
            childConstraints = TO THE RIGHT;
            NEW column1 {
                childConstraints = TO THE BOTTOM;
                ADD t.documentHeaderGroup {
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY (nameStockInnerOrderRec);
                    ADD PROPERTY (numberInnerOrderRec);
                    ADD PROPERTY (seriesInnerOrderRec);
                    ADD PROPERTY (dateInnerOrderRec);
                    ADD PROPERTY (timeInnerOrderRec);
                }
                ADD t.documentPrmGroup;
            }
            ADD t.documentSumGroup {
                childConstraints = TO THE BOTTOM;
            }
        }
    }
}

receiveOrderPicking 'Принять' (orderPicking) = ACTION (orderPicking) NEWSESSION {
    IF innerOrderRecOrderPicking(orderPicking) IS innerOrderRec THEN {
        FOR r == innerOrderRecOrderPicking(orderPicking) DO {
            IF r IS innerOrderRecPosted THEN {
                CHANGECLASS r TO innerOrderRec;
            }
            FORM innerOrderRec OBJECTS t = r MODAL;
        }
    } ELSE {
        ADDOBJ innerOrderRec;
        FOR t == addedObject() DO {
            SET orderPickingInnerOrderRec (t) <- orderPicking AS orderPicking;
            FORM innerOrderRec OBJECTS t = t MODAL;
        }
    }
    IF formResult() == formResult.ok THEN EXEC apply();
} TOOLBAR;



postRecOrderPicking 'Пометить как принятое' (orderPicking) = postInnerOrderRec(innerOrderRecOrderPicking(orderPicking)) TOOLBAR;
isDraftRecOrderPicking (orderPicking) = isDraftInnerOrderRec(innerOrderRecOrderPicking(orderPicking));

statusRecOrderPicking 'Статус приемки' (orderPicking) =
    IF innerOrderRecOrderPicking (orderPicking) IS innerOrderRecPosted THEN 'Принят' IF orderPicking IS orderPicking ELSE
    IF innerOrderRecOrderPicking (orderPicking) IS innerOrderRec THEN 'Принимается' IF orderPicking IS orderPicking ELSE
    'Не принят' IF orderPicking IS orderPicking PREFCHARWIDTH 15 MINCHARWIDTH 15;

quantityInnerOrderRecDetailOrderPicking 'Кол-во принятое' (orderPicking) = quantityInnerOrderRecDetailInnerOrderRec(innerOrderRecOrderPicking(orderPicking));

EXTEND FORM innerOrders
    PROPERTIES(p) quantityInnerOrderRecDetailOrderPicking, statusRecOrderPicking

    OBJECTS r = innerOrderRecDetail
    PROPERTIES(p) TODRAW r receiveOrderPicking, postRecOrderPicking SHOWIF isDraftRecOrderPicking(p)
    FILTERS orderPickingInnerOrderRecDetail(r) == p
;

EXTEND DESIGN innerOrders {
    picking1 {
        ADD r.box {
            title = 'Приемка';
        }
    }
}
///

@defineDocumentDetailSkuArticle(innerOrderRec);
@extendFormDocumentDetailSkuArticle(innerOrderRec, d, innerOrderRec);

@extendFormDocumentDetailSkuArticleReadonly(innerOrders, r, innerOrderRec);

// ---------------------------------------- Акт расхождений ------------------------------------ //

CLASS innerOrderDiff 'Акт расхождений приемки товаров по внутреннему заказу' : historyObject;

CLASS ABSTRACT innerOrderDiffDetail 'Строка расхождений акта приемки товаров по по внутреннему заказу';
CLASS innerOrderDiffEDetail 'Строка расхождений акта приемки товаров по внутреннему заказу (излишек)' : innerOrderDiffDetail;
CLASS innerOrderDiffSDetail 'Строка расхождений акта приемки товаров по внутреннему заказу (недостача)' : innerOrderDiffDetail;

//@defineDocumentTransferDiff(transfer, 'Акт расхождений приемки товаров по накладной (перемещение)', sku, stock);
//@defineDocumentFormTransferDiff(transfer);

diffSkuInnerOrderRec(sku, innerOrderRec) = (quantityInnerOrderRecDetailSkuInnerOrderRec(sku, innerOrderRec) (-)
                                        quantityOrderPickingDetailSkuOrderPicking(sku, orderPickingInnerOrderRec(innerOrderRec)))
                                        AND isPostedInnerOrderRec(innerOrderRec);

hasDiffSkuInnerOrderRec(sku, innerOrderRec) = diffSkuInnerOrderRec(sku, innerOrderRec) != 0;

countDiffInnerOrderRec 'Расхождение (позиции)' (innerOrderRec) = GROUP SUM 1 IF hasDiffSkuInnerOrderRec(sku, innerOrderRec) BY innerOrderRec;
quantityDiffInnerOrderRec 'Расхождение (кол-во)' (innerOrderRec) = GROUP SUM diffSkuInnerOrderRec(sku, innerOrderRec) BY innerOrderRec;

countDiffOrderPicking 'Расхождение (позиции)' (orderPicking) = countDiffInnerOrderRec(innerOrderRecOrderPicking(orderPicking));
quantityDiffOrderPicking 'Расхождение (кол-во)' (orderPicking) = quantityDiffInnerOrderRec(innerOrderRecOrderPicking(orderPicking));

hasShortageSkuInnerOrderRec(sku, innerOrderRec) = diffSkuInnerOrderRec(sku, innerOrderRec) < 0;
hasExcessSkuInnerOrderRec(sku, innerOrderRec) = diffSkuInnerOrderRec(sku, innerOrderRec) > 0;

// Создание документов

@defineDocumentTables(innerOrderDiff);
TABLE innerOrderDiffSDetail (innerOrderDiffSDetail);
TABLE innerOrderDiffEDetail (innerOrderDiffEDetail);

@defineAggregation(innerOrderRec, innerOrderDiff, countDiffInnerOrderRec);

@defineAggregation(sku, innerOrderRec, innerOrderDiffEDetail, hasExcessSkuInnerOrderRec);
quantityInnerOrderDiffEDetail 'Кол-во' (detail) = diffSkuInnerOrderRec(skuInnerOrderDiffEDetail(detail),
                                                                   innerOrderRecInnerOrderDiffEDetail(detail)) PERSISTENT;

@defineAggregation(sku, innerOrderRec, innerOrderDiffSDetail, hasShortageSkuInnerOrderRec);
quantityInnerOrderDiffSDetail 'Кол-во' (detail) =-diffSkuInnerOrderRec(skuInnerOrderDiffSDetail(detail),
                                                                   innerOrderRecInnerOrderDiffSDetail(detail)) PERSISTENT;

innerOrderRecInnerOrderDiffDetail (detail) = UNION EXCLUSIVE innerOrderRecInnerOrderDiffEDetail(detail), innerOrderRecInnerOrderDiffSDetail(detail) PERSISTENT;
orderPickingInnerOrderDiffDetail (detail) = orderPickingInnerOrderRec(innerOrderRecInnerOrderDiffDetail(detail));
innerOrderDiffInnerOrderDiffDetail (detail) = innerOrderDiffInnerOrderRec(innerOrderRecInnerOrderDiffDetail(detail)) PERSISTENT;

orderPickingInnerOrderDiffSDetail (detail) = orderPickingInnerOrderRec(innerOrderRecInnerOrderDiffSDetail(detail)) PERSISTENT;

innerOrderDiffInnerOrderDiffEDetail (detail) = innerOrderDiffInnerOrderRec(innerOrderRecInnerOrderDiffEDetail(detail));
innerOrderDiffInnerOrderDiffSDetail (detail) = innerOrderDiffInnerOrderRec(innerOrderRecInnerOrderDiffSDetail(detail));

@defineDocumentDetailIndex(innerOrderDiff);
@defineDocumentDetailIndex(innerOrderDiff, innerOrderDiffEDetail);
@defineDocumentDetailIndex(innerOrderDiff, innerOrderDiffSDetail);

@defineDocumentAggregationHeaderTime(innerOrderRec, innerOrderDiff);
@defineDocumentDetailTime(innerOrderDiff, innerOrderDiffEDetail);
@defineDocumentDetailTime(innerOrderDiff, innerOrderDiffSDetail);

@defineDocumentAggregationHeaderNumberCustom(innerOrderRec, innerOrderDiff, innerOrderRec);

@defineDocumentHeaderDescription(innerOrderDiff, seriesNumberInnerOrderDiff, 'Акт расхождений приемки товаров по внутреннему заказу');
@defineDocumentDetailDescription(innerOrderDiff, innerOrderDiffEDetail);
@defineDocumentDetailDescription(innerOrderDiff, innerOrderDiffSDetail);

@defineDocumentAggregationHeaderStock(innerOrderRec, innerOrderDiff, stock, 'Склад-получатель');
@defineDocumentDetailStock(innerOrderDiff, innerOrderDiffEDetail, stock, 'Склад-получатель');
@defineDocumentDetailStock(innerOrderDiff, innerOrderDiffSDetail, stock, 'Склад-получатель');

@defineDocumentAggregationHeaderPosted(innerOrderRec, innerOrderDiff);
@defineDocumentDetailPosted(innerOrderDiff, innerOrderDiffEDetail);
@defineDocumentDetailPosted(innerOrderDiff, innerOrderDiffSDetail);

skuInnerOrderDiffDetail (detail) = UNION EXCLUSIVE skuInnerOrderDiffEDetail(detail), skuInnerOrderDiffSDetail(detail) PERSISTENT;
quantityInnerOrderDiffDetail 'Кол-во' (detail) = UNION EXCLUSIVE quantityInnerOrderDiffEDetail(detail), quantityInnerOrderDiffSDetail(detail) PERSISTENT;
signedQuantityInnerOrderDiffDetail 'Кол-во' (detail) = UNION EXCLUSIVE quantityInnerOrderDiffEDetail(detail), -quantityInnerOrderDiffSDetail(detail) PERSISTENT;

EXTEND FORM innerOrders
    OBJECTS f = innerOrderDiffDetail
    PROPERTIES(p) READONLY countDiffOrderPicking, quantityDiffOrderPicking
    FILTERS orderPickingInnerOrderDiffDetail(f) == p
;

EXTEND DESIGN innerOrders {
    picking1 {
        ADD f.box {
            title = 'Акт расхождений';
        }
    }
}

@defineDocumentDetailSkuArticle(innerOrderDiff);
@extendFormDocumentDetailSkuArticleReadonlyQuantity(innerOrders, f, innerOrderDiff, signedQuantityInnerOrderDiffDetail);

costInnerOrderDiffEDetail(detail) = 0.0 IF detail IS innerOrderDiffEDetail;
EXTEND CLASS innerOrderDiffEDetail : batchB;
@implementBatchCustom(innerOrderDiffE, sku, stock, cost);
quantityBatch (batch) += quantityInnerOrderDiffEDetail(batch);
skipASkuLedger (ledger) += ledger IS innerOrderDiffEDetail;

@implementSkuLedgerOutFIFO(innerOrderDiffS, sku, stock);
quantityOutFIFOSkuLedger (ledger) += quantityInnerOrderDiffSDetail(ledger);
@implementSkuLedgerOutFIFOBalance (innerOrderDiffS, sku, stock);
orderOutFIFOSkuLedgerBatch (ledger, batch) += orderBBatch(batch) IF ledger IS innerOrderDiffSDetail;

//accountPriceInnerOrderRecBatchA (innerOrderRec, batchA) = GROUP MAX accountPriceInnerOrderDetailBatchA(detail, batchA) BY innerOrderRecInnerOrderDetail(detail), batchA;
//accountSumTransferDiffSDetail (detail) = GROUP SUM costDataSkuLedgerBatch(detail, batch) * accountPriceInnerOrderRecBatchA(innerOrderRecInnerOrderDiffSDetail(detail), batch) BY detail PERSISTENT;
//sumOutSkuLedger(ledger) += accountSumInnerOrderDiffSDetail(ledger);

skipASkuLedger (ledger) += ledger IS innerOrderDiffSDetail;

descriptionInnerOrderOrderPicking 'Внутренний заказ' (orderPicking) =
    [FORMULA STRING[200] '\'Внутр. заказ \' ||  \' \' || CAST($1 AS TEXT) ||  \' от \' || CAST($2 AS TEXT)'](
    seriesNumberObject(innerOrderOrderPicking(orderPicking)), dateInnerOrder(innerOrderOrderPicking(orderPicking)));

FORM orderPickings 'Подборки товара'
//    OBJECTS o = innerOrder
    OBJECTS p = orderPicking

    PROPERTIES (p) READONLY isPostedOrderPicking FORCE GRID, descriptionInnerOrderOrderPicking, numberObject, seriesObject, dateOrderPicking, timeOrderPicking,
        nameStockOrderPicking, nameDestinationStockOrderPicking,
        countOrderPickingDetailOrderPicking, quantityOrderPickingDetailOrderPicking, statusOrderPicking,
        quantityInnerDocumentOrderPicking, quantityPostedInnerDocumentOrderPicking, quantityDraftInnerDocumentOrderPicking
    ORDER BY descriptionInnerOrderOrderPicking
//    PROPERTIES (p) FORCE PANEL  createDocumentsOrderPicking

    OBJECTS id = innerDocument
    PROPERTIES(p) TODRAW id createDocumentsOrderPicking

    PROPERTIES (id) READONLY isPostedInnerDocument, objectClassName, seriesObject, numberObject, dateInnerDocument,
                    nameStockInnerDocument, nameDestinationStockInnerDocument, countDetailInnerDocument, quantityDetailInnerDocument

    PROPERTIES (id) editInnerDocument, delete

    FILTERS orderPickingInnerDocument(id)==p

    FILTERGROUP filters1
        FILTER 'Без сгенерированных документов' 'F10'needInnerDocumentOrderPicking(p) DEFAULT

;

notDraftOrderPicking 'Непринятые' (orderPicking) = orderPicking IS orderPicking  AND NOT innerOrderRecOrderPicking (orderPicking) IS innerOrderRecPosted;

FORM innerOrderRecs 'Приемка по внутр. заказам'
    OBJECTS st = stock FIXED PANEL
    PROPERTIES (st) SELECTOR name
    OBJECTS p = orderPicking

    PROPERTIES (p) READONLY isPostedOrderPicking FORCE GRID, descriptionInnerOrderOrderPicking, numberObject, seriesObject, dateOrderPicking, timeOrderPicking,
        nameStockOrderPicking, nameDestinationStockOrderPicking,
        countOrderPickingDetailOrderPicking, quantityOrderPickingDetailOrderPicking, statusRecOrderPicking,
//        quantityInnerDocumentOrderPicking, quantityPostedInnerDocumentOrderPicking, quantityDraftInnerDocumentOrderPicking,
        quantityInnerOrderRecDetailOrderPicking
    ORDER BY descriptionInnerOrderOrderPicking

    OBJECTS r = innerOrderRecDetail
    PROPERTIES(p) TODRAW r receiveOrderPicking, postRecOrderPicking SHOWIF isDraftRecOrderPicking(p)
    FILTERS orderPickingInnerOrderRecDetail(r) == p,
            destinationStockOrderPicking(p) == st

    FILTERGROUP filters1
        FILTER 'Только непринятые' 'F10' notDraftOrderPicking(p) DEFAULT

;
@extendFormDocumentDetailSkuArticleReadonly(innerOrderRecs, r, innerOrderRec);

FORM orderPickingPrint 'Подборочный лист' PRINT
    OBJECTS p = orderPicking FIXED PANEL

    PROPERTIES (p) numberObject, seriesObject, dateOrderPicking, timeOrderPicking,
        nameStockOrderPicking, nameDestinationStockOrderPicking,
        countOrderPickingDetailOrderPicking, quantityOrderPickingDetailOrderPicking,
        descriptionInnerOrderOrderPicking

    OBJECTS d = orderPickingDetail
    FILTERS orderPickingOrderPickingDetail(d) == p
;

@extendFormDocumentDetailSkuArticleReadonly(orderPickingPrint, d, orderPicking);

printOrderPicking 'Подборочный лист' (priceChangeDocument) = ACTION FORM orderPickingPrint OBJECTS p IMAGE 'print.png' IN printGroup TOOLBAR;

EXTEND FORM innerOrders
    PROPERTIES(p)  FORCE PANEL printOrderPicking
    PROPERTIES (id) printConsignment FORCE GRID
;

@defineDocumentRetailPrice(orderPicking, p);