MODULE Transfer;

REQUIRE System,
        Document,
        RomanDocument,
        Stock,
        RomanStock,
        DocumentTransfer,
        ListRegister,
        RetailPrice,
        Employee,
        StorePriceTransfer,
        Consignment,
        AccountDocument,
        RomanLogicsModule;

PRIORITY Stock, RomanLogicsModule;

// ---------------------------------------- Перемещение ------------------------------------ //

CLASS transferOut 'Перемещение товаров по накладным' : historyObject, numberedObject, innerDocument, consignment, outAccountDocumentLedger;
CLASS transferOutPosted 'Проведенное перемещение товаров по накладным' : transferOut, postedObject;
CLASS transferOutDetail 'Строка перемещения товаров по накладным' : outFIFOSkuLedger;
CLASS transferOutBatchDetail 'Строка перемещения товаров по накладным (по партиям)' : consignmentDetail;

@defineDocumentTransferOut(transfer, 'Перемещение товаров по накладным', sku, stock);
@defineDocumentFormTransferOut(transfer, 'Перемещение товаров по накладным');
@defineDocumentAndDeriveCurrency(transferOut, stock);

companyTransferOut (transferOut) = companyStock(stockTransferOut(transferOut));

CONSTRAINT companyTransferOut(transferOut) != companyStock(destinationStockTransferOut(transferOut)) CHECKED BY destinationStockTransferOut
    MESSAGE 'Перемещение товаров должно идти между складами одной компании';

@defineDocumentDetailSkuArticle(transferOut);
@extendDocumentFormDetailSkuArticle(transferOut, d, transferOut);
@extendDocumentFormDetailSkuArticleReadonly(transferOuts, d, transferOut);

// Перемещение по договорам комиссии
commissionContractSkuTransferOut = DATA contractSku (transferOut);
numberCommissionContractSkuTransferOut 'Номер договора комиссии' (transferOut) = numberContract(commissionContractSkuTransferOut(transferOut)) IN documentPrmGroup;

isCommissionTransferOut 'Продажа на комиссию' (transferOut) = TRUE IF commissionContractSkuTransferOut(transferOut);

CONSTRAINT typeContractSkuDate(commissionContractSkuTransferOut(transferOut), dateTransferOut(transferOut)) != contractSkuType.commission
    CHECKED BY commissionContractSkuTransferOut MESSAGE 'Для перемещения выбран договор, который не является договором комиссии';

CONSTRAINT partyBContract(commissionContractSkuTransferOut(transferOut)) != companyTransferOut(transferOut)
    CHECKED BY commissionContractSkuTransferOut MESSAGE 'Для перемещения выбран договор комиссии, у которого покупатель не совпадает с компанией склада';

EXTEND FORM transferOut
    PROPERTIES(t) numberCommissionContractSkuTransferOut
;

EXTEND FORM transferOuts
    PROPERTIES(t) READONLY numberCommissionContractSkuTransferOut
;

commissionContractSkuTransferOutDetail (detail) = commissionContractSkuTransferOut(transferOutTransferOutDetail(detail)) PERSISTENT;

@defineDocumentTransferOutBatch(transfer, sku);
@defineDocumentFormTransferOutBatch(transfer);

@extendDocumentFormDetailSkuArticleReadonly(transferOuts, o, transferOutBatch);

@defineDocumentTransferRetail(transferOut);

useRetailPriceTransferOutDetail(detail) = stockTransferOutDetail(detail) IS departmentStore OR commissionContractSkuTransferOutDetail(detail) IS contractSku;
@defineDocumentTransferAccount(transferOut, useRetailPriceTransferOutDetail);

importerPriceTransferOutBatchDetail 'Цена импортера' (detail) = importerPriceBatchA(batchTransferOutBatchDetail(detail));
supplierPriceTransferOutBatchDetail 'Цена поставщика' (detail) = supplierPriceBatchA(batchTransferOutBatchDetail(detail));
EXTEND FORM transferOuts
    PROPERTIES(o) READONLY importerPriceTransferOutBatchDetail, supplierPriceTransferOutBatchDetail
;

@defineDocumentSkipSkuLedgerCustom(transferOut, transferOutDetail);
@extendDocumentFormSkipSkuLedgerCustom(transferOut, t, transferOut);
@extendDocumentFormSkipSkuLedgerCustomReadonly(transferOuts, t, transferOut);

// Проводим по регистру
@implementSkuLedger(transferOut, sku, stock);
quantityOutFIFOSkuLedger (ledger) += quantityTransferOutDetail (ledger);
limitOutFIFOSkuLedgerBatch(ledger, batch) += IF commissionContractSkuTransferOutDetail(ledger)
                                                THEN currentBalanceABatchStock(batch, stockTransferOutDetail(ledger)) AND commissionContractSkuBatchA(batch) == commissionContractSkuTransferOutDetail(ledger)
                                                ELSE currentBalanceABatchStock(batch, stockTransferOutDetail(ledger)) AND NOT commissionContractSkuBatchA(batch)
                                             AND batch IS batchA;
sumOutSkuLedger(ledger) += accountSumTransferOutDetail(ledger);
skipSkuLedger (ledger) += skipSkuLedgerTransferOutDetail(ledger);

// Запрет на не полностью расписанные партии
@defineConstraintSkuLedgerCosted(transferOut, 'Не хватает остатков по партиям для перемещения товаров');

// Товарные накладные
@defineDocumentDetailPackWeightSku(transferOutBatch); // вес и к-во мест для партий

@defineConsignment(transferOut);
@implementConsignmentHeader(transferOut, stock);

senderConsignment (consignment) += companyStock(stockTransferOut(consignment));
recipientConsignment (consignment) += companyStock(destinationStockTransferOut(consignment));
consignmentConsignmentDetail (consignmentDetail) += transferOutTransferOutBatchDetail (consignmentDetail);
skuConsignmentDetail (consignmentDetail) += skuTransferOutBatchDetail (consignmentDetail);
quantityConsignmentDetail (consignmentDetail) += quantityTransferOutBatchDetail (consignmentDetail);

isStockToStockTransferOut 'Перемещение м/у складами' (transferOut) =  GROUP SUM 1
    IF stockTransferOut(transferOut) == warehouse AS warehouse AND destinationStockTransferOut(transferOut) == warehouse AS warehouse
    BY transferOut;

isStockToStockTransferOutBatchDetail 'Перемещение м/у складами' (transferOutBatchDetail) = isStockToStockTransferOut(transferOutTransferOutBatchDetail(transferOutBatchDetail));
isCommissionTransferOutBatchDetail 'Продажа на комиссию' (transferOutBatchDetail) = isCommissionTransferOut(transferOutTransferOutBatchDetail(transferOutBatchDetail));

supplierSumTransferOutBatchDetail (transferOutBatchDetail) =  supplierPriceBatchA(batchTransferOutBatchDetail(transferOutBatchDetail)) * quantityTransferOutBatchDetail (transferOutBatchDetail);
sumVatTransferOutBatchDetail (transferOutBatchDetail) = [round0(X*Y/100)](
    valueSupplierVATBatchA(batchTransferOutBatchDetail(transferOutBatchDetail)), supplierSumTransferOutBatchDetail(transferOutBatchDetail));
sumInvoiceTransferOutBatchDetail (transferOutBatchDetail) = supplierSumTransferOutBatchDetail(transferOutBatchDetail) (+) sumVatTransferOutBatchDetail(transferOutBatchDetail);

priceConsignmentDetail (consignmentDetail) += IF isStockToStockTransferOutBatchDetail(consignmentDetail) AND NOT isCommissionTransferOutBatchDetail(consignmentDetail)
    THEN supplierPriceBatchA(batchTransferOutBatchDetail(consignmentDetail))
    ELSE retailPriceTransferOutDetail(transferOutDetailTransferOutBatchDetail(consignmentDetail));

sumConsignmentDetail (consignmentDetail) += IF isStockToStockTransferOutBatchDetail(consignmentDetail) AND NOT isCommissionTransferOutBatchDetail(consignmentDetail)
    THEN supplierSumTransferOutBatchDetail(consignmentDetail)
    ELSE retailPriceTransferOutDetail(transferOutDetailTransferOutBatchDetail(consignmentDetail))* quantityTransferOutBatchDetail (consignmentDetail);

vatConsignmentDetail (consignmentDetail) += valueSupplierVATBatchA(batchTransferOutBatchDetail(consignmentDetail)) IF isStockToStockTransferOutBatchDetail(consignmentDetail) AND NOT isCommissionTransferOutBatchDetail(consignmentDetail);

sumVATConsignmentDetail (consignmentDetail) += sumVatTransferOutBatchDetail(consignmentDetail) IF isStockToStockTransferOutBatchDetail(consignmentDetail) AND NOT isCommissionTransferOutBatchDetail(consignmentDetail);

sumInvoiceConsignmentDetail (consignmentDetail) += IF isStockToStockTransferOutBatchDetail(consignmentDetail) AND NOT isCommissionTransferOutBatchDetail(consignmentDetail)
    THEN sumInvoiceTransferOutBatchDetail(consignmentDetail)
    ELSE retailPriceTransferOutDetail(transferOutDetailTransferOutBatchDetail(consignmentDetail)) * quantityTransferOutBatchDetail (consignmentDetail);

grossWeightConsignmentDetail (consignmentDetail) += grossWeightTransferOutBatchDetail (consignmentDetail);
packQuantityConsignmentDetail (consignmentDetail) += packQuantityTransferOutBatchDetail (consignmentDetail);
shortNameUOMConsignmentDetail (consignmentDetail) += shortNameUOMTransferOutBatchDetail (consignmentDetail);

@defineDocumentFormTransferOutConsignment(transfer, t);
// todo: заполнить необходимыми данными

EXTEND FORM transferOut
    PROPERTIES(t) nameCurrencyTransferOut
;
EXTEND FORM transferOuts
    PROPERTIES(t) READONLY nameCurrencyTransferOut
;
// ---------------------------------------- Поступление ------------------------------------ //

CLASS transferIn 'Поступление товаров по накладным' : historyObject, incAccountDocumentLedger;
CLASS transferInDetail 'Строка поступления товаров по накладным' : inLIFOSkuLedger;

@defineDocumentTransferIn(transfer, 'Поступление товаров по накладным', sku, stock);
@defineDocumentFormTransferIn(transfer, 'Поступление товаров по накладным');

costTransferInBatch(transferIn, batch) = GROUP SUM costSkuLedgerBatch(ledger, batch) BY transferInTransferInDetail(ledger), batch PERSISTENT;

retailPriceTransferInDetail 'Цена розничная' (detail) = retailPriceTransferOutDetail(transferOutDetailTransferInDetail(detail));
retailSumTransferInDetail 'Сумма розничная' (detail) = quantityTransferInDetail(detail) * retailPriceTransferInDetail(detail);

skipSkuLedgerTransferIn 'Не проводить по учету' (transferIn) = skipSkuLedgerTransferOut(transferOutTransferIn(transferIn));
skipSkuLedgerTransferInDetail (detail) = skipSkuLedgerTransferOutDetail(transferOutDetailTransferInDetail(detail));

commissionContractSkuTransferInDetail (detail) = commissionContractSkuTransferOutDetail(transferOutDetailTransferInDetail(detail));

@defineDocumentDetailSkuArticle(transferIn);
@extendDocumentFormDetailSkuArticleReadonly(transferIns, d, transferIn);
@extendDocumentFormSkipSkuLedgerCustomReadonly(transferIns, t, transferIn);

useRetailPriceTransferInDetail(detail) = stockTransferInDetail(detail) IS departmentStore OR commissionContractSkuTransferInDetail(detail) IS contractSku;
@defineDocumentTransferAccount(transferIn, useRetailPriceTransferInDetail);

@implementSkuLedger(transferIn, sku, stock);
quantityInLIFOSkuLedger (ledger) += quantityTransferInDetail(ledger);
limitInLIFOSkuLedgerBatch (ledger, batch) += costSkuLedgerBatch(transferOutDetailTransferInDetail(ledger), batch);
sumInSkuLedger(ledger) += accountSumTransferInDetail(ledger);
skipSkuLedger (ledger) += skipSkuLedgerTransferInDetail(ledger);

nameCurrencyTransferIn 'Валюта' (transferIn) = nameCurrencyTransferOut(transferOutTransferIn(transferIn)) MINCHARWIDTH 10 PREFCHARWIDTH 10 MAXCHARWIDTH 15;

// ---------------------------------------- Приемка ------------------------------------ //

CLASS transferRec 'Приемка товаров по накладной (перемещение)' : historyObject;
CLASS transferRecPosted 'Проведенная приемка товаров по накладной (перемещение)' : transferRec, postedObject;

CLASS transferRecDetail 'Строка приемки товаров по накладной (перемещение)';

@defineDocumentTransferRec(transfer, 'Приемка товаров по накладным (перемещение)', sku, stock);
@defineDocumentFormTransferRec(transfer, 'Приемка товаров по накладным (перемещение)');

@defineDocumentDetailSkuArticle(transferRec);
@extendDocumentFormDetailSkuArticle(transferRec, d, transferRec);

@extendDocumentFormDetailSkuArticleReadonly(transferIns, r, transferRec);

// ---------------------------------------- Акт расхождений ------------------------------------ //
CLASS transferDiff 'Акт расхождений приемки товаров по накладной (перемещение)' : historyObject;

CLASS ABSTRACT transferDiffDetail 'Строка расхождений акта приемки товаров по накладной (перемещение)';
CLASS transferDiffEDetail 'Строка расхождений акта приемки товаров по накладной (перемещение, излишек)' : transferDiffDetail, batchB;
CLASS transferDiffSDetail 'Строка расхождений акта приемки товаров по накладной (перемещение, недостача)' : transferDiffDetail, outFIFOSkuLedger;

@defineDocumentTransferDiff(transfer, 'Акт расхождений приемки товаров по накладной (перемещение)', sku, stock);
@defineDocumentFormTransferDiff(transfer);

@defineDocumentDetailSkuArticle(transferDiff);
@extendDocumentFormDetailSkuArticleReadonlyQuantity(transferIns, f, transferDiff, signedQuantityTransferDiffDetail);

costTransferDiffEDetail(detail) = 0.0 IF detail IS transferDiffEDetail;
@implementBatch(transferDiffE, sku, stock, cost);
quantityBatch (batch) += quantityTransferDiffEDetail(batch);
skipASkuLedger (ledger) += ledger IS transferDiffEDetail;

@implementSkuLedger(transferDiffS, sku, stock);
quantityOutFIFOSkuLedger (ledger) += quantityTransferDiffSDetail(ledger);
limitOutFIFOSkuLedgerBatch (ledger, batch) += costTransferInBatch(transferInTransferDiffSDetail(ledger), batch);
orderOutFIFOSkuLedgerBatch (ledger, batch) += orderBBatch(batch) IF ledger IS transferDiffSDetail;

accountPriceTransferRecBatchA (transferRec, batchA) = GROUP MAX accountPriceTransferInDetailBatchA(detail, batchA) BY transferRecTransferInDetail(detail), batchA;
accountSumTransferDiffSDetail (detail) = GROUP SUM costDataSkuLedgerBatch(detail, batch) * accountPriceTransferRecBatchA(transferRecTransferDiffSDetail(detail), batch) BY detail PERSISTENT;
sumOutSkuLedger(ledger) += accountSumTransferDiffSDetail(ledger);

skipASkuLedger (ledger) += ledger IS transferDiffSDetail;

// ---------------------------------------- Акт расценки ------------------------------------ //
CLASS transferPriceActTransfer 'Акт расценки' : transferPriceAct;
CLASS transferPriceActTransferDetail 'Строка акта расценки': transferPriceActDetail;

prevImporterPriceBatch (batch) = PREV(importerPriceBatchA(batch)) AND batch IS batch;
prevSupplierPriceBatch (batch) = PREV(supplierPriceBatchA(batch)) AND batch IS batch;
prevRangeVATBatch (batch) = PREV(rangeVATBatchA(batch)) AND batch IS batch;

@defineDocumentTransferPriceAct (transfer, 'Акт расценки (перемещение)', 'Строка акта расценки (перемещение)', sku, stock, prevImporterPriceBatch, prevSupplierPriceBatch, prevRangeVATBatch);

notPassToBookkeepingListRegister (inputListRegister) += destinationStockTransferOut(transferOutTransferPriceActTransfer(inputListRegister)) IS departmentStore
    AND stockTransferOut(transferOutTransferPriceActTransfer(inputListRegister)) IS departmentStore
    OR  isCommissionTransferOut(transferOutTransferPriceActTransfer(inputListRegister));

EXTEND FORM transferIns
    PROPERTIES(t) READONLY nameCurrencyTransferIn
;

//---------------------------------------- Товарный отчет ------------------------- //

// расход
dateTimeAccountDocumentLedger (ledger) += dateTimeTransferOut(ledger);
isPostedAccountDocumentLedger (ledger) += isPostedTransferOut(ledger);
stockAccountDocumentLedger (ledger) += stockTransferOut(ledger);
descriptionAccountDocumentLedger (ledger) += descriptionTransferOut(ledger);

sumOutAccountDocumentLedger (ledger) += accountSumTransferOut(ledger);
sumItemOutAccountDocumentLedger (ledger) += accountSumTransferOut(ledger);
sumContainerOutAccountDocumentLedger (ledger) += 0.0 IF ledger IS transferOut;

// приход
dateTimeAccountDocumentLedger (ledger) += dateTimeTransferIn(ledger);
isPostedAccountDocumentLedger (ledger) += isPostedTransferIn(ledger);
stockAccountDocumentLedger (ledger) += stockTransferIn(ledger);
descriptionAccountDocumentLedger (ledger) += descriptionTransferIn(ledger);

sumIncAccountDocumentLedger (ledger) += accountSumTransferIn(ledger);
sumItemIncAccountDocumentLedger (ledger) += accountSumTransferIn(ledger);
sumContainerIncAccountDocumentLedger (ledger) += 0.0 IF ledger IS transferIn;
