---
title: 'Оператор MESSAGE'
---

Оператор `MESSAGE` - создание [действия](Actions.md), показывающего пользователю [сообщение](Show_message_MESSAGE_ASK.md).

### Синтаксис

    MESSAGE expression [syncType]

### Описание

Оператор `MESSAGE` создает действие, которое выдает пользователю окно с текстовым сообщением. Текстовое сообщение может быть представлено как строковой константой, так и другим более сложным [выражением](Expression.md), значением которого является строка, либо значение которого преобразовывается в строку.

### Параметры

- `expression`

    Выражение, значением которого является текст сообщения.

- `syncType`

    Определяет, когда завершить выполнение созданного действия:

    - `WAIT` - после завершения действия клиентом (закрытия сообщения). Это значение используется по умолчанию.
    - `NOWAIT` -  сразу после подготовки информации для передачи клиенту (чтения текста сообщения). Если во время выполнения некоторого продолжительного действия вызываются несколько `MESSAGE` `NOWAIT` действий, то они не создают отдельных сообщений, а объединяются с предыдущими сообщениями такого же вида. В результате пользователю в конце выполнения продолжительного действия будет показано одно объединенное сообщение.

### Примеры

```lsf
message  { MESSAGE 'Hello World!'; }                                // простое текстовое сообщение

isGood = DATA BOOLEAN (Item);
stringData(Item i)   {
    // в зависимости от того, какой item будет передан действию, будет показываться окно
    // с текстом 'Good', либо с текстом 'Bad'
    MESSAGE IF isGood(i) THEN 'Good' ELSE 'Bad';   
}

// В этом случае пользователю будет выдано пять текстовых сообщений
testMessage()  {                    
    LOCAL i = INTEGER();
    i() <- 0;
    WHILE i() < 5 DO {
        i() <- i() + 1;
        MESSAGE i();
    }
}

// В случае NOWAIT пользователю будет выдано одно текстовое сообщение, объединяющее сообщения от пяти вызовов MESSAGE
testMessageNowait()  {              
    LOCAL i = INTEGER();
    i() <- 0;
    WHILE i() < 5 DO {
        i() <- i() + 1;
        MESSAGE i() NOWAIT;
    }
}
```
