---
title: 'Оператор MESSAGE'
---

Оператор `MESSAGE` - создание [действия](Actions.md), показывающего пользователю [сообщение](Show_message_MESSAGE_ASK.md).

### Синтаксис

```
MESSAGE expression options
```

Опции оператора `options` могут перечисляться друг за другом в произвольном порядке. Поддерживается следующий набор опций:

```
syncType
messageType
```

### Описание

Оператор `MESSAGE` создает действие, которое показывает сообщение пользователю либо в виде диалогового окна с текстовым сообщением, либо в [системном окне `System.log`](Navigator_design.md#systemwindows). Текстовое сообщение может быть представлено как строковой константой, так и другим более сложным [выражением](Expression.md), значением которого является строка или значение, преобразуемое в строку.

### Параметры

- `expression`

    Выражение, значением которого является текст сообщения.

- `syncType`

    Тип синхронизации. Определяет, когда завершается выполнение созданного действия. Задается одним из ключевых слов:

    - `WAIT` - после завершения показа сообщения (в случае показа диалогового окна с сообщением - после закрытия окна). Это значение используется по умолчанию.
  
    - `NOWAIT` - сразу после подготовки информации для передачи пользователю (чтения текста сообщения). Если во время выполнения некоторого продолжительного действия вызываются несколько `MESSAGE` `NOWAIT` действий, то они могут объединяться с предыдущими сообщениями такого же вида. В результате пользователю в конце выполнения продолжительного действия будет показано одно объединенное сообщение.

- `messageType`

    Тип сообщения. Определяет, как сообщение будет показываться на экране. Задается одним из ключевых слов:

    - `LOG` - сообщение в окне сообщений `System.log`.
  
    - `INFO` - информационное сообщение.
	
	- `SUCCESS` - сообщение об успешном выполнении.
	
	- `WARN` - сообщение с предупреждением.
	
	- `ERROR` - сообщение об ошибке.

	- `DEFAULT` - простое сообщение. Используется по умолчанию.	

### Примеры

```lsf
message { MESSAGE 'Hello World!'; } // простое текстовое сообщение

isGood = DATA BOOLEAN (Item);
stringData(Item i) {
    // в зависимости от того, какой item будет передан действию, будет показываться окно
    // с текстом 'Good', либо с текстом 'Bad'
    MESSAGE IF isGood(i) THEN 'Good' ELSE 'Bad';   
}

// В этом случае пользователю будет выдано пять текстовых сообщений, 
// а третье будет дополнительно записано в лог
testMessage() {                    
    LOCAL i = INTEGER();
    i() <- 0;
    WHILE i() < 5 DO {
        i() <- i() + 1;
        MESSAGE i();
        IF i() == 3 THEN {
            MESSAGE i() LOG;
        }   
    }
}

// В случае NOWAIT пользователю будет выдано одно текстовое сообщение, 
// объединяющее сообщения от пяти вызовов MESSAGE
testMessageNowait()  {              
    LOCAL i = INTEGER();
    i() <- 0;
    WHILE i() < 5 DO {
        i() <- i() + 1;
        MESSAGE i() NOWAIT;
    }
}
```
