MODULE Excel;

REQUIRE System, MasterData;

CLASS Template 'Шаблон';
TABLE template(Template);

name 'Имя' = DATA VARSTRING[100] (Template);
file 'Файл шаблона' (Template) = DATA EXCELFILE (Template);
loadFile 'Загрузить шаблон'(Template t) = ACTION LOADFILE file(t);
openFile 'Открыть шаблон'(Template t) = ACTION OPENFILE file(t);
id 'Идентификатор' = DATA VARISTRING[100] (Template) MINCHARWIDTH 5 MAXCHARWIDTH 10 PREFCHARWIDTH 10;

process = ACTION CUSTOM 'lsfusion.erp.utils.excel.ProcessTemplateActionProperty' (Template);

resultTemplate = DATA LOCAL EXCELFILE ();
openResultTemplate () = ACTION OPENFILE resultTemplate();

openProcess 'Сгенерировать документ'(Template template) = ACTION {
    process(template);
    openResultTemplate();
}

CLASS TemplateEntry 'Строка шаблона';
TABLE templateEntry(TemplateEntry);

template 'Шаблон' = DATA Template (TemplateEntry);
key 'Параметр' = DATA VARSTRING[100](TemplateEntry);
value 'Значение' = DATA LOCAL TEXT (TemplateEntry);

FORM template 'Шаблон Excel'

    OBJECTS t = Template FIXED PANEL
    PROPERTIES (t) name, id
    PROPERTIES (t) loadFile, openFile

    OBJECTS te = TemplateEntry
    PROPERTIES (te) key, value, ADDOBJ, DELETESESSION
    PROPERTIES (t) TODRAW te FORCE PANEL TOOLBAR openProcess

    FILTERS template (te) == t

    EDIT Template OBJECT t
;

DESIGN template {
    main{
        preferredSize = (1024, 768);
        type = CONTAINERV;
        MOVE t.box {
            type = CONTAINERH;
            NEW row1 {
                MOVE PROPERTY(name(t));
                MOVE PROPERTY(id(t));
            };
            NEW row2 {
                type = CONTAINERH;
                MOVE PROPERTY(loadFile(t));
                MOVE PROPERTY(openFile(t));
            };
        }
        MOVE te.box;
    }
    MOVE functions.box;
}

FORM templates 'Шаблоны Excel'

    OBJECTS t = Template
    PROPERTIES (t) READONLY name, id
    PROPERTIES (t) ADDFORM, EDITFORM, DELETE

    OBJECTS te = TemplateEntry
    PROPERTIES (te) READONLY key, value

    FILTERS template (te) == t
;

FORM dialogTemplates 'Шаблоны Excel'

    OBJECTS t = Template
    PROPERTIES (t) READONLY name, id
    PROPERTIES (t) ADDFORM, EDITFORM, DELETE

    DIALOG Template OBJECT t
;
DESIGN dialogTemplates {
    main {
        preferredSize = (1024, 768);
    }
}

NAVIGATOR {
    masterData {
        templatesNavigator {
            ADD templates;
        }
    }
}



