MODULE Backup;

REQUIRE System, Reflection, SchedulerDefaultData;

makeBackup 'Копировать'= ACTION CUSTOM 'lsfusion.erp.utils.backup.BackupActionProperty' () TOOLBAR;
save 'Скачать'= ACTION CUSTOM 'lsfusion.erp.utils.backup.SaveBackupActionProperty' (Backup) TOOLBAR;
delete 'Удалить'= ACTION CUSTOM 'lsfusion.erp.utils.backup.DeleteBackupActionProperty' (Backup) CONFIRM;

saveMondayBackups 'Оставлять бэкап за понедельник (от недели до месяца)' = DATA BOOLEAN ();
saveFirstDayBackups 'Оставлять бэкап за первое число месяца (старше месяца)' = DATA BOOLEAN ();
maxQuantityBackups 'Максимальное число сохраняемых бэкапов' = DATA INTEGER ();
decimateBackups 'Проредить'= ACTION CUSTOM 'lsfusion.erp.utils.backup.DecimateBackupsActionProperty' () TOOLBAR CONFIRM;

customRestore 'Восстановить таблицы' = ACTION CUSTOM 'lsfusion.erp.utils.backup.CustomRestoreActionProperty' (Backup);
inCustomRestore 'Вкл' = DATA BOOLEAN (Table);
restoreObjects 'Восстанавливать удалённые объекты' = DATA BOOLEAN (Table);
inCustomRestore 'Вкл' = DATA BOOLEAN (TableColumn);
replaceOnlyNull 'Не замещать' = DATA BOOLEAN (TableColumn);

CLASS Backup 'Резервная копия';
TABLE backup(Backup);

date 'Дата' = DATA DATE (Backup);
time 'Время' = DATA TIME (Backup);
file 'Адрес файла' = DATA VARSTRING[200] (Backup);
name 'Имя файла' = DATA VARSTRING[100] (Backup);
fileLog 'Адрес лога файла' = DATA VARSTRING[200] (Backup);
fileDeleted 'Файл удалён' = DATA BOOLEAN (Backup);
log 'Лог' = DATA TEXT (Backup);

backupFileName 'Имя файла резервной копии' = DATA LOCAL VARSTRING[100] ();
backupFilePath 'Файл резервной копии' = DATA LOCAL VARSTRING[200]();

exclude 'Исключить' = DATA LOCAL BOOLEAN (Table);

TABLE backupTable(Backup, Table);
exclude 'Исключена' = DATA BOOLEAN (Backup, Table);

FORM backup 'Резервное копирование'
    OBJECTS b = Backup
    PROPERTIES() saveFirstDayBackups, maxQuantityBackups, saveMondayBackups, decimateBackups TODRAW b FORCE PANEL, makeBackup TODRAW b FORCE PANEL
    PROPERTIES(b) save TODRAW b FORCE PANEL
    PROPERTIES(b) READONLY date, time, file, fileDeleted, log FORCE PANEL
    PROPERTIES(b) delete
    
    OBJECTS t1 = Table
    PROPERTIES(t1) sid, exclude
    
    OBJECTS t2 = Table
        PROPERTIES(t2) sid
        PROPERTIES(b,t2) exclude
;

DESIGN backup {
    NEW pane {
        fill = 1;
        type = TABBED;
        NEW pane1 {
            caption = 'Резервные копии';
            type = CONTAINERH;
            NEW leftPane {
                fill = 1;
                MOVE b.box;
                MOVE t1.box;
            }
            NEW rightPane {
                fill = 1;
                MOVE PROPERTY(log(b)) {
                    fill = 1;
                    panelCaptionAbove = TRUE;
                }
                MOVE t2.box {
                    caption = 'Скопированные таблицы';
                }
            }
        }
        NEW pane2 {
            caption = 'Настройки';
            align = STRETCH;
            MOVE PROPERTY(saveFirstDayBackups());
            MOVE PROPERTY(saveMondayBackups());
            MOVE PROPERTY(maxQuantityBackups());
        }        
    }
    MOVE functions.box;
}

FORM backups 'Резервные копии'
    OBJECTS b=Backup
    PROPERTIES(b) READONLY name, file, fileDeleted
    FILTERS NOT fileDeleted(b)
    DIALOG Backup OBJECT b
;

FORM customRestore 'Восстановление данных'
    OBJECTS t = Table
    PROPERTIES(t) inCustomRestore, restoreObjects, sid READONLY
    FILTERGROUP active FILTER 'Только включенные' inCustomRestore(t) 
        
    OBJECTS tc=TableColumn
    PROPERTIES(tc) inCustomRestore, replaceOnlyNull
    PROPERTIES(tc) READONLY sid, caption, canonicalName
    FILTERS table(tc) == t

    
    OBJECTS b = Backup FIXED PANEL
    PROPERTIES(b) name SELECTOR, customRestore
    ORDER BY name(b) DESC
;

NAVIGATOR {
    configuration {
        ADD backup;
        ADD customRestore;
    }
}

loadDefaultScheduledTasks () += ACTION   {    
    loadDefaultScheduledTask ('Резервное копирование', 2014_07_01_01:00, 86400, SchedulerStartType.afterStart);
    loadDefaultScheduledTaskDetail ('Резервное копирование', 1, 'Backup.makeBackup[]');
    loadDefaultScheduledTaskDetail ('Резервное копирование', 2, 'Backup.decimateBackups[]');        
}
