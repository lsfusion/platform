MODULE Numerator;

REQUIRE System, MasterData, DefaultData;

// ------------------------------------------- Нумераторы ----------------------------------------------- //

CLASS Numerator 'Нумератор';
TABLE numerator (Numerator);

nameNumerator 'Наименование' = DATA ISTRING[100](Numerator);

seriesNumerator 'Серия' = DATA STRING[2] (Numerator) IN recognize FIXEDCHARWIDTH 3;

minValueNumerator 'Мин. значение' = DATA LONG (Numerator) IN recognize MINCHARWIDTH 15;
maxValueNumerator 'Макс. значение' = DATA LONG (Numerator) IN recognize MINCHARWIDTH 15;

stringLengthNumerator 'Длина' = DATA INTEGER (Numerator) IN base;

curValueNumerator 'Тек. значение' = DATA LONG (Numerator) IN recognize MINCHARWIDTH 15;
curValueNumerator(numerator) <- minValueNumerator(numerator) WHEN ASSIGNED(numerator IS Numerator);

lpadStringWithZero = FORMULA STRING[18] 'lpad(CAST($1 AS text),$2,\'0\')';

curStringValueNumerator 'Тек. значение (строка)' (numerator) = lpadStringWithZero(curValueNumerator(numerator), stringLengthNumerator(numerator)) IN base;

incrementValueNumerator 'Увеличить значение' = ACTION (numerator) NEWSESSION {
    IF curValueNumerator(numerator) >= maxValueNumerator(numerator) THEN
        MESSAGE 'Счетчик нумератора достиг максимального значения. Обратитесь к администратору.'
    ELSE {
        SET curValueNumerator(numerator) <- curValueNumerator(numerator) + 1;
        EXEC apply();
    };
};

incrementValueSessionNumerator 'Увеличить значение' = ACTION (numerator) {
    IF curValueNumerator(numerator) >= maxValueNumerator(numerator) THEN
        MESSAGE 'Счетчик нумератора достиг максимального значения. Обратитесь к администратору.'
    ELSE
        SET curValueNumerator(numerator) <- curValueNumerator(numerator) + 1;
}

FORM numerator 'Нумератор'
    OBJECTS n = Numerator FIXED PANEL
    PROPERTIES(n) nameNumerator, seriesNumerator, minValueNumerator, maxValueNumerator, stringLengthNumerator, curValueNumerator, curStringValueNumerator

    EDIT Numerator OBJECT n
;

FORM numerators 'Нумераторы'
    OBJECTS n = Numerator
    PROPERTIES(n) READONLY nameNumerator, seriesNumerator, minValueNumerator, maxValueNumerator, stringLengthNumerator, curValueNumerator, curStringValueNumerator
    PROPERTIES(n)          ADDFORM, EDITFORM, DELETE

    DIALOG Numerator OBJECT n
;

FORM defaultNumerators 'Нумераторы'
    OBJECTS n = Numerator
    PROPERTIES(n) READONLY nameNumerator, seriesNumerator, minValueNumerator, maxValueNumerator, stringLengthNumerator, curValueNumerator, curStringValueNumerator
    PROPERTIES(n)          ADDFORM, EDITFORM, DELETE
;

DESIGN defaultNumerators FROM DEFAULT {
    main {
        NEW defaultContainer AFTER n.box {
            caption = 'Нумераторы по умолчанию';
        }
    }
}

NAVIGATOR {
    masterData {
        ADD defaultNumerators;
    }
}

// ---------------------------------------------- Нумерованные объекты ----------------------------------------------- //

GROUP numberedGroup 'Нумератор' : recognize;

CLASS ABSTRACT NumberedObject 'Нумерованный объект';
TABLE numberedObject (NumberedObject);

numberObject 'Номер' = DATA STRING[18] (NumberedObject) IN numberedGroup MINCHARWIDTH 7;
seriesObject 'Серия' = DATA STRING[2] (NumberedObject) IN numberedGroup FIXEDCHARWIDTH 3;

seriesNumberObject 'Серия/Номер' (numberedObject) = CONCAT '', seriesObject(numberedObject), numberObject(numberedObject) MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20 PERSISTENT;

initValueNumberObject = ABSTRACT STRING[18] (NumberedObject);
initValueSeriesObject = ABSTRACT STRING[2] (NumberedObject);

initWhenNumberedObject = ABSTRACT BOOLEAN (NumberedObject);

numberObject (object) <- initValueNumberObject(object) WHEN initWhenNumberedObject(object);
seriesObject (object) <- initValueSeriesObject(object) WHEN initWhenNumberedObject(object);

CLASS ABSTRACT NumeratedObject 'Нумерируемый объект' : NumberedObject;

numeratorObject 'Нумератор (ИД)' = DATA Numerator (NumeratedObject);
nameNumeratorObject 'Нумератор' (numeratedObject) = nameNumerator(numeratorObject(numeratedObject)) IN numberedGroup MINCHARWIDTH 7 PREFCHARWIDTH 15;

initValueNumeratorObject = ABSTRACT Numerator (NumeratedObject);
initWhenNumeratorObject = ABSTRACT BOOLEAN (NumeratedObject);

numeratorObject (object) <- initValueNumeratorObject (object) WHEN initWhenNumeratorObject(object);

setValueNumeratedObject 'Сгенерировать номер' = ACTION (numeratedObject) {
    SET numberObject(numeratedObject) <- curStringValueNumerator(numeratorObject(numeratedObject));
    SET seriesObject(numeratedObject) <- seriesNumerator(numeratorObject(numeratedObject));
    EXEC incrementValueNumerator(numeratorObject(numeratedObject));
}

setValueSessionNumeratedObject 'Сгенерировать номер' = ACTION (numeratedObject) {
    SET numberObject(numeratedObject) <- curStringValueNumerator(numeratorObject(numeratedObject));
    SET seriesObject(numeratedObject) <- seriesNumerator(numeratorObject(numeratedObject));
    EXEC incrementValueSessionNumerator(numeratorObject(numeratedObject));
}

WHEN CHANGED(numeratorObject(numeratedObject)) AND
     NOT CHANGED(numberObject(numeratedObject)) AND
     NOT CHANGED(seriesObject(numeratedObject))
     DO EXEC setValueSessionNumeratedObject(numeratedObject) ;

// --------------------------------------------- Стандартные значения ---------------------------- //

loadDefaultNumerators 'Загрузить стандартные нумераторы' = ABSTRACT ACTION LIST ();
@implementLoadDefaultData(loadDefaultNumerators);

// --------------------------------------------- Макрос по объявлению нумерируемого объекта ---------------------------- //

META defineNumeratedObjectDefault(class, caption, series)
    @defineNumeratedObjectDefaultPrefix(class, caption, series, );
END

META defineNumeratedObjectDefaultPrefix(class, caption, series, prefix)

    prefix###defaultNumerator###class = DATA Numerator ();
    name###prefix##DefaultNumerator###class caption = nameNumerator(prefix###defaultNumerator###class());

    EXTEND FORM defaultNumerators
        PROPERTIES() name###prefix##DefaultNumerator###class
    ;
    EXTEND DESIGN defaultNumerators {
        defaultContainer {
            ADD PROPERTY(name###prefix##DefaultNumerator###class);
        }
    }

    loadDefaultNumerators() += ACTION () {
        ADDOBJ Numerator;
        FOR n == addedObject() DO {
            SET nameNumerator(n) <- caption;
            SET seriesNumerator(n) <- series;
            SET minValueNumerator(n) <- 1L;
            SET maxValueNumerator(n) <- 99999L;
            SET stringLengthNumerator(n) <- 5;

            SET prefix###defaultNumerator###class() <- n;
        }
    }

    initValueNumeratorObject(object) += prefix###defaultNumerator###class() IF object IS class;
    initWhenNumeratorObject(object) += ASSIGNED(object IS class);

END

//------------------------------------------ Для двух объектов ---------------------------------------------//

META defineNumeratedObject(object1, object2)

    number###object1###object2 'Номер' = DATA STRING[18] (###object1, ###object2) MINCHARWIDTH 7;
    series###object1###object2 'Серия' = DATA STRING[2] (###object1, ###object2) FIXEDCHARWIDTH 3;
    seriesNumber###object1###object2 'Серия/Номер' (object1, object2) = CONCAT '', series###object1###object2(object1, object2), number###object1###object2(object1, object2) MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20 PERSISTENT;

    numerator###object1###object2 = DATA Numerator (###object1, ###object2);
    nameNumerator###object1###object2 'Нумератор' (object1, object2) = nameNumerator(numerator###object1###object2(object1, object2)) MINCHARWIDTH 7 PREFCHARWIDTH 15;

    setValueSessionNumerated###object1###object2 'Сгенерировать номер' = ACTION (object1, object2) {
        SET number###object1###object2(object1, object2) <- curStringValueNumerator(numerator###object1###object2(object1, object2));
        SET series###object1###object2(object1, object2) <- seriesNumerator(numerator###object1###object2(object1, object2));
        EXEC incrementValueSessionNumerator(numerator###object1###object2(object1, object2));
    } SHORTCUT number###object1###object2;

    WHEN CHANGED(numerator###object1###object2(object1, object2)) AND
         NOT CHANGED(number###object1###object2(object1, object2)) AND
         NOT CHANGED(series###object1###object2(object1, object2))
         DO EXEC setValueSessionNumerated###object1###object2(object1, object2) ;

END

META defineNumeratedObjectDefault (object1, object2, property, caption, series)

    defaultNumerator###object1###object2 = DATA Numerator ();
    nameDefaultNumerator###object1###object2 caption = nameNumerator(defaultNumerator###object1###object2());

    EXTEND FORM defaultNumerators
        PROPERTIES() nameDefaultNumerator###object1###object2
    ;
    EXTEND DESIGN defaultNumerators {
        defaultContainer {
            ADD PROPERTY(nameDefaultNumerator###object1###object2);
        }
    }

    loadDefaultNumerators() += ACTION () {
        ADDOBJ Numerator;
        FOR n == addedObject() DO {
            SET nameNumerator(n) <- caption;
            SET seriesNumerator(n) <- series;
            SET minValueNumerator(n) <- 1L;
            SET maxValueNumerator(n) <- 99999L;
            SET stringLengthNumerator(n) <- 5;

            SET defaultNumerator###object1###object2() <- n;
        }
    };

    numerator###object1###object2 (object1, object2) <- defaultNumerator###object1###object2 () WHEN ASSIGNED(property(object1, object2));
END
