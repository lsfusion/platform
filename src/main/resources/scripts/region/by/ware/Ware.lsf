MODULE Ware;

REQUIRE System, Historizable, Tax, Stock;

// ------------------------------------------ Глобальные настройки --------------------------------- //
showWare 'Показывать стеклопосуду' = DATA BOOLEAN ();
EXTEND FORM options
    PROPERTIES() showWare
;
EXTEND DESIGN options {
    commons {
        ADD PROPERTY(showWare);
    }
}

// ----------------------------------------- Группа посуды ----------------------------- //
CLASS WareGroup 'Группа посуды' : SkuGroup;
TABLE wareGroup (WareGroup);

nameWareGroup 'Наименование' = DATA ISTRING[100](WareGroup);

nameSkuGroup(group) += nameWareGroup(group) IF group IS WareGroup;

TABLE wareGroupWareGroup(WareGroup, WareGroup);
@defineHierarchy(wareGroup);

parentSkuGroup (wareGroup) += parentWareGroup(wareGroup);

// ------------------------------------------ Посуда ----------------------------------------------- //
CLASS Ware 'Посуда' : Sku;
TABLE ware(Ware);

nameWare 'Наименование' = DATA ISTRING[100](Ware);

wareGroupWare = DATA WareGroup(Ware) AUTOSET;
skuGroupSku(ware) += wareGroupWare(ware);

nameSku(ware) += nameWare(ware) IF ware IS Ware;

nameWareGroupWare 'Группа посуды' (ware) = nameWareGroup(wareGroupWare(ware));
canonicalNameWareGroupWare 'Группа посуды' (ware) = canonicalNameWareGroup(wareGroupWare(ware)) IN base;

// Товар
skuTypeSku (sku) += SkuType.skuTypeItem IF sku IS Ware;

TABLE wareGroupWare (WareGroup, Ware);

isParentWareGroupWare (wareGroup, ware) = isParentWareGroupWareGroup(wareGroupWare(ware), wareGroup);   //PERSISTENT

TABLE wareDate(Ware, DATE);

@defineHistorizable(warePrice, 'Цена', NUMERIC[14,2], ware, nameWare, base);

@defineHistorizableCustom(rangeWare, 'НДС', Range, numberRange, ware, nameWare, base);

CONSTRAINT taxRange(dataRangeWareDate(ware, date)) != Tax.taxVAT CHECKED BY dataRangeWareDate MESSAGE 'ошибка: Шкала должна соответствовать шкале НДС';

dataValueRangeWareDate 'НДС, %' (ware, date) = valueRateRangeDate(dataRangeWareDate(ware, date), date) IN base;
valueCurrentRateRangeWare 'НДС, %' (ware) = valueCurrentRateRange(rangeWare(ware)) IN base;

EXTEND FORM addRangeWare
    PROPERTIES READONLY dataValueRangeWareDate(a, d)
;
EXTEND FORM dialogRangeWare
    PROPERTIES dataValueRangeWareDate(a, d)
;

// ------------------ Формы  --------------------- //

FORM wareGroup 'Группа посуды'
    OBJECTS g=WareGroup FIXED PANEL
    PROPERTIES(g) nameWareGroup, nameParentWareGroup
    EDIT WareGroup OBJECT g
;

FORM wareGroups 'Группы посуды'
    TREE treeGroups tg=WareGroup PARENT parentWareGroup
    PROPERTIES READONLY nameWareGroup(tg)
    ORDER BY nameWareGroup

    OBJECTS g=WareGroup
    PROPERTIES(g) READONLY nameWareGroup, canonicalNameWareGroup
    PROPERTIES(g) DELETE, ADDFORM, EDITFORM
    ORDER BY canonicalNameWareGroup

    FILTERS isParentWareGroupWareGroup(g, tg)

    DIALOG WareGroup OBJECT g
;

DESIGN wareGroups FROM DEFAULT {
    main{
        preferredSize = (1024, 768);
            NEW rootContainer BEFORE functions.box{
                childConstraints = TO THE RIGHT;
                type = SPLITH;

            ADD treeGroups.tree.box {
                fillHorizontal = 2;
            }

            ADD g.box {
                fillHorizontal = 4;
            }
        }
    }
}


FORM ware 'Посуда'
    OBJECTS w=Ware FIXED PANEL
    PROPERTIES(w) nameWare, canonicalNameWareGroupWare, warePrice
    PROPERTIES(w) numberRangeRangeWare, valueCurrentRateRangeWare

    EDIT Ware OBJECT w
;

FORM wares 'Посуда'

    TREE treeGroups tg=WareGroup PARENT parentWareGroup
    PROPERTIES READONLY nameWareGroup(tg)
    ORDER BY nameWareGroup

    OBJECTS w=Ware
    PROPERTIES(w) READONLY nameWare, canonicalNameWareGroupWare, warePrice, numberRangeRangeWare, valueCurrentRateRangeWare
    PROPERTIES(w) ADDFORM, EDITFORM, deletew=DELETE
    FILTERS isParentWareGroupWare(tg, w)

    DIALOG Ware OBJECT w
;

DESIGN wares FROM DEFAULT {

    NEW rootContainer {
        type = SPLITH;
        childConstraints = TO THE RIGHT;
        ADD treeGroups.tree.box {
            title = 'Группы посуды';
            childConstraints = TO THE BOTTOM;
        }
        ADD w.box {
            defaultComponent = TRUE;
            fillHorizontal = 4;
        }
    }
    ADD functions.box;
}

NAVIGATOR {
    skuNavigator {
        ADD wareGroups AFTER UOMs;
        ADD wares AFTER wareGroups;
    }
}