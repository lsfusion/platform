MODULE PurchaseInvoiceCharge;

REQUIRE PurchaseInvoice;

NAMESPACE Purchase;

@defineDocumentInterfaceCreate(invoice, showChargePrice, 'Услуги');

// -- Операция
@defineOperationProperty(showChargePrice, 'Услуги', showContainer);
@deriveDocumentOperationProperty(UserInvoice, showChargePrice);

calcChargePriceUserInvoiceDetail  (detail) = [(X*Y)/(Z*W)](
    sumUserInvoiceDetail(detail),
    sumChargeUserInvoiceDetailUserInvoice(userInvoiceUserInvoiceDetail(detail)),
    sumNotChargeUserInvoiceDetailUserInvoice(userInvoiceUserInvoiceDetail(detail)),
    quantityUserInvoiceDetail(detail)
) IF isStockSkuInvoiceDetail(detail);

@defineDocumentInterfaceDetailPriceCustomPrefix (invoiceDetail, charge, ' услуг за ед.'); // объявляем цену услуг
@defineDocumentInterfaceDetailDataSumCustomPrefix (invoiceDetail, charge, ' услуг'); // объявляем сумму услуг
@deriveDocumentDetailSumCustomPrefix(userInvoiceDetail, charge, currency, quantity); // записываем сумму услуг

chargeSumUserInvoiceDetailUserInvoice 'Сумма услуг в документе (построчно)' (userInvoice) = GROUP SUM chargeSumUserInvoiceDetail(idetail)
    BY userInvoiceUserInvoiceDetail(idetail) IN documentSumGroup;
chargeSumInvoiceDetailInvoice 'Сумма услуг в документе (построчно)' (invoice) = GROUP SUM chargeSumInvoiceDetail(idetail)
    BY invoiceInvoiceDetail(idetail) IN documentSumGroup;

deriveChargePriceUserInvoice 'Расписать сумму услуг по товарам' = ACTION (userInvoice) {
    SET chargePriceUserInvoiceDetail(detail) <- calcChargePriceUserInvoiceDetail(detail) WHERE userInvoiceUserInvoiceDetail(detail) == userInvoice;
}

EXTEND CLASS SystemLedgerPriceListType {chargePriceStockPriceListType 'Услуги (последняя по складу)' }
batchLedgerPriceListType(type) += type == SystemLedgerPriceListType. chargePriceStockPriceListType;
inPriceListLedgerSystemLedgerPriceListType (ledger, type) += TRUE WHEN CLASS(chargePriceInvoiceDetail(ledger)) AND type == SystemLedgerPriceListType.chargePriceStockPriceListType;
pricePriceListLedgerSystemLedgerPriceListType (ledger, type) += chargePriceInvoiceDetail(ledger) WHEN CLASS(chargePriceInvoiceDetail(ledger)) AND type == SystemLedgerPriceListType.chargePriceStockPriceListType;

EXTEND CLASS SystemLedgerPriceListType { chargePricePriceListType 'Услуги (последняя по ценовой группе)' }
batchLedgerPriceListType(type) += type == SystemLedgerPriceListType.chargePricePriceListType;
inPriceListLedgerSystemLedgerPriceListType (ledger, type) += TRUE WHEN CLASS(chargePriceInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger))) AND type == SystemLedgerPriceListType.chargePricePriceListType;
pricePriceListLedgerSystemLedgerPriceListType (ledger, type) += chargePriceInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger)) WHEN CLASS(chargePriceInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger))) AND type == SystemLedgerPriceListType.chargePricePriceListType;

extraCostPriceUserInvoiceDetail(detail) += chargePriceUserInvoiceDetail(detail);

backgroundChargeInvoice 'Цвет' (invoice) = RGB(224, 255, 128) IF invoice IS Invoice;

EXTEND FORM userInvoice
    PROPERTIES(i) BACKGROUND backgroundChargeInvoice(i) showChargePriceUserInvoice
    PROPERTIES(i) BACKGROUND backgroundChargeInvoice(i) SHOWIF showChargePriceUserInvoice(i)
                                                        sumChargeUserInvoiceDetailUserInvoice
    PROPERTIES(i)                                       deriveChargePriceUserInvoice TODRAW d FORCE PANEL TOOLBAR
    PROPERTIES(d) BACKGROUND backgroundChargeInvoice(i) SHOWIF showChargePriceUserInvoice(i) BEFORE numberVATUserInvoiceDetail(d)
                                                        chargePriceUserInvoiceDetail, chargeSumUserInvoiceDetail
;

EXTEND DESIGN userInvoice {
    headerExtraParams {
        NEW headerCharge {
            title = 'Услуги';
            ADD PROPERTY(showChargePriceUserInvoice);
        }
    }
}

EXTEND FORM invoices
    PROPERTIES(i) BACKGROUND backgroundChargeInvoice(i) READONLY showChargePriceInvoice, sumChargeInvoiceDetailInvoice
    PROPERTIES(d) BACKGROUND backgroundChargeInvoice(i) SHOWIF showChargePriceInvoice(i) READONLY BEFORE numberVATInvoiceDetail(d)
                                                        chargePriceInvoiceDetail, chargeSumInvoiceDetail
;

//-------------Расчет транспорта по коэффициенту в шапке накладной-------------//

chargePercentInvoice '% трансп. расходов' = ABSTRACT NUMERIC[6,3] (Invoice) IN documentSumGroup;

chargePercentUserInvoice '% трансп. расходов' (userInvoice) = DATA NUMERIC[6,3] (UserInvoice) IN documentSumGroup FIXEDCHARWIDTH 6;
chargePercentUserInvoiceDetail '% трансп. расходов' (userInvoiceDetail) = DATA NUMERIC[6,3] (UserInvoiceDetail) MINCHARWIDTH 6;

calcChargePercentUserInvoiceDetail (userInvoiceDetail) =
    chargePercentUserInvoiceDetail(userInvoiceDetail) * priceUserInvoiceDetail(userInvoiceDetail) / 100;

deriveCalcChargePriceUserInvoice 'Рассчитать сумму услуг по товарам' = ACTION (userInvoice) {
    SET chargePercentUserInvoiceDetail(detail) <- chargePercentUserInvoice(userInvoiceUserInvoiceDetail(detail)) WHERE userInvoiceUserInvoiceDetail(detail) == userInvoice;
    SET chargePriceUserInvoiceDetail(detail) <- calcChargePercentUserInvoiceDetail (detail) WHERE userInvoiceUserInvoiceDetail(detail) == userInvoice;
}

WHEN SESSION FORMS userInvoice CHANGED(chargePercentUserInvoice(userInvoice)) DO EXEC deriveCalcChargePriceUserInvoice(userInvoice);

chargePercentInvoice(invoice) += chargePercentUserInvoice(invoice);

EXTEND FORM userInvoice
    PROPERTIES(i) BACKGROUND backgroundChargeInvoice(i) SHOWIF showChargePriceUserInvoice(i) chargePercentUserInvoice
    PROPERTIES(d) BACKGROUND backgroundChargeInvoice(i) SHOWIF showChargePriceUserInvoice(i) chargePercentUserInvoiceDetail BEFORE chargePriceUserInvoiceDetail(d)
;
EXTEND DESIGN userInvoice {
    headerCharge {
        ADD PROPERTY(chargePercentUserInvoice);
    }
}

EXTEND FORM invoices
    PROPERTIES(i) BACKGROUND backgroundChargeInvoice(i) READONLY chargePercentInvoice
;

