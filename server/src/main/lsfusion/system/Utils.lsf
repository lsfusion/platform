MODULE Utils;

REQUIRE System, Time;

// --------------------------------------------- Стандартные группы --------------------------------- //

GROUP print '{utils.group.print}' : public;

// ---------------------------------- Чтение списка файлов по пути ---------------------------------- //
fileName '{utils.file.name.integer}' = DATA LOCAL ISTRING[500] (INTEGER);
fileIsDirectory '{utils.file.is.directory.integer}' = DATA LOCAL BOOLEAN (INTEGER);
fileModifiedDateTime '{utils.file.modified.datetime.integer}' = DATA LOCAL DATETIME (INTEGER);

//path, recursive, isClient
listFiles '{utils.list.files}'  INTERNAL  'lsfusion.server.physics.dev.integration.external.to.file.ListFilesAction' (ISTRING, BOOLEAN, BOOLEAN); 
listFiles '{utils.list.files}' (ISTRING[500] path, BOOLEAN recursive)  { listFiles(path, recursive, NULL); }
listFiles '{utils.list.files}' (ISTRING[500] path)  { listFiles(path, NULL, NULL); }
listFilesClient '{utils.list.files.client}' (ISTRING[500] path, BOOLEAN recursive)  { listFiles(path, recursive, TRUE); }
listFilesClient '{utils.list.files.client}' (ISTRING[500] path)  { listFiles(path, NULL, TRUE); }

// ------------------------------ Проверка существования файла по пути ------------------------------ //
fileExists = DATA LOCAL BOOLEAN ();
//path, isClient
fileExists '{utils.file.exists}'  INTERNAL  'lsfusion.server.physics.dev.integration.external.to.file.FileExistsAction' (ISTRING[500], BOOLEAN); 
fileExists '{utils.file.exists}' (ISTRING[500] path)  { fileExists(path, NULL); }
fileExistsClient '{utils.file.exists.client}' (ISTRING[500] path)  { fileExists(path, TRUE); } 

// --------------------------------- BPSTRING -> FILE/LINK/RICHTEXT -> BPSTRING --------------------------------- //
resultFile = DATA LOCAL FILE ();
//value, charset, extension
stringToFile '{utils.string.to.file}'  INTERNAL  'lsfusion.server.physics.dev.integration.external.to.file.StringToFileAction' (TEXT, STRING[10], STRING[10]);
stringToFile(TEXT str) { stringToFile(str, 'UTF-8', ''); }

resultString = DATA LOCAL TEXT ();
//file, charset
fileToString '{utils.file.to.string}'  INTERNAL  'lsfusion.server.physics.dev.integration.external.to.file.FileToStringAction' (FILE, STRING[10]);
fileToString(FILE file) { fileToString(file, 'UTF-8'); }
//cast link to string
linkToString '{utils.link.to.string}' INTERNAL 'lsfusion.server.physics.dev.integration.external.to.file.LinkToStringAction' (LINK);
//cast richtext to string
richTextToString '{utils.rich.text.to.string}' INTERNAL 'lsfusion.server.logics.classes.data.utils.string.RichTextToStringAction' (RICHTEXT);

//path, text, charset
appendToFile '{utils.append.to.file}' INTERNAL 'lsfusion.server.physics.dev.integration.external.to.file.AppendToFileAction' (STRING[1000], TEXT, STRING[10]);
appendToFile(STRING[1000] path, TEXT text) { appendToFile(path, text, 'UTF-8'); };

setExtension = FORMULA FILE 'cast_to_custom_file(($1),$2)'; // for RAWFILE, PDFFILE...
changeExtension(FILE file, TEXT extension) = setExtension(RAWFILE(file), extension); // for FILE

// --------------------------------- Logging --------------------------------- //

printToLog '{utils.print.to.log}'  INTERNAL  'lsfusion.server.physics.admin.log.action.PrintToLogAction' (TEXT);
logToFile (STRING[1000] file, TEXT message) {
    appendToFile('logs/' + file + '.log', (CONCAT ' - ', currentDateTime(), message, '\n')); 
}
logToFile (STRING[1000] file, FILE message) {
    fileToString(message, 'UTF-8');
    logToFile(file, resultString());
}

// ----------------------------------- Работа с файловой системой ----------------------------------- //
//path, isClient
mkdir '{utils.mkdir}'  INTERNAL  'lsfusion.server.physics.dev.integration.external.to.file.MkdirAction' (ISTRING[500], BOOLEAN);
mkdir '{utils.mkdir}' (ISTRING[500] path)  { mkdir(path, NULL); }
mkdirClient '{utils.mkdir.client}' (ISTRING[500] path)  { mkdir(path, TRUE); }
//path, isClient
delete '{utils.delete}'  INTERNAL  'lsfusion.server.physics.dev.integration.external.to.file.DeleteAction' (ISTRING[500], BOOLEAN);
delete '{utils.delete}' (ISTRING[500] path)  { delete(path, NULL); }
deleteClient '{utils.delete.client}' (ISTRING[500] path)  { delete(path, TRUE); }
//sourcePath, destinationPath, isClient
copy '{utils.move}'  INTERNAL  'lsfusion.server.physics.dev.integration.external.to.file.CopyAction' (ISTRING[500], ISTRING[500], BOOLEAN);
copy '{utils.move}' (ISTRING[500] source, ISTRING[500] destination)  { copy(source, destination, NULL); }
copyClient '{utils.move.client}' (ISTRING[500] source, ISTRING[500] destination)  { copy(source, destination, TRUE); }
//sourcePath, destinationPath, isClient
move '{utils.move}'  INTERNAL  'lsfusion.server.physics.dev.integration.external.to.file.MoveAction' (ISTRING[500], ISTRING[500], BOOLEAN);
move '{utils.move}' (ISTRING[500] source, ISTRING[500] destination)  { move(source, destination, NULL); }
moveClient '{utils.move.client}' (ISTRING[500] source, ISTRING[500] destination)  { move(source, destination, TRUE); }

fileSize = DATA LOCAL LONG ();
getFileSize '{utils.get.file.size}'  INTERNAL  'lsfusion.server.physics.dev.integration.external.to.file.GetFileSizeAction' (FILE);

//zip files
zipping '{utils.zipping.file}' = DATA LOCAL FILE (STRING[1000]);
zippingPath '{utils.zipping.path}' = DATA LOCAL STRING (STRING[1000]);
zipped '{utils.zipped}' = DATA LOCAL FILE ();
makeZipFile '{utils.make.zip.file}'  INTERNAL  'lsfusion.server.physics.dev.integration.external.to.file.MakeZipFileAction' ();

//unzip files
unzipping '{utils.unzipping}' = DATA LOCAL FILE ();
unzipped '{utils.unzipped.string}' = DATA LOCAL FILE (STRING[100]);
makeUnzipFile '{utils.make.unzip.file}'  INTERNAL  'lsfusion.server.physics.dev.integration.external.to.file.MakeUnzipFileAction' ();

resourceFile '{utils.resource.file}' = DATA LOCAL FILE ();
readResource '{utils.read.resource}'  INTERNAL  'lsfusion.server.physics.dev.integration.external.to.file.ReadResourceAction' (STRING[100]);

resourceFiles = DATA LOCAL RAWFILE (STRING);
readResources  INTERNAL  'lsfusion.server.physics.dev.integration.external.to.file.ReadResourcesAction' (STRING);

// ---------------------------------- Выполнение cmd  ---------------------------------- //
cmd '{utils.cmd}'  INTERNAL  'lsfusion.server.physics.admin.interpreter.action.RunCommandAction' (TEXT, BOOLEAN);
cmd '{utils.cmd}' (TEXT command)  {cmd(command, NULL); }
cmdClient '{utils.cmd.client}' (TEXT command)  {cmd(command, TRUE); }

pingError '{utils.ping.error}' = DATA LOCAL BOOLEAN ();
ping '{utils.ping}'  INTERNAL  'lsfusion.server.physics.dev.integration.external.to.net.PingAction' (TEXT, BOOLEAN);
ping '{utils.ping}' (TEXT host)  {ping(host, NULL); }
pingClient '{utils.ping.client}' (TEXT host)  {ping(host, TRUE); }

// ---------------------------------- Excel file actions  ---------------------------------- //
protectedExcel '{utils.excel.protected}' = DATA LOCAL EXCELFILE ();
protectExcel '{utils.excel.protect}'  INTERNAL  'lsfusion.server.logics.classes.data.utils.excel.ProtectExcelAction' (EXCELFILE, STRING[100]);

columnsCount '{utils.excel.columns.count}' = DATA LOCAL INTEGER ();
columnsCount '{utils.excel.columns.count}' (EXCELFILE file, INTEGER sheet)  INTERNAL  'lsfusion.server.logics.classes.data.utils.excel.ColumnsCountExcelAction';
columnsCount '{utils.excel.columns.count}' (EXCELFILE file)  { columnsCount(file, NULL); }

// ---------------------------------- Pdf file actions  ---------------------------------- //
pagesCountPdf '{utils.pdf.pages.count}' = DATA LOCAL INTEGER ();
pagesCountPdf '{utils.pdf.pages.count}'  INTERNAL  'lsfusion.server.logics.classes.data.utils.pdf.PagesCountPdfAction' (PDFFILE);

mergedExcel '{utils.excel.merged}' = DATA LOCAL EXCELFILE ();
mergeExcel '{utils.excel.merge}'  INTERNAL  'lsfusion.server.logics.classes.data.utils.excel.MergeExcelAction' (EXCELFILE, EXCELFILE);

// ---------------------------------- Генерация паролей  ---------------------------------- //
generatedPassword 'Сгенерированный пароль' = DATA LOCAL TEXT();
//length, use at least one digit requirement, use both registers requirement
generatePassword 'Сгенерировать пароль' INTERNAL 'lsfusion.server.physics.admin.backup.action.GeneratePasswordAction' (INTEGER, BOOLEAN, BOOLEAN);

// --------------------------------------------- Работа со строками --------------------------------- //

lower (TEXT p1) = FORMULA 'lower($1)';

isWordInCSV = FORMULA NULL BOOLEAN PG 'CASE WHEN CAST($1 AS text)=ANY(string_to_array($2,\',\')) THEN 1 ELSE NULL END', MS 'CASE WHEN CHARINDEX(\' \' + CAST($1 AS nvarchar(max)) + \' \',\' \' + REPLACE($2,\',\',\' \') + \' \') > 0 THEN 1 ELSE NULL END';

getWord (TEXT p1, TEXT p2, INTEGER p3) = FORMULA NULL TEXT '(string_to_array($1,$2))[($3)]';
wordCount (TEXT p1, TEXT p2) = FORMULA INTEGER 'array_length((string_to_array($1,$2)),1)';

istartsWith = FORMULA NULL BOOLEAN PG 'CASE WHEN (($1) ILIKE (($2)||\'%\')) THEN 1 ELSE NULL END', MS 'CASE WHEN (($1) LIKE (($2)+\'%\')) THEN 1 ELSE NULL END';
startsWith = FORMULA NULL BOOLEAN PG 'CASE WHEN (($1) LIKE (($2)||\'%\')) THEN 1 ELSE NULL END', MS 'CASE WHEN (($1) LIKE (($2)+\'%\')) THEN 1 ELSE NULL END';
endsWith = FORMULA NULL BOOLEAN PG 'CASE WHEN (($1) LIKE (\'%\'||($2))) THEN 1 ELSE NULL END', MS 'CASE WHEN (($1) LIKE (\'%\'+($2))) THEN 1 ELSE NULL END';

// todo : для MS SQL пофиксить формулу, чтобы правильно работало, если в подстроке есть %
isISubstring = FORMULA NULL BOOLEAN PG 'CASE WHEN position(lower($2) in lower($1)) > 0 THEN 1 ELSE NULL END', MS 'CASE WHEN (($1) LIKE (\'%\'+($2)+\'%\')) THEN 1 ELSE NULL END'; 
isSubstring = FORMULA NULL BOOLEAN PG 'CASE WHEN position(($2) in ($1)) > 0 THEN 1 ELSE NULL END', MS 'CASE WHEN (($1) LIKE (\'%\'+($2)+\'%\')) THEN 1 ELSE NULL END';

replace (TEXT p1, TEXT p2, TEXT p3) = FORMULA TEXT 'replace($1,$2,$3)';

length (TEXT p1) = FORMULA INTEGER PG 'length($1)', MS 'len($1)';

strpos (TEXT string, TEXT substring) = FORMULA INTEGER PG 'strpos($1, $2)', MS 'charindex($2, $1)'; 
regexpReplace (STRING source, STRING pattern, STRING replace, STRING flags) = FORMULA STRING PG 'REGEXP_REPLACE($1, $2, $3, $4)'; 

substr (TEXT p1, INTEGER p2, INTEGER p3) = FORMULA TEXT 'substring($1, $2, $3)';
substrFrom (TEXT p1, INTEGER p2) = FORMULA TEXT PG 'substring($1, $2)', MS 'substring($1, $2, len($1) - $2 + 1)'; 

ltrim (TEXT p1) = FORMULA TEXT 'ltrim($1)';
rtrim (TEXT p1) = FORMULA TEXT 'rtrim($1)';

ltrim (TEXT p1, TEXT p2) = FORMULA TEXT PG 'ltrim($1,$2)';
rtrim (TEXT p1, TEXT p2) = FORMULA TEXT PG 'rtrim($1,$2)';

trim (TEXT p1) = FORMULA TEXT PG 'trim($1)', MS 'ltrim(rtrim($1))';

left (TEXT p1, INTEGER p2) = FORMULA TEXT 'left($1,$2)';
right (TEXT p1, INTEGER p2) = FORMULA TEXT 'right($1,$2)';

lpad (TEXT p1, INTEGER p2, TEXT p3) = FORMULA TEXT PG 'lpad($1,$2,$3)';
rpad (TEXT p1, INTEGER p2, TEXT p3) = FORMULA TEXT PG 'rpad($1,$2,$3)';

repeat (TEXT p1, INTEGER p2) = FORMULA TEXT PG 'repeat($1,$2)', MS 'REPLICATE ($1,$2)';

onlyDigits (TEXT p1) = FORMULA NULL BOOLEAN PG 'CASE WHEN trim($1) ~ \'^[0-9]*$\' THEN 1 ELSE NULL END', MS 'CASE WHEN ltrim($1) NOT LIKE \'%[^0-9]%\' THEN 1 ELSE NULL END';

encode = FORMULA TEXT 'encode(($1),($2))';
decode = FORMULA RAWFILE 'decode(($1),($2))';
decodeText = FORMULA TEXT 'decode(($1),($2))';

encodedBase64 = DATA LOCAL TEXT();
encodeBase64  INTERNAL  'lsfusion.server.logics.classes.data.utils.string.EncodeBase64Action' (TEXT);

decodedBase64 = DATA LOCAL TEXT();
decodeBase64  INTERNAL  'lsfusion.server.logics.classes.data.utils.string.DecodeBase64Action' (TEXT);

urlEncoded = DATA LOCAL TEXT();
urlEncode  INTERNAL 'lsfusion.server.logics.classes.data.utils.string.UrlEncodeAction' (TEXT, BPSTRING[20]);
urlEncode (TEXT text) { urlEncode(text, 'UTF-8'); };

urlDecoded = DATA LOCAL TEXT();
urlDecode  INTERNAL 'lsfusion.server.logics.classes.data.utils.string.UrlDecodeAction' (TEXT, BPSTRING[20]);
urlDecode (TEXT text) { urlDecode(text, 'UTF-8'); };

generatedUUID = DATA LOCAL TEXT();
generateUUID  INTERNAL 'lsfusion.server.logics.classes.data.utils.string.GenerateUUIDAction' ();

escapeJSONValue (TEXT t) = replace(replace(replace(replace(replace(t, '\\', '\\\\'), '"', '\\"'), '\n', ''), '\r', ''), '\t', '');
escapeXMLValue(TEXT t) = replace(replace(replace(replace(replace(t, '&', '&amp;'), '<', '&lt;'), '>', '&gt;'), '"', '&quot;'), '\'', '&apos;');

// --------------------------------------------- Работа с числами --------------------------------- //

floor = FORMULA 'floor($1)';
floor = FORMULA NULL 'floor(($1)/notZero($2))*($2)';
ceil = FORMULA 'ceiling($1)';
ceil = FORMULA NULL 'ceiling(($1)/notZero($2))*($2)';
min = FORMULA '($1+$2-ABS($1-$2))/2';
max = FORMULA '($1+$2+ABS($1-$2))/2';
trunc = FORMULA 'trunc($1,$2)';

sqr(a) = a*a;
sqrt = FORMULA 'sqrt($1)';
power = FORMULA 'power($1, $2)';
percent = FORMULA '(($1)*($2)/100)';
share = FORMULA '(($1)*100/($2))';
round = FORMULA PG 'round(CAST(($1) as numeric),$2)', MS 'round(CAST(($1) as numeric(38,19)),$2)';
abs = FORMULA 'abs($1)';
delta = FORMULA 'abs(($1)-($2))';

ln = FORMULA 'ln($1)';
exp = FORMULA 'exp($1)';

divideInteger(a,b) = INTEGER(a) / INTEGER(b);
divideIntegerNeg = FORMULA INTEGER 'CASE WHEN CAST(($1) AS integer)<0 THEN -CAST(((-CAST(($1) as integer)-1)/CAST(($2) as integer)) as integer) ELSE CAST(CAST(($1) as integer)/CAST(($2) as integer) as integer) END';
divideIntegerRnd = FORMULA INTEGER 'CAST(round(($1)/($2),0) as integer)';

mod = FORMULA 'mod($1,$2)';

iterate(INTEGER i, INTEGER from, INTEGER to) = RECURSION i==from AND from IS INTEGER AND to IS INTEGER STEP i==$i+1 AND i<=to CYCLES IMPOSSIBLE;
count(INTEGER i, INTEGER count) = iterate(i, 1, count);

toChar (object, format) = FORMULA TEXT 'to_char(($1),($2))'; 
toInteger (i) = FORMULA INTEGER PG 'convert_to_integer($1)', MS 'dbo.convert_to_integer($1)';
toNumeric (i) = FORMULA NUMERIC[38,19] PG 'convert_to_numeric($1)', MS 'dbo.convert_to_integer($1)';
toNumericNull (i) = FORMULA NUMERIC[38,19] PG 'convert_to_numeric_null($1)', MS 'dbo.convert_to_integer($1)';

// ---------------------------------------Побитовые операции --------------------------------- //
bitwiseAnd = FORMULA INTEGER '$1 & $2';
bitwiseOr = FORMULA INTEGER '$1 | $2';
bitwiseNot = FORMULA INTEGER '~ $1';

// --------------------------------------------- Округления --------------------------------- //
roundM1 (number) = round(number, -1);
round0 (number) = round(number, 0);
round1 (number) = round(number, 1);
round2 (number) = round(number, 2);
round3 (number) = round(number, 3);
round4 (number) = round(number, 4);
round5 (number) = round(number, 5);
round6 (number) = round(number, 6);

// ---------------------------------- Работа с датами  ---------------------------------- //
resultMilliseconds '{utils.result.milliseconds}' = DATA LOCAL LONG();
toMilliseconds '{utils.to.milliseconds}' INTERNAL 'lsfusion.server.logics.classes.data.utils.time.ToMillisecondsAction' (DATETIME);

// -------------------------------------------- Свойства под файлы ---------------------------- //

file = DATA LOCAL FILE ();
wordFile = DATA LOCAL WORDFILE ();
imageFile = DATA LOCAL IMAGEFILE ();
pdfFile = DATA LOCAL PDFFILE ();
rawFile = DATA LOCAL RAWFILE ();
excelFile = DATA LOCAL EXCELFILE();
textFile = DATA LOCAL TEXTFILE();
csvFile = DATA LOCAL CSVFILE();
htmlFile = DATA LOCAL HTMLFILE();
jsonFile = DATA LOCAL JSONFILE();
xmlFile = DATA LOCAL XMLFILE();
tableFile = DATA LOCAL TABLEFILE();

// --------------------------------------------- Yes/No --------------------------------- //
CLASS YesNo '{utils.yes.no}' {
    yes '{utils.yes.no.yes}',
    no '{utils.yes.no.no}'
}
TABLE yesNo(YesNo);

META defineHistorizableYesNo(property, defaultProperty, caption, object1, object1Identity, object2, object2Identity, group)
    @defineHistorizableCustomDefault(property, defaultProperty, caption, YesNo, staticCaption,
        object1, object1Identity, object2, object2Identity, 7, group);
END

META defineHistorizableYesNo(property, defaultProperty, caption, object1, object1Identity, object2, object2Identity, object3, object3Identity, group)
    @defineHistorizableCustomDefault(property, defaultProperty, caption, YesNo, staticCaption,
        object1, object1Identity, object2, object2Identity, object3, object3Identity, 7, group);
END

// --------------------------------------------- Диалоги --------------------------------- //

FORM dialogString '{utils.form.dialog.string}'
    OBJECTS s = STRING[200] PANEL
    PROPERTIES(s) objValue = VALUE
;

DESIGN dialogString {
    NEW string BEFORE TOOLBARBOX {
        fill = 1;
        MOVE PROPERTY (objValue) {
            fill = 1;
            caption = '{utils.caption.enter.string}';
            font = 'bold 64';
            panelCaptionVertical = TRUE;
        }
    }
}

FORM dialogDate '{utils.form.dialog.date}'
    OBJECTS d = DATE PANEL
    PROPERTIES(d) objValue = VALUE
;

DESIGN dialogDate {
    NEW date BEFORE TOOLBARBOX {
        fill = 1;
        MOVE PROPERTY (objValue) {
            fill = 1;
            caption = '{utils.caption.enter.date}';
            font = 'bold 64';
            panelCaptionVertical = TRUE;
        }
    }

}

FORM dialogInteger '{utils.form.dialog.integer}'
    OBJECTS i = INTEGER PANEL
    PROPERTIES(i) objValue = VALUE
;

DESIGN dialogInteger {
    PROPERTY (objValue) {
        caption = '{utils.caption.enter.integer}';
        font = 'bold 64';
        panelCaptionVertical = TRUE;
    }
}

FORM dialogNumeric '{utils.form.dialog.numeric}'
    OBJECTS n = NUMERIC[20,7] PANEL
    PROPERTIES(n) objValue = VALUE
;

DESIGN dialogNumeric {
    PROPERTY (objValue) {
        caption = '{utils.caption.enter.numeric}';
        font = 'bold 64';
        panelCaptionVertical = TRUE;
    }
}

// --------------------------------------------- Печать --------------------------------- //

FORM printBlankPagePortrait;

FORM printBlankPageLandscape;

// --------------------------- Ссылки на файлы через FILELINK --------------------------- //

META defineFileLink (caption, fileType, property, class1)
    fileLink caption (class1 param1) = fileType (webServerUrl() + '/readFile?sid=' + [ REFLECTION CANONICALNAME property[class1]]() + '&p=' + param1) IF property(param1); 
END