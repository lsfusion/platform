MODULE Authentication;

REQUIRE Time, Utils;

CLASS DesignEnv;
name '{authentication.name}'= ABSTRACT ISTRING(DesignEnv);

// ------------------------- Computers ---------------------- //

CLASS Computer '{authentication.computer}' : DesignEnv;
TABLE computer (Computer);

hostname '{authentication.host.name.computer}' = DATA ISTRING[100] (Computer) CHARWIDTH 10 IN id;
name(Computer c) += hostname(c);
computer = GROUP LAST Computer computer ORDER computer BY hostname(computer);

FORM computer '{authentication.computer}'
    OBJECTS c = Computer PANEL
    PROPERTIES(c) hostname
    
    EDIT Computer OBJECT c
;

FORM computers '{authentication.computers}'
    OBJECTS c = Computer
    PROPERTIES(c) READONLY hostname
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE GRID
;

FORM computersDialog '{authentication.computers}'
    OBJECTS c = Computer
    
    PROPERTIES(c) READONLY hostname 
    
    LIST Computer OBJECT c     
;
DESIGN computersDialog { BOX { size = (1024, 768); }}

currentComputer = NATIVE Computer ();
hostnameCurrentComputer () = hostname(currentComputer());

EXTEND FORM computers
    FILTERGROUP current FILTER '{logics.property.current.computer}' c = currentComputer()
; 

// -------------------------- Users ---------------------------- //

// Contact
CLASS ABSTRACT Contact '{authentication.contact}';
TABLE contact (Contact);

firstName '{authentication.first.name.contact}' = DATA ISTRING[100] (Contact) CHARWIDTH 10 IN id;
lastName '{authentication.last.name.contact}' = DATA ISTRING[100] (Contact) CHARWIDTH 15 IN id;
name '{authentication.name.contact}' (Contact contact) = CONCAT ' ', firstName(contact), lastName(contact) IN id CHARWIDTH 20;

phone '{authentication.phone.contact}' = DATA STRING[100] (Contact) CHARWIDTH 10;
postAddress '{authentication.post.address.contact}' = DATA STRING[150] (Contact) CHARWIDTH 20;
birthday '{authentication.birthday.contact}' = DATA DATE (Contact);

email '{authentication.email.contact}'= DATA STRING[400] (Contact) CHARWIDTH 20 REGEXP '[-!#-\'*+/-9=?A-Z^-~]+(\\.[-!#-\'*+/-9=?A-Z^-~]+)*@[0-9A-Za-z]([0-9A-Za-z-]{0,61}[0-9A-Za-z])?(\\.[0-9A-Za-z]([0-9A-Za-z-]{0,61}[0-9A-Za-z])?)+' '<html>{authentication.email.contact.incorrect}</html>';
contact (email) = GROUP MAX Contact contact BY email(contact);

// User

CLASS ABSTRACT User '{authentication.user}' : DesignEnv;
TABLE user (User);

CLASS SystemUser '{authentication.system.user}' : User;

CLASS CustomUser '{authentication.custom.user}' : User, Contact;
TABLE customUser (CustomUser);

name '{authentication.name.user}' (User user) = IF user IS SystemUser 
                        THEN STRING[100]('{authentication.system.user}')
                        ELSE name[Contact](user) CHARWIDTH 10;
name '{authentication.name.user}' (CustomUser user) = name[User](user AS CustomUser) CHARWIDTH 10;

// login
login '{authentication.login}' = DATA ISTRING[100] (CustomUser) CHARWIDTH 15;
name(CustomUser c) += login(c);
customUserLower (login) = GROUP AGGR CustomUser customUser WHERE customUser IS CustomUser BY ISTRING[100](lower(login(customUser)));
customUserNormalized (ISTRING[100] login) = customUserLower(ISTRING[100](lower(login)));
logName (CustomUser user) = OVERRIDE name(user), login(user);

//backward compatibility
customUser (login) = GROUP AGGR CustomUser customUser WHERE customUser IS CustomUser BY login(customUser);
upcaseLogin (CustomUser customUser) = upper(login(customUser));
customUserUpcase (login) = GROUP MAX CustomUser customUser BY upcaseLogin(customUser);

//migration 4 -> 5 version
intersectingLoginsCount '{authentication.intersecting.logins}' (CustomUser user) = GROUP SUM 1 IF upper(login(CustomUser u)) == upper(login(user));
intersectingLoginsCount () = GROUP SUM 1 IF intersectingLoginsCount(CustomUser u) > 1;
FORM intersectingLogins '{authentication.intersecting.logins}'
OBJECTS c = CustomUser
PROPERTIES(c) login, intersectingLoginsCount
FILTERS intersectingLoginsCount(c) > 1 
;

showIntersectingLogins '{authentication.intersecting.logins}' ()  {
    SHOW intersectingLogins;
}

EXTEND FORM migrationData 
    PROPERTIES showIntersectingLogins()
;

DESIGN migrationData {
    commons {
        MOVE PROPERTY(showIntersectingLogins());
    }
}

// password
sha256Password '{authentication.password}' = DATA STRING[100] (CustomUser) ECHO;
calculatedHash = DATA LOCAL STRING[100] ();
calculateBase64Hash INTERNAL  'lsfusion.server.physics.admin.authentication.action.CalculateBase64HashAction' (BPSTRING[10], BPSTRING[30]);
changeSHA256Password(CustomUser customUser)  { 
    INPUT st=BPSTRING[30] DO {
        calculateBase64Hash('SHA-256', st);
        sha256Password(customUser) <- calculatedHash();
    }
}
setSHA256Password(CustomUser customUser, STRING str)  { 
    calculateBase64Hash('SHA-256', str);
    sha256Password(customUser) <- calculatedHash();
}
passwordResetToken = DATA STRING (CustomUser);
expiryPasswordResetTokenDate = DATA DATETIME (CustomUser);
userByPassResetToken(STRING str) = GROUP NAGGR CustomUser cu BY passwordResetToken(cu) IF cu IS CustomUser;

// tokens
currentAuthToken = NATIVE TEXT();    
secret = DATA TEXT();
getAuthToken() {
    exportText() <- currentAuthToken();
} @@api;

resultAuthToken = DATA LOCAL TEXT();
generateAuthToken INTERNAL 'lsfusion.server.physics.admin.authentication.action.GenerateAuthTokenAction' (STRING);

// LDAP
useLDAP '{authentication.use.ldap}' = DATA BOOLEAN ();
serverLDAP '{authentication.server.ldap}' = DATA STRING[100] ();
portLDAP '{authentication.port.ldap}' = DATA INTEGER ();
baseDNLDAP '{authentication.base.dn.ldap}' = DATA STRING[100] ();
userDNSuffixLDAP '{authentication.user.dn.suffix.ldap}' = DATA STRING[100] ();

// OAuth and Web-client authentication
CLASS OAuth2;
id 'Id' = DATA STRING (OAuth2);
clientId 'Client id' = DATA STRING (OAuth2);
clientSecret 'Client secret' = DATA STRING (OAuth2);
clientAuthenticationMethod 'Client authentication method' = DATA STRING (OAuth2);
scope 'scope' = DATA STRING (OAuth2);
authorizationUri 'Authorization URI' = DATA STRING (OAuth2);
tokenUri 'Token URI' = DATA STRING (OAuth2);
jwkSetUri 'Jwk set URI' = DATA STRING (OAuth2);
userInfoUri 'User info URI' = DATA STRING (OAuth2);
userNameAttributeName 'Username attribute name' = DATA STRING (OAuth2);
clientName 'Client name' = DATA STRING (OAuth2);

auth (id) = GROUP AGGR OAuth2 oa BY id(oa);
clientName(STRING str) = clientName(auth(str));

webClientSecretKey 'Web-client secret key' = DATA STRING (); 

writeDefaultCredentials() {
    IF NOT clientName('github') THEN {
        NEW oa = OAuth2{
            id(oa) <- 'github';
            clientAuthenticationMethod(oa) <- 'basic';
            scope(oa) <- 'read:user';
            authorizationUri(oa) <- 'https://github.com/login/oauth/authorize';
            tokenUri(oa) <- 'https://github.com/login/oauth/access_token';
            userInfoUri(oa) <- 'https://api.github.com/user';
            userNameAttributeName(oa) <- 'node_id';
            clientName(oa) <- 'GitHub';
        }
    }
    IF NOT clientName('google') THEN {
        NEW oa = OAuth2{
            id(oa) <- 'google';
            clientAuthenticationMethod(oa) <- 'basic';
            scope(oa) <- 'openid profile email';
            authorizationUri(oa) <- 'https://accounts.google.com/o/oauth2/v2/auth';
            tokenUri(oa) <- 'https://www.googleapis.com/oauth2/v4/token';
            jwkSetUri(oa) <- 'https://www.googleapis.com/oauth2/v3/certs';
            userInfoUri(oa) <- 'https://www.googleapis.com/oauth2/v3/userinfo';
            userNameAttributeName(oa) <- 'sub';
            clientName(oa) <- 'Google';
        }
    }
    IF NOT clientName('facebook') THEN {
        NEW oa = OAuth2{
            id(oa) <- 'facebook';
            clientAuthenticationMethod(oa) <- 'post';
            scope(oa) <- 'public_profile email';
            authorizationUri(oa) <- 'https://www.facebook.com/v2.8/dialog/oauth';
            tokenUri(oa) <- 'https://graph.facebook.com/v2.8/oauth/access_token';
            userInfoUri(oa) <- 'https://graph.facebook.com/me?fields=id,name,email';
            userNameAttributeName(oa) <- 'id';
            clientName(oa) <- 'Facebook';
        }
    }
    IF NOT clientName('yandex') THEN {
        NEW oa = OAuth2{
            id(oa) <- 'yandex';
            clientAuthenticationMethod(oa) <- 'basic';
            scope(oa) <- 'login:info login:email';
            authorizationUri(oa) <- 'https://oauth.yandex.ru/authorize';
            tokenUri(oa) <- 'https://oauth.yandex.ru/token';
            userInfoUri(oa) <- 'https://login.yandex.ru/info';
            userNameAttributeName(oa) <- 'id';
            clientName(oa) <- 'Yandex';
        }
    }
    IF NOT webClientSecretKey() THEN {
            webClientSecretKey() <- 'secretKey';
    }
    APPLY;
}

getClientCredentials(STRING str) {
    IF str != webClientSecretKey() THEN {
        EXPORT FROM error = STRING s WHERE s = '{authentication.oauth2.incorrect.web.client.auth.token}';
    } ELSE {
        EXPORT FROM id(OAuth2 oa),
            clientId(oa),
            clientSecret(oa),
            clientAuthenticationMethod(oa),
            scope(oa),
            authorizationUri(oa),
            tokenUri(oa),
            jwkSetUri(oa),
            userInfoUri(oa),
            userNameAttributeName(oa),
            clientName(oa) WHERE clientId(oa) AND clientSecret(oa) AND oa IS OAuth2;
    }
} @@api @@noauth;

onUserRegister ABSTRACT LIST (User);

checkPasswordStrength ABSTRACT LIST (STRING);
passwordStrengthError = DATA LOCAL STRING ();

registerUser(JSONFILE user) {
    IMPORT FROM user FIELDS STRING[100] login, STRING[100] password, STRING[100] firstName, STRING[100] lastName, STRING[400] email DO {
        checkPasswordStrength(password);
        IF customUserNormalized(login) THEN {
            EXPORT FROM error = '{registration.not.successful.login.exist}';
        } ELSE IF passwordStrengthError() THEN {
            EXPORT FROM error = passwordStrengthError();
        } ELSE {
            NEW cu = CustomUser { 
                login(cu) <- login;
                setSHA256Password(cu, password);
                firstName(cu) <- firstName;
                lastName(cu) <- lastName;
                email(cu) <- email;
                onUserRegister(cu);
                APPLY;
                EXPORT FROM success = '{registration.successful}';
            }
        } 
    }
} @@api @@noauth;

resetPassword(JSONFILE input) {
    IMPORT FROM input FIELDS STRING userNameOrEmail DO {
        LOCAL token = STRING();
        LOCAL NESTED success = BOOLEAN();
        LOCAL NESTED subject = STRING();
        LOCAL NESTED body = STRING();
        LOCAL NESTED email = STRING();
        TRY {
            FOR email(CustomUser cu) == userNameOrEmail OR login(cu) == userNameOrEmail DO {
                token() <- randomUUID();
                passwordResetToken(cu) <- token();
                expiryPasswordResetTokenDate(cu) <- currentDateTime();
                FOR STRING href = CONCAT '', webPath(), '/change-password', '?token=', token(), IF query() != '' THEN (CONCAT '', '&', query()) DO {
                    subject() <- '$I{resetPasswordSubject.html}';
                    body() <- '$I{resetPasswordBody.html}';    
                }
                APPLY;
                IF NOT canceled() THEN {
                    EMAIL 
                        SUBJECT subject()
                        TO email(cu)
                        BODY body();
                    success() <- TRUE;
                }
                email() <- email(cu);
            }
            IF success() THEN {
                EXPORT FROM success = '{password.reset.email.sent}', email = email();  
            } ELSE {
                EXPORT FROM error = '{password.reset.error}';
            }
        } CATCH {
            EXPORT FROM error = messageCaughtException();
        }
    }
} @@api @@noauth;

changePassword(JSONFILE input) {
    IMPORT FROM input FIELDS STRING token, STRING newPassword DO {
        LOCAL NESTED success = BOOLEAN();
        FOR CustomUser cu = userByPassResetToken(token) AND cu IS CustomUser DO {
            checkPasswordStrength(newPassword);
            IF passwordStrengthError() THEN {
                EXPORT FROM error = passwordStrengthError(), passwordStrengthError = TRUE;
                RETURN;
            }
        
            setSHA256Password(cu, newPassword);
            passwordResetToken(cu) <- NULL;
            expiryPasswordResetTokenDate(cu) <- NULL;
            APPLY;
            IF NOT canceled() THEN {
                success() <- TRUE;
            }
        }
        IF success() THEN {
            EXPORT FROM success = '{password.changed}';
        } ELSE {
            EXPORT FROM error = '{password.reset.token.invalid}';
        }
    }
} @@api @@noauth;

clearPassResetToken ABSTRACT LIST ( ) ;
clearPassResetToken () + { 
	NEWSESSION {
	    FOR expiryPasswordResetTokenDate(CustomUser cu) <= subtractSeconds(currentDateTime(), 3600) DO {
	        expiryPasswordResetTokenDate(cu) <- NULL;
	        passwordResetToken(cu) <- NULL;
	    }
        APPLY;
	}
}

// lock
isLocked '{authentication.is.locked.user}' = DATA BOOLEAN (CustomUser);
isLockedLogin (login) = isLocked(customUserNormalized(lower(login)));

// Current User
currentUser = NATIVE User ();
currentUserName '{authentication.current.user.name}' () = name(currentUser());

// User management
logOut (restart, reconnect)  INTERNAL  'lsfusion.server.physics.admin.authentication.action.LogOutAction' (BOOLEAN, BOOLEAN);
shutdown { logOut(NULL, NULL); }
reconnect { logOut(TRUE, TRUE); }
restart { logOut(TRUE, NULL); }

// Locale settings
userLanguage '{authentication.user.language.user}' = DATA STRING[3] (CustomUser) CHARWIDTH 5;
userCountry '{authentication.user.country.user}' = DATA STRING[3] (CustomUser) CHARWIDTH 5;
userTimeZone '{authentication.user.time.zone.user}' = DATA STRING[30] (CustomUser);
userTwoDigitYearStart '{authentication.user.two.digit.year.start.user}' = DATA INTEGER (CustomUser);
userDateFormat '{authentication.user.date.format.user}' = DATA STRING (CustomUser);
userTimeFormat '{authentication.user.time.format.user}' = DATA STRING (CustomUser);

clientLanguage '{authentication.client.language.user}' = DATA STRING[3] (CustomUser) CHARWIDTH 5;
clientCountry '{authentication.client.country.user}' = DATA STRING[3] (CustomUser) CHARWIDTH 5;
clientTimeZone '{authentication.client.time.zone.user}' = DATA STRING[30] (CustomUser);
useClientLocale '{authentication.use.client.locale.user}' = DATA BOOLEAN (CustomUser) CHARWIDTH 12;

clientDateFormat '{authentication.client.date.format.user}' = DATA STRING (CustomUser);
clientTimeFormat '{authentication.client.time.format.user}' = DATA STRING (CustomUser);
useClientDateTimeFormat '{authentication.use.client.date.time.format.user}' = DATA BOOLEAN (CustomUser);

defaultUserLanguage '{authentication.default.user.language}' = DATA STRING[3] ();
defaultUserCountry '{authentication.default.user.country}' = DATA STRING[3] ();
defaultUserTimezone '{authentication.default.user.time.zone}' = DATA STRING[30] ();
defaultUserTwoDigitYearStart '{authentication.default.user.two.digit.year.start}' = DATA INTEGER ();
defaultUserDateFormat '{authentication.default.user.date.format}' = DATA STRING ();
defaultUserTimeFormat '{authentication.default.user.time.format}' = DATA STRING ();

serverLanguage '{authentication.server.language}' = DATA STRING[3] ();
serverCountry '{authentication.server.country}' = DATA STRING[3] ();
serverTimezone '{authentication.server.time.zone}' = DATA STRING[30] ();
serverTwoDigitYearStart '{authentication.server.two.digit.year.start}' = DATA INTEGER ();
serverDateFormat '{authentication.server.date.format}' = DATA STRING ();
serverTimeFormat '{authentication.server.time.format}' = DATA STRING ();

language '{authentication.language.user}' (CustomUser user) = OVERRIDE (clientLanguage(user) IF useClientLocale(user)), userLanguage(user), defaultUserLanguage(), serverLanguage();
country '{authentication.country.user}' (CustomUser user) = IF useClientLocale(user) AND clientLanguage(user) 
                                                                THEN clientCountry(user)
                                                                ELSE IF userLanguage(user) THEN userCountry(user) ELSE IF defaultUserLanguage() THEN defaultUserCountry() ELSE serverCountry();

dateFormat (CustomUser user) = OVERRIDE (clientDateFormat(user) IF useClientDateTimeFormat(user)), userDateFormat(user), defaultUserDateFormat(), serverDateFormat();
timeFormat (CustomUser user) = OVERRIDE (clientTimeFormat(user) IF useClientDateTimeFormat(user)), userTimeFormat(user), defaultUserTimeFormat(), serverTimeFormat();

lsfParams(STRING s) += country(currentUser()) IF s = 'googleMapAutocompleteCountry';
lsfParams(STRING s) += language(currentUser()) IF s = 'googleGeocodingLanguage';

timeZone '{authentication.time.zone.user}' (CustomUser user) = OVERRIDE (clientTimeZone(user) IF useClientLocale(user)), userTimeZone(user), defaultUserTimezone(), serverTimezone();
twoDigitYearStart '{authentication.two.digit.year.start.user}' (CustomUser user) = OVERRIDE userTwoDigitYearStart(user), defaultUserTwoDigitYearStart(), serverTwoDigitYearStart();

// "duplicate" of loadLocalePreferences, but web-client needs this data just right after authentication
getCurrentUserLocale() {
    FOR CustomUser user AS CustomUser = currentUser() DO
        EXPORT JSON FROM language(user), country(user); 
} @@api;


// GUI settings -- should be moved to Service (but needs migration)

fontSize '{authentication.font.size.user}' = DATA INTEGER (CustomUser); 

storeNavigatorSettingsForComputer '{system.events.navigator.store.settings.for.computer}' = DATA BOOLEAN ();
WHEN CHANGED(storeNavigatorSettingsForComputer()) DO {
    reload();
}

currentDesignEnv = IF storeNavigatorSettingsForComputer() THEN currentComputer() ELSE currentUser();

CLASS ColorTheme '{authentication.color.theme}' {
    light '{authentication.color.theme.light}',
    dark '{authentication.color.theme.dark}'
}
TABLE colorTheme(ColorTheme);
designEnvColorTheme '{authentication.color.theme}' (DesignEnv) = DATA ColorTheme (DesignEnv);
currentColorTheme = designEnvColorTheme(currentDesignEnv());
clientColorTheme '{authentication.color.theme}' = DATA ColorTheme (DesignEnv);
colorTheme '{authentication.color.theme}' (DesignEnv designEnv) = OVERRIDE designEnvColorTheme(designEnv), clientColorTheme(designEnv);
name '{authentication.color.theme}' (ColorTheme theme) = staticCaption(theme) IF theme IS ColorTheme;
colorThemeName '{authentication.color.theme}' (DesignEnv designEnv) = name(designEnvColorTheme(designEnv));

isDarkTheme() = colorTheme(currentDesignEnv()) == ColorTheme.dark;

changeColorThemeAction INTERNAL 'lsfusion.server.logics.navigator.ChangeColorThemeAction'(ColorTheme) NULL;
WHEN CHANGED(designEnvColorTheme(DesignEnv designEnv)) DO {
    changeColorThemeAction(colorTheme(designEnv));    
}

FORM dialogColorThemes '{authentication.color.themes}'
    OBJECTS o = ColorTheme
    PROPERTIES(o) READONLY name
    
    LIST ColorTheme OBJECT o
;

// -------------------------- Forms ---------------------------- //

// sync user logins saved on client 
syncUsers ABSTRACT LIST (ISTRING[100], JSONFILE) @@noauth;
syncUsers (ISTRING[100] computer, JSONFILE users) + { 
    LOCAL syncSavedUser = BPSTRING[100](INTEGER);
    IMPORT FROM users TO syncSavedUser=value;
    EXPORT FROM value=STRING s WHERE s = syncSavedUser(INTEGER i) AND customUserNormalized(s) AND NOT isLockedLogin(s);
}

FORM customUser '{authentication.custom.user}'
    OBJECTS u=CustomUser PANEL
    PROPERTIES(u) login, sha256Password ON CHANGE changeSHA256Password(u), isLocked ,
                  firstName, lastName, email, phone, postAddress, birthday,
                  useClientLocale, userLanguage SHOWIF NOT useClientLocale(u), userCountry SHOWIF NOT useClientLocale(u), 
                  userTimeZone, userTwoDigitYearStart,
                  useClientDateTimeFormat, userDateFormat SHOWIF NOT useClientDateTimeFormat(u), userTimeFormat SHOWIF NOT useClientDateTimeFormat(u)

    EDIT CustomUser OBJECT u
;

DESIGN customUser {
    OBJECTS {
        NEW pane {
            horizontal = TRUE;
            fill = 1;
            NEW header {
                fill = 1;
                NEW login {
                    alignment = STRETCH;
                    caption = '{authentication.custom.user.login}';
                    MOVE PROPERTY(login(u));    
                    MOVE PROPERTY(sha256Password(u));
                    MOVE PROPERTY(isLocked(u));    
                }
                NEW info {
                    alignment = STRETCH;
                    caption = '{authentication.custom.user.info}';
                    MOVE PROPERTY(firstName(u));
                    MOVE PROPERTY(lastName(u));
                    MOVE PROPERTY(email(u));
                    MOVE PROPERTY(phone(u));
                    MOVE PROPERTY(postAddress(u));
                    MOVE PROPERTY(birthday(u));
                }
                NEW locale {
                    alignment = STRETCH;
                    caption = '{authentication.custom.user.locale}';
                    MOVE PROPERTY(useClientLocale(u));
                    MOVE PROPERTY(userLanguage(u));
                    MOVE PROPERTY(userCountry(u));
                    MOVE PROPERTY(userTimeZone(u));
                    MOVE PROPERTY(userTwoDigitYearStart(u));
                    MOVE PROPERTY(useClientDateTimeFormat(u));
                    MOVE PROPERTY(userDateFormat(u));
                    MOVE PROPERTY(userTimeFormat(u));
                }
                NEW security {
                    alignment = STRETCH;
                    caption = '{authentication.custom.user.security}';
                }
                MOVE BOX(u);
            }
            NEW details {
                tabbed = TRUE;
                fill = 3;
            }
        }
    }
    NEW container BEFORE TOOLBARBOX {
        horizontal = TRUE;
        fill = 1;
        MOVE BOX(u) {
            flex = 0.3;
        };
        GROUP(,u) {
            lines = 1;
        }
    }
}

FORM customUsers '{authentication.users}'
    OBJECTS u = CustomUser

    PROPERTIES(u) READONLYIF isReadonly() name, login, email
    PROPERTIES(u) READONLYIF isReadonly() isLocked

    PROPERTIES(u) NEWSESSION NEW, EDIT, DELETE 

    OBJECTS lu = CustomUser
    PROPERTIES(lu) READONLY                name, login, email
    PROPERTIES(lu) READONLYIF isReadonly() userLanguage BACKGROUND RGB(232,232,255), 
                                           userCountry BACKGROUND RGB(232,232,255), 
                                           useClientLocale, clientLanguage READONLY, clientCountry READONLY,
                                           userTimeZone BACKGROUND RGB(232,232,255), userTwoDigitYearStart BACKGROUND RGB(232,232,255),
                                           userDateFormat BACKGROUND RGB(232,232,255), 
                                           userTimeFormat BACKGROUND RGB(232,232,255), 
                                           useClientDateTimeFormat, clientDateFormat READONLY, clientTimeFormat READONLY 
    PROPERTIES(lu) NEWSESSION EDIT

    PROPERTIES() READONLY defaultUserLanguage, defaultUserCountry, defaultUserTimezone, defaultUserTwoDigitYearStart, defaultUserDateFormat, defaultUserTimeFormat
    PROPERTIES() READONLY serverLanguage, serverCountry, serverTimezone, serverTwoDigitYearStart, serverDateFormat, serverTimeFormat

    PROPERTIES() useLDAP, serverLDAP, portLDAP, baseDNLDAP, userDNSuffixLDAP
    
    OBJECTS auth = OAuth2 
    PROPERTIES(auth)  NEW, DELETE,
                      id,
                      clientId,
                      clientSecret,
                      clientAuthenticationMethod,
                      scope,
                      authorizationUri,
                      tokenUri,
                      jwkSetUri,
                      userInfoUri,
                      userNameAttributeName,
                      clientName
    PROPERTIES () webClientSecretKey
;

DESIGN customUsers {
    OBJECTS {
        NEW pane {
            tabbed = TRUE;
            fill = 1;
            NEW users {
                caption = '{authentication.users}';
                MOVE BOX(u);
            }
            NEW locale {
                caption = '{authentication.locale}';
                NEW headerLocale {
                    horizontal = TRUE;
                    MOVE PROPERTY(defaultUserLanguage());
                    MOVE PROPERTY(defaultUserCountry());
                    MOVE PROPERTY(defaultUserTimezone());
                    MOVE PROPERTY(defaultUserTwoDigitYearStart());
                    MOVE PROPERTY(defaultUserDateFormat());
                    MOVE PROPERTY(defaultUserTimeFormat());
                    MOVE PROPERTY(serverLanguage());
                    MOVE PROPERTY(serverCountry());
                    MOVE PROPERTY(serverTimezone());
                    MOVE PROPERTY(serverTwoDigitYearStart());
                    MOVE PROPERTY(serverDateFormat());
                    MOVE PROPERTY(serverTimeFormat());
                }
                MOVE BOX(lu) {
                    GRID(lu) { headerHeight = 50; }
                }
            }
            NEW LDAP {
                caption = 'LDAP';
                MOVE PROPERTY(useLDAP());
                MOVE PROPERTY(serverLDAP());
                MOVE PROPERTY(portLDAP());
                MOVE PROPERTY(baseDNLDAP());
                MOVE PROPERTY(userDNSuffixLDAP());
            } 
            NEW OAUTH2 {
                caption = 'OAUTH2';
                MOVE BOX (auth){
                    GRID (auth){
                    headerHeight = 50;
                    }
                }
            }
            NEW WEBAUTH {
                caption = 'WEB-AUTH';
                MOVE PROPERTY(webClientSecretKey());
            }
        }
    }
}

@extendFormEditable(customUsers);

FORM customUsersDialog '{authentication.users}'
    OBJECTS u=CustomUser
    PROPERTIES(u) READONLY name, login, email

    LIST CustomUser OBJECT u
;

DESIGN customUsersDialog { BOX { size = (1024, 768); } }

FORM editProfile '{navigator.account.edit.profile}'
    OBJECTS u = CustomUser PANEL
    PROPERTIES(u) login READONLY
    PROPERTIES(u) firstName, lastName, email, userLanguage

    FILTERS u = currentUser()
;

DESIGN editProfile {
    OBJECTS {
        NEW commons {
            caption = '{navigator.account.edit.profile.commons}';
            MOVE PROPERTY(login(u)) { fontSize = 36; }
            MOVE PROPERTY(firstName(u)) { fontSize = 36; }
            MOVE PROPERTY(lastName(u)) { fontSize = 36; }
            MOVE PROPERTY(email(u)) { fontSize = 36; }
            MOVE PROPERTY(userLanguage(u)) { fontSize = 36; }
        }
    }
    
}

// ---------------- Relogin ------------------- //

userChanged() = DATA LOCAL BOOLEAN();

// change by login and password
FORM reloginLoginUser '{authentication.relogin}'
    OBJECTS data = (login = BPSTRING[100], password = BPSTRING[30])  PANEL
    PROPERTIES valueLogin = VALUE(login), valuePassword = VALUE(password)
;

DESIGN reloginLoginUser {
    BOX(data) {
        caption = '';
        GROUP(, data) {
            lines = 1;
            PROPERTY(valueLogin){
                caption = '{authentication.login}';
                fontSize = 36;
            }
            PROPERTY(valuePassword){
                caption = '{authentication.password}';
                echoSymbols = TRUE;
                fontSize = 36;
            }
        }
    }
    REMOVE TOOLBARLEFT;
    REMOVE PROPERTY(formRefresh());
}

relogin '{authentication.relogin}'  INTERNAL  'lsfusion.server.physics.admin.authentication.action.ReloginUserAction' (CustomUser);

reloginLoginUser()  { 
    DIALOG reloginLoginUser OBJECTS login INPUT, password INPUT DO {
        LOCAL user = CustomUser();
        user() <- customUserNormalized(login);
        IF user() THEN {
            IF isLocked(user()) THEN {
                MESSAGE '{authentication.relogin.user.locked}';
            } ELSE {
                calculateBase64Hash('SHA-256', password);
                IF sha256Password(user()) == calculatedHash() THEN {
                    relogin(user());
                    IF userChanged() THEN {
                        MESSAGE '{authentication.relogin.user.changed}';
                    }
                } ELSE {
                    MESSAGE '{authentication.relogin.user.incorrect.password}';
                }
            }
        } ELSE {
            MESSAGE '{authentication.relogin.user.not.found}';
        }
    }
    APPLY;
}

// Change by password
FORM reloginPasswordUser '{authentication.relogin}'
    OBJECTS password = BPSTRING[30] PANEL
    PROPERTIES valuePassword = VALUE(password)
;

DESIGN reloginPasswordUser {
    BOX(password) {
        caption = '';
        PROPERTY(valuePassword){
            caption = '{authentication.password}';
            fontSize = 36;
        }
    }
    REMOVE TOOLBARLEFT;
    REMOVE PROPERTY(formRefresh());
}

reloginPassword(CustomUser customUser)  { 
    DIALOG reloginPasswordUser OBJECTS password INPUT DO {
        IF isLocked(customUser) THEN {
            MESSAGE '{authentication.relogin.user.locked}';
        }  ELSE {
                calculateBase64Hash('SHA-256', password);
                IF sha256Password(customUser)==calculatedHash() THEN {
                    relogin(customUser);
                    IF userChanged() THEN {
                        MESSAGE '{authentication.relogin.user.changed}';
                    }
                } ELSE {
                    MESSAGE '{authentication.relogin.user.incorrect.password}';
                }
        }
    }
}

// ---------------- Change password ------------------- //

// Strength
pwLength (STRING[30] p) = length(p);
pwNumeric (STRING[30] p) = length(regexpReplace (p, '\\D', '', 'g'));
pwNumsymbols (STRING[30] p) = length(regexpReplace(p, '[A-Z,a-z,а-я,А-Я,0-9]', '', 'g'));
pwUpper (STRING[30] p) = length(regexpReplace(p, '[^A-Z,А-Я]', '', 'g'));

passwordStrength (STRING[30] p) = min(max(min(pwLength(p), 5) * 10 (-) 20 (+) min(pwNumeric(p), 3) * 10 (+) min(pwNumsymbols(p), 3) * 15 (+) min(pwUpper(p), 3) * 10, 0), 100);

passwordColor (STRING[30] p) = CASE
    WHEN passwordStrength(p) < 70 THEN #FFC0C0
    WHEN passwordStrength(p) < 80 THEN #FFFFC0
    WHEN passwordStrength(p) < 90 THEN #F0FFF0
    ELSE #C0FFC0 IF p IS STRING
;

// Policy
GROUP policyStrengthPassword '{authentication.password.requirements}';
 
passwordMinLength '{authentication.password.minimum.length}' () = DATA INTEGER () IN policyStrengthPassword;
passwordContainsDigits '{authentication.password.must.contain.digits}' = DATA BOOLEAN () IN policyStrengthPassword;
passwordContainsSymbols '{authentication.password.must.contain.special.symbols}' = DATA BOOLEAN () IN policyStrengthPassword;
passwordContainsUpper '{authentication.password.must.contain.upcase.letters}' = DATA BOOLEAN () IN policyStrengthPassword;

weakPassword (STRING[30] p) = TEXT(
    CONCAT ';\n',
        '{authentication.password.minimum.length} - ' + passwordMinLength() + ' ' + '{authentication.password.symbols}' IF pwLength(p) < passwordMinLength(),
        '{authentication.password.must.contain.digits}' IF passwordContainsDigits() AND NOT pwNumeric(p) > 0,
        '{authentication.password.must.contain.special.symbols}' IF passwordContainsSymbols() AND NOT pwNumsymbols(p) > 0,
        '{authentication.password.must.contain.upcase.letters}' IF passwordContainsUpper() AND NOT pwUpper(p) > 0
); 

passwordRequirements '{authentication.password.requirements}' () = weakPassword('' AS STRING[30]);

check (BOOLEAN b) = IF b THEN '✔' ELSE ' ';
check '' (STRING[30] pass) = check(NOT weakPassword(pass)) CHARWIDTH 2;
check '' (STRING[30] new1, STRING[30] new2) = check(new1 = new2 AND NOT weakPassword(new1)) CHARWIDTH 2;

changePasswordOnNextLogin '{authentication.password.change.next.login}' = DATA BOOLEAN (CustomUser);

EXTEND FORM customUsers
    PROPERTIES() PANEL passwordMinLength, passwordContainsDigits, passwordContainsSymbols, passwordContainsUpper
    
    OBJECTS up = CustomUser
    PROPERTIES(up) READONLY name, login, email
    PROPERTIES(up)          changePasswordOnNextLogin
;

DESIGN customUsers {
    pane {
        NEW policyStrengthPassword {
            caption = '{authentication.passwords}';
            MOVE GROUP(policyStrengthPassword);
            MOVE BOX(up) {
                PROPERTY(changePasswordOnNextLogin(up)) { charWidth = 30; }
            }
        }
    }
}

FORM changePasswordUser '{authentication.change.password}'
    OBJECTS u=CustomUser PANEL
    PROPERTIES(u) READONLY login
    FILTERS u = currentUser()

    OBJECTS passwords = (old = BPSTRING[30], new1 = BPSTRING[30], new2 = BPSTRING[30]) PANEL
    PROPERTIES valueOld = VALUE(old), valueNew1 = VALUE(new1), valueNew2 = VALUE(new2)

    PROPERTIES () READONLY passwordRequirements SHOWIF passwordRequirements()
    PROPERTIES checkNew1 = check(new1) READONLY FOREGROUND passwordColor(new1), checkNew2 = check(new1, new2) READONLY FOREGROUND #C0FFC0
;

DESIGN changePasswordUser {
    OBJECTS {
        NEW changePassword {
            MOVE PROPERTY(login(u)) {
                fontSize = 36;
            }
            MOVE PROPERTY(valueOld){
                caption = '{authentication.change.password.old}';
                echoSymbols = TRUE;
                fontSize = 36;
            }
            MOVE PROPERTY(passwordRequirements()) FIRST {
                fill = 1;
                fontSize = 14;
            }
            NEW newPass1 {
                horizontal = TRUE;
                fill = 1;
                MOVE PROPERTY(valueNew1){
                    caption = '{authentication.change.password.new}';
                    echoSymbols = TRUE;
                    fontSize = 36;
                }
                MOVE PROPERTY (checkNew1) {
                    fontSize = 36;
                    charWidth = 2;
                    focusable = FALSE;
                }
            }
            NEW newPass2 {
                horizontal = TRUE;
                fill = 1;
                MOVE PROPERTY(valueNew2){
                    caption = '{authentication.change.password.repeat}';
                    echoSymbols = TRUE;
                    fontSize = 36;
                }
                MOVE PROPERTY (checkNew2) {
                    fontSize = 36;
                    charWidth = 2;
                    focusable = FALSE;
                }
            }
        }
    }
    REMOVE TOOLBARLEFT;
    REMOVE PROPERTY(formRefresh());
}

checkPasswordStrength(STRING password) + {
    IF weakPassword(password) THEN passwordStrengthError() <- '{authentication.password.does.not.match.requirements}' + ' \n' + weakPassword(password); 
}

changePassword (CustomUser u)  { 
    DIALOG changePasswordUser OBJECTS old INPUT, new1 INPUT, new2 INPUT DO {
        beforeCanceled() <- NULL;    
        calculateBase64Hash('SHA-256', old);
        IF calculatedHash() != sha256Password(u) THEN {
            MESSAGE '{authentication.change.password.incorrect.old}';
            RETURN;    
        }
        IF old = new1 THEN {
            MESSAGE '{authentication.password.must.be.different}';
            RETURN;    
        }
        IF weakPassword(new1) THEN {
            MESSAGE '{authentication.password.must.meet.complexity.requirements}:\n' + weakPassword(new1);
            RETURN;    
        }
        IF new1 != new2 THEN {
            MESSAGE '{authentication.change.password.do.not.match}';
            RETURN;    
        }

        APPLY {
            calculateBase64Hash('SHA-256', new1);
            sha256Password(u) <- calculatedHash();
            
            changePasswordOnNextLogin(u) <- NULL;
        }
        IF NOT canceled() THEN
            MESSAGE '{authentication.change.password.complete}';
    }
}

changePassword () { 
    changePassword(currentUser());
}

NAVIGATOR {
    system {
        NEW FOLDER security '{navigator.security}' AFTER scheduler {
            NEW customUsers;
            NEW computers;
        }
    }
    NEW FOLDER account '{navigator.account}' HEADER '{navigator.account}' IF NOT noSystemToolbarCaptions() LAST WINDOW system PARENT IMAGE 'lock.png'{
        NEW ACTION reloginLoginUserAction '{navigator.account.relogin}' = reloginLoginUser[] WINDOW toolbar PARENT IMAGE 'relogin.png';
        NEW editProfile WINDOW toolbar PARENT IMAGE 'editProfile.png';
        NEW ACTION changePasswordAction '{navigator.account.change.password}' = changePassword[] WINDOW toolbar PARENT IMAGE 'changePassword.png';
        NEW ACTION logOutAction '{navigator.account.logout}' = restart[] WINDOW toolbar PARENT IMAGE 'logout.png';
    }
}

// Notifications
deliveredNotificationAction  ABSTRACT LIST ( CustomUser);

// -------------------------- Object locks ---------------------------- //

locked = DATA User (Object);
lockResult = DATA LOCAL NESTED BOOLEAN ();

lock(Object object)  { 
	NEWSESSION {    
	    lockResult() <- NULL;
	    APPLY SERIALIZABLE {
	        IF NOT object THEN
	            RETURN;
	        IF locked(object) THEN {
	            cancel();
	        } ELSE {
	            locked(object) <- currentUser();
	            lockResult() <- TRUE;
	        }
	    }
	}
}

unlock(Object object)  { 
	NEWSESSION { 
	    APPLY locked(object) <- NULL;
	}
}

//dateTimeRanges
dateTimeRange '{date.time.picker.range.predefined.date}' (DateTimePickerRanges r) = staticCaption(r);
isDateTimeRangeSelected '{logics.property.select}' = DATA BOOLEAN (DateTimePickerRanges, CustomUser);

//dateTimeIntervalRanges
intervalRange '{date.time.picker.range.predefined.date}' (DateTimeIntervalPickerRanges r) = staticCaption(r);
isIntervalRangeSelected '{logics.property.select}' = DATA BOOLEAN (DateTimeIntervalPickerRanges, CustomUser);

selectedDateTimeRangesCount(user) = GROUP SUM 1 IF isDateTimeRangeSelected(DateTimePickerRanges r, user);
selectedIntervalRangesCount(user) = GROUP SUM 1 IF isIntervalRangeSelected(DateTimeIntervalPickerRanges ir, user);
CONSTRAINT selectedDateTimeRangesCount(CustomUser cu) > 7 OR selectedIntervalRangesCount(cu) > 7 MESSAGE '{date.time.picker.range.constraint}';

setDefaultRanges(CustomUser cu) {
    //dateTime
    isDateTimeRangeSelected(DateTimePickerRanges r, cu) <- TRUE WHERE 
    r == DateTimePickerRanges.rangeToday OR 
    r == DateTimePickerRanges.rangeYesterday OR 
    r == DateTimePickerRanges.rangeSevenDaysAgo OR 
    r == DateTimePickerRanges.rangeThirtyDaysAgo OR 
    r == DateTimePickerRanges.rangeMonthStart OR 
    r == DateTimePickerRanges.rangePreviousMonthStart OR 
    r == DateTimePickerRanges.rangeThisYearStart;
    
    //dateTime interval
    isIntervalRangeSelected(DateTimeIntervalPickerRanges r, cu) <- TRUE WHERE
    r == DateTimeIntervalPickerRanges.rangeIntervalToday OR 
    r == DateTimeIntervalPickerRanges.rangeIntervalYesterday OR 
    r == DateTimeIntervalPickerRanges.rangeLast7Days OR 
    r == DateTimeIntervalPickerRanges.rangeLast30Days OR 
    r == DateTimeIntervalPickerRanges.rangeThisMonth OR 
    r == DateTimeIntervalPickerRanges.rangePreviousMonth OR 
    r == DateTimeIntervalPickerRanges.rangeThisYear;
}

WHEN SET (CustomUser cu IS CustomUser) DO {
    setDefaultRanges(cu);
}

EXTEND FORM customUser
    OBJECTS r = DateTimePickerRanges
    PROPERTIES dateTimeRange(r) READONLY, isDateTimeRangeSelected(r, u)
    
    OBJECTS ir = DateTimeIntervalPickerRanges
    PROPERTIES intervalRange(ir) READONLY, isIntervalRangeSelected(ir, u)
;

DESIGN customUser {
    header {
        NEW predefinedRanges {
            horizontal = TRUE;
            MOVE BOX(r);
            MOVE BOX(ir);
            REMOVE TOOLBARSYSTEM(r);
            REMOVE TOOLBARSYSTEM(ir);
        }
    }
} 
