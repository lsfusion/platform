MODULE Messenger;

REQUIRE Reflection, Utils;

CLASS Messenger {}
isSynchronize = ABSTRACT CASE BOOLEAN (Messenger);
showTitle = ABSTRACT CASE BOOLEAN (Messenger);

CLASS ChatType {
    private 'Private',
    group 'Group',
    channel 'Channel'
}

CLASS Account '{messenger.account}';
TABLE account (Account);

name '{messenger.account.name}' = DATA STRING (Account);
account(LONG id) = GROUP NAGGR Account a BY LONG (a) IF a IS Account; 
messenger '{messenger.messenger}' = DATA Messenger (Account);
captionMessenger '{messenger.messenger}' (Account a) = staticCaption(messenger(a));
token '{messenger.token}' = DATA STRING (Account) NONULL CHARWIDTH 50;

CLASS Chat '{messenger.chat}';
TABLE chat (Chat);

CLASS Message '{messenger.messages}';
TABLE messages (Message);

chat = DATA Chat (Message);
message '{messenger.message}' = DATA STRING (Message);
from '{messenger.from}' = DATA STRING (Message);
dateTime '{messenger.datetime}' = DATA DATETIME (Message);
account = DATA Account (Chat);
chatType = DATA ChatType (Chat);
captionChatType '{messenger.chat.type}' (Chat c) = staticCaption(chatType(c)) CHARWIDTH 10 IN id;
id '{messenger.chat.id}' = DATA STRING (Chat) CHARWIDTH 10;
chatIdAccount = GROUP AGGR Chat c BY account(c), id(c);
name '{messenger.chat.name}' = DATA STRING (Chat) IN id;
title '{messenger.chat.title}' = DATA STRING (Chat);
newMessage '{messenger.chat.new.message}' = DATA LOCAL TEXT (Chat);

sendMessageResult = DATA LOCAL STRING();
sendMessage '{messenger.chat.send.message}' ABSTRACT CASE (Chat, STRING);

sendMessage '{messenger.chat.send.message}' (Chat chat) {
    sendMessage(chat, newMessage(chat));
    IF sendMessageResult() THEN {
        MESSAGE sendMessageResult() NOWAIT;
    }
    newMessage(chat) <- NULL;
}

synchronizeChats ABSTRACT LIST (Account);
WHEN SET (Account a IS Account) DO {
    synchronizeChats(a);
}

synchronizeChatsAction '{messenger.account.synchronize}' (Account a) {
    synchronizeChats(a);
    APPLY;
}

FORM messengers '{messenger.messengers}' AUTOREFRESH 20
    OBJECTS a = Account
    PROPERTIES(a) READONLY name, captionMessenger, token 
    PROPERTIES(a) NEWSESSION synchronizeChatsAction TOOLBAR SHOWIF isSynchronize(messenger(a)), NEW, EDIT, DELETE 
    OBJECTS c = Chat
    PROPERTIES(c) READONLY id, captionChatType, name, title SHOWIF showTitle(messenger(a))
    PROPERTIES(c) DELETE
    PROPERTIES(c) PANEL newMessage, sendMessage
    
    OBJECTS m = Message
    PROPERTIES(m) READONLY from, dateTime, message 
    FILTERS chat(m) == c 

    FILTERS account(c) == a;

FORM messenger '{messenger.messenger}'
    OBJECTS a = Account PANEL
    PROPERTIES(a) name, captionMessenger, token     
    EDIT Account OBJECT a;

DESIGN messengers {
    NEW accounts {
        fill = 1;
        MOVE BOX(a);
    }
    NEW chat {
        fill = 3;
        type = SPLITH;
        MOVE BOX(c);
        MOVE BOX(m);
    }
    MOVE PROPERTY(newMessage(c)) {
        panelCaptionVertical = TRUE;
    }
    MOVE PROPERTY(sendMessage(c));                              
    MOVE TOOLBARBOX; 
}

NAVIGATOR {
    notification {
        NEW messengers;
    }
}