MODULE RabbitMQ;

REQUIRE SystemEvents;

sendMessage 'Send message' INTERNAL 'lsfusion.server.physics.dev.integration.external.to.net.rabbitmq.SendMessageRabbitMQAction' (Channel, STRING);
startConsumer 'Start' INTERNAL 'lsfusion.server.physics.dev.integration.external.to.net.rabbitmq.StartConsumerRabbitMQAction' (Channel);
stopConsumer 'Stop' INTERNAL 'lsfusion.server.physics.dev.integration.external.to.net.rabbitmq.StopConsumerRabbitMQAction' (Channel);
//queue, message
onMessage ABSTRACT LIST (Channel, STRING);

stopConsumers INTERNAL 'lsfusion.server.physics.dev.integration.external.to.net.rabbitmq.StopConsumersRabbitMQAction' ();

CLASS Channel 'Channel';

host 'Host' = DATA STRING (Channel);
queue 'Queue name' = DATA STRING (Channel);
user 'User' = DATA STRING (Channel);
password 'Password' = DATA STRING (Channel) ECHO;
isConsumer 'Consumer' = DATA BOOLEAN (Channel);
local 'Local' = DATA BOOLEAN (Channel);
started 'Started' = DATA BOOLEAN (Channel);

stop(Channel c) {
    stopConsumer(c);
    started(c) <- NULL;
    apply();
}

start(Channel c) {
    startConsumer(c);
    started(c) <- TRUE;
    apply();
}

restartConsumers 'Restart all consumers' () {
    stopConsumers();
    FOR host(Channel c) AND queue(c) AND isConsumer(c) DO {
        startConsumer(c);
    }
}

FORM rabbitMQ
    OBJECTS c = Channel, s = STRING PANEL
    PROPERTIES(c) local, isConsumer, host, queue, user, password, 
        start GRID SHOWIF isConsumer(c) AND NOT started(c),
        stop GRID SHOWIF isConsumer(c) AND started(c),
        NEW, DELETE
    PROPERTIES restartConsumers(), VALUE(s), sendMessage(c, s)
;

NAVIGATOR {
    system {
        NEW rabbitMQ;
    }
}