MODULE RabbitMQ;

REQUIRE SystemEvents;

CLASS Channel 'Channel';

host 'Host' = DATA STRING (Channel);
vHost 'vHost' = DATA STRING (Channel);
queue 'Queue name' = DATA STRING (Channel);
isDurable 'Durable' = DATA BOOLEAN (Channel);
persistentDeliveryMode 'Persistent Delivery Mode' = DATA BOOLEAN (Channel);
threadCount 'Thread count' = DATA INTEGER (Channel);
prefetchCount 'Prefetch count' = DATA INTEGER (Channel);
user 'User' = DATA STRING (Channel);
password 'Password' = DATA STRING (Channel) ECHO;
isConsumer 'Consumer' = DATA BOOLEAN (Channel);
local 'Local' = DATA BOOLEAN (Channel);
started 'Started' = DATA BOOLEAN (Channel);

channel = GROUP AGGR Channel c BY host(c), queue(c);

startConsumer 'Start' INTERNAL 'lsfusion.server.physics.dev.integration.external.to.net.rabbitmq.StartConsumerRabbitMQAction' (Channel);

start 'Start' (Channel c) {
    checkIsServer();
    IF isServer() THEN {
        startConsumer(c);
        started(c) <- TRUE;
        APPLY NESTED LOCAL;
    }
}
startForm 'Start' (Channel c) {
    start(c);
    IF NOT isServer() THEN MESSAGE 'Consuming disabled, change serverComputer() to enable';
}

sendMessage 'Send message' INTERNAL 'lsfusion.server.physics.dev.integration.external.to.net.rabbitmq.SendMessageRabbitMQAction' (Channel, STRING);

onMessage ABSTRACT LIST (Channel, STRING);
onMessage(STRING host, STRING queue, STRING message) {
    onMessage(channel(host, queue), message);
}

stopConsumer 'Stop' INTERNAL 'lsfusion.server.physics.dev.integration.external.to.net.rabbitmq.StopConsumerRabbitMQAction' (Channel);

stop 'Stop' (Channel c) {
    stopConsumer(c);
    started(c) <- NULL;
    APPLY NESTED LOCAL;
}

restartConsumers 'Restart all consumers' () {
    FOR host(Channel c) AND queue(c) AND isConsumer(c) DO {
        stop(c);
        start(c);
    }
}
restartConsumersForm 'Restart all consumers' () {
    restartConsumers();
    IF NOT isServer() THEN MESSAGE 'Consuming disabled, change serverComputer() to enable';
}

status = DATA LOCAL STRING(Channel);
getStatus INTERNAL 'lsfusion.server.physics.dev.integration.external.to.net.rabbitmq.GetStatusRabbitMQAction' (Channel);
showStatus 'Show Status' (Channel c) {
    getStatus(c);
    MESSAGE status(c) HEADER 'RabbitMQ';
}

FORM rabbitMQ
    OBJECTS c = Channel, s = STRING PANEL
    PROPERTIES(c) local, isConsumer
    PROPERTIES(c) READONLYIF started(c) host, vHost, queue, isDurable, persistentDeliveryMode SHOWIF isDurable(c)
    PROPERTIES(c) threadCount, prefetchCount, user, password,
        startForm GRID SHOWIF isConsumer(c) AND NOT started(c),
        stop GRID SHOWIF isConsumer(c) AND started(c),
        showStatus GRID, NEW, DELETE
    PROPERTIES restartConsumersForm(), VALUE(s), sendMessage(c, s)
;

NAVIGATOR {
    system {
        NEW rabbitMQ;
    }
}