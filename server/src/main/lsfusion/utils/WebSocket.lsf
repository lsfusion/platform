MODULE WebSocket;

REQUIRE SystemEvents;

CLASS WebSocketClient;

id = DATA STRING(WebSocketClient);
webSocketClient = GROUP AGGR WebSocketClient c BY id(c);
hostname = DATA STRING(WebSocketClient);

onStarted() + {
    DELETE WebSocketClient c WHERE c IS WebSocketClient;
}

message = DATA TEXT(WebSocketClient);

onOpen(STRING id, STRING hostname) {
    NEWSESSION {
        NEW c = WebSocketClient {
            id(c) <- id;
            hostname(c) <- hostname;
        }
        APPLY;
    }
}

onClose(STRING id) {
    NEWSESSION {
        DELETE webSocketClient(id);
        APPLY;
    }
}

//test client, not for production
runClient 'Run Client' INTERNAL 'lsfusion.server.physics.dev.integration.external.to.net.websocket.RunClientAction' ();

sendMessage 'Send message to client' INTERNAL 'lsfusion.server.physics.dev.integration.external.to.net.websocket.SendMessageAction' (WebSocketClient);

FORM webSockets 'WebSockets'
    PROPERTIES() runClient

    OBJECTS c = WebSocketClient
    PROPERTIES(c) id, hostname, message PANEL, sendMessage
;

NAVIGATOR {
    system {
        NEW webSockets;
    }
}