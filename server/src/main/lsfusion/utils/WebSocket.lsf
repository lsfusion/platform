MODULE WebSocket;

REQUIRE SystemEvents;

CLASS WebSocketClient;

id = DATA STRING(WebSocketClient);
webSocketClient = GROUP AGGR WebSocketClient c BY id(c);
hostname = DATA STRING(WebSocketClient);

onStarted() + {
    DELETE WebSocketClient c WHERE c IS WebSocketClient;
}

message = DATA TEXT(WebSocketClient);

connectionId = DATA LOCAL STRING();
onOpen ABSTRACT LIST (STRING);
onOpen(STRING hostname) + {
    NEW c = WebSocketClient {
        id(c) <- STRING(c);
        connectionId() <- id(c);
        hostname(c) <- hostname;
    }
}

//id, message
onStringMessage ABSTRACT LIST (STRING, STRING);

//id, message
onBinaryMessage ABSTRACT LIST (STRING, RAWFILE);

onClose ABSTRACT LIST (STRING);
onClose(STRING id) + {
    DELETE webSocketClient(id);
}

//test client, not for production
runClient 'Run Client' INTERNAL 'lsfusion.server.physics.dev.integration.external.to.net.websocket.RunClientAction' ();

sendMessage 'Send message to client' INTERNAL 'lsfusion.server.physics.dev.integration.external.to.net.websocket.SendMessageAction' (WebSocketClient);

FORM webSockets 'WebSockets'
    PROPERTIES() runClient

    OBJECTS c = WebSocketClient
    PROPERTIES(c) id, hostname, message PANEL, sendMessage
;

NAVIGATOR {
    system {
        NEW webSockets;
    }
}