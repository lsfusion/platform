MODULE WebSocket;

REQUIRE SystemEvents;

CLASS WebSocketClient;

webSocketClient = GROUP AGGR WebSocketClient c BY LONG(c) IF c IS WebSocketClient;
hostname = DATA STRING(WebSocketClient);

onStarted() + {
    DELETE WebSocketClient c WHERE c IS WebSocketClient;
}

message = DATA TEXT(WebSocketClient);

connectionId = DATA LOCAL LONG();
onOpen ABSTRACT LIST (STRING);
onOpen(STRING hostname) + {
    NEW c = WebSocketClient {
        connectionId() <- LONG(c);
        hostname(c) <- hostname;
    }
}

onStringMessage ABSTRACT LIST (WebSocketClient, STRING);

onBinaryMessage ABSTRACT LIST (WebSocketClient, RAWFILE);

onClose ABSTRACT LIST (WebSocketClient);
onClose(WebSocketClient c) + {
    DELETE c;
}

sendMessage 'Send message to client' INTERNAL 'lsfusion.server.physics.dev.integration.external.to.net.websocket.SendMessageAction' (WebSocketClient);

FORM webSockets 'WebSockets'
    OBJECTS c = WebSocketClient
    PROPERTIES(c) id 'id' = VALUE, hostname, message PANEL, sendMessage
;

NAVIGATOR {
    system {
        NEW webSockets;
    }
}