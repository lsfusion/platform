MODULE EvalScript;

REQUIRE System, Time;

NAMESPACE System;

scriptStorage 'Скрипт' () = DATA TEXT ();

CLASS Script 'Скрипт';
TABLE script(Script);

name 'Наименование' = DATA VARSTRING[100] (Script);
text 'Текст' = DATA TEXT (Script);
textScript = GROUP MAX Script script BY text(script); 
dateTime 'Дата запуска' = DATA DATETIME (Script);
succeeded = DATA BOOLEAN (Script);

//при первом старте помечает все старые скрипты как succeeded
onStartedSucceeded = DATA BOOLEAN ();
onStarted() += ACTION {
    IF NOT onStartedSucceeded() THEN {
        succeeded(Script s) <- TRUE;
        onStartedSucceeded() <- TRUE;
    }
}

updateStorageNewSession(Script script) = ACTION NEWSESSION {
    scriptStorage() <- text(script);
    apply();
}

updateStorage(Script script) = ACTION {
    updateStorageNewSession(script);
    cancel();
}

saveTextScript(Script s, TEXT t) = ACTION {
    text(s) <- t;
}

updateDateTimeScript(Script s) = ACTION {
    dateTime(s) <- currentDateTime();
}

setSucceeded(Script s) = ACTION {
    succeeded(s) <- TRUE;
}

preRunScript() = ACTION {
    LOCAL localText = TEXT();
    localText() <- scriptStorage();
    NEWSESSION NESTED (localText) {
        scriptStorage() <- localText();
        LOCAL script = Script ();
        script() <- textScript(localText());
        IF NOT script() THEN {
            ADDOBJ Script; 
            script() <- addedObject();
            saveTextScript(script(), scriptStorage());
        }
        updateDateTimeScript(script());
        apply();
    }
}

afterRunScript() = ACTION {
    setSucceeded(textScript(scriptStorage()));
    apply();
}


scriptExecution 'Выполнить скрипт' () = ACTION { TRY { preRunScript(); EVAL scriptStorage(); afterRunScript(); } CATCH; }

actionBodyExecution 'Выполнить действие' () = ACTION { TRY { preRunScript(); EVAL CONCAT ' ', 'run() = ACTION {', scriptStorage(), ';\n};'; afterRunScript(); } CATCH; };
formExecution 'Показать форму' () = ACTION { TRY { preRunScript(); EVAL CONCAT ' ', 'FORM form', scriptStorage(), ';\nrun() = ACTION FORM form;'; afterRunScript(); } CATCH; };
javaActionBodyExecution 'Выполнить Java-код' () = ACTION { TRY { preRunScript(); EVAL CONCAT ' ', 'run() = ACTION CODE ', scriptStorage(), ' END;'; afterRunScript(); } CATCH; };

FORM chooseScriptName 'Сохранить как'
    OBJECTS name = VARSTRING[100] FIXED PANEL
    PROPERTIES valueName = OBJVALUE(name)
;

DESIGN chooseScriptName {
    main{
        caption = 'Сохранить как';
        PROPERTY(valueName){
            caption = 'Наименование';
            font = 'Tahoma 24';
        }
    REMOVE leftControls;
    REMOVE PROPERTY(formRefresh());
    }
}

saveAsScriptAction 'Сохранить как'() = ACTION NEWSESSION {
    FORM chooseScriptName MODAL;
    IF formResult() == FormResult.ok THEN {
        FOR ADDOBJ s = Script DO {
            name(s) <- chosenString('name');
            text(s) <- scriptStorage();
            dateTime(s) <- currentDateTime();
        }
        apply();
    }
}

saveAction 'Сохранить'(Script script) = ACTION {
    text(script) <- scriptStorage();
    dateTime(script) <- currentDateTime();
}

// Универсальная таблица для последующего импорта
CLASS Row 'Ряд';
TABLE row(Row);

string1 'Строка 1' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string2 'Строка 2' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string3 'Строка 3' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string4 'Строка 4' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string5 'Строка 5' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string6 'Строка 6' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string7 'Строка 7' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string8 'Строка 8' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string9 'Строка 9' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string10 'Строка 10' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;

numeric1 'Число 1' = DATA NUMERIC[20,7] (Row);
numeric2 'Число 2' = DATA NUMERIC[20,7] (Row);
numeric3 'Число 3' = DATA NUMERIC[20,7] (Row);
numeric4 'Число 4' = DATA NUMERIC[20,7] (Row);
numeric5 'Число 5' = DATA NUMERIC[20,7] (Row);

date1 'Дата 1' = DATA DATE (Row);
date2 'Дата 2' = DATA DATE (Row);
date3 'Дата 3' = DATA DATE (Row);

FORM script 'Скрипт'
    OBJECTS s = Script FIXED PANEL
    PROPERTIES(s) dateTime, name SELECTOR EVENTS ON CHANGE s updateStorage(s)
    PROPERTIES() scriptStorage,
                 scriptExecution, actionBodyExecution, formExecution, javaActionBodyExecution, saveAsScriptAction
    PROPERTIES(s) saveAction
    ORDER BY dateTime(s) DESC
                 
    OBJECTS r = Row FIXED GRID
    PROPERTIES(r) string1, string2, string3, string4, string5, string6, string7, string8, string9, string10 
    PROPERTIES(r) numeric1, numeric2, numeric3, numeric4, numeric5, date1, date2, date3
    PROPERTIES(r) ADDOBJ, DELETESESSION
;

DESIGN script {
    PROPERTY(dateTime(s)) { hide = TRUE; }
    NEW pane {
        fill = 1;
        type = TABBED;
        NEW scriptPane {
            fill = 1;        
            caption = 'Скрипт';
            NEW script {
                caption = 'Сохранённые скрипты';
                type = CONTAINERH;
                MOVE PROPERTY(name(s)) { font = 'bold 24'; alignment = STRETCH; }
                MOVE PROPERTY(saveAction(s)) { font = 'bold 24'; alignment = STRETCH; }
                MOVE PROPERTY(saveAsScriptAction()) { font = 'bold 24'; alignment = STRETCH; }
            }
            MOVE PROPERTY(scriptStorage()) {
                panelCaptionAbove = TRUE;    
                fill = 1;
            }
            NEW run {
                caption = 'Запуск';
                type = CONTAINERH;
                MOVE PROPERTY(scriptExecution()) { font = 'bold 24'; }
                MOVE PROPERTY(actionBodyExecution()) { font = 'bold 24'; }
                MOVE PROPERTY(formExecution()) { font = 'bold 24'; }
                MOVE PROPERTY(javaActionBodyExecution()) { font = 'bold 24'; }
            }
        }
        MOVE r.box {
            caption = 'Данные';
        }
    }
    MOVE functions.box;
}

FORM scriptLog 'Скрипт'
    OBJECTS s = Script FIXED PANEL
    PROPERTIES(s) name, text, dateTime

    EDIT Script OBJECT s
;

FORM scriptsLog 'Скрипты'
    
    OBJECTS s=Script
    PROPERTIES(s) name, text READONLY, dateTime READONLY 
    PROPERTIES(s) DELETE FORCE PANEL TOOLBAR
    ORDER BY dateTime(s) DESC

    FILTERGROUP user FILTER 'Только пользовательские' name(s) 'F9'
    FILTERGROUP succeeded FILTER 'Только без ошибок' succeeded(s) 'F10' DEFAULT

    DIALOG Script OBJECT s
;

NAVIGATOR {
    configuration {
        ADD script;
    }
}

