MODULE Email;

REQUIRE System, Reflection;

GROUP email 'Настройки почтового сервера' : public;   //rootGroup

CLASS EncryptedConnectionTypeStatus 'Тип шифрованного подключения' {
    SSL 'SSL',
    TLS 'TLS'
}
FORM encryptedConnectionTypeStatuses
    OBJECTS s = EncryptedConnectionTypeStatus
    PROPERTIES(s) staticCaption
    DIALOG EncryptedConnectionTypeStatus OBJECT s
;

encryptedConnectionType 'Тип шифрованного подключения' = DATA EncryptedConnectionTypeStatus();
nameEncryptedConnectionType 'Тип шифрованного подключения' = staticCaption(encryptedConnectionType()) PREFCHARWIDTH 3 IN email;

CLASS Notification 'Уведомление';
TABLE notification (Notification);
TABLE notificationProperty (Notification, Property);

isEventNotification 'При любом изменении' = DATA BOOLEAN (Notification);
emailFromNotification 'Адрес отправителя' = DATA VARSTRING[50] (Notification);
emailToNotification 'Адрес получателя' = DATA VARSTRING[50] (Notification);
emailToCCNotification 'Копия' = DATA VARSTRING[50] (Notification);
emailToBCNotification 'Скрытая копия' = DATA VARSTRING[50] (Notification);
textNotification 'Текст письма' = DATA TEXT (Notification);
subjectNotification 'Тема письма'= DATA VARSTRING[100] (Notification);
inNotificationProperty 'Вкл.' = DATA BOOLEAN (Notification, Property);

smtpHost 'SMTP хост' = DATA VARSTRING[50]() IN email;
smtpPort 'SMTP порт' = DATA VARSTRING[10]() IN email;

emailAccount 'Имя аккаунта' = DATA VARSTRING[50]() IN email;
emailPassword 'Пароль' = DATA VARSTRING[50]() IN email;
emailBlindCarbonCopy 'Копия (BCC)'= DATA VARSTRING[50]() IN email;
disableEmail 'Отключить отсылку почты' = DATA BOOLEAN() IN email;


FORM remindUserPass 'Напоминание пароля'

    OBJECTS u=CustomUser FIXED PANEL
    PROPERTIES(u) READONLY loginCustomUser, passwordCustomUser, nameContact
;

emailUserPassUser 'Напоминание пароля' (user) = ACTION EMAIL
                               SUBJECT 'Напоминание пароля' IF user IS CustomUser
                               TO emailContact(user)
                               INLINE remindUserPass OBJECTS u = user;

FORM notification 'Почта'

    PROPERTIES() smtpHost, smtpPort, nameEncryptedConnectionType, fromAddress, emailAccount, emailPassword,
                                     emailBlindCarbonCopy, disableEmail

    OBJECTS n=Notification
    OBJECTS p=Property

    PROPERTIES(n, p) inNotificationProperty
    PROPERTIES(n) subjectNotification, textNotification FORCE PANEL, emailFromNotification, emailToNotification, emailToCCNotification, emailToBCNotification, isEventNotification, ADDOBJ
    PROPERTIES(p) READONLY captionProperty, SIDProperty
    ORDER BY SIDProperty

    FILTERGROUP emailFilter
        FILTER 'Только отмеченные' 'F9' inNotificationProperty(n, p)
;

EXTEND DESIGN notification {
    main {
        NEW specContainer {
            type = TABBED;
            ADD n.box;
            ADD p.box;
            NEW textContainer {
                caption = 'Текст письма';
                childConstraints = TO THE BOTTOM;
                fillHorizontal = 1;
                fillVertical = 1;
                ADD PROPERTY(textNotification(n)) {
                    fillHorizontal = 1;
                    preferredSize = ( -1, 600);
                    panelLabelAbove = TRUE;
                }
            }
        }
        ADD functions.box;
    }
}

NAVIGATOR {
    //ADD remindUserPass;
    configuration {
        ADD Email.notification;
    }
}